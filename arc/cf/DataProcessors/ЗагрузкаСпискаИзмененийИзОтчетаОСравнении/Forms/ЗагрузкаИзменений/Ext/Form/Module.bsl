
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ТехническийПроект") Тогда
		
		ТекстПодсистема = НСтр("ru = 'Подсистема.'");
		ТехническийПроект = Параметры.ТехническийПроект;
		ТаблицаОбъектовМетаданных = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		ОбъектыМетаданных.Загрузить(ТаблицаОбъектовМетаданных);
		
		ВыбратьФайлАвтоматически();
		
	Иначе
		
		ТекстИсключения = НСтр("ru = 'Обработка предназначена для открытия только из карточки технического проекта.'");
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_Файл" Тогда
		Если Параметр.ВладелецФайла = ТехническийПроект Тогда
			ПрисоединенныйФайл = Параметр.Файл;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//Работа с присоединенным файлом
&НаКлиенте
Процедура ПрисоединенныйФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла", ТехническийПроект);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ТекущаяВерсияРасширение", "txt"));

	ОписаниеОповещения = Новый ОписаниеОповещения("ПрисоединенныйФайлНачалоВыбораЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.Файлы.Форма.ФормаВыбора",
				 ПараметрыФормы,
				 ЭтаФорма,
				 ,
				 ,
				 ,
				 ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисоединенныйФайлНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ВыбранныйФайл = Результат;
	
    Если ВыбранныйФайл <> Неопределено Тогда
        ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(ВыбранныйФайл, Неопределено, УникальныйИдентификатор);
        
        Если ДанныеФайла.Расширение <> "txt" Тогда
            
            ТекстСообщения = НСтр("ru='Присоединенный файл должен иметь формат "".txt"":'");
            
            ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
            
            Возврат;
        КонецЕсли;	
        
        ПрисоединенныйФайл = ВыбранныйФайл;		
        
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзменения(Команда)
	
	Если Не ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Присоединенный файл"" не заполнено.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ПрисоединенныйФайл");
		
		Возврат;
		
	КонецЕсли;
	
	Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не удалось подключить расширение работы с файлами.'"));
		Возврат;
	КонецЕсли;
	
	ТекстПояснения = НСтр("ru = 'Идет обработка файла сравнения конфигураций.
	                      |Пожалуйста, подождите...'");
	
	Состояние(ТекстПояснения);
	
	ПараметрСтруктура = ЗагрузитьДанныеФайлаНаСервере();
	
	Если ПараметрСтруктура <> Неопределено Тогда
		
		Оповестить("ЗагрузкаСпискаИзмененийЗавершена", ПараметрСтруктура);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнен список объектов метаданных.'"));
		Закрыть();
		
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Нет данных для заполнения списка метаданных.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьДанныеФайлаНаСервере()
	
	СтруктураДанных = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаИДвоичныеДанные(ПрисоединенныйФайл);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt");
	
	СтруктураДанных.ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяВременногоФайла);
	ТекстПрисоединенногоФайла = ЧтениеТекста.Прочитать();
	
	Попытка
		УдалитьФайлы(ИмяВременногоФайла);
	Исключение
	КонецПопытки;
	
	Возврат  ЗагрузитьИзмененияНаСервере();
	
КонецФункции

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	
	ПараметрыДобавления = Новый Структура;
	ПараметрыДобавления.Вставить("ОбработчикРезультата", Неопределено);
	ПараметрыДобавления.Вставить("ВладелецФайла",        ТехническийПроект);
	ПараметрыДобавления.Вставить("ФормаВладелец",        ЭтаФорма);
	ПараметрыДобавления.Вставить("НеОткрыватьКарточкуПослеСозданияИзФайла", Истина);

	РаботаСФайламиСлужебныйКлиент.ДобавитьИзФайловойСистемыСРасширением(ПараметрыДобавления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаИзменений

&НаСервере
Функция ЗагрузитьИзмененияНаСервере()
	
	Отказ = Ложь;
	
	ОбработатьПоСтрокам(Отказ);
	
	Если Не Отказ Тогда
		Если УдалятьНеОбработанныеСтроки Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Обработана", Истина);
			
			АдресВХранилище = ПоместитьВоВременноеХранилище(ОбъектыМетаданных.Выгрузить(ПараметрыОтбора));
			
		Иначе 
			
			АдресВХранилище = ПоместитьВоВременноеХранилище(ОбъектыМетаданных.Выгрузить());
			
		КонецЕсли;
		
		ПараметрСтруктура = Новый Структура;
		ПараметрСтруктура.Вставить("ТехническийПроект", ТехническийПроект);
		ПараметрСтруктура.Вставить("АдресВХранилище",   АдресВХранилище);
		
		Возврат ПараметрСтруктура;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбработатьПоСтрокам(Отказ)	
	
	ЗагрузкаМетаданных.ЗагрузитьИзмененияМетаданныхИзТекста(ОбъектыМетаданных, ТехническийПроект, ТекстПрисоединенногоФайла, ПрисоединенныйФайл, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ВыбратьФайлАвтоматически()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Файлы.Ссылка,
	|	Файлы.Наименование
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.ВладелецФайла = &ВладелецФайла
	|	И (Файлы.Расширение = ""txt""
	|			ИЛИ Файлы.Расширение = ""TXT"")
	|	И НЕ Файлы.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ВладелецФайла", ТехническийПроект);
	
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	ФайлОтчетОСравнении = Неопределено;
	Пока Выборка.Следующий() Цикл
		
		Если НРег(Выборка.Наименование) = НРег(НСтр("ru = 'ОтчетОСравнении'")) Тогда
			Если ФайлОтчетОСравнении = Неопределено Тогда
				ФайлОтчетОСравнении = Выборка.Ссылка;
			Иначе
				// Файлов с таким именем несколько - нельзя автоматически выбрать файл
				ФайлОтчетОСравнении = Неопределено;
				Прервать;
			КонецЕсли;
		КонецЕсли; 
	
	КонецЦикла;

	Если ФайлОтчетОСравнении <> Неопределено Тогда
		ПрисоединенныйФайл = ФайлОтчетОСравнении;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
