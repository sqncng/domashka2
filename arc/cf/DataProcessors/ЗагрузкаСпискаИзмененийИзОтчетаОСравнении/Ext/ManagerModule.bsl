#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция УбратьТабуляциюВНачалеСтрок(Строка) Экспорт
	
	Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Строка, Символы.ПС, Ложь);
	
	КоличествоЗнаковТабуляции = Неопределено;
	
	Для Каждого Подстрока Из Подстроки Цикл
		
		Если Подстрока = "" Тогда
			Продолжить;
		КонецЕсли; 
		
		КоличествоСимволов = КоличествоСимволовВНачалеСтроки(Подстрока, Символы.Таб);
		
		Если     КоличествоЗнаковТабуляции = Неопределено
			   ИЛИ КоличествоЗнаковТабуляции > КоличествоСимволов Тогда
			КоличествоЗнаковТабуляции = КоличествоСимволов;
		КонецЕсли; 
	
	КонецЦикла; 
	
	Если КоличествоЗнаковТабуляции = Неопределено Тогда
		КоличествоЗнаковТабуляции = 0;
	КонецЕсли;
	
	Если КоличествоЗнаковТабуляции > 0 Тогда
		
		Для Счетчик = 0 По Подстроки.Количество() - 1 Цикл
			Подстроки[Счетчик] = Сред(Подстроки[Счетчик], КоличествоЗнаковТабуляции + 1);
		КонецЦикла; 
		
	КонецЕсли; 
	
	Возврат СтрСоединить(Подстроки, Символы.ПС);
	
КонецФункции

Функция КоличествоСимволовВНачалеСтроки(Знач Строка, Символ = " ")
	
	КоличествоСимволов = 0;
	
	Для Счетчик = 1 По СтрДлина(Строка) Цикл
		
		Если Лев(Строка, 1) = Символ Тогда
			КоличествоСимволов = КоличествоСимволов + 1;
			Строка = Сред(Строка, 2);
		Иначе 
			Возврат КоличествоСимволов;
		КонецЕсли; 
		
	КонецЦикла; 
	
	Возврат КоличествоСимволов;
	
КонецФункции

#КонецОбласти

#КонецЕсли