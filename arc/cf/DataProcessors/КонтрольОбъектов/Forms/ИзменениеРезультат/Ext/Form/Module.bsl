
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПереданныеПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияДляЧегоРедактируемОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	
	Если Модифицированность Тогда
		Закрыть(ТекущееСостояниеДел);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	ОбъектыКонтроля = ОбъектыНаКонтроле.ОбъектыКонтроляПоКлючамЗаписиРегистраОбъектыНаКонтроле(Параметры.ИзменяемыеСтрокиСписка);
	
	Элементы.ДекорацияДляЧегоРедактируем.Заголовок = ОбъектыНаКонтроле.ТекстДекорацииИзменяемыеЗаписиКонтроля(ОбъектыКонтроля);
	ТекущееСостояниеДел = Параметры.Результат;
	Решение             = Параметры.Решение;
	ДатаКонтроля        = Параметры.ДатаКонтроля;
	
	КоличествоОбъектовКонтроля = Параметры.ИзменяемыеСтрокиСписка.Количество();
	
	Если КоличествоОбъектовКонтроля = 1 Тогда
		
		ИзменитьОтветственного = Истина;
		ИзменитьДатуОкончания  = Истина;
		Элементы.ГруппаРешение.Заголовок = СтрШаблон(НСтр("ru = 'Решение от %1'"), Формат(ДатаКонтроля, "ДЛФ=DD"));
		
	Иначе
		
		Элементы.ГруппаРешение.Видимость = Ложь;
		Высота = Высота - 4;
		СброситьРазмерыИПоложениеОкна();
		Высота = Высота + КоличествоОбъектовКонтроля - 1;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("Обработка.КонтрольОбъектов.Форма.ИзменениеРезультат", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
