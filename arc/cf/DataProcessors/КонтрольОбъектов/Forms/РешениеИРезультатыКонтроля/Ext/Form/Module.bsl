
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПереданныеПараметры();
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИзменитьОтветственногоПриИзменении(Элемент)
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуОкончанияПриИзменении(Элемент)
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДляЧегоРедактируемОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	
	Закрыть(СтруктураВозврата());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	Если ЗначениеЗаполнено(Параметры.ОбъектКонтроля)
		И ЗначениеЗаполнено(Параметры.СписокКонтроля) Тогда
		
		ОбъектыКонтроля = Новый Массив;
		ОбъектыКонтроля.Добавить(Параметры.ОбъектКонтроля);
		
		СписокКонтроля = Параметры.СписокКонтроля;
		ОбъектКонтроля = Параметры.ОбъектКонтроля;
		
		ДанныеПоследнейЗаписи = РегистрыСведений.ОбъектыНаКонтроле.ДанныеПоследнейЗаписиКонтроля(ОбъектКонтроля, СписокКонтроля);
		
		ПредыдущееРешение       = ДанныеПоследнейЗаписи.Решение;
		ОжидаемаяДатаВыполнения = ДанныеПоследнейЗаписи.ОжидаемаяДатаВыполнения;
		Ответственный           = ДанныеПоследнейЗаписи.Ответственный;
		Результат               = ДанныеПоследнейЗаписи.Результат;
		ДатаПоследнегоКонтроля  = ДанныеПоследнейЗаписи.ДатаКонтроля;
		КоличествоЗаписей       = 1;
		ЭтоСнятиеСКонтроля      = Параметры.ЭтоСнятиеСКонтроля;
		ЭтоВозвратНаКонтроль    = Не ЭтоСнятиеСКонтроля И ДанныеПоследнейЗаписи.ИсторическаяЗапись;
		
		ВариантВеденияСписка = 
		ОбъектыНаКонтролеКлиентСервер.ВариантВеденияСпискаКонтроля(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СписокКонтроля, "ВариантВеденияСписка"));
		
	Иначе
		
		ОбъектыКонтроля = ОбъектыНаКонтроле.ОбъектыКонтроляПоКлючамЗаписиРегистраОбъектыНаКонтроле(Параметры.ИзменяемыеСтрокиСписка);
		
		ПредыдущееРешение       = Параметры.Решение;
		ОжидаемаяДатаВыполнения = Параметры.ОжидаемаяДатаВыполнения;
		Ответственный           = Параметры.Ответственный;
		Результат               = Параметры.Результат;
		ДатаПоследнегоКонтроля  = Параметры.ДатаКонтроля;
		КоличествоЗаписей       = Параметры.ИзменяемыеСтрокиСписка.Количество();
		ЭтоСнятиеСКонтроля      = Параметры.ЭтоСнятиеСКонтроля;
		
		СпискиКонтроля = ОбъектыНаКонтроле.СпискиКонтроляПоОбъектамКонтроля(Параметры.ИзменяемыеСтрокиСписка);
		Если ОбъектыНаКонтроле.ЕстьСписокВМассивеВариантКонтроляПоДатеОбъекта(СпискиКонтроля) Тогда
			ВариантВеденияСписка = Перечисления.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВариантВеденияСписка = Перечисления.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля Тогда
		СледующаяДатаКонтроля = НачалоДня(ТекущаяДатаСеанса() + 86400);
	КонецЕсли;
	
	Элементы.ДекорацияДляЧегоРедактируем.Заголовок = ОбъектыНаКонтроле.ТекстДекорацииИзменяемыеЗаписиКонтроля(ОбъектыКонтроля);
	
	Если ЭтоСнятиеСКонтроля Тогда
		Элементы.ГруппаРешение.Заголовок = СтрШаблон(НСтр("ru = 'Решение по снятию с контроля от %1'"), Формат(НачалоДня(ТекущаяДатаСеанса()), "ДЛФ=DD"));
		Заголовок = НСтр("ru = 'Снятие с контроля'");
		Элементы.ГруппаУсловноИзменяемыеРеквизиты.Видимость  = Ложь;
	Иначе
		Элементы.ГруппаРешение.Заголовок = СтрШаблон(НСтр("ru = 'Новое решение от %1'"), Формат(НачалоДня(ТекущаяДатаСеанса()), "ДЛФ=DD"));
		Если ЭтоВозвратНаКонтроль Тогда
			Заголовок = НСтр("ru = 'Возврат на контроль'");
		Иначе
			Заголовок = НСтр("ru = 'Результаты контроля'");
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоЗаписей = 1 Тогда
		
		ИзменитьОтветственного = Истина;
		ИзменитьДатуОкончания  = Истина;
		Элементы.ГруппаПредыдущееРешение.Заголовок = СтрШаблон(НСтр("ru = 'Решение от %1'"), Формат(ДатаПоследнегоКонтроля, "ДЛФ=DD"));
		Элементы.ГруппаРезультат.Заголовок         = СтрШаблон(НСтр("ru = 'Результат по решению от %1'"), Формат(ДатаПоследнегоКонтроля, "ДЛФ=DD"));
		
	Иначе
		
		Элементы.ГруппаПредыдущееРешение.Видимость = Ложь;
		Высота = Высота - 4;
		СброситьРазмерыИПоложениеОкна();
		Если КоличествоЗаписей > 1 Тогда
			Высота = Высота + КоличествоЗаписей - 1;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураВозврата()
	
	СтруктураВозврата = Новый Структура;
	
	Если ЭтоСнятиеСКонтроля 
		И ПустаяСтрока(Решение) Тогда
		
		СтруктураВозврата.Вставить("Решение",  НСтр("ru = 'Объект снят с контроля'"));
		
	ИначеЕсли КоличествоЗаписей = 1
		И ПустаяСтрока(Решение) Тогда
		СтруктураВозврата.Вставить("Решение", Элементы.Решение.ПодсказкаВвода);
	Иначе
		СтруктураВозврата.Вставить("Решение", Решение);
	КонецЕсли;
	
	СтруктураВозврата.Вставить("Результат",               Результат);
	СтруктураВозврата.Вставить("Ответственный",           Ответственный);
	СтруктураВозврата.Вставить("ОжидаемаяДатаВыполнения", ОжидаемаяДатаВыполнения);
	СтруктураВозврата.Вставить("ИзменитьОтветственного",  ИзменитьОтветственного);
	СтруктураВозврата.Вставить("ИзменитьДатуОкончания",   ИзменитьДатуОкончания);
	СтруктураВозврата.Вставить("ЭтоСнятиеСКонтроля",      ЭтоСнятиеСКонтроля);
	
	Если ВариантВеденияСписка 
		= ПредопределенноеЗначение("Перечисление.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля") Тогда 
		
		СтруктураВозврата.Вставить("СледующаяДатаКонтроля", СледующаяДатаКонтроля);
		
	Иначе
		
		СтруктураВозврата.Вставить("СледующаяДатаКонтроля", Дата(1, 1, 1));
		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)

	ЭлементыФормы = Форма.Элементы;
	
	Если Форма.КоличествоЗаписей > 1 Тогда 
		
		ЭлементыФормы.ИзменитьОтветственного.Видимость           = Истина;
		ЭлементыФормы.ИзменитьДатуОкончания.Видимость            = Истина;
		ЭлементыФормы.Ответственный.Доступность                  = Форма.ИзменитьОтветственного;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.Доступность        = Форма.ИзменитьДатуОкончания;
		ЭлементыФормы.Ответственный.ПоложениеЗаголовка           = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		
	Иначе
		
		ЭлементыФормы.ИзменитьОтветственного.Видимость           = Ложь;
		ЭлементыФормы.ИзменитьДатуОкончания.Видимость            = Ложь;
		ЭлементыФормы.Ответственный.Доступность                  = Истина;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.Доступность        = Истина;
		ЭлементыФормы.Ответственный.ПоложениеЗаголовка           = ПоложениеЗаголовкаЭлементаФормы.Лево;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
		
		ЭлементыФормы.Решение.ПодсказкаВвода = Форма.ПредыдущееРешение;
		
	КонецЕсли;
	
	Если Форма.ЭтоСнятиеСКонтроля Тогда
		
		ЭлементыФормы.Решение.ПодсказкаВвода = НСтр("ru = 'Объект снят с контроля'");
		
	ИначеЕсли Форма.ЭтоВозвратНаКонтроль Тогда
		
		ЭлементыФормы.Решение.ПодсказкаВвода = НСтр("ru = 'Объект возвращен на контроль'");
		
	КонецЕсли;
	
	ЭлементыФормы.СледующаяДатаКонтроля.Видимость = 
		(Форма.ВариантВеденияСписка = ПредопределенноеЗначение("Перечисление.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля"))
		И Не Форма.ЭтоСнятиеСКонтроля;
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("Обработка.КонтрольОбъектов.Форма.ИзменениеРезультат", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
