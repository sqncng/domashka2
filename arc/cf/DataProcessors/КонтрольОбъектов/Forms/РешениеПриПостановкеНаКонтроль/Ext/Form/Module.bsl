#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПереданныеПараметры();
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИзменитьОтветственногоПриИзменении(Элемент)
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуОкончанияПриИзменении(Элемент)
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДляЧегоРедактируемОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	
	Закрыть(СтруктураВозврата());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СтруктураВозврата()
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Решение", Решение);
	
	ДанныеОбъектовКонтроля = Новый Массив;
	Для Каждого СтрокаТаблицы Из ОбъектыКонтроля Цикл
		
		ДанныеОбъектаКонтроля = Новый Структура;
		ДанныеОбъектаКонтроля.Вставить("ОбъектКонтроля",    СтрокаТаблицы.ОбъектКонтроля);
		ДанныеОбъектаКонтроля.Вставить("СтатусКонтроля",    СтрокаТаблицы.СтатусКонтроля);
		ДанныеОбъектаКонтроля.Вставить("ТехническийПроект", СтрокаТаблицы.ТехническийПроект);
		
		Если ИзменитьОтветственного Тогда
			ДанныеОбъектаКонтроля.Вставить("Ответственный", Ответственный);
		Иначе
			ДанныеОбъектаКонтроля.Вставить("Ответственный", СтрокаТаблицы.Ответственный);
		КонецЕсли;
		
		Если ИзменитьДатуОкончания Тогда
			ДанныеОбъектаКонтроля.Вставить("ОжидаемаяДатаВыполнения", ОжидаемаяДатаВыполнения);
		Иначе
			ДанныеОбъектаКонтроля.Вставить("ОжидаемаяДатаВыполнения", СтрокаТаблицы.ОжидаемаяДатаВыполнения);
		КонецЕсли;
		
		Если ВариантВеденияСписка 
				= ПредопределенноеЗначение("Перечисление.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля") Тогда 
			
			ДанныеОбъектаКонтроля.Вставить("СледующаяДатаКонтроля", СледующаяДатаКонтроля);
			
		Иначе
			
			ДанныеОбъектаКонтроля.Вставить("СледующаяДатаКонтроля", Дата(1, 1, 1));
			
		КонецЕсли;
		
		ДанныеОбъектовКонтроля.Добавить(ДанныеОбъектаКонтроля);
		
	КонецЦикла;
	
	СтруктураВозврата.Вставить("ОбъектыКонтроля" , ДанныеОбъектовКонтроля);
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	Элементы.ДекорацияДляЧегоРедактируем.Заголовок = ОбъектыНаКонтроле.ТекстДекорацииИзменяемыеЗаписиКонтроля(Параметры.ОбъектКонтроля);
	
	Решение           = Параметры.Решение;
	КоличествоЗаписей = Параметры.ОбъектКонтроля.Количество();
	СписокКонтроля    = Параметры.СписокКонтроля;
	
	ВариантВеденияСписка = 
		ОбъектыНаКонтролеКлиентСервер.ВариантВеденияСпискаКонтроля(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СписокКонтроля, "ВариантВеденияСписка")); 
		
	Если ВариантВеденияСписка = Перечисления.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля Тогда
		СледующаяДатаКонтроля = НачалоДня(ТекущаяДатаСеанса() + 86400);
	КонецЕсли;
	
	ДанныеОбъектовКонтроля = ОбъектыНаКонтроле.ДанныеОбъектовКонтроляДляПостановкиНаКонтроль(Параметры.ОбъектКонтроля, Параметры.СписокКонтроля);
	
	Для Каждого ДанныеОбъектаКонтроля Из ДанныеОбъектовКонтроля Цикл
		
		НоваяСтрока = ОбъектыКонтроля.Добавить();
		НоваяСтрока.ОбъектКонтроля          = ДанныеОбъектаКонтроля.ОбъектКонтроля;
		НоваяСтрока.Ответственный           = ДанныеОбъектаКонтроля.Ответственный;
		НоваяСтрока.ОжидаемаяДатаВыполнения = ДанныеОбъектаКонтроля.ПлановаяДатаОкончания;
		НоваяСтрока.СтатусКонтроля          = ДанныеОбъектаКонтроля.СтатусКонтроля;
		НоваяСтрока.ТехническийПроект       = ДанныеОбъектаКонтроля.ТехническийПроект;
		
	КонецЦикла;
	
	Если КоличествоЗаписей = 1 Тогда
		ИзменитьОтветственного = Истина;
		ИзменитьДатуОкончания  = Истина;
		
		Если ОбъектыКонтроля.Количество() = 1 Тогда
			Ответственный           = ОбъектыКонтроля[0].Ответственный;
			ОжидаемаяДатаВыполнения = ОбъектыКонтроля[0].ОжидаемаяДатаВыполнения;
		КонецЕсли;
		
	Иначе
		
		СброситьРазмерыИПоложениеОкна();
		Если КоличествоЗаписей > 1 Тогда
			Высота = Высота +  КоличествоЗаписей - 1;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)

	ЭлементыФормы = Форма.Элементы;
	
	Если Форма.КоличествоЗаписей > 1 Тогда 
		
		ЭлементыФормы.ИзменитьОтветственного.Видимость           = Истина;
		ЭлементыФормы.ИзменитьДатуОкончания.Видимость            = Истина;
		ЭлементыФормы.Ответственный.Доступность                  = Форма.ИзменитьОтветственного;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.Доступность        = Форма.ИзменитьДатуОкончания;
		ЭлементыФормы.Ответственный.ПоложениеЗаголовка           = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		
	Иначе
		
		ЭлементыФормы.ИзменитьОтветственного.Видимость           = Ложь;
		ЭлементыФормы.ИзменитьДатуОкончания.Видимость            = Ложь;
		ЭлементыФормы.Ответственный.Доступность                  = Истина;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.Доступность        = Истина;
		ЭлементыФормы.Ответственный.ПоложениеЗаголовка           = ПоложениеЗаголовкаЭлементаФормы.Лево;
		ЭлементыФормы.ОжидаемаяДатаВыполнения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
		
	КонецЕсли;
	
	ЭлементыФормы.СледующаяДатаКонтроля.Видимость = 
		(Форма.ВариантВеденияСписка = ПредопределенноеЗначение("Перечисление.ВариантыВеденияСпискаКонтроля.ДатаКонтроляНазначаетсяНаОбъектКонтроля"));
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("Обработка.КонтрольОбъектов.Форма.ИзменениеРезультат", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

