
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ДанныеОшибки) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ДанныеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Изменить(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответственный = ТекущийОтветственный Тогда
		
		ТекстВопроса = НСтр("ru='Ошибка СППР уже направлена ответственному ""%Ответственный"".
			|Перенести указанные ошибки АПК в отдельную ошибку СППР?'");
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "%Ответственный", СокрЛП(Ответственный));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru='Разделение ошибки СППР'"));
    Иначе
		ВыполнитьИзменение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    ВыполнитьИзменение();

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьИзменение()
    
    Перем Результат;
    
    Результат = Новый Структура("Ответственный, Комментарий");
    ЗаполнитьЗначенияСвойств(Результат, ЭтаФорма);
    
    Закрыть(Результат);

КонецПроцедуры

#КонецОбласти