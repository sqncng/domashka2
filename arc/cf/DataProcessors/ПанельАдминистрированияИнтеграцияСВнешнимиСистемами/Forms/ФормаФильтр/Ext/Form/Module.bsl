
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Константы.ПараметрыЗагрузкиОшибокИзФайлов.Получить().Получить();
	
	Если ТипЗнч(ПараметрыЗагрузки) = Тип("ТаблицаЗначений") Тогда
		ТаблицаПроекты.Загрузить(ПараметрыЗагрузки);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПроектыПроектПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаПроекты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Проект) Тогда
		ТекущиеДанные.ИмяКонфигурации = ПолучитьИмяКонфигурации(ТекущиеДанные.Проект);
	Иначе
		ТекущиеДанные.ИмяКонфигурации = "";
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Проекты.ФормаВыбора", ПараметрыФормы, Элементы.ТаблицаПроекты,,,, Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ЭтаФорма.ПроверитьЗаполнение();
	
	Если Не ПроверитьЗаполненностьИмениКонфигурации() Тогда
		Возврат;
	КонецЕсли; 
	
	СохранитьПараметрыЗагрузки();
	ОбновитьПовторноИспользуемыеЗначения();
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаПроектыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Для Каждого Значение из ВыбранноеЗначение Цикл
		
		НоваяСтрока = ТаблицаПроекты.Добавить();
		НоваяСтрока.Проект = Значение;
		НоваяСтрока.ИмяКонфигурации = ПолучитьИмяКонфигурации(НоваяСтрока.Проект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверитьЗаполненностьИмениКонфигурации()
	
	Отбор = Новый Структура("ИмяКонфигурации", "");
	Если ТаблицаПроекты.НайтиСтроки(Отбор).Количество() > 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Поле ""Имя конфигурации"" должно быть заполнено'");
		Сообщение.Сообщить(); 
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура СохранитьПараметрыЗагрузки()
	
	ПараметрыЗагрузки = ТаблицаПроекты.Выгрузить();
	Хранилище = Новый ХранилищеЗначения(ПараметрыЗагрузки);
	Константы.ПараметрыЗагрузкиОшибокИзФайлов.Установить(Хранилище);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяКонфигурации(Проект)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проект, "ИмяКонфигурации");
	
КонецФункции

#КонецОбласти
