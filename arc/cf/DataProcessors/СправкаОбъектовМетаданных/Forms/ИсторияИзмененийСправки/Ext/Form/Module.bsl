
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВариантПроектСправки     = Параметры.ВариантПроектСправки;
	АвторПоследнегоИзменения = Параметры.АвторПоследнегоИзменения;
	ДатаПоследнегоИзменения  = Параметры.ДатаПоследнегоИзменения;
	
	ДатаПровереннойВерсии = Версионирование.ДатаВремяПровереннойВерсии(Параметры.Ссылка, Перечисления.ВидыОбъектов.СправкаОбъектаМетаданных);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ИсторияИзмененийСправки, "ВерсионируемыйОбъект", Параметры.Ссылка);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ИсторияИзмененийСправки, "Пользователь", Пользователи.ТекущийПользователь());
	
	УправлениеДоступностьюСравненияВерсий();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОбъектов

&НаКлиенте
Процедура ИсторияИзмененийСправкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ИсторияИзмененийСправки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ссылка",               Параметры.Ссылка);
	ПараметрыФормы.Вставить("ВариантПроектСправки", ВариантПроектСправки);
	ПараметрыФормы.Вставить("ДатаВерсииСправки",    ТекущиеДанные.Период);
	
	ОткрытьФорму("ОбщаяФорма.ФормаHTMLДокумента", ПараметрыФормы, Параметры.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СравнитьВыбраннуюВерсиюСПоследней(Команда)
	
	Если Элементы.ИсторияИзмененийСправки.ВыделенныеСтроки.Количество() <> 1 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Необходимо выбрать одну версию'")); 
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ИсторияИзмененийСправки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		СравнитьВерсии('000101010000', ТекущиеДанные.Период);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьВыбраннуюВерсиюСПроверенной(Команда)
	
	Если Элементы.ИсторияИзмененийСправки.ВыделенныеСтроки.Количество() <> 1 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Необходимо выбрать одну версию'")); 
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ИсторияИзмененийСправки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		СравнитьВерсии(Неопределено, ТекущиеДанные.Период);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДвеВыбранныеВерсии(Команда)
	
	Если Элементы.ИсторияИзмененийСправки.ВыделенныеСтроки.Количество() <> 2 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Необходимо выбрать две версии'")); 
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные1 = Элементы.ИсторияИзмененийСправки.ДанныеСтроки(Элементы.ИсторияИзмененийСправки.ВыделенныеСтроки[0]);
	ТекущиеДанные2 = Элементы.ИсторияИзмененийСправки.ДанныеСтроки(Элементы.ИсторияИзмененийСправки.ВыделенныеСтроки[1]);
	Если ТекущиеДанные1.Период < ТекущиеДанные2.Период Тогда
		СравнитьВерсии(ТекущиеДанные2.Период, ТекущиеДанные1.Период);
	Иначе
		СравнитьВерсии(ТекущиеДанные1.Период, ТекущиеДанные2.Период);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СравнитьВерсии(ДатаВерсии1, ДатаВерсии2)
	
	ДанныеДляСравнения = Версионирование.ПодготовитьДанныеДляСравнения(
								Параметры.Ссылка, 
								ДатаВерсии1, 
								ДатаВерсии2, 
								ПредопределенноеЗначение("Перечисление.ВидыОбъектов.СправкаОбъектаМетаданных"));
								
	Если ДанныеДляСравнения = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отсутствует проверенная версия'"),, НСтр("ru = 'Сравнение невозможно'"));
		Возврат;
	КонецЕсли;
								
 	ДанныеДляСравнения.Вставить("ВариантСправки1", ВариантПроектСправки);
	ДанныеДляСравнения.Вставить("ВариантСправки2", ВариантПроектСправки);
	
	ВерсионированиеКлиент.СравнитьОбъекты(ДанныеДляСравнения);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностьюСравненияВерсий()
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		Элементы.ФормаСравнитьДвеВыбранныеВерсии.Видимость = Ложь;
		Элементы.ФормаСравнитьВыбраннуюВерсиюСПоследней.Видимость = Ложь;
		Элементы.ФормаСравнитьВыбраннуюВерсиюСПроверенной.Видимость = Ложь;
		Элементы.ИсторияИзмененийСправкиКонтекстноеМенюСравнитьДвеВыбранныеВерсии.Видимость = Ложь;
		Элементы.ИсторияИзмененийСправкиКонтекстноеМенюСравнитьВыбраннуюВерсиюСПоследней.Видимость = Ложь;
		Элементы.ИсторияИзмененийСправкиКонтекстноеМенюСравнитьВыбраннуюВерсиюСПроверенной.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
