
#Область ПрограммныйИнтерфейс

// Выполняет сравнение версий объекта
//
// Параметры:
//  ДанныеДляСравнения	 - Структура - формируется с помощью Версионирование.ПодготовитьДанныеДляСравнения
//
Процедура СравнитьОбъекты(ДанныеДляСравнения) Экспорт
	
	#Если ТонкийКлиент ИЛИ ВебКлиент Тогда
		Если ДанныеДляСравнения.НастройкиСравнения.СравнениеСредствами1СПредприятия Тогда
			
			ТекстВопроса = НСтр("ru='Сравнение средствами 1С:Предприятия возможно только в толстом клиенте.
									|Выполнить сравнение средствами MS Word?'");
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьСравнениеСредствамиWord", ЭтотОбъект, ДанныеДляСравнения);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Возврат
		КонецЕсли;
	#КонецЕсли
	
	СравнитьОбъектыЗавершение(ДанныеДляСравнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодтвердитьСравнениеСредствамиWord(РезультатВопроса, ДанныеДляСравнения) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляСравнения.НастройкиСравнения.СравнениеСредствами1СПредприятия = Ложь;
	СравнитьОбъектыЗавершение(ДанныеДляСравнения);
	
КонецПроцедуры

Процедура СравнитьОбъектыЗавершение(ДанныеДляСравнения)
	
	СравнениеСредствами1СПредприятия = ДанныеДляСравнения.НастройкиСравнения.СравнениеСредствами1СПредприятия;
	Если ДанныеДляСравнения.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектов.СправкаОбъектаМетаданных") Тогда
		ТекстовыйФормат = Истина;
	Иначе
		ТекстовыйФормат = ДанныеДляСравнения.НастройкиСравнения.ТекстовыйФормат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ЗаголовокДиалога = НСтр("ru='Укажите каталог, в который будут сохранены сравниваемые файлы'");
		
		ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогВыбораКаталога.Заголовок = ЗаголовокДиалога;
		
		Если ДиалогВыбораКаталога.Выбрать() Тогда
			Каталог = ДиалогВыбораКаталога.Каталог;
		Иначе
			Возврат;
		КонецЕсли;
	#Иначе
	    Каталог = КаталогВременныхФайлов();
	#КонецЕсли
	
	Если СравнениеСредствами1СПредприятия Тогда
		ТипФайла        = ?(ТекстовыйФормат, ТипФайлаТабличногоДокумента.TXT, ТипФайлаТабличногоДокумента.MXL);
		РасширениеФайла = ?(ТекстовыйФормат, ".txt", ".mxl");;
	Иначе
		ТипФайла        = ТипФайлаТабличногоДокумента.HTML;
		РасширениеФайла = ".html";
	КонецЕсли; 
	
	ИмяФайла1 = ?(ЗначениеЗаполнено(ДанныеДляСравнения.ДатаВерсии1), "Версия 1 от " + Формат(ДанныеДляСравнения.ДатаВерсии1, "ДФ='dd-MM-yyyy ЧЧ-мм'"), НСтр("ru='Текущая версия 1'"));
	ИмяФайла2 = ?(ЗначениеЗаполнено(ДанныеДляСравнения.ДатаВерсии2), "Версия 2 от " + Формат(ДанныеДляСравнения.ДатаВерсии2, "ДФ='dd-MM-yyyy ЧЧ-мм'"), НСтр("ru='Текущая версия 2'"));
	
	ПолноеИмяФайла1 =  Каталог + ИмяФайла1 + РасширениеФайла;
	ПолноеИмяФайла2 =  Каталог + ИмяФайла2 + РасширениеФайла;
	
	Если ДанныеДляСравнения.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектов.СправкаОбъектаМетаданных") Тогда
		
		ТекстСправки1 = Неопределено;
		ТекстСправки2 = Неопределено;
		Если ДанныеДляСравнения.ВариантыСправки1 <> Неопределено Тогда
			ТекстСправки1 = ДанныеДляСравнения.ВариантыСправки1.Получить(ДанныеДляСравнения.ВариантСправки1);
		КонецЕсли; 
		Если ДанныеДляСравнения.ВариантыСправки2 <> Неопределено Тогда
			ТекстСправки2 = ДанныеДляСравнения.ВариантыСправки2.Получить(ДанныеДляСравнения.ВариантСправки2);
		КонецЕсли; 
		Если ТекстСправки1 = Неопределено ИЛИ ТекстСправки2 = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Отсутствует выбранный вариант справки'"),, НСтр("ru = 'Сравнение невозможно'"));
			Возврат;
		ИначеЕсли ТекстСправки1 = ТекстСправки2 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Нет отличий'"));
			Возврат;
		КонецЕсли;
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		
		ТекстовыйДокумент.УстановитьТекст(ТекстСправки1);
		ТекстовыйДокумент.Записать(ПолноеИмяФайла1);
		
		ТекстовыйДокумент.УстановитьТекст(ТекстСправки2);
		ТекстовыйДокумент.Записать(ПолноеИмяФайла2);
		
	Иначе
		
		ДанныеДляСравнения.Версия1.Записать(ПолноеИмяФайла1, ТипФайла);
		ДанныеДляСравнения.Версия2.Записать(ПолноеИмяФайла2, ТипФайла);
		
	КонецЕсли; 
	
	Если СравнениеСредствами1СПредприятия Тогда
		
		#Если НЕ ТонкийКлиент И НЕ ВебКлиент Тогда
			Сравнение = Новый СравнениеФайлов;
			Сравнение.СпособСравнения = ?(ТекстовыйФормат, СпособСравненияФайлов.ТекстовыйДокумент, СпособСравненияФайлов.ТабличныйДокумент);
			Сравнение.ПервыйФайл = ПолноеИмяФайла1;
			Сравнение.ВторойФайл = ПолноеИмяФайла2;
			Сравнение.ПоказатьРазличия();
		#КонецЕсли
		
	Иначе
		
		Попытка
			Word = Новый COMОбъект("Word.Application");
		Исключение
			Word = Неопределено;
			ПоказатьПредупреждение(, НСтр("ru='Сравнение недоступно, т.к. на компьютере не установлен Word'"));
			Возврат;
		КонецПопытки;
		
		Попытка
			ТекДокумент          = Word.Documents.Open(ПолноеИмяФайла1);
			ТекДокументСравнения = ТекДокумент.Compare(ПолноеИмяФайла2);
			ТекДокумент.Close(0);
			Word.visible = Истина;
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ИнформацияОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			
			ТекДокумент = Неопределено;
			ТекДокументСравнения = Неопределено;
			Word = Неопределено;
			
			ТекстСообщения = НСтр("ru='Не удалось сравнить файлы по причине:'");
			ТекстСообщения = ТекстСообщения + Символы.ПС + ИнформацияОбОшибке;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
