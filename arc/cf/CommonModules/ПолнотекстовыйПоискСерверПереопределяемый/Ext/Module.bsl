///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Позволяет внести изменения в дерево с разделами полнотекстового поиска.
// По умолчанию, дерево разделов формируется на основании командного интерфейса конфигурации.
//
// Параметры:
//   РазделыПоиска - ДеревоЗначений - области поиска. Содержит колонки:
//     ** Раздел - Строка - Представление раздела: подсистемы или объекта метаданных.
//     ** Пометка - Булево, флаг выбора объекта пользователем. Если установить флаг в значение
//                  Истина, то раздел по умолчанию будет отмечен в настройках.
//     ** ЭтоОбъектМетаданных - Булево - Устанавливается в значение Истина, если это дочерний узел дерева,
//                              который не является подсистемой.
//     ** ПутьКДанным - Строка - Предназначена для определения точного адреса строки в дереве.
//     ** ОбъектМД - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Задается только для объектов метаданных,
//                   для подсистем остается пустым.
// Пример:
//     НовыйРазделПоиска = ДеревоРазделов.Строки.Добавить();
//     НовыйРазделПоиска.Раздел = "МояРазделПоиска";
//
//     МетаданныеПодраздела = НовыйРазделПоиска.Строки.Добавить();
//     МетаданныеПодраздела.Раздел = "Валюты";
//     МетаданныеПодраздела.ЭтоОбъектМетаданных = Истина;
//     МетаданныеПодраздела.ПутьКДанным = НоваяОбластьПоиска.Раздел + "," + МетаданныеПодраздела.Раздел;
//     МетаданныеПодраздела.ОбъектМД = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.Валюты);
//
Процедура ПриПолученииРазделовПолнотекстовогоПоиска(РазделыПоиска) Экспорт
	
	РазделГлавное = РазделыПоиска.Строки.Добавить();
	РазделГлавное.Раздел = "Главное";
	РазделГлавное.Картинка = БиблиотекаКартинок.РазделГлавное;
	
	ДобавитьОбъектВРаздел(Метаданные.Справочники.Проекты, РазделГлавное);
	ДобавитьОбъектВРаздел(Метаданные.Справочники.Ошибки, РазделГлавное);
	ДобавитьОбъектВРаздел(Метаданные.Справочники.ТехническиеПроекты, РазделГлавное);
	ДобавитьОбъектВРаздел(Метаданные.Справочники.Идеи, РазделГлавное);

	// Сортировка корневых разделов.
	
	ПорядокРазделов = Новый Массив;
	ПорядокРазделов.Добавить("Главное");
	ПорядокРазделов.Добавить("Моделирование системы");
	ПорядокРазделов.Добавить("Разработка");
	ПорядокРазделов.Добавить("Органайзер");
	ПорядокРазделов.Добавить("Администрирование");
	
	КолонкаПорядка = РазделыПоиска.Колонки.Добавить("Порядок");
	
	Для Индекс = 0 По ПорядокРазделов.ВГраница() Цикл
		ИмяРаздела = ПорядокРазделов[Индекс];
		СтрокаРаздел = РазделыПоиска.Строки.Найти(ИмяРаздела, "Раздел");
		Если СтрокаРаздел <> Неопределено Тогда
			СтрокаРаздел.Порядок = Индекс;
		КонецЕсли;
	КонецЦикла;
	
	РазделыПоиска.Строки.Сортировать("Порядок");
	РазделыПоиска.Колонки.Удалить(КолонкаПорядка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьОбъектВРаздел(ОбъектМетаданных, Раздел)
	
	Если ПравоДоступа("Просмотр", ОбъектМетаданных)
		И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда 
		
		НовыйОбъектРаздела = Раздел.Строки.Добавить();
		НовыйОбъектРаздела.Раздел = ОбъектМетаданных.ПредставлениеСписка;
		НовыйОбъектРаздела.ОбъектМД = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти