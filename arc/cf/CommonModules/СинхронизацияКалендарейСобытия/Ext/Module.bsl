////////////////////////////////////////////////////////////////////////////////
// Методы, обслуживающие подписки на события.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик подписки на событие "СинхронизацияКалендарейПередЗаписью".
//
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.КалендариПользователей - Записи календаря.
//  Отказ    - Булево                                   - Отказ от записи.
//  Замещение- Булево                                   - Признак замещения.
//
Процедура СинхронизацияКалендарейПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСинхронизациюКалендарей") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Источник.Отбор.Пользователь.Использование Тогда
		Пользователь = Источник.Отбор.Пользователь.Значение;
		ДляВсехПользователей = Ложь;
	Иначе
		Пользователь = Неопределено;
		ДляВсехПользователей = Истина;
	КонецЕсли;
	
	УзлыДляВыгрузки = СинхронизацияКалендарей.УзлыДляВыгрузки(Пользователь, ДляВсехПользователей);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
		УзлыДляВыгрузки,
		Источник.ОбменДанными.Отправитель);
	Если Не ЗначениеЗаполнено(УзлыДляВыгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого УзелДляВыгрузки Из УзлыДляВыгрузки Цикл
		Источник.ОбменДанными.Получатели.Добавить(УзелДляВыгрузки);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти