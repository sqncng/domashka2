////////////////////////////////////////////////////////////////////////////////
// Серверные функции, с кэшируемым результатом выполнения подсистемы "Приоритеты работ и очереди задач".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет право изменения приоритета по типу работы и проекту
//
// Параметры:
//  Проект  - СправочникСсылка.Проекты - проект
//  ПустаяСсылкаНаРаботу  - СправочникСсылка - пустая ссылка на работу
//
// Возвращаемое значение:
//   Булево - признак наличия права изменения
//
Функция ПравоИзмененияПриоритета(Проект, ПустаяСсылкаНаРаботу) Экспорт
	
	Если Пользователи.РолиДоступны("ПолныеПрава",, Ложь) Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	ИмяРоли = "";
	
	Если ТипЗнч(ПустаяСсылкаНаРаботу) = Тип("СправочникСсылка.ЗадачиПроцесса") Тогда
		ИмяРоли = "ИзменениеПриоритетаЗадач";
	ИначеЕсли ТипЗнч(ПустаяСсылкаНаРаботу) = Тип("СправочникСсылка.ТехническиеПроекты") Тогда
		ИмяРоли = "ИзменениеПриоритетаТехническихПроектов";
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Если Пользователи.РолиДоступны(ИмяРоли, , Ложь)
		И УправлениеДоступомСППР.РольДоступнаПоПроекту(ИмяРоли, Проект) Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

#КонецОбласти