#Область ПрограммныйИнтерфейс

// Обрабатывает начало выбора объекта метаданных в ошибке
//
// Параметры:
//  ТекущиеДанные - ДанныеФормыСтруктура - Текущие данные таблицы, в которой выполняется выбор.
//  Элемент - ЭлементыФормы - Элемент, в котором производится выбор.
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной обработки выбора.
//  Проект - СправочникСсылка.Проекты - проект-владелец ошибки.
//
Процедура ОбъектМетаданныхНачалоВыбора(ТекущиеДанные, Элемент, СтандартнаяОбработка, Проект) Экспорт

	Если ТекущиеДанные.Свойство("ПроблемныйОбъект") Тогда
		ТипОбъекта = ТипЗнч(ТекущиеДанные.ПроблемныйОбъект);
	Иначе 
		ТипОбъекта = ТипЗнч(ТекущиеДанные.ОбъектМетаданных);
	КонецЕсли; 
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПометкаУдаления", Ложь);
	Отбор.Вставить("Владелец", Проект);
	
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	
	Если ТипОбъекта = Тип("СправочникСсылка.ОбъектыМетаданных") Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ОбъектыМетаданных.ФормаВыбора", ПараметрыФормы, Элемент,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Подсистемы") Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.Подсистемы.ФормаВыбора", ПараметрыФормы, Элемент,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе 
		
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.ОбъектыМетаданных"));
		Массив.Добавить(Тип("СправочникСсылка.Подсистемы"));
		ОписаниеТипов = Новый ОписаниеТипов(Массив);
		Элемент.ОграничениеТипа = ОписаниеТипов;
		
	КонецЕсли; 
	
КонецПроцедуры

// Обрабатывает начало выбора обработчика обновления в ошибке
//
// Параметры:
//  ТекущиеДанные - ДанныеФормыСтруктура - Текущие данные таблицы, в которой выполняется выбор.
//  Элемент - ЭлементыФормы - Элемент, в котором производится выбор.
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной обработки выбора.
//  Проект - СправочникСсылка.Проекты - Проект-владелец ошибки.
//  ВерсияИсправления - СправочникСсылка.ВерсииПроекта - Версия исправления ошибки.
//
Процедура ОбрабочикОбновленияНачалоВыбора(ТекущиеДанные, Элемент, СтандартнаяОбработка, Проект, ВерсияИсправления) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПометкаУдаления", Ложь);
	Отбор.Вставить("Владелец", Проект);
	Отбор.Вставить("Версия",   ВерсияИсправления);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("Отбор",       Отбор);
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ОбработчикиОбновленияИнформационнойБазы.ФормаВыбора", ПараметрыФормы, Элемент,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// Выполняет заполнение списка проблемных объектов для ошибки
//
// Параметры:
//  ИмяТаблицы - Строка - имя таблицы, которую требуется заполнить.
//  ВариантЗаполнения - Строка - вариант заполнения списка:
//   * ПоискОбъектов
//   * ПоискСсылок
//   * ГлобальныйПоиск.
//  ТаблицаФормы - ТаблицаФормы - таблица, которая заполняется.
//  МодульФормы - Модуль - модуль, содержащий обработчик оповещения.
//
Процедура ЗаполнитьСписокОбъектов(ИмяТаблицы, ВариантЗаполнения, ТаблицаФормы, МодульФормы) Экспорт
	
	#Если НЕ ВебКлиент Тогда
		ПараметрыЗаполнения = РаботаСОбъектамиМетаданныхКлиент.ПараметрыЗаполненияТаблицыОбъектов();
		ПараметрыЗаполнения.ПараметрыВыполнения.Вариант = ВариантЗаполнения; 
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыВыполнения", ПараметрыЗаполнения.ПараметрыВыполнения);
		ДополнительныеПараметры.Вставить("ИмяТаблицы",          ИмяТаблицы);
		
		ОбработчикВФорме = Новый ОписаниеОповещения("Подключаемый_ЗаполнитьТаблицуОбъектовМетаданныхЗавершение",
		                                            МодульФормы, ДополнительныеПараметры);
													
		ПараметрыЗаполнения.Действие = ОбработчикВФорме; 
		
		РаботаСОбъектамиМетаданныхКлиент.ЗаполнитьТаблицуОбъектовМетаданных(ТаблицаФормы, ПараметрыЗаполнения);
	#КонецЕсли

КонецПроцедуры

#КонецОбласти