#Область ПрограммныйИнтерфейс

// Возвращает макет с двоичными данными ключевых слов Gherkin
//
// Возвращаемое значение:
//  ДвоичныеДанные
Функция ПолучитьКлючевыеСловаGherkin() Экспорт
	Возврат ПолучитьОбщийМакет("КлючевыеСловаGherkin");
Конецфункции

// Возвращает данные шагов редактора из справочника ШагиСценариевПользователя
//
// Параметры:
//   Язык - Строка
//
// Возвращаемое значение:
//   Массив
//
Функция ДанныеШагов(Язык) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШагиСценариевПользователя.Наименование КАК Наименование,
		|	ШагиСценариевПользователя.СнипетШага КАК СнипетШага,
		|	ШагиСценариевПользователя.Описание КАК Описание,
		|	ШагиСценариевПользователя.Представление КАК Представление
		|ИЗ
		|	Справочник.ШагиСценариевПользователя КАК ШагиСценариевПользователя
		|ГДЕ
		|	НЕ ШагиСценариевПользователя.ПометкаУдаления
		|	И НЕ ШагиСценариевПользователя.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Результат = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДанныеШага = Новый Структура;
		ДанныеШага.Вставить("insertText", ВыборкаДетальныеЗаписи.Наименование);
		Поз = Найти(ВыборкаДетальныеЗаписи.Наименование, " ");
		Если Поз > 0 Тогда
			ДанныеШага.Вставить("filterText", СокрЛ(Сред(ВыборкаДетальныеЗаписи.Наименование, Поз)));
		Иначе	
			ДанныеШага.Вставить("filterText", ВыборкаДетальныеЗаписи.Наименование);
		КонецЕсли;	
		ДанныеШага.Вставить("sortText", ДанныеШага.insertText);
		ДанныеШага.Вставить("documentation", ВыборкаДетальныеЗаписи.Описание);
		ДанныеШага.Вставить("kind", 4);
		ДанныеШага.Вставить("section", "Секция");
		
		Результат.Добавить(ДанныеШага);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

// Возвращает массив имен подсценариев
// Параметры:
//  Массив - Массив сценариев пользователя
//
// Возвращаемое значение:
//  Массив
Функция ИменаПодсценариев(Массив) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СценарииРаботыПользователей.Наименование КАК Наименование
		|ИЗ
		|	Справочник.СценарииРаботыПользователей КАК СценарииРаботыПользователей
		|ГДЕ
		|	СценарииРаботыПользователей.Ссылка В(&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Массив);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Наименование");
	
КонецФункции

#КонецОбласти
