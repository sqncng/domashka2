#Область ПрограммныйИнтерфейс

// Устанавливает компоненту сканирования
//
Процедура УстановитьКомпоненту() Экспорт
	
	КомпонентаПолученияКартинкиИзБуфера = ПараметрыПриложения["СППР.КомпонентаПолученияКартинкиИзБуфера"];

	Если КомпонентаПолученияКартинкиИзБуфера = Неопределено Тогда
		КодВозврата = ПодключитьВнешнююКомпоненту("ОбщийМакет.КомпонентаПолученияКартинкиИзБуфера", "ImageFromClipboard", ТипВнешнейКомпоненты.Native);
		
		Если КодВозврата Тогда
			Состояние(НСтр("ru = 'Компонента сканирования уже установлена.'"));
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"НачатьУстановкуВнешнейКомпонентыПродолжение",
				ЭтотОбъект);
			НачатьУстановкуВнешнейКомпоненты(ОписаниеОповещения, "ОбщийМакет.КомпонентаПолученияКартинкиИзБуфера");		
			Возврат;
		КонецЕсли;
		
		ИмяПараметра = "СППР.КомпонентаПолученияКартинкиИзБуфера";
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый("AddIn.ImageFromClipboard.AddInNativeExtension"));

	Иначе
		Состояние(НСтр("ru = 'Компонента сканирования уже установлена.'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуВнешнейКомпонентыПродолжение(Параметры) Экспорт
	
	КодВозврата = ПодключитьВнешнююКомпоненту("ОбщийМакет.КомпонентаПолученияКартинкиИзБуфера", "ImageFromClipboard", ТипВнешнейКомпоненты.Native);
	
	Если КодВозврата Тогда
		
		Оповестить("КомпонентаСканированияУстановлена");
		
		ИмяПараметра = "СППР.КомпонентаПолученияКартинкиИзБуфера";
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый("AddIn.ImageFromClipboard.AddInNativeExtension"));
		
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует компоненту
//
Функция ПроинициализироватьКомпоненту() Экспорт
	
	КомпонентаПолученияКартинкиИзБуфера = ПараметрыПриложения["СППР.КомпонентаПолученияКартинкиИзБуфера"];
	
	Если КомпонентаПолученияКартинкиИзБуфера = Неопределено Тогда
		КодВозврата = ПодключитьВнешнююКомпоненту("ОбщийМакет.КомпонентаПолученияКартинкиИзБуфера", "ImageFromClipboard", ТипВнешнейКомпоненты.Native);
		
		Если Не КодВозврата Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ИмяПараметра = "СППР.КомпонентаПолученияКартинкиИзБуфера";
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый("AddIn.ImageFromClipboard.AddInNativeExtension"));
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает версию компоненты
//
Функция ВерсияКомпоненты() Экспорт

	Если Не ПроинициализироватьКомпоненту() Тогда
		Возврат НСтр("ru= 'Компонента не установлена'");
	КонецЕсли;
	
	КомпонентаПолученияКартинкиИзБуфера = ПараметрыПриложения["СППР.КомпонентаПолученияКартинкиИзБуфера"];
	
	Возврат КомпонентаПолученияКартинкиИзБуфера.Версия();
	
КонецФункции

#КонецОбласти