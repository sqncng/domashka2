#Область СлужебныйПрограммныйИнтерфейс
// Возвращает имена реквизитов плана обмена, которые должны заполняться для переданного вида фильтра.
// Параметры:
//  ВидФильтра - Строка - один из предопределенных видов фильтра, содержащихся в таблице НастройкаОтборов.
// Возвращаемое значение:
//  Структура
//   * ИмяТЧ - Имя табличной части плана обмена, содержащей значения отбора.
//   * ИмяРеквизитаТЧ - Имя реквизита табличной части плана обмена.
//   * ИмяРеквизитаОтбор - Имя реквизита шапки плана обмена (признак использования отбора).
//   * ИмяСправочника - Имя справочника, в котором хранятся значения отбора.
//   * ЗаголовокФормыВыбора - Заголовок для формы, в которой будет  происходить выбор значений отбора.
//   * РежимУпорядочивания - Используется в запросе, отбирающем возможные значения для выбора.
Функция ИменаРеквизитовДляОтбора(ВидФильтра) Экспорт
	ИменаРеквизитовОтбора = Новый Структура("ИмяТЧ, ИмяРеквизитаТЧ, ИмяРеквизитаОтбор");
	ИменаРеквизитовОтбора.Вставить("ИмяСправочника");
	ИменаРеквизитовОтбора.Вставить("ЗаголовокФормыВыбора");
	ИменаРеквизитовОтбора.Вставить("РежимУпорядочивания","");

	Если ВидФильтра = "По проектам" Тогда
		ИменаРеквизитовОтбора.ИмяТЧ = "Проекты";
		ИменаРеквизитовОтбора.ИмяРеквизитаТЧ = "Проект";
		ИменаРеквизитовОтбора.ИмяРеквизитаОтбор = "ИспользоватьОтборПоПроектам";
		ИменаРеквизитовОтбора.ИмяСправочника = "Проекты";
		ИменаРеквизитовОтбора.ЗаголовокФормыВыбора = НСтр("ru = 'Отправлять данные по проектам'");
	ИначеЕсли ВидФильтра = "По разделам проектов" Тогда
		ИменаРеквизитовОтбора.ИмяТЧ = "РазделыПроектов";
		ИменаРеквизитовОтбора.ИмяРеквизитаТЧ = "РазделПроекта";
		ИменаРеквизитовОтбора.ИмяРеквизитаОтбор = "ИспользоватьОтборПоРазделамПроектов";
		ИменаРеквизитовОтбора.ИмяСправочника = "РазделыПроекта";
		ИменаРеквизитовОтбора.ЗаголовокФормыВыбора = НСтр("ru = 'Отправлять данные по разделам проекта'");
	ИначеЕсли ВидФильтра = "По папкам файлов" Тогда
		ИменаРеквизитовОтбора.ИмяТЧ = "ПапкиФайлов";
		ИменаРеквизитовОтбора.ИмяРеквизитаТЧ = "ПапкаФайлов";
		ИменаРеквизитовОтбора.ИмяРеквизитаОтбор = "ИспользоватьОтборПоПапкамФайлов";
		ИменаРеквизитовОтбора.ИмяСправочника = "ПапкиФайлов";
		ИменаРеквизитовОтбора.ЗаголовокФормыВыбора = НСтр("ru = 'Отправлять данные по папкам файлов'");
		ИменаРеквизитовОтбора.РежимУпорядочивания = "ИЕРАРХИЯ";
	ИначеЕсли ВидФильтра = "По пользователям" Тогда
		ИменаРеквизитовОтбора.ИмяТЧ = "Пользователи";
		ИменаРеквизитовОтбора.ИмяРеквизитаТЧ = "Пользователь";
		ИменаРеквизитовОтбора.ИмяРеквизитаОтбор = "ИспользоватьСкрытиеИменПользователей";
		ИменаРеквизитовОтбора.ИмяСправочника = "Пользователи";
		ИменаРеквизитовОтбора.ЗаголовокФормыВыбора = НСтр("ru = 'Отправлять данные по пользователям'");
	ИначеЕсли ВидФильтра = "По группам доступа" Тогда
		ИменаРеквизитовОтбора.ИмяТЧ = "ГруппыДоступа";
		ИменаРеквизитовОтбора.ИмяРеквизитаТЧ = "ГруппаДоступа";
		ИменаРеквизитовОтбора.ИмяРеквизитаОтбор = "ИспользоватьОтборПоГруппамДоступа";
		ИменаРеквизитовОтбора.ИмяСправочника = "ГруппыДоступа";
		ИменаРеквизитовОтбора.ЗаголовокФормыВыбора = НСтр("ru = 'Отправлять данные по группам доступа'");
	КонецЕсли;
	Возврат ИменаРеквизитовОтбора;
КонецФункции

// Заполняет строковое представление отбора в текущей строке таблицы настроек отбора.
// Параметры:
//  ИсточникЗаполнения - УзелОбменаОбъект - в случае формы узла, либо Управляемая форма в случае настройки узла.
//  ТекущиеДанныеТабличногоПоля - ДанныеФормыЭлементКоллекции - текущие данные табличного поля настройки отборов.
Процедура ЗаполнитьПредставлениеОтбора(ИсточникЗаполнения, ТекущиеДанныеТабличногоПоля) Экспорт
	ИменаРеквизитовОтбора = ИменаРеквизитовДляОтбора(ТекущиеДанныеТабличногоПоля.ВидФильтра);
	ТекущиеДанныеТабличногоПоля.СоставФильтра = ОбменДаннымиСППРКлиентСервер.СтроковоеПредставлениеОтбора(ИсточникЗаполнения, 
										ИменаРеквизитовОтбора.ИмяТЧ, ИменаРеквизитовОтбора.ИмяРеквизитаТЧ, 
										ТекущиеДанныеТабличногоПоля.Использовать);
КонецПроцедуры

#КонецОбласти
