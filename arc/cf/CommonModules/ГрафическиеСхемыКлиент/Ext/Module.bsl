// Процедура перехода (открытия связанного элемента)
// по гиперссылке схемы
Процедура ОткрытьГиперссылкуИзСхемыФункции(ЭлементСхемы, ГиперссылкиСвязей, ПризнакиСсылок) Экспорт
	
	ТипЭлементаСхемы = ТипЗнч(ЭлементСхемы);
	
	Если ТипЭлементаСхемы <> Тип("ЭлементГрафическойСхемыДекорация") Тогда
		Возврат;
	КонецЕсли;
	
	Гиперссылка = Неопределено;
	
	Если ГиперссылкиСвязей.Свойство(ЭлементСхемы.Имя, Гиперссылка) Тогда
		
		ИмяФормы = "";
		Если ТипЗнч(Гиперссылка) = Тип("СправочникСсылка.ФункцииСистемы") Тогда
			
			ЭтоГруппа = ПризнакиСсылок.Получить(Гиперссылка);
			Если ЭтоГруппа = Неопределено Тогда
				ЭтоГруппа = Ложь;
			КонецЕсли;
			
			Если ЭтоГруппа Тогда
				ИмяФормы = "Справочник.ФункцииСистемы.ФормаГруппы";
			Иначе
				ИмяФормы = "Справочник.ФункцииСистемы.ФормаОбъекта";
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Гиперссылка) = Тип("СправочникСсылка.ПрофилиПользователей") Тогда
			ИмяФормы = "Справочник.ПрофилиПользователей.ФормаОбъекта";
		ИначеЕсли ТипЗнч(Гиперссылка) = Тип("Массив") Тогда
			
			Если Гиперссылка.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			
			ТипПервогоЭлемента = ТипЗнч( Гиперссылка[0]);
			
			Если ТипПервогоЭлемента = Тип("СправочникСсылка.ФункцииСистемы") Тогда
				ИмяФормы = "Справочник.ФункцииСистемы.Форма.СвязиФункции";
			ИначеЕсли ТипПервогоЭлемента = Тип("СправочникСсылка.ПрофилиПользователей") Тогда
				ИмяФормы = "Справочник.ПрофилиПользователей.Форма.СвязиФункции";
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;		
		
		Если ТипЗнч(Гиперссылка) = Тип("Массив") Тогда
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("СписокСвязей", Гиперссылка);
			ОткрытьФорму(ИмяФормы, ПараметрыФормы);
		Иначе
			Если ЗначениеЗаполнено(Гиперссылка) Тогда
				ПараметрыФормы = Новый Структура();
				ПараметрыФормы.Вставить("Ключ", Гиперссылка);
				ПараметрыФормы.Вставить("ФормаОткрытаИзСхемы", Истина);
				ОткрытьФорму(ИмяФормы, ПараметрыФормы);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет открытие формы объекта по переданной ссылке
//
// Параметры:
//  Гиперссылка - ссылка на объект
//
Процедура ОткрытьФормуГиперссылки(Гиперссылка) Экспорт
	
	ИмяФормы = "";
	Если ТипЗнч(Гиперссылка) = Тип("СправочникСсылка.ФункцииСистемы") Тогда
		ИмяФормы = "Справочник.ФункцииСистемы.ФормаОбъекта";
	ИначеЕсли ТипЗнч(Гиперссылка) = Тип("СправочникСсылка.ПрофилиПользователей") Тогда
		ИмяФормы = "Справочник.ПрофилиПользователей.ФормаОбъекта";	
	Иначе
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", Гиперссылка);
	ОткрытьФорму(ИмяФормы, ПараметрыФормы);
	
КонецПроцедуры

// Процедура выгрузки графической схемы в файл
Процедура ВыгрузитьСхемуВФайл(Схема) Экспорт
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.Расширение = "grs";
	ДиалогВыбораФайла.Фильтр = "Графическая схема (*.grs)|*.grs";
	
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	Схема.Записать(ДиалогВыбораФайла.ПолноеИмяФайла);
	
КонецПроцедуры

// Процедура загрузки графической схемы из файла
Процедура ЗагрузитьСхемуИзФайла(Схема) Экспорт
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.Расширение = "grs";
	ДиалогВыбораФайла.Фильтр = "Графическая схема (*.grs)|*.grs";
	
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	Схема.Прочитать(ДиалогВыбораФайла.ПолноеИмяФайла);
	
КонецПроцедуры