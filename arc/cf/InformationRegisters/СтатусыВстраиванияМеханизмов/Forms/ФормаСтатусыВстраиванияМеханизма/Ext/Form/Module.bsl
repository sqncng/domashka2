#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Справочники.ОбъектыМетаданных.УстановитьТекстИндексаКартикиВТекстеЗапроса(Элементы.Список);
	
	Если Параметры.Свойство("Механизм") Тогда
		Механизм = Параметры.Механизм;
	КонецЕсли;
	УстановитьПараметрФункцияМеханизма();
	
	УстановитьУсловноеОформление();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФункцияМеханизмаПриИзменении(Элемент)
	
	УстановитьПараметрФункцияМеханизма();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоСтатусамОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УстановитьОтборПоСтатусу(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Встраивается(Команда)
	
	УстановитьСтатусФункции(ПредопределенноеЗначение("Перечисление.СтатусыВстраиванияФункцийМеханизмов.Встраивается"));
	
КонецПроцедуры

&НаКлиенте
Процедура НеВстраивается(Команда)
	
	УстановитьСтатусФункции(ПредопределенноеЗначение("Перечисление.СтатусыВстраиванияФункцийМеханизмов.НеВстраивается"));
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяАнализ(Команда)
	
	УстановитьСтатусФункции(ПредопределенноеЗначение("Перечисление.СтатусыВстраиванияФункцийМеханизмов.ТребуетсяАнализ"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомментарийВстраивания(Команда)
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не выбраны функции для установки комментария.'"));
	Иначе
		ПараметрыОбработчика = Новый Структура("НовыйСтатус", Неопределено);
		Обработчик = Новый ОписаниеОповещения("ОбработкаВводаКомментария", ЭтаФорма, ПараметрыОбработчика);
		ПоказатьВводСтроки(Обработчик,Элементы.Список.ТекущиеДанные.Комментарий,НСтр("ru = 'Укажите комментарий'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокКомментарий" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыОбработчика = Новый Структура("НовыйСтатус", Неопределено);
		Обработчик = Новый ОписаниеОповещения("ОбработкаВводаКомментария",ЭтаФорма, ПараметрыОбработчика);
		ПоказатьВводСтроки(Обработчик,Элемент.ТекущиеДанные.Комментарий,НСтр("ru = 'Укажите комментарий'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьУсловноеОформление()
	
	УсловноеОформлениеСписка = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеСписка.Элементы.Очистить();
	ОбщегоНазначенияСППР.УстановитьУсловноеОформлениеСпискаОтветственный(УсловноеОформлениеСписка);
	
	РегистрыСведений.СтатусыВстраиванияМеханизмов.УстановитьУсловноеОформлениеСтатусовВстраивания(УсловноеОформлениеСписка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрФункцияМеханизма()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Механизм",
		Механизм,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Механизм));
	
	Проект = ПараметрыСеанса.ТекущийПроект;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Владелец",
		Проект,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Проект));
		
	ВидыОбъектов = Новый Массив;
	ВидыОбъектов.Добавить("Документ");
	ВидыОбъектов.Добавить("Справочник");
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"ВидыОбъектовМетаданных", 
		ВидыОбъектов);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"Механизм", 
		Механизм);
		
	СформироватьОтборыПоСтатусам();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтборыПоСтатусам()
	
	ОтборПоСтатусам = РегистрыСведений.СтатусыВстраиванияМеханизмов.СтрокаОтборовПоСтатусам(Список);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСтатусу(ОтборСтатуса)
	
	РегистрыСведений.СтатусыВстраиванияМеханизмов.УстановитьОтборСпискаПоСтатусу(Список, ОтборСтатуса, ОтборПоСтатусам);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусФункции(НовыйСтатус) Экспорт
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не выбраны объекты метаданных для установки статуса.'"));
	ИначеЕсли НовыйСтатус = ПредопределенноеЗначение("Перечисление.СтатусыВстраиванияФункцийМеханизмов.НеобходимоВстроить") Тогда
		ПараметрыОбработчика = Новый Структура("НовыйСтатус", НовыйСтатус);
		Обработчик = Новый ОписаниеОповещения("ОбработкаВводаКомментария",ЭтаФорма, ПараметрыОбработчика);
		ПоказатьВводСтроки(Обработчик,,НСтр("ru = 'Укажите комментарий'"));
	Иначе
		УстановитьСтатусФункцииНаСервере(НовыйСтатус);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВводаКомментария(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		УстановитьСтатусФункцииНаСервере(ДополнительныеПараметры.НовыйСтатус, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусФункцииНаСервере(НовыйСтатус, Комментарий = "")
	
	Если НовыйСтатус = Неопределено Тогда
		РегистрыСведений.СтатусыВстраиванияМеханизмов.УстановитьКомментарийВстраивания(
			Комментарий,
			Механизм,
			Элементы.Список.ВыделенныеСтроки);
	Иначе
		РегистрыСведений.СтатусыВстраиванияМеханизмов.УстановитьСтатусВстраивания(
			НовыйСтатус,
			Механизм,
			Элементы.Список.ВыделенныеСтроки,
			Неопределено,
			Комментарий);
	КонецЕсли;
	
	Элементы.Список.Обновить();
	СформироватьОтборыПоСтатусам();
	
КонецПроцедуры

#КонецОбласти