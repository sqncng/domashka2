#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Механизм = ЭтотОбъект.Отбор.Механизм.Значение;
	ОбъектМетаданных = ЭтотОбъект.Отбор.ОбъектМетаданных.Значение;
	ПустаяФункция = Справочники.ФункцииМеханизмов.ПустаяСсылка();
	
	НовыеСтатусы = ЭтотОбъект.Выгрузить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НовыеСтатусы.Механизм КАК Механизм,
	|	НовыеСтатусы.ОбъектМетаданных КАК ОбъектМетаданных,
	|	НовыеСтатусы.СтатусВстраивания КАК СтатусВстраивания,
	|	НовыеСтатусы.ФункцияМеханизма
	|ПОМЕСТИТЬ НовыеСтатусы
	|ИЗ
	|	&НовыеСтатусы КАК НовыеСтатусы
	|ГДЕ
	|	НовыеСтатусы.ФункцияМеханизма <> ЗНАЧЕНИЕ(Справочник.ФункцииМеханизмов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НовыеСтатусы.Механизм КАК Механизм,
	|	НовыеСтатусы.ОбъектМетаданных КАК ОбъектМетаданных,
	|	НовыеСтатусы.СтатусВстраивания КАК СтатусВстраивания,
	|	НовыеСтатусы.ФункцияМеханизма
	|ИЗ
	|	НовыеСтатусы КАК НовыеСтатусы
	|ГДЕ
	|	НовыеСтатусы.Механизм = &Механизм
	|	И НовыеСтатусы.ОбъектМетаданных = &ОбъектМетаданных
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФункцииМеханизмов.Владелец КАК Механизм,
	|	&ОбъектМетаданных КАК ОбъектМетаданных,
	|	ВЫБОР
	|		КОГДА СтатусыМеханизмов.СтатусВстраивания = ЗНАЧЕНИЕ(Перечисление.СтатусыВстраиванияФункцийМеханизмов.НеВстраивается)
	|			ТОГДА СтатусыМеханизмов.СтатусВстраивания
	|		ИНАЧЕ
	|			ЕСТЬNULL(СтатусыФункций.СтатусВстраивания, 
	|				ЕСТЬNULL(СтатусыМеханизмов.СтатусВстраивания, ЗНАЧЕНИЕ(Перечисление.СтатусыВстраиванияФункцийМеханизмов.ТребуетсяАнализ))
	|			)
	|	КОНЕЦ КАК СтатусВстраивания,
	|	ФункцииМеханизмов.Ссылка КАК ФункцияМеханизма
	|ИЗ
	|	Справочник.ФункцииМеханизмов КАК ФункцииМеханизмов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыВстраиванияМеханизмов КАК СтатусыФункций
	|		ПО ФункцииМеханизмов.Владелец = СтатусыФункций.Механизм
	|			И ФункцииМеханизмов.Ссылка = СтатусыФункций.ФункцияМеханизма
	|			И СтатусыФункций.ОбъектМетаданных = &ОбъектМетаданных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыВстраиванияМеханизмов КАК СтатусыМеханизмов
	|		ПО ФункцииМеханизмов.Владелец = СтатусыМеханизмов.Механизм
	|			И СтатусыМеханизмов.ФункцияМеханизма = ЗНАЧЕНИЕ(Справочник.ФункцииМеханизмов.ПустаяСсылка)
	|			И СтатусыМеханизмов.ОбъектМетаданных = &ОбъектМетаданных
	|ГДЕ
	|	НЕ ФункцииМеханизмов.ПометкаУдаления
	|	И НЕ ФункцииМеханизмов.ЭтоГруппа
	|	И (ФункцииМеханизмов.ВстраиваетсяВДокументы ИЛИ ФункцииМеханизмов.ВстраиваетсяВСправочники)
	|	И ФункцииМеханизмов.Владелец = &Механизм
	|	И НЕ ФункцииМеханизмов.Ссылка В (&ФункцииНабора)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НовыеСтатусы", НовыеСтатусы);
	Запрос.УстановитьПараметр("ФункцииНабора", НовыеСтатусы.ВыгрузитьКолонку("ФункцияМеханизма"));
	Запрос.УстановитьПараметр("Механизм", Механизм);
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	
	// Определим статус механизма
	СтатусыВсехФункций = Запрос.Выполнить().Выгрузить();
	НовыйСтатус = Перечисления.СтатусыВстраиванияФункцийМеханизмов.НеВстраивается;
	Для Каждого Запись Из СтатусыВсехФункций Цикл
		Если Запись.СтатусВстраивания = Перечисления.СтатусыВстраиванияФункцийМеханизмов.Встроена
			ИЛИ Запись.СтатусВстраивания = Перечисления.СтатусыВстраиванияФункцийМеханизмов.НеобходимоВстроить Тогда
			НовыйСтатус = Перечисления.СтатусыВстраиванияФункцийМеханизмов.Встраивается;
		ИначеЕсли Запись.СтатусВстраивания = Перечисления.СтатусыВстраиванияФункцийМеханизмов.ТребуетсяАнализ Тогда
			НовыйСтатус = Перечисления.СтатусыВстраиванияФункцийМеханизмов.ТребуетсяАнализ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Запишем статус механизма
	Набор = РегистрыСведений.СтатусыВстраиванияМеханизмов.СоздатьНаборЗаписей();
	Набор.Отбор.Механизм.Установить(Механизм);
	Набор.Отбор.ОбъектМетаданных.Установить(ОбъектМетаданных);
	Набор.Отбор.ФункцияМеханизма.Установить(ПустаяФункция);
	
	Запись = Набор.Добавить();
	Запись.Механизм = Механизм;
	Запись.ОбъектМетаданных = ОбъектМетаданных;
	Запись.СтатусВстраивания = НовыйСтатус;
	
	Набор.ОбменДанными.Загрузка = Истина;
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли