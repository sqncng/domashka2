#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ДатаНачала") Тогда
		ДатаНачала.Вариант = ВариантСтандартнойДатыНачала.ПроизвольнаяДата;
		ДатаНачала.Дата = Параметры.ДатаНачала;
		УстановитьОтборПоДате(Список, ДатаНачала);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	 УстановитьОтборПоДате(Список, ДатаНачала)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подробнее(Команда)
	ПоказатьЗначение(Неопределено, Элементы.Список.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяСтрока <> Неопределено Тогда
		Если Поле.Имя = "СсылкаНаСборку" Тогда
			ПерейтиПоНавигационнойСсылке(АдресСсылки(ВыбраннаяСтрока, Элемент.ТекущиеДанные.ЗапускТестирования));
			Возврат;
		ИначеЕсли Элемент.ТекущиеДанные.Свойство(Поле.Имя)
			И ЗначениеЗаполнено(Элемент.ТекущиеДанные[Поле.Имя]) 
			И ЗначениеСсылочногоТипа(Элемент.ТекущиеДанные[Поле.Имя]) Тогда  
			ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные[Поле.Имя]);
			Возврат;
		Иначе
			ПоказатьЗначение(Неопределено, Элементы.Список.ТекущаяСтрока);
			Возврат;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция АдресСсылки(ВыбраннаяСтрока, ЗапускТестирования)
	Возврат Документы.ЗапускТестирования.АдресСсылки(ВыбраннаяСтрока.Ошибка.Владелец, ЗапускТестирования.Pipeline_ID);
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеСсылочногоТипа(Значение)
	Возврат ОбщегоНазначения.ЗначениеСсылочногоТипа(Значение);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоДате(Список, ДатаНачала)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"Период",
																			ДатаНачала,
																			ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
																			,
																			ЗначениеЗаполнено(ДатаНачала));
	
КонецПроцедуры

#КонецОбласти