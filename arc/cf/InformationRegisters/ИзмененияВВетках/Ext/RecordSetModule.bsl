#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ПривилегированныйРежим() ИЛИ УправлениеДоступом.ЕстьРоль("ПолныеПрава") Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьРазрешениеИзмененияПоВеткам(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьРазрешениеИзмененияПоВеткам(Отказ)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ОтборПоВетке = ЭтотОбъект.Отбор.Ветка.Использование;
	ОтборПоОбъектуМетаданных = ЭтотОбъект.Отбор.ОбъектМетаданных.Использование;
	ОтборПоПодчиненномуОбъекту = ЭтотОбъект.Отбор.ПодчиненныйОбъект.Использование;
	
	Ветка = ЭтотОбъект.Отбор.Ветка.Значение;
	ОбъектМетаданных = ЭтотОбъект.Отбор.ОбъектМетаданных.Значение;
	ПодчиненныйОбъект = ЭтотОбъект.Отбор.ПодчиненныйОбъект.Значение;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИзмененияВВетках.Ветка КАК Ветка,
	|	Ветки.Владелец КАК Проект,
	|	ЛОЖЬ КАК ЕстьРазрешение
	|ПОМЕСТИТЬ ВТВеткиИПроекты
	|ИЗ
	|	РегистрСведений.ИзмененияВВетках КАК ИзмененияВВетках
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ветки КАК Ветки
	|		ПО ИзмененияВВетках.Ветка = Ветки.Ссылка
	|ГДЕ
	|	(&ОтборПоВетке
	|				И ИзмененияВВетках.Ветка = &Ветка
	|			ИЛИ НЕ &ОтборПоВетке)
	|	И (&ОтборПоОбъектуМетаданных
	|				И ИзмененияВВетках.ОбъектМетаданных = &ОбъектМетаданных
	|			ИЛИ НЕ &ОтборПоОбъектуМетаданных)
	|	И (&ОтборПоПодчиненномуОбъекту
	|				И ИзмененияВВетках.ПодчиненныйОбъект = &ПодчиненныйОбъект
	|			ИЛИ НЕ &ОтборПоПодчиненномуОбъекту)
	|   И ИзмененияВВетках.Согласующий <> &ТекущийПользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТВеткиИПроекты.Проект КАК Проект
	|ИЗ
	|	ВТВеткиИПроекты КАК ВТВеткиИПроекты"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОтборПоВетке", ОтборПоВетке);
	Запрос.УстановитьПараметр("ОтборПоОбъектуМетаданных", ОтборПоОбъектуМетаданных);
	Запрос.УстановитьПараметр("ОтборПоПодчиненномуОбъекту", ОтборПоПодчиненномуОбъекту );
	Запрос.УстановитьПараметр("Ветка", Ветка);
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	Запрос.УстановитьПараметр("ПодчиненныйОбъект", ПодчиненныйОбъект);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СписокПроектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Проект");
	
	РазрешенияПоВеткам = Запрос.МенеджерВременныхТаблиц.Таблицы["ВТВеткиИПроекты"].ПолучитьДанные().Выгрузить();
	
	РазрешенияПоПроектам =  Новый Соответствие;
	
	Для Каждого Проект из СписокПроектов Цикл
		ЕстьРазрешение = УправлениеДоступомСППР.РольДоступнаПоПроекту("ЗаписьИзмененийМетаданныхЗаДругихСогласующих", Проект);
		РазрешенияПоПроектам.Вставить(Проект, ЕстьРазрешение);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы из РазрешенияПоВеткам Цикл
		
		СтрокаТаблицы.ЕстьРазрешение = РазрешенияПоПроектам.Получить(СтрокаТаблицы.Проект);
		
		Если НЕ СтрокаТаблицы.ЕстьРазрешение Тогда
			ТекстСообщения = НСтр("ru='Текущему пользователю запрещено изменять записи, в которых он не является согласующим, по ветке %Ветка%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ветка%", СтрокаТаблицы.Ветка);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли