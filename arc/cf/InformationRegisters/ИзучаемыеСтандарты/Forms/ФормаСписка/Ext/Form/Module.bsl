#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьИдентификаторыЭлементовОтбораСписка();
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ПрименитьБыстрыйОтбор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрименитьБыстрыйОтбор()
	
	Настройки = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	НастройкаПользователя = Настройки.Найти("Пользователь");
	НастройкаСтандарта = Настройки.Найти("Стандарт");
	
	Если Параметры.Пользователь <> Неопределено Тогда
		Если ЗначениеЗаполнено(Параметры.Пользователь) Тогда
			НастройкаПользователя.ПравоеЗначение = Параметры.Пользователь;
		Иначе
			НастройкаПользователя.ПравоеЗначение = Пользователи.ТекущийПользователь();
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Стандарт <> Неопределено Тогда
		НастройкаСтандарта.ПравоеЗначение = Параметры.Стандарт;
	КонецЕсли;
	
	НастройкаПользователя.Использование = ЗначениеЗаполнено(НастройкаПользователя.ПравоеЗначение);
	НастройкаСтандарта.Использование = ЗначениеЗаполнено(НастройкаСтандарта.ПравоеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИдентификаторыЭлементовОтбораСписка()
	
	ПолеПользователь = Новый ПолеКомпоновкиДанных("Пользователь");
	ПолеСтандарт = Новый ПолеКомпоновкиДанных("Стандарт");
	
	Для каждого Элемент Из Список.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если Элемент.ЛевоеЗначение = ПолеПользователь Тогда
				Элемент.ИдентификаторПользовательскойНастройки = "Пользователь";
			ИначеЕсли Элемент.ЛевоеЗначение = ПолеСтандарт Тогда
				Элемент.ИдентификаторПользовательскойНастройки = "Стандарт";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
