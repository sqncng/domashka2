#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список текущих дел пользователя.
// В раздел "Органайзер/Стандарты разработки" добавляются ссылки на новые и требующие изучения стандарты.
//
// Параметры:
//  ТекущиеДела - ТаблицаЗначений - таблица значений с колонками:
//    * Идентификатор - Строка - внутренний идентификатор дела, используемый механизмом "Текущие дела".
//    * ЕстьДела      - Булево - если Истина, дело выводится в списке текущих дел пользователя.
//    * Важное        - Булево - если Истина, дело будет выделено красным цветом.
//    * Представление - Строка - представление дела, выводимое пользователю.
//    * Количество    - Число  - количественный показатель дела, выводится в строке заголовка дела.
//    * Форма         - Строка - полный путь к форме, которую необходимо открыть при нажатии на гиперссылку
//                               дела на панели "Текущие дела".
//    * ПараметрыФормы- Структура - параметры, с которыми нужно открывать форму показателя.
//    * Владелец      - Строка, объект метаданных - строковый идентификатор дела, которое будет владельцем для текущего
//                      или объект метаданных подсистема.
//    * Подсказка     - Строка - текст подсказки.
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	Пользователь = Пользователи.ТекущийПользователь();
	
	ПараметрыОтбораНовых = Новый Структура;
	ПараметрыОтбораНовых.Вставить("ПоказыватьИзученные", Ложь);
	ПараметрыОтбораНовых.Вставить("ПоказыватьНовые", Истина);
	ПараметрыОтбораНовых.Вставить("ПоказыватьТребующиеИзучения", Ложь);
	ПараметрыОтбораНовых.Вставить("Пользователь", Пользователь);
	
	ПараметрыОтбораТребующихИзучения = Новый Структура;
	ПараметрыОтбораТребующихИзучения.Вставить("ПоказыватьИзученные", Ложь);
	ПараметрыОтбораТребующихИзучения.Вставить("ПоказыватьНовые", Ложь);
	ПараметрыОтбораТребующихИзучения.Вставить("ПоказыватьТребующиеИзучения", Истина);
	ПараметрыОтбораТребующихИзучения.Вставить("Пользователь", Пользователь);
	
	Разделитель =
		"
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|";
	
	Запросы = Новый Массив;
	Запросы.Добавить(СтандартыРазработки.ТекстЗапросаСтатистикиНовых());
	Запросы.Добавить(СтандартыРазработки.ТекстЗапросаСтатистикиСостояния());
	
	Запрос = Новый Запрос(СтрСоединить(Запросы, Разделитель));
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияСтандартов.ТребуетИзучения);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Новых = СтандартыРазработки.КоличествоИзРезультатаЗапроса(РезультатЗапроса[0]);
	ТребующихИзучения = СтандартыРазработки.КоличествоИзРезультатаЗапроса(РезультатЗапроса[1]);
	ВсегоСтандартов = Новых + ТребующихИзучения;
	
	РазделСтандарты = "СтандартыРазработки";
	Разделы = ТекущиеДелаСервер.РазделыДляОбъекта(Метаданные.РегистрыСведений.ИзучаемыеСтандарты.ПолноеИмя());
	
	Для Каждого Раздел Из Разделы Цикл
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = РазделСтандарты;
		Дело.ЕстьДела       = ВсегоСтандартов > 0;
		Дело.Представление  = НСтр("ru = 'Стандарты разработки'");
		Дело.Количество     = ВсегоСтандартов;
		Дело.Важное         = Ложь;
		Дело.Владелец       = Раздел;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = "НовыеСтандарты";
		Дело.ЕстьДела       = Новых > 0;
		Дело.Представление  = НСтр("ru = 'Новые'");
		Дело.Количество     = Новых;
		Дело.Важное         = Ложь;
		Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбораНовых);
		Дело.Форма          = "Справочник.СтандартыРазработки.Форма.ФормаСписка";
		Дело.Владелец       = РазделСтандарты;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = "ТребующиеИзученияСтандарты";
		Дело.ЕстьДела       = ТребующихИзучения > 0;
		Дело.Представление  = НСтр("ru = 'Требующие изучения'");
		Дело.Количество     = ТребующихИзучения;
		Дело.Важное         = Истина;
		Дело.ПараметрыФормы = Новый Структура("СтруктураБыстрогоОтбора", ПараметрыОтбораТребующихИзучения);
		Дело.Форма          = "Справочник.СтандартыРазработки.Форма.ФормаСписка";
		Дело.Владелец       = РазделСтандарты;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
