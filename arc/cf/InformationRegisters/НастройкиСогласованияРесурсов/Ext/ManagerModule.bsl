#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура РассчитатьНастройкиСогласования(ВидСогласуемогоРесурса = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыПользователей") Тогда
		
		РассчитатьНастройкиСогласованияСУчетомГруппПользователей(ВидСогласуемогоРесурса);
		
	Иначе
		
		РассчитатьНастройкиСогласованияБезГруппПользователей(ВидСогласуемогоРесурса);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьНастройкиСогласованияБезГруппПользователей(ВидСогласуемогоРесурса)

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка       КАК ВидСогласуемогоРесурса,
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь КАК Пользователь,
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Согласующий  КАК Согласующий
	|ИЗ
	|	Справочник.ВидыСогласуемыхРесурсов.НастройкиСогласования КАК ВидыСогласуемыхРесурсовНастройкиСогласования
	|ГДЕ
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь ССЫЛКА Справочник.Пользователи
	|	И НЕ ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка.ПометкаУдаления
	|	И ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка = &ВидСогласуемогоРесурса
	|";
	
	Если ЗначениеЗаполнено(ВидСогласуемогоРесурса) Тогда
		Запрос.УстановитьПараметр("ВидСогласуемогоРесурса", ВидСогласуемогоРесурса);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка = &ВидСогласуемогоРесурса", "");
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиСогласованияРесурсов.СоздатьНаборЗаписей();
	
	Если ЗначениеЗаполнено(ВидСогласуемогоРесурса) Тогда
		НаборЗаписей.Отбор.ВидСогласуемогоРесурса.Установить(ВидСогласуемогоРесурса);
	КонецЕсли;
	
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура РассчитатьНастройкиСогласованияСУчетомГруппПользователей(ВидСогласуемогоРесурса)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ГруппыПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ГруппыПользователей КАК ГруппыПользователей
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппыПользователей.Предопределенный УБЫВ,
	|	Ссылка ИЕРАРХИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка       КАК ВидСогласуемогоРесурса,
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь КАК Пользователь,
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Согласующий  КАК Согласующий
	|ПОМЕСТИТЬ СогласующийНазначенДляПользователя
	|ИЗ
	|	Справочник.ВидыСогласуемыхРесурсов.НастройкиСогласования КАК ВидыСогласуемыхРесурсовНастройкиСогласования
	|ГДЕ
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь ССЫЛКА Справочник.Пользователи
	|	И НЕ ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка.ПометкаУдаления
	|	И ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка = &ВидСогласуемогоРесурса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка КАК ВидСогласуемогоРесурса,
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь КАК ГруппаПользователей,
	|	ВидыСогласуемыхРесурсовНастройкиСогласования.Согласующий КАК Согласующий,
	|	СоставыГруппПользователей.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ СогласующиеПоСоставамГруппПользователей
	|ИЗ
	|	Справочник.ВидыСогласуемыхРесурсов.НастройкиСогласования КАК ВидыСогласуемыхРесурсовНастройкиСогласования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
	|		ПО ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь = СоставыГруппПользователей.ГруппаПользователей
	|ГДЕ
	|	НЕ ВидыСогласуемыхРесурсовНастройкиСогласования.Пользователь ССЫЛКА Справочник.Пользователи
	|	И НЕ ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка.ПометкаУдаления
	|	И ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка = &ВидСогласуемогоРесурса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СогласующиеПоСоставамГруппПользователей.ВидСогласуемогоРесурса КАК ВидСогласуемогоРесурса,
	|	СогласующиеПоСоставамГруппПользователей.ГруппаПользователей    КАК ГруппаПользователей,
	|	СогласующиеПоСоставамГруппПользователей.Согласующий            КАК Согласующий,
	|	СогласующиеПоСоставамГруппПользователей.Пользователь           КАК Пользователь
	|ИЗ
	|	СогласующиеПоСоставамГруппПользователей КАК СогласующиеПоСоставамГруппПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ СогласующийНазначенДляПользователя КАК СогласующийНазначенДляПользователя
	|		ПО СогласующиеПоСоставамГруппПользователей.ВидСогласуемогоРесурса = СогласующийНазначенДляПользователя.ВидСогласуемогоРесурса
	|			И СогласующиеПоСоставамГруппПользователей.Пользователь = СогласующийНазначенДляПользователя.Пользователь
	|ГДЕ
	|	СогласующийНазначенДляПользователя.Согласующий ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СогласующийНазначенДляПользователя.ВидСогласуемогоРесурса КАК ВидСогласуемогоРесурса,
	|	СогласующийНазначенДляПользователя.Пользователь           КАК Пользователь,
	|	СогласующийНазначенДляПользователя.Согласующий            КАК Согласующий
	|ИЗ
	|	СогласующийНазначенДляПользователя КАК СогласующийНазначенДляПользователя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СогласующийНазначенДляПользователя.ВидСогласуемогоРесурса КАК ВидСогласуемогоРесурса
	|ИЗ
	|	СогласующийНазначенДляПользователя КАК СогласующийНазначенДляПользователя
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СогласующиеПоСоставамГруппПользователей.ВидСогласуемогоРесурса
	|ИЗ
	|	СогласующиеПоСоставамГруппПользователей КАК СогласующиеПоСоставамГруппПользователей";
	
	
	Если ЗначениеЗаполнено(ВидСогласуемогоРесурса) Тогда
		Запрос.УстановитьПараметр("ВидСогласуемогоРесурса", ВидСогласуемогоРесурса);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВидыСогласуемыхРесурсовНастройкиСогласования.Ссылка = &ВидСогласуемогоРесурса", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаГруппы = РезультатЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ТаблицаСогласующиеПоСоставамГруппПользователей = РезультатЗапроса[3].Выгрузить();
	ТаблицаСогласующиеПоСоставамГруппПользователей.Индексы.Добавить("ГруппаПользователей");
	ТаблицаСогласующиеПоСоставамГруппПользователей.Индексы.Добавить("ВидСогласуемогоРесурса");
	ТаблицаСогласующиеПоСоставамГруппПользователей.Индексы.Добавить("Пользователь");
	
	ТаблицаСогласующиеНазначенныеДляПользователя = РезультатЗапроса[4].Выгрузить();
	
	ТаблицаСогласующиеПоПользователям = Новый ТаблицаЗначений;
	ТаблицаСогласующиеПоПользователям.Колонки.Добавить("ВидСогласуемогоРесурса", Новый ОписаниеТипов("СправочникСсылка.ВидыСогласуемыхРесурсов"));
	ТаблицаСогласующиеПоПользователям.Колонки.Добавить("ГруппаПользователей",    Новый ОписаниеТипов("СправочникСсылка.ГруппыПользователей"));
	ТаблицаСогласующиеПоПользователям.Колонки.Добавить("Пользователь",           Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ТаблицаСогласующиеПоПользователям.Колонки.Добавить("Согласующий",            Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	
	ТаблицаСогласующиеПоПользователям.Индексы.Добавить("ВидСогласуемогоРесурса");
	ТаблицаСогласующиеПоПользователям.Индексы.Добавить("ГруппаПользователей");
	ТаблицаСогласующиеПоПользователям.Индексы.Добавить("Пользователь");
	
	МассивОбрабатываемыхГруппПользователей = Новый Массив;
	МассивОбрабатываемыхГруппПользователей.Добавить(Справочники.ГруппыПользователей.ВсеПользователи);
	
	Пока ВыборкаГруппы.Следующий() Цикл
		
		ПрименитьНастройкиСогласованияДляГруппыПользователей(ВыборкаГруппы, ТаблицаСогласующиеПоПользователям, ТаблицаСогласующиеПоСоставамГруппПользователей, МассивОбрабатываемыхГруппПользователей);
		
	КонецЦикла;
	
	ТаблицаСогласующиеПоПользователям.Свернуть("ВидСогласуемогоРесурса, Пользователь, Согласующий");
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаСогласующиеНазначенныеДляПользователя, ТаблицаСогласующиеПоПользователям); 
	
	НаборЗаписей = РегистрыСведений.НастройкиСогласованияРесурсов.СоздатьНаборЗаписей();
	
	Если ЗначениеЗаполнено(ВидСогласуемогоРесурса) Тогда
		НаборЗаписей.Отбор.ВидСогласуемогоРесурса.Установить(ВидСогласуемогоРесурса);
	КонецЕсли;
	
	НаборЗаписей.Загрузить(ТаблицаСогласующиеПоПользователям);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ПрименитьНастройкиСогласованияДляГруппыПользователей(ВыборкаГруппы, ТаблицаСогласующиеПоПользователям, ТаблицаСогласующиеПоСоставамГруппПользователей, МассивОбрабатываемыхГруппПользователей)
	
	ТекущийМассивОбработанныхГрупп = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивОбрабатываемыхГруппПользователей);
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ГруппаПользователей", ВыборкаГруппы.Ссылка);
	
	НайденныеСтроки = ТаблицаСогласующиеПоСоставамГруппПользователей.НайтиСтроки(ПараметрыПоиска);
	
	Если ВыборкаГруппы.Ссылка = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		
			НоваяСтрока = ТаблицаСогласующиеПоПользователям.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
		
		КонецЦикла;
		
	Иначе
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			СтрокаОбработана = Ложь;
			
			Для Каждого ЭлементМассива Из МассивОбрабатываемыхГруппПользователей Цикл
			
				ПараметрыПоискаТаблицаСогласующихПоПользователям = Новый Структура;
				ПараметрыПоискаТаблицаСогласующихПоПользователям.Вставить("ГруппаПользователей",    ЭлементМассива);
				ПараметрыПоискаТаблицаСогласующихПоПользователям.Вставить("ВидСогласуемогоРесурса", НайденнаяСтрока.ВидСогласуемогоРесурса);
				ПараметрыПоискаТаблицаСогласующихПоПользователям.Вставить("Пользователь",           НайденнаяСтрока.Пользователь);
				
				НайденныеСтрокиТаблицаСогласующихПоПользователям = ТаблицаСогласующиеПоПользователям.НайтиСтроки(ПараметрыПоискаТаблицаСогласующихПоПользователям);
				
				Если НайденныеСтрокиТаблицаСогласующихПоПользователям.Количество() > 0 Тогда
					
					СтрокаНастроекСогласования =  НайденныеСтрокиТаблицаСогласующихПоПользователям[0];
					СтрокаОбработана = Истина;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не СтрокаОбработана Тогда
				
				СтрокаНастроекСогласования =  ТаблицаСогласующиеПоПользователям.Добавить();
				
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтрокаНастроекСогласования, ПараметрыПоискаТаблицаСогласующихПоПользователям);
			СтрокаНастроекСогласования.Согласующий         = НайденнаяСтрока.Согласующий;
			СтрокаНастроекСогласования.ГруппаПользователей = ВыборкаГруппы.Ссылка;
			
		КонецЦикла;
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущийМассивОбработанныхГрупп.Добавить(ВыборкаГруппы.Ссылка);
		КонецЕсли;
		
		ВыборкаПодчиненныеГруппы = ВыборкаГруппы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Пока ВыборкаПодчиненныеГруппы.Следующий() Цикл
			
			ПрименитьНастройкиСогласованияДляГруппыПользователей(ВыборкаПодчиненныеГруппы, ТаблицаСогласующиеПоПользователям, ТаблицаСогласующиеПоСоставамГруппПользователей, ТекущийМассивОбработанныхГрупп);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли