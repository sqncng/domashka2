#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Удаление строк, для которых право не требуется, не используется, но без комментариев.
	// Для неудаленных строк установим автоматический флажок ТребуетсяНеРавноИспользуется.
	НомерЗаписи = Количество() - 1;
	Пока НомерЗаписи >= 0 Цикл
		
		Если НЕ ЭтотОбъект[НомерЗаписи].Требуется И
		     НЕ ЭтотОбъект[НомерЗаписи].Используется И
		     ПустаяСтрока(ЭтотОбъект[НомерЗаписи].Комментарии) Тогда
			ЭтотОбъект.Удалить(НомерЗаписи);
		Иначе
			ЭтотОбъект[НомерЗаписи].ТребуетсяНеРавноИспользуется = ЭтотОбъект[НомерЗаписи].Требуется <> ЭтотОбъект[НомерЗаписи].Используется;
			
			Если НЕ ЭтотОбъект[НомерЗаписи].Используется Тогда
				ЭтотОбъект[НомерЗаписи].ПоляБезОграниченияДоступа = "";
				ЭтотОбъект[НомерЗаписи].ПоляСОграничениемДоступа  = "";
				ЭтотОбъект[НомерЗаписи].КоличествоПолейБезОграниченияДоступа = 0;
				ЭтотОбъект[НомерЗаписи].КоличествоПолейСОграничениемДоступа  = 0;
			КонецЕсли;
		КонецЕсли;
		
		НомерЗаписи = НомерЗаписи - 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли