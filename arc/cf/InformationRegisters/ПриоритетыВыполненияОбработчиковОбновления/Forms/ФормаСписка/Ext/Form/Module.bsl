#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	
	Обработчик = Параметры.Обработчик;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																		"Обработчик1",
																		Обработчик,
																		ВидСравненияКомпоновкиДанных.Равно,
																		,
																		ЗначениеЗаполнено(Обработчик));

	Элементы.ОбработчикСсылка.Видимость = ЗначениеЗаполнено(Обработчик);
	Элементы.Обработчик1.Видимость = Не ЗначениеЗаполнено(Обработчик);
	Элементы.Обработчик1Ответственный.Видимость = Элементы.Обработчик1.Видимость;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПорядокВсеРавноВКакойПоследовательности(Команда)
	
	УстановитьПорядокВВыделенныхСтроках("ВсеРавноВКакойПоследовательности");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядокНебходимАнализ(Команда)
	УстановитьПорядокВВыделенныхСтроках("НеобходимАнализ");
КонецПроцедуры


&НаКлиенте
Процедура УстановитьПорядокНужноПеределатьОбработчики(Команда)
	
	УстановитьПорядокВВыделенныхСтроках("НужноПеределатьОбработчики");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядокСначалаОбработчик1(Команда)
	
	УстановитьПорядокВВыделенныхСтроках("СначалаОбработчик1");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядокСначалаОбработчик2(Команда)
	
	УстановитьПорядокВВыделенныхСтроках("СначалаОбработчик2");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядокПоУмолчанию(Команда)
	УстановитьПорядокПоУмолчаниюСервер();
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ПриоритетыВыполненияОбработчиковОбновления"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПорядокВВыделенныхСтроках(ИмяЗначения)

	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПорядокВВыделенныхСтрокахНаСервере(ИмяЗначения);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ПриоритетыВыполненияОбработчиковОбновления"));

КонецПроцедуры

&НаСервере
Процедура УстановитьПорядокВВыделенныхСтрокахНаСервере(ИмяЗначения)

	Для каждого ЭлементКоллекции Из Элементы.Список.ВыделенныеСтроки Цикл
		Набор = РегистрыСведений.ПриоритетыВыполненияОбработчиковОбновления.СоздатьНаборЗаписей();
		Набор.Отбор.Обработчик1.Установить(ЭлементКоллекции.Обработчик1);
		Набор.Отбор.Обработчик2.Установить(ЭлементКоллекции.Обработчик2);
		Набор.Прочитать();
		
		Для Каждого СтрТабл Из Набор Цикл
			СтрТабл.ПорядокПомещенияВОчередь = Перечисления.ПорядокПомещенияОбработчиковВОчередь[ИмяЗначения];
		КонецЦикла;
		Если Набор.ПроверитьЗаполнение() Тогда
			Набор.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПорядокПоУмолчаниюСервер()
	Для каждого ЭлементКоллекции Из Элементы.Список.ВыделенныеСтроки Цикл
		Набор = РегистрыСведений.ПриоритетыВыполненияОбработчиковОбновления.СоздатьНаборЗаписей();
		Набор.Отбор.Обработчик1.Установить(ЭлементКоллекции.Обработчик1);
		Набор.Отбор.Обработчик2.Установить(ЭлементКоллекции.Обработчик2);
		Набор.Прочитать();
		
		ЕстьИзменения = Ложь;
		
		Для Каждого СтрТабл Из Набор Цикл
			ЕстьИзменения = РегистрыСведений.ПриоритетыВыполненияОбработчиковОбновления.РасчитатьПорядокВыполненияОбработчиковПоДефолту(СтрТабл);
		КонецЦикла;
		
		Если ЕстьИзменения Тогда
			Если Набор.ПроверитьЗаполнение() Тогда
				Набор.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти
