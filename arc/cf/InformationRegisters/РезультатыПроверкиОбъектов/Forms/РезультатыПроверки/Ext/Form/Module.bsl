#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОбъектПроверкиВЗаголовке") Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Результаты проверки (%1)'"),
							Строка(Параметры.ОбъектПроверкиВЗаголовке));
							
		АвтоЗаголовок = Ложь;
	КонецЕсли; 
	
	Если Параметры.Отбор.Свойство("ПравилоПроверкиОбъектов") Тогда
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПравилоПроверки", Параметры.Отбор.ПравилоПроверкиОбъектов);
		Элементы.ПравилоПроверки.Видимость = Ложь;
	
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ОбъектПроверки") Тогда
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ОбъектПроверки", Параметры.Отбор.ОбъектПроверки);
		Элементы.ОбъектПроверки.Видимость = Ложь;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяСтрока<>Неопределено Тогда
		Если Поле.Имя = "ОбъектПроверки" ИЛИ Поле.Имя = "ПравилоПроверки" Тогда
			ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные[Поле.Имя]);
		ИначеЕсли Поле.Имя = "Ошибка" Тогда
			ОткрытьФорму("Справочник.Ошибки.ФормаОбъекта",Новый Структура("Ключ", Элемент.ТекущиеДанные.Ошибка));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписок

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПроверкуВсехПравил(Команда)
	
	ТекстЗаголовка = НСтр("ru='Выполнение правил проверки'");
	ТекстСостояния = НСтр("ru='Выполняется применение правил проверки'");
	Состояние(ТекстЗаголовка,,ТекстСостояния);
	
	ВыполнитьПроверкуВсехПравилСервер();
	
	Оповестить("Запись_РезультатыПроверкиОбъектов");
	
	ТекстЗаголовка = НСтр("ru='Выполнение правил проверки'");
	ТекстПояснения = НСтр("ru='Выполнено применение всех правил проверки'");
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка,,ТекстПояснения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкуИсключения(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтметкиИсключенияСервер(ВыделенныеСтроки, Истина);
	
	Оповестить("Запись_РезультатыПроверкиОбъектов");
	
	ТекстЗаголовка = НСтр("ru='Установка отметок'");
	ТекстПояснения = НСтр("ru='Для выделенных строк установлены отметки исключения'");
	ПоказатьОповещениеПользователя(ТекстЗаголовка,,ТекстПояснения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуИсключения(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтметкиИсключенияСервер(ВыделенныеСтроки, Ложь);
	
	Оповестить("Запись_РезультатыПроверкиОбъектов");
	
	ТекстЗаголовка = НСтр("ru='Снятие отметок'");
	ТекстПояснения = НСтр("ru='Для выделенных строк сняты отметки исключения'");
	ПоказатьОповещениеПользователя(ТекстЗаголовка,,ТекстПояснения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПрименениеПравилПроверки" ИЛИ ИмяСобытия = "ИзмененТекущийПроект" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьОшибки(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;

	КолОшибок = ЗарегистрироватьОшибкиСервер(ВыделенныеСтроки);
	
	Если КолОшибок > 0 Тогда	
		ТекстЗаголовка = НСтр("ru='Регистрация ошибок'");
		ТекстПояснения = НСтр("ru='по выделенным строкам'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстПояснения, БиблиотекаКартинок.Информация32);	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьПроверкуВсехПравилСервер()
	
	ПроверкаОбъектов.ВыполнитьПроверкуОбъектов();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкиИсключенияСервер(ВыделенныеСтроки, ЗначениеОтметки)
	
	ПроверкаОбъектов.УстановитьСнятьОтметкуИсключения(ВыделенныеСтроки, ЗначениеОтметки);
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Функция ЗарегистрироватьОшибкиСервер(ВыделенныеЗаписи)
	
	ОшибкиПоМетаданным = Новый Массив;
	
	Для каждого КлючЗаписи из ВыделенныеЗаписи Цикл
		Если ТипЗнч(КлючЗаписи.ОбъектПроверки) = Тип("СправочникСсылка.ОбъектыМетаданных") Тогда
			ОшибкиПоМетаданным.Добавить(КлючЗаписи);
		КонецЕсли;
	КонецЦикла;
	
	Если ОшибкиПоМетаданным.Количество() = 0 Тогда
		Возврат 0;
	КонецЕсли; 
	
	РегистрыСведений.РезультатыПроверкиОбъектов.ЗарегистрироватьОшибки(ОшибкиПоМетаданным);
	Элементы.Список.Обновить();
	
	Возврат ОшибкиПоМетаданным.Количество();
	
КонецФункции

#КонецОбласти