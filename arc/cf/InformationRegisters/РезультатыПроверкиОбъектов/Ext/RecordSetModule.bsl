#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Измерение Номер заполняется для обеспечения уникальности записей
	
	Если НЕ ЭтотОбъект.Отбор.Номер.Использование Тогда
		ПрисвоитьНомераЗаписямНабора();
	КонецЕсли;
	
	// Флаг исключения заполняется на основании ранее установленного значения
	
	УстановитьФлагиИсключенияЗаписямНабора();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрисвоитьНомераЗаписямНабора()
	
	Номер = 0;
	Для каждого Запись Из ЭтотОбъект Цикл
		Номер = Номер + 1;
		Запись.Номер = Номер;
	КонецЦикла;
		
КонецПроцедуры

Процедура УстановитьФлагиИсключенияЗаписямНабора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РезультатыПроверкиОбъектов.ПравилоПроверкиОбъектов,
	|	РезультатыПроверкиОбъектов.ОбъектПроверки,
	|	РезультатыПроверкиОбъектов.ЭлементОбъекта,
	|	РезультатыПроверкиОбъектов.Информация
	|ИЗ
	|	РегистрСведений.РезультатыПроверкиОбъектов КАК РезультатыПроверкиОбъектов
	|ГДЕ
	|	РезультатыПроверкиОбъектов.Исключение";
	
	Если ЭтотОбъект.Отбор.ПравилоПроверкиОбъектов.Использование Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И РезультатыПроверкиОбъектов.ПравилоПроверкиОбъектов = &ПравилоПроверкиОбъектов";
		
		Запрос.УстановитьПараметр("ПравилоПроверкиОбъектов", ЭтотОбъект.Отбор.ПравилоПроверкиОбъектов.Значение);
	КонецЕсли;
	
	Если ЭтотОбъект.Отбор.ОбъектПроверки.Использование Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И РезультатыПроверкиОбъектов.ОбъектПроверки = &ОбъектПроверки";
		
		Запрос.УстановитьПараметр("ОбъектПроверки", ЭтотОбъект.Отбор.ОбъектПроверки.Значение);
	КонецЕсли;
	
	Если ЭтотОбъект.Отбор.ЭлементОбъекта.Использование Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И РезультатыПроверкиОбъектов.ЭлементОбъекта = &ЭлементОбъекта";
		
		Запрос.УстановитьПараметр("ЭлементОбъекта", ЭтотОбъект.Отбор.ОбъектПроверки.Значение);
	КонецЕсли;
	
	ТаблицаИсключений = Запрос.Выполнить().Выгрузить();
	
	ТаблицаИсключений.Свернуть("ПравилоПроверкиОбъектов, ОбъектПроверки, ЭлементОбъекта, Информация");
	ТаблицаИсключений.Индексы.Добавить("ПравилоПроверкиОбъектов, ОбъектПроверки, ЭлементОбъекта, Информация");
	
	ОтборИсключений = Новый Структура("ПравилоПроверкиОбъектов, ОбъектПроверки, ЭлементОбъекта, Информация");
	
	Для каждого Запись Из ЭтотОбъект Цикл
		ЗаполнитьЗначенияСвойств(ОтборИсключений, Запись);
		
		Если ТаблицаИсключений.НайтиСтроки(ОтборИсключений).Количество() > 0 Тогда
			Запись.Исключение = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли