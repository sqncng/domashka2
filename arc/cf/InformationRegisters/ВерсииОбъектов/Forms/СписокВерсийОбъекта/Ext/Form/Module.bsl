
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ТекущийПользователь);
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("ВерсионируемыйОбъект") Тогда
		
		ВидОбъекта = Версионирование.ВидВерсионируемогоОбъекта(Параметры.Отбор.ВерсионируемыйОбъект);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,	"ВидОбъекта", ВидОбъекта);
		
		ЗаписьИзменений = РегистрыСведений.ДатыИзмененияОбъектов.СоздатьМенеджерЗаписи();
		ЗаписьИзменений.КонтролируемыйОбъект = Параметры.Отбор.ВерсионируемыйОбъект;
		ЗаписьИзменений.ВидОбъекта = ВидОбъекта;
		
		ЗаписьИзменений.Прочитать();
		
		Если ЗаписьИзменений.Выбран() Тогда
			ДатаПоследнегоИзменения = ЗаписьИзменений.ДатаИзменения;
			ПользовательПоследнегоИзменения = ЗаписьИзменений.Пользователь;
			КомментарииПоследнегоИзменения = ЗаписьИзменений.Комментарии;
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ОбщегоНазначенияСППРКлиент.ОткрытьОписаниеСхемуВерсииОбъекта(ТекущиеДанные.ВерсионируемыйОбъект, ТекущиеДанные.Период);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	Элементы.ФормаСравнитьВыбраннуюВерсиюСПоследней.Доступность		= ВыделенныеСтроки.Количество() = 1;
	Элементы.ФормаСравнитьВыбраннуюВерсиюСПроверенной.Доступность	= ВыделенныеСтроки.Количество() = 1;
	Элементы.ФормаСравнитьДвеВыбранныеВерсии.Доступность			= ВыделенныеСтроки.Количество() = 2;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СравнитьВыбраннуюВерсиюСПоследней(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		СравнитьВерсии(ТекущиеДанные.Период, '000101010000', ТекущиеДанные.ВерсионируемыйОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДвеВыбранныеВерсии(Команда)
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() <> 2 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Необходимо выбрать две версии'")); 
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные1 = Элементы.Список.ДанныеСтроки(Элементы.Список.ВыделенныеСтроки[0]);
	ТекущиеДанные2 = Элементы.Список.ДанныеСтроки(Элементы.Список.ВыделенныеСтроки[1]);
	Если ТекущиеДанные1.Период < ТекущиеДанные2.Период Тогда
		СравнитьВерсии(ТекущиеДанные1.Период, ТекущиеДанные2.Период, ТекущиеДанные1.ВерсионируемыйОбъект);
	Иначе
		СравнитьВерсии(ТекущиеДанные2.Период, ТекущиеДанные1.Период, ТекущиеДанные1.ВерсионируемыйОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьВыбраннуюВерсиюСПроверенной(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		СравнитьВерсии(Неопределено, ТекущиеДанные.Период, ТекущиеДанные.ВерсионируемыйОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПараметрыСравнения(Команда)
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("ВерсионируемыйОбъект");
	
	ВерсионируемыйОбъект = Неопределено;
	
	Для Каждого ЭлементОтбора из Список.Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.ЛевоеЗначение = ПолеОтбора
				И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				ВерсионируемыйОбъект = ЭлементОтбора.ПравоеЗначение;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("Объект", ВерсионируемыйОбъект);
	ОткрытьФорму("ОбщаяФорма.СравнениеВерсий", ПараметрыФормы,,,,, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СравнитьВерсии(ДатаВерсии1, ДатаВерсии2, ВерсионируемыйОбъект)
	
	ДанныеДляСравнения = Версионирование.ПодготовитьДанныеДляСравнения(ВерсионируемыйОбъект, ДатаВерсии1, ДатаВерсии2);
	
	Если ДанныеДляСравнения = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отсутствует проверенная версия'"),, НСтр("ru = 'Сравнение невозможно'"));
		Возврат;
	КонецЕсли;
	
	ВерсионированиеКлиент.СравнитьОбъекты(ДанныеДляСравнения);
	
КонецПроцедуры

#КонецОбласти
