#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Запись.ДатаНачалаЗамещения)
		И ЗначениеЗаполнено(Запись.ДатаОкончанияЗамещения)
		И Запись.ДатаНачалаЗамещения > Запись.ДатаОкончанияЗамещения Тогда
		
		ТекстСообщения = НСтр("ru='Неверно указаны даты замещения. Дата окончания не должна быть ранее даты начала'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Запись.ДатаОкончанияЗамещения",, Отказ);
		Отказ = Истина;
		
		Возврат;
		
	КонецЕсли;
	
	// Проверка пересечений по периоду с существующими записями
	
	ДанныеОКонфликте = РегистрыСведений.ЗамещениеПользователейПоРаботеСОшибками.ДанныеКонфликтующейЗаписиОЗамещении(Запись,
		               Запись.ИсходныйКлючЗаписи);
					   
	Если ЗначениеЗаполнено(ДанныеОКонфликте.Пользователь) Тогда
		
		ТекстСообщения = НСтр("ru='Для указанного пользователя и проекта уже существует запись о замещении с %ДатаНачала% по %ДатаОкончания%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДатаНачала%", Формат(ДанныеоКонфликте.ДатаНачалаЗамещения, "ДФ=дд.ММ.гг"));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДатаОкончания%", Формат(ДанныеоКонфликте.ДатаОкончанияЗамещения, "ДФ=дд.ММ.гг"));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСОобщения);
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти