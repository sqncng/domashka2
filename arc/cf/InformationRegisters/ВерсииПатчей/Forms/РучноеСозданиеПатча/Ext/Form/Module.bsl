#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УникальныйИдентификатор", Параметры.ИдентификаторПатча);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииПатчей.Патч КАК Патч,
		|	ВерсииПатчей.Версия КАК Версия,
		|	ВерсииПатчей.НачальнаяСборка КАК НачальнаяСборка,
		|	ВерсииПатчей.КонечнаяСборка КАК КонечнаяСборка,
		|	ВерсииПатчей.ИзменяемыеМетаданные КАК ИзменяемыеМетаданные,
		|	ВерсииПатчей.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.ВерсииПатчей КАК ВерсииПатчей
		|ГДЕ
		|	ВерсииПатчей.УникальныйИдентификатор = &УникальныйИдентификатор";
	Результат = Запрос.Выполнить().Выгрузить();
	СвойстваПатча = Результат[0]; 
	
	Если СвойстваПатча.Состояние = Перечисления.СтатусыПатчей.Опубликован
		Или СвойстваПатча.Состояние = Перечисления.СтатусыПатчей.Отозван Тогда
		ВызватьИсключение НСтр("ru = 'Редактирование опубликованного патча запрещено.'");
	КонецЕсли;
	
	Версия = СвойстваПатча.Версия;
	Патч = СвойстваПатча.Патч;
	ИдентификаторПатча   = Параметры.ИдентификаторПатча;
	ИзменяемыеМетаданные = СвойстваПатча.ИзменяемыеМетаданные;
	Если ЗначениеЗаполнено(СвойстваПатча.НачальнаяСборка) Тогда
		НачальнаяСборка = СвойстваПатча.НачальнаяСборка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СвойстваПатча.КонечнаяСборка) Тогда
		КонечнаяСборка = СвойстваПатча.КонечнаяСборка;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Редактирование патча для ошибки ""%1""'"),
		Строка(Патч));
	
	ДанныеПатча = ПатчиСлужебный.ДвоичныеДанныеПатча(ИдентификаторПатча);
	Если ДанныеПатча = Неопределено Тогда
		Элементы.ФормаСохранитьСозданныйПатч.Видимость = Ложь;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Версия));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Опубликована", Истина));
	Элементы.НачальнаяСборка.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Элементы.КонечнаяСборка.ПараметрыВыбора  = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ПатчСоздан = ДанныеПатча <> Неопределено;
	Элементы.ФормаСохранитьСозданныйПатч.Видимость = ПатчСоздан;
	Элементы.ФормаШаблонПатча.Видимость = Не ПатчСоздан;
	
	// Определение доступных режимов совместимости.
	ЗаполнитьРежимСовместимости();
	
	СозданиеВручную = (СвойстваПатча.Состояние = Перечисления.СтатусыПатчей.СозданиеВручную);
	Если СозданиеВручную Тогда
		Если Не Параметры.РедактированиеПатча Тогда
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаДействияСПатчем;
		КонецЕсли;
		Элементы.ДекорацияПояснение.Заголовок = НСтр("ru = '• сохранить шаблон патча по кнопке ""Шаблон патча для ручного создания..."";
			|  • если шаблон уже есть, то по кнопке ""Сохранить патч"";
			|• внести в него изменения по ошибке;
			|• не допускается редактировать свойства расширения и автоматически созданный макет патча;
			|• определить номер сборки, начиная с которой можно устанавливать патч, а также заполнить поле ""Конечная сборка"";
			|• указать режим совместимости для патча, он должен соответствовать минимальному режиму совместимости конфигурации.'");
		СтандартныеПодсистемыСервер.УстановитьКлючНазначенияФормы(ЭтотОбъект, "СозданиеПатчаВручную");
	КонецЕсли;
	
	Если СозданиеВручную И Параметры.РедактированиеПатча Тогда
		Элементы.ВариантДополнить.Видимость = Ложь;
		Элементы.ВариантПрименимость.Видимость = Ложь;
		Варианты = "Пересоздать";
	ИначеЕсли Не СозданиеВручную Тогда
		Варианты = "Пересоздать";
		Элементы.ВариантПродолжитьВручную.Видимость = Ложь;
		СтандартныеПодсистемыСервер.УстановитьКлючНазначенияФормы(ЭтотОбъект, "РедактированиеПатча");
	КонецЕсли;
	
	АктивнаяДлительнаяОперация = Параметры.АктивнаяДлительнаяОперация;
	Если ЗначениеЗаполнено(АктивнаяДлительнаяОперация) Тогда
		ЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(АктивнаяДлительнаяОперация.ИдентификаторЗадания);
		Если Не ЗаданиеВыполнено Тогда
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаИндикаторСохраненияПатча;
			Элементы.ЗаписатьИЗакрыть.Видимость = Ложь;
			Элементы.ФормаЗакрыть.Видимость     = Истина;
			Если АктивнаяДлительнаяОперация.Свойство("Прогресс") Тогда
				ИндикаторПрогресса = АктивнаяДлительнаяОперация.Прогресс;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонечнаяСборкаПриИзменении(Элемент)
	КонечнаяСборкаИзменялась = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьСозданныйПатч(Команда)
	ИнформацияОПатче = ИнформацияОПатче();
	ПолучитьФайл(ИнформацияОПатче.ДанныеПатча, ИнформацияОПатче.ИмяПатча, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепитьПатч(Команда)
	
	Если Не ЗначениеЗаполнено(НачальнаяСборка) Или Не ЗначениеЗаполнено(КонечнаяСборка) Тогда
		ТекстСообщения = НСтр("ru = 'Перед добавлением патча необходимо заполнить поля
			|""Начальная сборка"" и ""Конечная сборка"".'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПрикрепитьПатчПродолжение", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если СборкаШаблона Тогда
		ОтменитьВыполнениеДлительнойОперации();
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = Неопределено;
	
	Если Варианты <> "Пересоздать" И Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Варианты = "Пересоздать" Тогда
		ПересоздатьПатчНаСервере();
		Оповестить("Патчи_ИзменениеСтатуса");
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресФайла) И Не ИзмененияНеТребуются Тогда
		ТекстПредупреждения = НСтр("ru = 'Не приложен файл с исправленным патчем.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ЗаписатьИЗакрытьНаСервере();
	Оповестить("Патчи_РучноеСоздание", ДлительнаяОперация, ИдентификаторПатча);
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШаблонПатча(Команда)
	Если Не ЗначениеЗаполнено(РежимСовместимости) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Режим совместимости"".'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение); 
	Диалог.ПолноеИмяФайла = ИмяПатча();
	Диалог.Расширение = "cfe";
	Диалог.Фильтр = НСтр("ru = 'Файлы исправлений (*.cfe*)|*.cfe*'");
	Диалог.МножественныйВыбор = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПутиСохраненияПатча", ЭтотОбъект);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, Диалог);
	
КонецПроцедуры 

&НаКлиенте
Процедура КомандаДалее(Команда)
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВариантыСоздания Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаДействияСПатчем;
	Иначе
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВариантыСоздания;
	КонецЕсли;
	
	Элементы.ГруппаПредупреждение.Видимость = (Варианты <> "ИзменитьПрименимость");
	
	УстановитьСостояниеЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ВариантыПриИзменении(Элемент)
	УстановитьСостояниеЭлементовФормы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СохранениеШаблонаПатча

&НаКлиенте
Процедура ПослеВыбораПутиСохраненияПатча(ПутьСохраненияПатча, ДополнительныеПараметры) Экспорт
	
	Если ПутьСохраненияПатча = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьСохраненияПатча = ПутьСохраненияПатча[0];
	
	ДлительнаяОперация = ЗапускПодготовкиШаблонаПатча(); 
	
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	Элементы.НадписьДлительнаяОперация.Заголовок = НСтр("ru = 'Выполняется сборка шаблона для ручного создания патча...'");
	Элементы.ЗаписатьИЗакрыть.Заголовок = НСтр("ru = 'Отмена'");
	Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию = Ложь;
	СборкаШаблона = Истина;
	
	ШаблонПредупреждения = НСтр("ru = 'Шаблон патча успешно сохранен по пути:
		|%1'");
	Предупреждение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредупреждения,
		ПутьСохраненияПатча);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗапускПодготовкиШаблонаПатчаЗавершение", ЭтотОбъект, ПутьСохраненияПатча);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Шаблон патча сохранен'");
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = Предупреждение;
	ПараметрыОжидания.ОповещениеПользователя.Важное = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Картинка = БиблиотекаКартинок.ДиалогИнформация;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапускПодготовкиШаблонаПатча()
	
	СвойстваПатча = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Патч, "Ошибка, Описание");
	Если ТипЗнч(СвойстваПатча.Ошибка) = Тип("Строка") Тогда
		ОписаниеПатча = СвойстваПатча.Описание;
	Иначе
		ОписаниеПатча = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвойстваПатча.Ошибка, "ПубликуемоеОписание");
	КонецЕсли;
	
	НомерПатча = ПатчиСлужебный.НомерПатча(ИдентификаторПатча);
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Версия", Версия);
	ПередаваемыеПараметры.Вставить("НазваниеПатча", ПатчиСлужебный.ИмяПатча(Патч, НомерПатча, Ложь));
	ПередаваемыеПараметры.Вставить("УникальныйИдентификатор", ИдентификаторПатча);
	ПередаваемыеПараметры.Вставить("Описание", ОписаниеПатча);
	ПередаваемыеПараметры.Вставить("ИзменяемыеМетаданные", "");
	ПередаваемыеПараметры.Вставить("ИмяКонфигурации", "");
	ПередаваемыеПараметры.Вставить("ПрименимДляСборок", Новый Массив);
	ПередаваемыеПараметры.Вставить("СвязанныеКонфигурации", Новый Соответствие);
	ПередаваемыеПараметры.Вставить("РежимСовместимости", РежимСовместимости);
	ПередаваемыеПараметры.Вставить("КонечныеСборкиСвязанныхКонфигураций", Новый Соответствие);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = Ложь;
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ПатчиСлужебный.ПатчДляРучногоСоздания",
		ПередаваемыеПараметры);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ЗапускПодготовкиШаблонаПатчаЗавершение(Результат, ПутьСохраненияПатча) Экспорт
	
	Элементы.СтраницыПомощника.ТекущаяСтраница  = Элементы.СтраницаДействияСПатчем;
	Элементы.ЗаписатьИЗакрыть.Заголовок         = НСтр("ru = 'Готово'");
	Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию = Истина;
	СборкаШаблона = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	СохранитьПатчДляРучногоСоздания(Результат.АдресРезультата);
	Элементы.ФормаСохранитьСозданныйПатч.Видимость = Истина;
	Элементы.ФормаШаблонПатча.Видимость            = Ложь;
	Элементы.РежимСовместимости.Видимость          = Ложь;
	ПатчСоздан = Истина;
	
	ПараметрыСохраненияФайла = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохраненияФайла.Интерактивно = Ложь;
	ФайловаяСистемаКлиент.СохранитьФайл(Неопределено, Результат.АдресРезультата, ПутьСохраненияПатча, ПараметрыСохраненияФайла);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПатчДляРучногоСоздания(ПатчВХранилище)
	
	ДанныеПатча = ПолучитьИзВременногоХранилища(ПатчВХранилище);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = Блокировка.Добавить("РегистрСведений.ВерсииПатчей");
	ЭлементБлокировкиДанных.УстановитьЗначение("Патч", Патч);
	ЭлементБлокировкиДанных.УстановитьЗначение("Версия", Версия);
	ЭлементБлокировкиДанных.УстановитьЗначение("УникальныйИдентификатор", ИдентификаторПатча);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ВерсииПатчей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Патч.Установить(Патч);
		НаборЗаписей.Отбор.Версия.Установить(Версия);
		НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(ИдентификаторПатча);
		НаборЗаписей.Прочитать();
		
		ВерсияПатча = НаборЗаписей[0];
		ВерсияПатча.ДанныеПатча = Новый ХранилищеЗначения(ДанныеПатча, Новый СжатиеДанных(9));
		ВерсияПатча.РежимСовместимости = РежимСовместимости;
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ИмяПатча()
	Возврат ПатчиСлужебный.ИмяПатча(Патч,, Ложь);
КонецФункции

#КонецОбласти

&НаСервере
Функция ИнформацияОПатче()
	ДанныеПатча = ПатчиСлужебный.ДвоичныеДанныеПатча(ИдентификаторПатча);
	ИмяПатча = ПатчиСлужебный.ИмяПатча(Патч);
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеПатча", ПоместитьВоВременноеХранилище(ДанныеПатча));
	Результат.Вставить("ИмяПатча", ИмяПатча);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПрикрепитьПатчПродолжение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		Режим = РежимДиалогаВыбораФайла.Открытие;
		ДиалогВыбора = Новый ДиалогВыбораФайла(Режим);
		ДиалогВыбора.МножественныйВыбор = Ложь;
		ДиалогВыбора.Фильтр = НСтр("ru = 'Файл патча'") + "(*.cfe)|*.cfe";
		ОписаниеОповещения = Новый ОписаниеОповещения("ПрикрепитьПатчЗавершение", ЭтотОбъект);
		
		НачатьПомещениеФайлов(ОписаниеОповещения, , ДиалогВыбора, Истина, УникальныйИдентификатор);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ПрикрепитьПатчПродолжениеПослеПомещенияФайла", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещения,,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепитьПатчЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФайла = Результат[0].Хранение;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепитьПатчПродолжениеПослеПомещенияФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФайла = Адрес;
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИЗакрытьНаСервере()
	
	ПараметрыСохранения = ПараметрыСохраненияПатча();
	Если ЗначениеЗаполнено(АдресФайла) Тогда
		// Только изменение применимости без редактирования патча.
		ПараметрыСохранения.ДанныеПатча = ПолучитьИзВременногоХранилища(АдресФайла);
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ПараметрыСохранения, ЭтотОбъект);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
	ПараметрыВыполнения.ОжидатьЗавершение = Ложь;
	ПараметрыВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	ПараметрыВыполнения.КлючФоновогоЗадания = Строка(ИдентификаторПатча);
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
		"РегистрыСведений.ВерсииПатчей.СохранитьИзмененияПатча",
		ПараметрыСохранения);
	
	Модифицированность = Ложь;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРежимСовместимости()
	
	Если ПатчСоздан Тогда
		СвойстваПатча = ПатчиСлужебный.ЗначенияСвойствВерсииПатча(ИдентификаторПатча, "РежимСовместимости");
		Если ЗначениеЗаполнено(СвойстваПатча.РежимСовместимости) Тогда
			РежимСовместимости = СвойстваПатча.РежимСовместимости;
		КонецЕсли;
		Элементы.РежимСовместимости.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	КаталогПрограммы = Константы.ПутьКВерсиямПлатформыНаСервере.Получить();
	СписокДоступныхВерсийПлатформы = ОбщегоНазначенияСППР.СписокДоступныхВерсийПлатформы(КаталогПрограммы);
	
	Для Каждого СборкаПлатформы Из СписокДоступныхВерсийПлатформы Цикл
		ВерсияПлатформы = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(СборкаПлатформы);
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ВерсияПлатформы, "8.3.12") < 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Элементы.РежимСовместимости.СписокВыбора.НайтиПоЗначению(ВерсияПлатформы) = Неопределено Тогда
			Элементы.РежимСовместимости.СписокВыбора.Добавить(ВерсияПлатформы);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваПатча = ПатчиСлужебный.ЗначенияСвойствВерсииПатча(ИдентификаторПатча, "РежимСовместимости");
	Если ЗначениеЗаполнено(СвойстваПатча.РежимСовместимости) Тогда
		РежимСовместимости = СвойстваПатча.РежимСовместимости;
		Возврат;
	КонецЕсли;
	
	ПлатформаИзВерсии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Версия, "ВерсияПлатформы");
	Если Не ЗначениеЗаполнено(ПлатформаИзВерсии) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрРазделить(ПлатформаИзВерсии, ".").Количество() = 3 Тогда
		РежимСовместимости = ПлатформаИзВерсии;
	ИначеЕсли СтрРазделить(ПлатформаИзВерсии, ".").Количество() = 4 Тогда
		РежимСовместимости = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(СборкаПлатформы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеЭлементовФормы()
	ВыборВарианта = (Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВариантыСоздания);
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаДействияСПатчем Тогда
		Элементы.ГруппаКомандыИПояснение.Видимость = (Варианты = "СоздатьВручную") Или (Варианты = "ПродолжитьВручную");
		
		ИзмененияНеТребуются = (Варианты = "ИзменитьПрименимость");
		Элементы.ФормаКомандаДалее.Заголовок = НСтр("ru = '< Назад'");
	Иначе
		Элементы.ФормаКомандаДалее.Заголовок = НСтр("ru = 'Далее >'");
	КонецЕсли;
	
	Элементы.ЗаписатьИЗакрыть.Видимость          = (Варианты = "Пересоздать") Или Не ВыборВарианта;
	Элементы.ФормаКомандаДалее.Видимость         = (Варианты <> "Пересоздать");
	Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию  = (Варианты = "Пересоздать") Или Не ВыборВарианта;
	Элементы.ФормаКомандаДалее.КнопкаПоУмолчанию = Не Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию;
	
КонецПроцедуры

&НаСервере
Процедура ПересоздатьПатчНаСервере()
	ПараметрыПатча = Новый Структура;
	ПараметрыПатча.Вставить("Патч", Патч);
	ПараметрыПатча.Вставить("Версия", Версия);
	ПараметрыПатча.Вставить("УникальныйИдентификатор", ИдентификаторПатча);
	
	ПатчиСлужебный.ПересоздатьПатч(ПараметрыПатча);
КонецПроцедуры

&НаСервере
Функция ПараметрыСохраненияПатча()
	
	СтрокаПараметры = "ДанныеПатча, КонечнаяСборкаИзменялась, ИзменяемыеМетодыВычисленное, ИзменяемыеМетаданные,
		|НовыйРежимСовместимости, ИдентификаторПатча, Патч, Версия,
		|НачальнаяСборка, КонечнаяСборка, ИзмененияНеТребуются,
		|РежимСовместимости, ПатчСоздан";
	ПараметрыСохранения = Новый Структура(СтрокаПараметры);
	
	Возврат ПараметрыСохранения;
	
КонецФункции

&НаСервере
Процедура ОтменитьВыполнениеДлительнойОперации()
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Патчи_ПрогрессЗавершение"
		И Источник = ИдентификаторПатча Тогда
		Закрыть();
	КонецЕсли;
	
	Если ИмяСобытия = "Патчи_Прогресс"
		И Источник = ИдентификаторПатча Тогда
		ИндикаторПрогресса = Параметр.Процент;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти