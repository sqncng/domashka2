#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщаяЗадача = Параметры.ОбщаяЗадача;
	
	СтатусОбщейЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбщаяЗадача, "Статус");
	
	Список.Параметры.УстановитьЗначениеПараметра("ОбщаяЗадача", ОбщаяЗадача);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ОбщаяЗадача", ОбщаяЗадача,,, Истина);
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("Проект")<>Неопределено Тогда
		УстановитьОтборПоПроекту();
	КонецЕсли;
	
	Если Настройки.Получить("Ответственный")<>Неопределено Тогда
		УстановитьОтборПоОтветственному();
	КонецЕсли;
	
	Если Настройки.Получить("СписокСтатусов")<>Неопределено Тогда
		УстановитьОтборПоСтатусу();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПроектПриИзменении(Элемент)
	
	УстановитьОтборПоПроекту();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	
	УстановитьОтборПоОтветственному();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСтатусовВыполненияПриИзменении(Элемент)
	
	УстановитьОтборПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСтатусовВыполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураПараметров = Новый Структура("СписокВыбора", СписокСтатусовВыполнения);
	
	ОткрытьФорму("Перечисление.СтатусыОбщихЗадач.Форма.ВыборСпискаСтатусов", СтруктураПараметров, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСтатусовВыполненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СписокСтатусовВыполнения = ВыбранноеЗначение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапланироватьЗадачу(Команда)
	
	ОчиститьСообщения();
	
	Если СтатусОбщейЗадачи <> ПредопределенноеЗначение("Перечисление.СтатусыОбщихЗадач.ТребуетсяЗапланировать") Тогда
		ТекстСообщения = НСтр("ru='Запланировать по проектам можно только в статусе ""Требуется запланировать""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОбщаяЗадача", ОбщаяЗадача);
	
	ОткрытьФорму("Справочник.ОбщиеЗадачи.Форма.ПланированиеПоПроектам", СтруктураПараметров);
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоПроекту()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Проект", Проект, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Проект));
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОтветственному()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ответственный", Ответственный, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Ответственный));
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСтатусу()
	
	ИспользоватьОтбор = СписокСтатусовВыполнения.Количество()>0;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"СтатусВыполнения",
																			СписокСтатусовВыполнения,
																			ВидСравненияКомпоновкиДанных.ВСписке,
																			,
																			ИспользоватьОтбор);
																			
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	Если Элементы.Найти("СписокЗапланироватьЗадачу") <> Неопределено Тогда
		ЕстьПравоДоступа = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ВыполнениеОбщихЗадач);
		Элементы.СписокЗапланироватьЗадачу.Видимость = ЕстьПравоДоступа;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти