
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьОтборыПараметрыДинамическогоСписка();
	
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	ОбщегоНазначенияСППРКлиентСервер.ОтборПоЗначениюСпискаПередЗагрузкойИзНастроек(Список,
	                                                                               "Статус",
	                                                                               Статус,
	                                                                               СтруктураБыстрогоОтбора, 
	                                                                               Настройки);
	
	ОбщегоНазначенияСППРКлиентСервер.ОтборПоЗначениюСпискаПередЗагрузкойИзНастроек(Список,
	                                                                               "ТипПлана",
	                                                                               ТипПлана,
	                                                                               СтруктураБыстрогоОтбора, 
	                                                                               Настройки);
	
	Если СтруктураБыстрогоОтбора <> Неопределено 
		   И ТипЗнч(СтруктураБыстрогоОтбора) = Тип("Структура") Тогда
		
		Настройки.Удалить("ПоказыватьВидыПланов")
		
	Иначе
		
		ПоказыватьВидыПланов = Настройки.Получить(ПоказыватьВидыПланов);
		ОбработатьИзменениеОтбораПоказывать(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВидыПланов" Тогда
		УстановитьПараметрыДинамическихСписков();
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
	                                                                        "Статус",
	                                                                        Статус,
	                                                                        ВидСравненияКомпоновкиДанных.Равно,
	                                                                        ,
	                                                                        ЗначениеЗаполнено(Статус));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьВидыПлановПриИзменении(Элемент)
	
	ОбработатьИзменениеОтбораПоказывать(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборыПараметрыДинамическогоСписка()
	
	СтруктураБыстрогоОтбора = Неопределено;
	
	УстановитьПараметрыДинамическихСписков();
	
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	ОбщегоНазначенияСППРКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(Список,
	                                                                           "Статус",
	                                                                           Статус,
	                                                                           СтруктураБыстрогоОтбора,,
	                                                                           ВидСравненияКомпоновкиДанных.ВСписке); 
	
	ОбщегоНазначенияСППРКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(Список,
	                                                                           "ТипПлана",
	                                                                           ТипПлана,
	                                                                           СтруктураБыстрогоОтбора,,
	                                                                           ВидСравненияКомпоновкиДанных.Равно);
	
	Если ТипЗнч(СтруктураБыстрогоОтбора) = Тип("Структура")
		И СтруктураБыстрогоОтбора.Свойство("ПоказыватьВидыПланов") Тогда
		ПоказыватьВидыПланов = СтруктураБыстрогоОтбора.ПоказыватьВидыПланов;
	КонецЕсли;
	
	ОбработатьИзменениеОтбораПоказывать(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Условное оформление динамического списка "Список"
	СписокУсловноеОформление = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	СписокУсловноеОформление.Элементы.Очистить();
	
#Область Статус
	
	// Выделение цветом состояния "Не действует"
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Выделение цветом статус ""Не действует""'");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыВидовПланов.НеАктуален;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);
	
#КонецОбласти

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДинамическихСписков()

	МассивВидовПлановЯВладелец = Новый Массив;
	МассивВидовПлановЯУчастник = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	ВидыПлановВладельцы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыПланов.ВладельцыПлана КАК ВидыПлановВладельцы 
	|ГДЕ
	|	ВидыПлановВладельцы.Владелец = &ТекущийПользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыПлановУчастникиПланирования.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыПланов.УчастникиПланирования КАК ВидыПлановУчастникиПланирования
	|ГДЕ
	|	ВидыПлановУчастникиПланирования.Участник = &ТекущийПользователь";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаВидыПлановЯВладелец = Результат[0].Выбрать();
	ВыборкаВидыПлановЯУчастник = Результат[1].Выбрать();
	
	Пока ВыборкаВидыПлановЯВладелец.Следующий() Цикл
	
		МассивВидовПлановЯВладелец.Добавить(ВыборкаВидыПлановЯВладелец.Ссылка);
	
	КонецЦикла;
	
	Пока ВыборкаВидыПлановЯУчастник.Следующий() Цикл
	
		МассивВидовПлановЯУчастник.Добавить(ВыборкаВидыПлановЯУчастник.Ссылка);
	
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ВидыПланаЯУчастник", МассивВидовПлановЯУчастник);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ВидыПланаЯВладелец", МассивВидовПлановЯВладелец);
	
	ОбработатьИзменениеОтбораПоказывать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьИзменениеОтбораПоказывать(Форма)
	
	ПоказыватьВидыПлановЯУчастник          = Ложь;
	ПоказыватьВидыПлановЯВладелец          = Ложь;
	ПоказыватьВидыПлановЯВладелецЯУчастник = Ложь;
	
	Если Форма.ПоказыватьВидыПланов = "УчастникИлиВладелец" Тогда
		ПоказыватьВидыПлановЯВладелецЯУчастник = Истина;
	ИначеЕсли Форма.ПоказыватьВидыПланов = "ВидыПлановЯУчастник" Тогда
		ПоказыватьВидыПлановЯУчастник = Истина;
	ИначеЕсли Форма.ПоказыватьВидыПланов = "ВидыПлановЯВладелец" Тогда
		ПоказыватьВидыПлановЯВладелец = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,"ПоказыватьВидыПлановЯВладелецЯУчастник", ПоказыватьВидыПлановЯВладелецЯУчастник);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,"ПоказыватьВидыПлановЯУчастник",          ПоказыватьВидыПлановЯУчастник);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,"ПоказыватьВидыПлановЯВладелец",          ПоказыватьВидыПлановЯВладелец);
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)
	
	Если Форма.ТипПлана = ПредопределенноеЗначение("Перечисление.ТипыПланов.Занятости") Тогда
		
		Форма.Заголовок = НСтр("ru = 'Виды планов занятости'");
		Форма.Элементы.РодительскийПлан.Видимость = Ложь;
		
	ИначеЕсли Форма.ТипПлана = ПредопределенноеЗначение("Перечисление.ТипыПланов.Рабочий") Тогда
		
		Форма.Заголовок = НСтр("ru = 'Виды рабочих планов'");
		
	Иначе
		
		Форма.Заголовок = НСтр("ru = 'Виды планов'");
		
	КонецЕсли;

КонецПроцедуры 

#КонецОбласти
