#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтметитьИсправлениеПриИзменении(Элемент)
	
	Если НЕ ОтметитьИсправление Тогда
		ОписаниеВыполненныхИзменений = "";
		ЕстьИзмененияВПроектныхРешениях = Ложь;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ Признать И НЕ ОтметитьИсправление И НЕ ПодтвердитьИсправление И НЕ Закрыть Тогда
		ТекстСообщения = НСтр("ru='Не выбраны действия по обработке ошибок. Установите соответствующие флажки для выполнения требуемых действий'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСОобщения);
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("ПубликуемоеОписание", ПубликуемоеОписание);
	Структура.Вставить("ОписаниеВыполненныхИзменений", ОписаниеВыполненныхИзменений);
	Структура.Вставить("ЕстьИзмененияВПроектныхРешениях", ЕстьИзмененияВПроектныхРешениях);
	Структура.Вставить("КомуНаправлена", КомуНаправить);
	Структура.Вставить("Комментарий", Комментарий);
	Структура.Вставить("Признать", Признать);
	Структура.Вставить("ОтметитьИсправление", ОтметитьИсправление);
	Структура.Вставить("ПодтвердитьИсправление", ПодтвердитьИсправление);
	Структура.Вставить("Закрыть", Закрыть);
	
	Если Открыта() Тогда
		Закрыть(Структура);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	Элементы.ОписаниеВыполненныхИзменений.Доступность = ОтметитьИсправление;
	Элементы.ЕстьИзмененияВПроектныхРешеинях.Доступность = ОтметитьИсправление;
	
КонецПроцедуры

#КонецОбласти