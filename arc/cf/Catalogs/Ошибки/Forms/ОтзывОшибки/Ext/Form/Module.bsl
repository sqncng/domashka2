
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Ошибка = Параметры.Ошибка;
	
	РеквизитыОшибки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ошибка, "Владелец, РазделПроекта");
	
	Проект = РеквизитыОшибки.Владелец;
	Раздел = РеквизитыОшибки.РазделПроекта;
	
	СтрокаРеквизитов = "ИспользоватьЗакрытиеОшибок,
	                    |ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями,
						|ВариантНаправленияОшибокНаЗакрытие,
						|ЗакрывающийОшибки,
						|Ответственный";
						
	РеквизитыПроекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Проект, СтрокаРеквизитов);
	
	ИспользоватьЗакрытиеОшибок = РеквизитыПроекта.ИспользоватьЗакрытиеОшибок;
	ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями =
		РеквизитыПроекта.ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями;
	
	Если ИспользоватьЗакрытиеОшибок = Неопределено Тогда
		ИспользоватьЗакрытиеОшибок = Ложь;
	КонецЕсли;
	
	Если ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями = Неопределено Тогда
		ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями = Ложь;
	КонецЕсли;
	
	ЕстьИзмененияПоСравнениюСРанееПринятымиПроектнымиРешениями =
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ошибка, "ЕстьИзмененияПоСравнениюСРанееПринятымиПроектнымиРешениями");
		
	Если ЕстьИзмененияПоСравнениюСРанееПринятымиПроектнымиРешениями = Неопределено Тогда
		ЕстьИзмененияПоСравнениюСРанееПринятымиПроектнымиРешениями = Ложь;
	КонецЕсли;
		
	ОшибкаПодлежитЗакрытию = 
		ИспользоватьЗакрытиеОшибок
		И НЕ ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями
		ИЛИ ИспользоватьЗакрытиеТолькоДляОшибокСПроектнымиИзменениями
		И ЕстьИзмененияПоСравнениюСРанееПринятымиПроектнымиРешениями;
	
	Если ОшибкаПодлежитЗакрытию Тогда
		
		ВариантНаправленияОшибокНаЗакрытие = РеквизитыПроекта.ВариантНаправленияОшибокНаЗакрытие;
		
		Если ВариантНаправленияОшибокНаЗакрытие = Перечисления.ВариантыНаправленияОшибокНаЗакрытие.ОтветственномуЗаПроект Тогда
			КомуНаправить = РеквизитыПроекта.Ответственный;
		ИначеЕсли ВариантНаправленияОшибокНаЗакрытие = Перечисления.ВариантыНаправленияОшибокНаЗакрытие.ОтветственномуЗаРаздел Тогда
			Если ЗначениеЗаполнено(Раздел) Тогда
				КомуНаправить = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Раздел, "Ответственный");
			КонецЕсли;
		ИначеЕсли ВариантНаправленияОшибокНаЗакрытие = Перечисления.ВариантыНаправленияОшибокНаЗакрытие.УказанномуПользователю Тогда
			КомуНаправить = РеквизитыПроекта.ЗакрывающийОшибки;
		КонецЕсли;
		
	КонецЕсли;
	
	Справочники.Ошибки.ЗаместитьПользователяПриРаботеСОшибкой(КомуНаправить, Проект, Комментарий, Истина);
	
	Если ЗначениеЗаполнено(КомуНаправить) Тогда
		ТекущийЭлемент = Элементы.Комментарий;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ОтозватьСервер(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ОшибкаОтозвана", Параметры.Ошибка, ЭтаФорма);
	ОповеститьОбИзменении(Параметры.Ошибка);
	
	ТекстОповещения = НСтр("ru='Ошибка отозвана'");
	ПоказатьОповещениеПользователя(ТекстОповещения, ПолучитьНавигационнуюСсылку(Параметры.Ошибка), Параметры.Ошибка);
	ИсторияРаботыПользователя.Добавить(Параметры.Ошибка);
	
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если НЕ ОшибкаПодлежитЗакрытию Тогда
		НепроверяемыеРеквизиты.Добавить("КомуНаправить");
	КонецЕсли;
	
	Если НепроверяемыеРеквизиты.Количество()>0 Тогда
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтозватьСервер(Отказ)
	
	ОшибкаОбъект = Ошибка.ПолучитьОбъект();
	
	ОшибкаОбъект.Статус = Перечисления.СтатусыОшибок.Отозвана;
	ОшибкаОбъект.ДатаОтзыва = ТекущаяДата();
	ОшибкаОбъект.Отозвал = ТекущийПользователь;
	ОшибкаОбъект.Отозвана = Истина;
	
	ОшибкаОбъект.ДобавитьЗаписьВПротокол(ТекущийПользователь, Комментарий);
	
	Попытка
		ОшибкаОбъект.Записать();
	Исключение
		Отказ = Истина;
		ВызватьИсключение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры
	
#КонецОбласти
