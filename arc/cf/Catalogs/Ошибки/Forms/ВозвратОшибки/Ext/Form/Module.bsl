
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КомуНаправить = Справочники.Ошибки.ПолучательВозвращеннойОшибки(Параметры.Ссылка);
	
	Проект = Параметры.Проект;
	
	Если Не ЗначениеЗаполнено(Проект) Тогда
		Проект = ПараметрыСеанса.ТекущийПроект;
	КонецЕсли;
	
	Справочники.Ошибки.ЗаместитьПользователяПриРаботеСОшибкой(КомуНаправить, Проект, Комментарий, Истина);
	
	Если ЗначениеЗаполнено(КомуНаправить) Тогда
		ТекущийЭлемент = Элементы.Комментарий;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(КомуНаправить) Тогда
		ТекстСообщения = НСтр("ru='Не указано, кому направляется ошибка'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"КомуНаправить");
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("КомуНаправлена", КомуНаправить);
	Структура.Вставить("Комментарий", Комментарий);
	
	Если Открыта() Тогда
		Закрыть(Структура);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти