
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Цвет = Новый ХранилищеЗначения(Цвет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Владелец", Объект.Владелец);
	Оповестить("Запись_ПриоритетРаботы", ПараметрыОповещения , Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	СформироватьНадписьДоступныеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы


#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	Цвет = СправочникОбъект.Цвет.Получить();
	
	СформироватьНадписьДоступныеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьДоступныеЗначения()

	ДанныеДоступныеЗначения = Справочники.ПриоритетыРабот.ДоступныеДляПроектаЗначенияПорядкаПриоритета(Объект.Владелец, Объект.Ссылка);
	Элементы.ДекорацияДоступныеЗначения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока("<span style=""color: ПоясняющийТекст"">%1</span>",
	                                                                                       ДанныеДоступныеЗначения.Представление);
	Элементы.Порядок.СписокВыбора.ЗагрузитьЗначения(ДанныеДоступныеЗначения.ДоступныеЗначения);

КонецПроцедуры

#КонецОбласти

