
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбработатьПереданныеПараметры();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПриоритетРаботы"
		И ЗначениеЗаполнено(Проект) Тогда
		ПриИзмененииПроектаСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	ПриИзмененииПроектаСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриИзмененииПроектаСервер()

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
	                                                                        "Владелец", 
	                                                                        Проект, 
	                                                                        ВидСравненияКомпоновкиДанных.Равно,
	                                                                        НСтр("ru = 'Отбор по проекту'"),
	                                                                        Истина, 
	                                                                        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	Если ЗначениеЗаполнено(Проект) Тогда
		ДанныеЗаголовка = Справочники.ПриоритетыРабот.ДанныеЗаголовкаНастроеныПриоритеты(Проект);
		Если Не ДанныеЗаголовка.ЕстьПроблемы Тогда
			Элементы.ДекорацияИнформацияОКорректностиЗаполнения.Заголовок = "";
			Элементы.ДекорацияИнформацияОКорректностиЗаполнения.Видимость = Ложь;
		Иначе
			Элементы.ДекорацияИнформацияОКорректностиЗаполнения.Заголовок = Новый ФорматированнаяСтрока(ДанныеЗаголовка.ТекстЗаголовка,,ДанныеЗаголовка.ЦветЗаголовка);
			Элементы.ДекорацияИнформацияОКорректностиЗаполнения.Видимость = Истина;
		КонецЕсли;
	Иначе
		Элементы.ДекорацияИнформацияОКорректностиЗаполнения.Заголовок = "";
		Элементы.ДекорацияИнформацияОКорректностиЗаполнения.Видимость = Ложь;
	
	КонецЕсли;
	
	ОбновитьУсловноеОформлениеЦветаПриоритета();
	
КонецПроцедуры 

&НаСервере
Процедура ОбработатьПереданныеПараметры()

	Проект = Параметры.Владелец;
	ПриИзмененииПроектаСервер();

КонецПроцедуры

&НаСервере
Процедура ОбновитьУсловноеОформлениеЦветаПриоритета()
	
	ПриоритетыОчередиРабот.УстановитьУсловноеОформлениеСписка(Список, Неопределено, "Наименование", "Ссылка");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Владелец", Проект);
	Оповестить("Запись_ПриоритетРаботы", ПараметрыОповещения , ТекущиеДанные.Ссылка);
	
КонецПроцедуры


#КонецОбласти
