
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбработатьПереданныеПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	
	Если Не ДанныеУказаныКорректно() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Закрыть(Согласовать);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	Согласовать = Параметры.Запрошено;
	Запрошено   = Параметры.Запрошено;
	
	Элементы.ДекорацияЗапрошено.Заголовок = СтрШаблон(НСтр("ru = 'из %1 %2'"), Параметры.Запрошено, Параметры.ЕдиницаИзмеренияРесурса);
	
	Если Запрошено > 0 Тогда
		
		Элементы.Согласовать.МаксимальноеЗначение = Согласовать;
		Элементы.Согласовать.МинимальноеЗначение  = 0;
		
	ИначеЕсли Запрошено < 0 Тогда
		
		Элементы.Согласовать.МаксимальноеЗначение = 0;
		Элементы.Согласовать.МинимальноеЗначение  = Согласовать;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеУказаныКорректно()

	ЗаполненоКорректно = Истина;
	
	Если Запрошено > 0 Тогда
		
		Если Согласовать > Запрошено 
			Или Согласовать < 0 Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Согласуемое количество должно быть в интервале от 0 до %1'"), Запрошено);
			ЗаполненоКорректно = Ложь;
			
		КонецЕсли;
		
	ИначеЕсли Запрошено < 0 Тогда
		
		 Если Согласовать < Запрошено 
			Или Согласовать > 0 Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Согласуемое количество должно быть в интервале от %1 до 0'"), Запрошено);
			ЗаполненоКорректно = Ложь;
			
		КонецЕсли
		
	КонецЕсли;
	
	Если Не ЗаполненоКорректно Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Согласовать");
	КонецЕсли;
	
	Возврат ЗаполненоКорректно;

КонецФункции

#КонецОбласти

