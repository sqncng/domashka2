
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТаблицаЗадачКИзменению = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	
	Если ТаблицаЗадачКИзменению = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭтоПонижениеСтатуса = Параметры.ЭтоПонижениеСтатуса;
	
	Если Не ЭтоПонижениеСтатуса Тогда
		
		Заголовок = НСтр("ru = 'Пересоздание задач'");
		АдресТаблицы = Параметры.АдресТаблицы;
		
		СформироватьОтчетПересозданиеЗадач(ТаблицаЗадачКИзменению);
		
	Иначе
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ЕстьНедоступныеДляИзменения, АдресТаблицы, НовыйСтатус");
		Элементы.ФормаПродолжить.Доступность = Не ЕстьНедоступныеДляИзменения;
		
		СформироватьОтчетПонижениеСтатуса(ТаблицаЗадачКИзменению);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродолжитьОперацию(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("НовыйСтатус", НовыйСтатус);
	СтруктураВозврата.Вставить("ЭтоПонижениеСтатуса", ЭтоПонижениеСтатуса);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчетПонижениеСтатуса(ТаблицаЗадачКИзменению)
	
	ТаблицаОтчета.Очистить();
	
	Макет = Справочники.ЗадачиПроцесса.ПолучитьМакет("ПонижениеСтатусаПересозданиеЗадач");
	
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	МассивТиповЗадач = Новый Массив;
	
	Если ЭтоПонижениеСтатуса Тогда
	
		ОбластьЗаголовок    = Макет.ПолучитьОбласть("ЗаголовокПонижениеСтатуса");
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаПонижениеСтатуса");
		ОбластьГруппировка  = Макет.ПолучитьОбласть("ГруппировкаПонижениеСтатуса");
		ОбластьСтрока       = Макет.ПолучитьОбласть("СтрокаПонижениеСтатуса");
		
		МассивТиповЗадач.Добавить("Текущая");
		МассивТиповЗадач.Добавить("Подчиненные");
		МассивТиповЗадач.Добавить("Последующие");
		МассивТиповЗадач.Добавить("Родительские");
		
	КонецЕсли;
	
	ТаблицаОтчета.Вывести(ОбластьПустаяСтрока);
	
	ОбластьЗаголовок.Параметры.Заголовок = ЗаголовокОтчета();
	
	Если ЕстьНедоступныеДляИзменения Тогда
		ОбластьЗаголовок.Области.ЗаголовокПонижениеСтатуса.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
	КонецЕсли;
	
	ТаблицаОтчета.Вывести(ОбластьЗаголовок);
	
	ТаблицаОтчета.Вывести(ОбластьПустаяСтрока);
	ТаблицаОтчета.Вывести(ОбластьШапкаТаблицы);
	
	Для Каждого ТипЗадачи Из МассивТиповЗадач Цикл
		
		ПараметрыПоиска = Новый Структура("ТипЗадачи", ТипЗадачи);
		ПараметрыПоиска.Вставить("ТипЗадачи", ТипЗадачи);
		
		НайденныеСтроки = ТаблицаЗадачКИзменению.НайтиСтроки(ПараметрыПоиска);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьГруппировка.Параметры.Имя = ТипЗадачи;
		ТаблицаОтчета.Вывести(ОбластьГруппировка);
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, НайденнаяСтрока);
			ОбластьСтрока.Параметры.ТекущийСтатус = Параметры.ТекущийСтатус;
			ОбластьСтрока.Параметры.НовыйСтатус   = НайденнаяСтрока.НовыйСтатус;
			
			Если Не НайденнаяСтрока.ПользователюДоступноИзменение Тогда
				ОбластьСтрока.Области.СтрокаПонижениеСтатуса.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
			КонецЕсли;
			
			ТаблицаОтчета.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетПересозданиеЗадач(ТаблицаЗадачКИзменению)

	ТаблицаОтчета.Очистить();
	
	Макет = Справочники.ЗадачиПроцесса.ПолучитьМакет("ПонижениеСтатусаПересозданиеЗадач");
	
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьЗаголовок    = Макет.ПолучитьОбласть("ЗаголовокПересозданиеЗадач");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаПересозданиеЗадач");
	ОбластьСтрока       = Макет.ПолучитьОбласть("СтрокаПересозданиеЗадач");
	
	ТаблицаОтчета.Вывести(ОбластьПустаяСтрока);
	
	ОбластьЗаголовок.Параметры.Заголовок = ЗаголовокОтчета();
	
	ТаблицаОтчета.Вывести(ОбластьЗаголовок);
	
	ТаблицаОтчета.Вывести(ОбластьПустаяСтрока);
	ТаблицаОтчета.Вывести(ОбластьШапкаТаблицы);
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗадачКИзменению Цикл
		
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, СтрокаТаблицы);
		ТаблицаОтчета.Вывести(ОбластьСтрока);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ЗаголовокОтчета()
	
	ТекстЗаголовка = "";
	
	Если ЭтоПонижениеСтатуса Тогда
		
		Если ЕстьНедоступныеДляИзменения Тогда
			
			ШаблонЗаголовка = НСтр("ru = 'Понижение статуса задачи ""%1"" из ""%2"" в  ""%3"" невозможно, так требуется понизить статус следующих задач, но вы не имеете права на понижение статуса одной или нескольких задач.'");
			
		Иначе
			
			ШаблонЗаголовка = НСтр("ru = 'При понижении статуса задачи ""%1"" из ""%2"" в ""%3"" будет выполнено понижение статуса следующих задач.'");
			
		КонецЕсли;
		
		ТекстЗаголовка  = СтрШаблон(ШаблонЗаголовка, Параметры.Задача, Параметры.ТекущийСтатус, Параметры.НовыйСтатус);
		
	Иначе
		
		ТекстЗаголовка = НСтр("ru = 'Будут вновь в статусе ""Запланирована"" созданы следующие задачи.'");
	
	КонецЕсли;
		
	Возврат ТекстЗаголовка;
	
КонецФункции

#КонецОбласти
