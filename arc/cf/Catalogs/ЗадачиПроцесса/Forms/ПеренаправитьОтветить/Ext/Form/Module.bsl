

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИмяКоманды          = Параметры.ИмяКоманды;
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ОбработатьПереданныеПараметры(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(СтруктураВозврата());
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиРезультатыСогласования(Команда)
	
	ТекстСогласования = ЗадачиПроцессовКлиентСервер.ТекстРезультатыСогласования(ПредставлениеРесурса,
	                                                                            Согласовать,
	                                                                            КСогласованию - Согласовать);
	
	Если СтрНайти(ТекстПоручения, ТекстСогласования) > 0 Тогда
		Возврат;
	КонецЕсли;
		
	ТекстПоручения = ТекстПоручения 
	                + ?(ПустаяСтрока(ТекстПоручения), "", Символы.ПС)
	                + ТекстСогласования;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СтруктураВозврата()
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ТекстПоручения",               ТекстПоручения);
	СтруктураВозврата.Вставить("ИсполнительПорученияПоЗадаче", ИсполнительПорученияПоЗадаче);
	СтруктураВозврата.Вставить("НеСогласовано",                КСогласованию - Согласовать);
	СтруктураВозврата.Вставить("Согласовано",                  Согласовать);
	СтруктураВозврата.Вставить("ИмяКоманды",                   ИмяКоманды);
	СтруктураВозврата.Вставить("ТолькоРесурсыТекущейЗадачи",   ТолькоРесурсыТекущейЗадачи);
	СтруктураВозврата.Вставить("ПредставлениеВидаРесурса",     ПредставлениеРесурса);
	
	Возврат СтруктураВозврата;
	
КонецФункции 

&НаСервере
Процедура ОбработатьПереданныеПараметры(Отказ)

	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		
		Если Параметры.ИмяКоманды = "Ответить" Тогда
			
			ОбработатьПараметрыОтветить(Отказ);
			
		ИначеЕсли Параметры.ИмяКоманды = "НеСогласовать" Тогда
			
			СогласуемыйРесурс = Параметры.СогласуемыйРесурс;
			ОбработатьПараметрыНеСогласовать(Отказ, Ложь);
			
		ИначеЕсли Параметры.ИмяКоманды = "ЧастичноСогласовать" Тогда
			
			СогласуемыйРесурс = Параметры.СогласуемыйРесурс;
			ОбработатьПараметрыНеСогласовать(Отказ, Истина);
			
		КонецЕсли;
		
	Иначе
		
		Если Параметры.ИмяКоманды = "НаправитьИсполнителямЗадачи" Тогда
			
			Элементы.ИсполнительПорученияПоЗадаче.Видимость = Ложь;
			Элементы.ДекорацияИсполнителямЗадачи.Видимость  = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметрыНеСогласовать(Отказ, ЭтоЧастичноеСогласование)

	ШаблонОшибкиСогласующий           = НСтр("ru='Вы не являетесь согласующим ресурса ""%1"" задачи %2.'");
	ШаблонОшибкиНечегоНеСогласовывать = НСтр("ru='Для задачи %1 нет направленных на согласование ресурсов.'");
	ШаблонОшибкиОтменена              = НСтр("ru = 'Задача %1 находится в статусе ""Отменена"".'");
	ШаблонОшибкиПомеченНаУдаление     = НСтр("ru='Задача %1 помечена на удаление.'");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ЗадачиПроцессов.ТекстЗапросаПоЗадачамДляСогласованияТекущихЗадач();
	
	МассивЗадач = Новый Массив;
	МассивЗадач.Добавить(Параметры.Задача);
	Запрос.УстановитьПараметр("МассивЗадач",            МассивЗадач);
	Запрос.УстановитьПараметр("ВидСогласуемогоРесурса", СогласуемыйРесурс);
	Запрос.УстановитьПараметр("ТекущийПользователь",    ТекущийПользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если Выборка.ПометкаУдаления Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ШаблонОшибкиПомеченНаУдаление, Выборка.Представление), Выборка.Задача);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если Выборка.ТекущийСтатус = Перечисления.СтатусыЗадачПроцессов.Отменена Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ШаблонОшибкиОтменена, Выборка.Представление), Выборка.Задача);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ПредставлениеРесурса = ЗадачиПроцессовКлиентСервер.ПредставлениеВидаРесурса(Выборка.НаименованиеРесурса, Выборка.ЕдиницаИзмерения);
		
		Если Не Выборка.ЯвляетсяСогласующим Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ШаблонОшибкиСогласующий, ПредставлениеРесурса, Выборка.Представление), Выборка.Задача);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		КСогласованию = Выборка.КСогласованию;
		ТолькоРесурсыТекущейЗадачи = Истина;
		
		Если КСогласованию = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ШаблонОшибкиНечегоНеСогласовывать, Выборка.Представление), Выборка.Задача);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если Параметры.ИмяКоманды = "НеСогласовать" Тогда
			ТекстПоручения = ЗадачиПроцессовКлиентСервер.ТекстРезультатыСогласования(ПредставлениеРесурса, 0, КСогласованию) + Символы.ПС;
			Заголовок = НСтр("ru = 'Не согласовать и направить исполнителю'");
		Иначе
			
			ПредставлениеКСогласованию                     = Строка(КСогласованию) + " " + Выборка.ЕдиницаИзмерения + ".";
			Согласовать                                    = КСогласованию;
			Элементы.ГруппаЧастичноеСогласование.Видимость = Истина;
			Элементы.ПеренестиРезультатыСогласования.Видимость = Истина;
			Элементы.Согласовать.МаксимальноеЗначение      = КСогласованию;
			Заголовок = НСтр("ru = 'Частично согласовать и направить исполнителю'");
			
		КонецЕсли;
		
		ИсполнительПорученияПоЗадаче = Выборка.Исполнитель;
		
		ТекущийЭлемент = Элементы.ТекстПоручения;
		
	Иначе
		
		Отказ = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметрыОтветить(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗадачиПроцессаПротоколВзаимодействия.Автор
	|ИЗ
	|	Справочник.ЗадачиПроцесса.ПротоколВзаимодействия КАК ЗадачиПроцессаПротоколВзаимодействия
	|ГДЕ
	|	ЗадачиПроцессаПротоколВзаимодействия.Ссылка = &Ссылка
	|	И ЗадачиПроцессаПротоколВзаимодействия.Автор <> &ТекущийПользователь
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗадачиПроцессаПротоколВзаимодействия.НомерСтроки УБЫВ";
	
	Запрос.УстановитьПараметр("Ссылка", Параметры.Задача);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ИсполнительПорученияПоЗадаче = Выборка.Автор;
		
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Ответить'");
	
КонецПроцедуры

#КонецОбласти
