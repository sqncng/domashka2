#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ТекущиеДела

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.Ошибки) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	// Патчи, которые требуется проверить разработчику, исправившему ошибку.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Состояние", Перечисления.СтатусыПатчей.СозданНеПроверен);
	Запрос.УстановитьПараметр("Исправил", ТекущийПользователь);
	Запрос.УстановитьПараметр("ПодписанДляКонфигурации", Справочники.Проекты.ПустаяСсылка());
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВерсииПатчей.Патч КАК Патч,
		|	Патчи.Владелец КАК Проект,
		|	ВерсииПатчей.УникальныйИдентификатор КАК УникальныйИдентификатор
		|ИЗ
		|	РегистрСведений.ВерсииПатчей КАК ВерсииПатчей
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патчи КАК Патчи
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Ошибки КАК Ошибки
		|			ПО Патчи.Ошибка = Ошибки.Ссылка
		|		ПО ВерсииПатчей.Патч = Патчи.Ссылка
		|ГДЕ
		|	ВерсииПатчей.Состояние = &Состояние
		|	И (Ошибки.Исправил = &Исправил
		|			ИЛИ Патчи.АвторИсправления = &Исправил)
		|	И НЕ Патчи.ПометкаУдаления
		|	И ВерсииПатчей.ПодписанДляКонфигурации = &ПодписанДляКонфигурации";
	Результат = Запрос.Выполнить().Выгрузить();
	ДополнительныйКонтрольПоПроектам = Новый Соответствие;
	ПатчиПоАвтору = Новый Массив;
	Для Каждого Строка Из Результат Цикл
		Если ДополнительныйКонтрольПоПроектам[Строка.Проект] = Неопределено Тогда
			СвойстваПроекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.Проект, "ПроверкаПатчаПередПубликацией,АудитПатчаПередПубликацией");
			НуженКонтроль = СвойстваПроекта.ПроверкаПатчаПередПубликацией И СвойстваПроекта.АудитПатчаПередПубликацией;
			ДополнительныйКонтрольПоПроектам.Вставить(Строка.Проект, НуженКонтроль);
		КонецЕсли;
		
		Если ДополнительныйКонтрольПоПроектам[Строка.Проект] Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Роль", "Разработчик");
			Запрос.УстановитьПараметр("ИдентификаторПатча", Строка.УникальныйИдентификатор);
			Запрос.Текст =
				"ВЫБРАТЬ
				|	ПатчиСостоянияПроверкиПатча.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Патчи.СостоянияПроверкиПатча КАК ПатчиСостоянияПроверкиПатча
				|ГДЕ
				|	ПатчиСостоянияПроверкиПатча.Роль = &Роль
				|	И ПатчиСостоянияПроверкиПатча.ИдентификаторПатча = &ИдентификаторПатча";
			Если Запрос.Выполнить().Выгрузить().Количество() = 0
				И ПатчиПоАвтору.Найти(Строка.Патч) = Неопределено Тогда
				ПатчиПоАвтору.Добавить(Строка.Патч);
			КонецЕсли;
		ИначеЕсли ПатчиПоАвтору.Найти(Строка.Патч) = Неопределено Тогда
			ПатчиПоАвтору.Добавить(Строка.Патч);
		КонецЕсли;
		
	КонецЦикла;
	
	// Патчи, которые требуется проверить проверяющему или аудитору.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("ПодписанДляКонфигурации", Справочники.Проекты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Состояние", Перечисления.СтатусыПатчей.СозданНеПроверен);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Патчи.Ссылка КАК Ссылка,
		|	ВерсииПатчей.УникальныйИдентификатор КАК УникальныйИдентификатор,
		|	Патчи.Проверяющий КАК Проверяющий,
		|	Патчи.Аудитор КАК Аудитор
		|ИЗ
		|	Справочник.Патчи КАК Патчи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииПатчей КАК ВерсииПатчей
		|		ПО (ВерсииПатчей.Патч = Патчи.Ссылка)
		|ГДЕ
		|	(Патчи.Проверяющий = &ТекущийПользователь
		|			ИЛИ Патчи.Аудитор = &ТекущийПользователь)
		|	И ВерсииПатчей.Состояние = &Состояние
		|	И НЕ Патчи.ПометкаУдаления
		|	И ВерсииПатчей.ПодписанДляКонфигурации = &ПодписанДляКонфигурации";
	ОжидаютПроверки = Запрос.Выполнить().Выгрузить();
	ТребуютПроверки = Новый Массив;
	Для Каждого ОжидаетПроверки Из ОжидаютПроверки Цикл
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИдентификаторПатча", ОжидаетПроверки.УникальныйИдентификатор);
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПатчиСостоянияПроверкиПатча.Роль КАК Роль
			|ИЗ
			|	Справочник.Патчи.СостоянияПроверкиПатча КАК ПатчиСостоянияПроверкиПатча
			|ГДЕ
			|	ПатчиСостоянияПроверкиПатча.ИдентификаторПатча = &ИдентификаторПатча";
		Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Роль");
		Если Результат.Найти("Разработчик") <> Неопределено Тогда
			Если (ОжидаетПроверки.Проверяющий = ТекущийПользователь
				   И Результат.Найти("Проверяющий") = Неопределено)
				Или (ОжидаетПроверки.Аудитор = ТекущийПользователь
				   И Результат.Найти("Аудитор") = Неопределено) Тогда
				ТребуютПроверки.Добавить(ОжидаетПроверки.Ссылка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
	// Проекты, за которые ответственнен пользователь.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПроектыОтветственныеЗаПатчи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Проекты.ОтветственныеЗаПатчи КАК ПроектыОтветственныеЗаПатчи
		|ГДЕ
		|	ПроектыОтветственныеЗаПатчи.Пользователь = &Пользователь";
	ПроектыПользователя = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	// Патчи, которые необходимо опубликовать.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущиийПользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("Проекты", ПроектыПользователя);
	Запрос.УстановитьПараметр("Состояние", Перечисления.СтатусыПатчей.СозданПроверен);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Патчи.Ссылка КАК Ссылка,
		|	Патчи.Владелец КАК Проект
		|ИЗ
		|	РегистрСведений.ВерсииПатчей КАК ВерсииПатчей
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патчи КАК Патчи
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Ошибки КАК Ошибки
		|			ПО Патчи.Ошибка = Ошибки.Ссылка
		|		ПО ВерсииПатчей.Патч = Патчи.Ссылка
		|ГДЕ
		|	(Патчи.АвторИсправления = &ТекущиийПользователь
		|			ИЛИ Ошибки.Исправил = &ТекущиийПользователь
		|			ИЛИ Патчи.Проверяющий = &ТекущиийПользователь
		|			ИЛИ Патчи.Аудитор = &ТекущиийПользователь
		|			ИЛИ Патчи.Владелец В (&Проекты))
		|	И ВерсииПатчей.Состояние = &Состояние";
	Результат = Запрос.Выполнить().Выгрузить();
	ПатчиДляПубликации = Новый Массив;
	ВозможностьПубликацииПоПроекту = Новый Соответствие;
	Для Каждого Строка Из Результат Цикл
		Если ВозможностьПубликацииПоПроекту[Строка.Проект] = Неопределено Тогда
			ПубликацияДоступна = УправлениеДоступомСППР.РольДоступнаПоПроекту("ПубликацияПатчей", Строка.Проект);
			ВозможностьПубликацииПоПроекту[Строка.Проект] = ПубликацияДоступна;
		КонецЕсли;
		
		Если ВозможностьПубликацииПоПроекту[Строка.Проект]
			И ПатчиДляПубликации.Найти(Строка.Ссылка) = Неопределено Тогда
			ПатчиДляПубликации.Добавить(Строка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПатчиПоАвтору, ТребуютПроверки);
	
	ИдентификаторРаздела = НСтр("ru = 'Патчи'");
	Отбор = Новый Структура;
	Отбор.Вставить("Ссылка", ПатчиПоАвтору);
	
	ОтборОпубликовать = Новый Структура;
	ОтборОпубликовать.Вставить("Ссылка", ПатчиДляПубликации);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ПроверитьПатчи";
	Дело.ЕстьДела       = ПатчиПоАвтору.Количество() > 0;
	Дело.Представление  = НСтр("ru = 'Проверить патчи'");
	Дело.Количество     = ПатчиПоАвтору.Количество();
	Дело.ВыводитьВОповещениях = Истина;
	Дело.Важное         = Истина;
	Дело.ПараметрыФормы = Новый Структура("Отбор", Отбор);
	Дело.Форма          = "Справочник.Патчи.ФормаСписка";
	Дело.Владелец       = ИдентификаторРаздела;
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ОпубликоватьПатчи";
	Дело.ЕстьДела       = ПатчиДляПубликации.Количество() > 0;
	Дело.Представление  = НСтр("ru = 'Опубликовать патчи'");
	Дело.Количество     = ПатчиДляПубликации.Количество();
	Дело.ВыводитьВОповещениях = Истина;
	Дело.Важное         = Истина;
	Дело.ПараметрыФормы = Новый Структура("Отбор", ОтборОпубликовать);
	Дело.Форма          = "Справочник.Патчи.ФормаСписка";
	Дело.Владелец       = ИдентификаторРаздела;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ТекущиеДела

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВариантыОтчетов

// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ОпубликованныеПатчиПоСборкамКонфигураций) Тогда
		Команда = КомандыОтчетов.Добавить();
		Команда.Представление      = НСтр("ru = 'Опубликованные патчи по сборкам конфигурации'");
		Команда.КлючВарианта       = "ОпубликованныеПатчиПоСборкам";
		Команда.Менеджер           = "Отчет.ОпубликованныеПатчиПоСборкамКонфигураций";
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СтатистикаПатчейПоСборкам) Тогда
		Команда = КомандыОтчетов.Добавить();
		Команда.Представление      = НСтр("ru = 'Статистика патчей по сборкам'");
		Команда.КлючВарианта       = "СтатистикаПатчейПоСборкам";
		Команда.Менеджер           = "Отчет.СтатистикаПатчейПоСборкам";
	КонецЕсли;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СтатусПатча(Патч) Экспорт
	
	Публикуется = Новый Массив;
	Публикуется.Добавить(Перечисления.СтатусыПатчей.Публикуется);
	Публикуется.Добавить(Перечисления.СтатусыПатчей.ОжидаетПубликации);
	
	Создание = Новый Массив;
	Создание.Добавить(Перечисления.СтатусыПатчей.ОжиданиеИсправления);
	Создание.Добавить(Перечисления.СтатусыПатчей.СозданиеПатча);
	Создание.Добавить(Перечисления.СтатусыПатчей.СозданиеВручную);
	
	Ошибка = Новый Массив;
	Ошибка.Добавить(Перечисления.СтатусыПатчей.Ошибка);
	Ошибка.Добавить(Перечисления.СтатусыПатчей.ОшибкаОтзыва);
	Ошибка.Добавить(Перечисления.СтатусыПатчей.ОшибкаПубликации);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Опубликован", Перечисления.СтатусыПатчей.Опубликован);
	Запрос.УстановитьПараметр("ТребуетПроверки", Перечисления.СтатусыПатчей.СозданНеПроверен);
	Запрос.УстановитьПараметр("Проверен", Перечисления.СтатусыПатчей.СозданПроверен);
	Запрос.УстановитьПараметр("НеТребуется", Перечисления.СтатусыПатчей.ПатчНеТребуется);
	Запрос.УстановитьПараметр("Публикуется", Публикуется);
	Запрос.УстановитьПараметр("Создание", Создание);
	Запрос.УстановитьПараметр("Ошибка", Ошибка);
	Запрос.УстановитьПараметр("Отозван", Перечисления.СтатусыПатчей.Отозван);
	Запрос.УстановитьПараметр("ПатчСсылка", Патч);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииПатчей.Патч КАК Патч,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние = &Опубликован
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Опубликовано,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние В (&Публикуется)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Публикуется,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние = &ТребуетПроверки
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ТребуютПроверки,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние = &Проверен
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Проверен,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние В (&Создание)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Создается,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние В (&Ошибка)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Ошибка,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние = &НеТребуется
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеТребуется,
		|	СУММА(ВЫБОР
		|			КОГДА ВерсииПатчей.Состояние = &Отозван
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Отозван,
		|	ВерсииПатчей.ПодписанДляКонфигурации КАК ПодписанДляКонфигурации
		|ИЗ
		|	РегистрСведений.ВерсииПатчей КАК ВерсииПатчей
		|ГДЕ
		|	ВерсииПатчей.Патч = &ПатчСсылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВерсииПатчей.Патч,
		|	ВерсииПатчей.ПодписанДляКонфигурации";
	Результат = Запрос.Выполнить().Выгрузить();
	
	ОбщееСостояние = "";
	Для Каждого ПатчИСостояние Из Результат Цикл
		Если ЗначениеЗаполнено(ПатчИСостояние.ПодписанДляКонфигурации) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщееСостояние = "";
		ВсеОпубликованы = Истина;
		Если ПатчИСостояние.Опубликовано <> 0 Тогда
			Состояние = НСтр("ru = 'Опубликовано: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.Опубликовано);
		КонецЕсли;
		
		Если ПатчИСостояние.Публикуется <> 0 Тогда
			Состояние = НСтр("ru = 'Публикуется: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.Публикуется);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ПатчИСостояние.ТребуютПроверки <> 0 Тогда
			Состояние = НСтр("ru = 'Требуется проверка: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.ТребуютПроверки);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ПатчИСостояние.Проверен <> 0 Тогда
			Состояние = НСтр("ru = 'Проверено: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.Проверен);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ПатчИСостояние.Создается <> 0 Тогда
			Состояние = НСтр("ru = 'Создание: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.Создается);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ПатчИСостояние.Ошибка <> 0 Тогда
			Состояние = НСтр("ru = 'Ошибка: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.Ошибка);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ПатчИСостояние.НеТребуется <> 0 Тогда
			Состояние = НСтр("ru = 'Не требуется: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.НеТребуется);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ПатчИСостояние.Отозван <> 0 Тогда
			Состояние = НСтр("ru = 'Отозвано: %1'");
			ДобавитьСостояние(ОбщееСостояние, Состояние, ПатчИСостояние.Отозван);
			ВсеОпубликованы = Ложь;
		КонецЕсли;
		
		Если ВсеОпубликованы Тогда
			ОбщееСостояние = НСтр("ru = 'Опубликовано'");
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбщееСостояние;
КонецФункции

Процедура ДобавитьСостояние(ОбщееСостояние, ДобавляемоеСостояние, Количество)
	ДобавляемоеСостояние = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ДобавляемоеСостояние, Количество);
	Если ЗначениеЗаполнено(ОбщееСостояние) Тогда
		ОбщееСостояние = ОбщееСостояние + ", " + ДобавляемоеСостояние;
	Иначе
		ОбщееСостояние = ДобавляемоеСостояние;
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АвторИсправления", Справочники.Пользователи.ПустаяСсылка());
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Патчи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Патчи КАК Патчи
		|ГДЕ
		|	Патчи.АвторИсправления = &АвторИсправления";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмя = "Справочник.Патчи";
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмя);
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмя);
			Ссылка = Выборка.Ссылка;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
			Блокировка.Заблокировать();
			
			Объект = Ссылка.ПолучитьОбъект();
			Если ТипЗнч(Объект.Ошибка) = Тип("СправочникСсылка.Ошибки") Тогда
				АвторИсправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ошибка, "Исправил"); 
				Объект.АвторИсправления = АвторИсправления;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать патч %1 по причине:
					|%2'"), 
				Выборка.Ссылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения, Выборка.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмя);
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать некоторые патчи (пропущены): %1'"), 
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Обработана очередная порция патчей: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
