#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СвойстваПатча  = ПатчиСлужебный.ЗначенияСвойствВерсииПатча(Параметры.ИдентификаторПатча, "Параметры, Состояние");
	Пересоздание   = Ложь;
	Если СвойстваПатча.Состояние = Перечисления.СтатусыПатчей.ОжиданиеИсправления Или Параметры.УстановкаКоммитов Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Коммиты для сборки патча версии %1'"),
			Параметры.Версия);
		Элементы.ГруппаПереключатели.Видимость = Ложь;
		Элементы.Пояснение.Видимость = Истина;
		Элементы.ФормаПересоздатьПатч.Видимость = Параметры.УстановкаКоммитов
			И СвойстваПатча.Состояние <> Перечисления.СтатусыПатчей.ОжиданиеИсправления;
		Элементы.ФормаСохранить.КнопкаПоУмолчанию = Не Элементы.ФормаПересоздатьПатч.Видимость;
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Пересоздание патча для версии %1'"),
			Параметры.Версия);
		Элементы.ФормаСохранить.Видимость = Ложь;
		Пересоздание = Истина;
	КонецЕсли;
	
	Если СвойстваПатча.Состояние = Перечисления.СтатусыПатчей.Отозван Или СвойстваПатча.Состояние = Перечисления.СтатусыПатчей.Опубликован Тогда
		Элементы.Коммиты.Доступность = Ложь;
		Элементы.ФормаПересоздатьПатч.Доступность = Ложь;
		Элементы.ФормаСохранить.Доступность = Ложь;
	КонецЕсли;
	
	ПараметрыПатча = ПатчиСлужебный.ПараметрыПатча(СвойстваПатча.Параметры.Получить());
	
	Если ПараметрыПатча.КоммитыИсправления.Количество() = 0 И Пересоздание Тогда
		ПолеПереключателя = "ПоХранилищу";
		Элементы.Коммиты.Доступность = Ложь;
	Иначе
		ПолеПереключателя = "ПоУказаннымКоммитам";
		ТекущийЭлемент = Элементы.Вручную;
	КонецЕсли;
	Коммиты = СтрСоединить(ПараметрыПатча.КоммитыИсправления, ", ");
	ИдентификаторПатча = Параметры.ИдентификаторПатча;
	Версия = Параметры.Версия;
	Патч   = Параметры.Патч;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере(Ложь);
	Закрыть(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПересоздатьПатч(Команда)
	СохранитьНаСервере(Истина);
	Закрыть(Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНаСервере(Пересоздать)
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ВерсииПатчей");
	ЭлементБлокировкиДанных.УстановитьЗначение("Патч", Патч);
	ЭлементБлокировкиДанных.УстановитьЗначение("Версия", Версия);
	ЭлементБлокировкиДанных.УстановитьЗначение("УникальныйИдентификатор", ИдентификаторПатча);
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных.Заблокировать();
		
		Набор = РегистрыСведений.ВерсииПатчей.СоздатьНаборЗаписей();
		Набор.Отбор.Патч.Установить(Патч);
		Набор.Отбор.Версия.Установить(Версия);
		Набор.Отбор.УникальныйИдентификатор.Установить(ИдентификаторПатча);
		Набор.Прочитать();
		
		Запись = Набор[0];
		ПараметрыПатча = ПатчиСлужебный.ПараметрыПатча(Запись.Параметры.Получить());
		ПараметрыПатча.КоммитыИсправления = КоммитыИсправления();
		Запись.Параметры = Новый ХранилищеЗначения(ПараметрыПатча);
		
		Набор.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если Пересоздать Тогда
		ПараметрыПатча = Новый Структура;
		ПараметрыПатча.Вставить("Патч", Патч);
		ПараметрыПатча.Вставить("Версия", Версия);
		ПараметрыПатча.Вставить("УникальныйИдентификатор", ИдентификаторПатча);
		ПатчиСлужебный.ПересоздатьПатч(ПараметрыПатча);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция КоммитыИсправления()
	
	Если ПолеПереключателя = "ПоХранилищу" Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	КоммитыИсправления = Новый Массив;
	Для Каждого Коммит Из СтрРазделить(Коммиты, ", " + Символы.ПС, Ложь) Цикл
		КоммитыИсправления.Добавить(СокрЛП(Коммит));
	КонецЦикла;
	
	Возврат КоммитыИсправления;
	
КонецФункции

&НаКлиенте
Процедура ПереключательПриИзменении(Элемент)
	Если ПолеПереключателя = "ПоХранилищу" Тогда
		Элементы.Коммиты.Доступность = Ложь;
	Иначе
		Элементы.Коммиты.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти