#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторыПатчей = Параметры.ИдентификаторыПатчей;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("Подключаемый_ВыгрузитьРасширенияВФайлы", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ВыгрузитьРасширенияВФайлы()
	ДлительнаяОперация = ЗапускВыгрузкиРасширенийВФайлы();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапускВыгрузкиРасширенийВФайлы()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выгрузка патчей в файлы для сравнения.'");
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "ПатчиСлужебный.ИсходныеФайлыРасширений", ИдентификаторыПатчей);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если Результат.Статус = "Отменено" Тогда
		ВызватьИсключение НСтр("ru = 'Задание было отменено.'");
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ВыгруженныеПатчи = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
#Если Не ВебКлиент Тогда
	Для Каждого ВыгрузкаПатча Из ВыгруженныеПатчи Цикл
		ПапкаВыгрузки = ПолучитьИмяВременногоФайла(ВыгрузкаПатча.ИмяПатча + "_" + ВыгрузкаПатча.Версия);
		СоздатьКаталог(ПапкаВыгрузки);
		ПутьКАрхиву = ПолучитьИмяВременногоФайла(".zip");
		ВыгрузкаПатча.Архив.Записать(ПутьКАрхиву);
		
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ПутьКАрхиву);
		ЧтениеZipФайла.ИзвлечьВсе(ПапкаВыгрузки);
		
		Текст = ВыгрузкаПатча.Версия + ":" + Символы.ПС + ПапкаВыгрузки + Символы.ПС;
		ПолеПутиКВыгрузкам = ПолеПутиКВыгрузкам + Текст;
	КонецЦикла;
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти