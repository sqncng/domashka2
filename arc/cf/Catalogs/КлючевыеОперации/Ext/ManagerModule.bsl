#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует перечень печатных форм, используемых для выода описания и сравнения версий объектов.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, для которых строятся печатные формы.
//
// Возвращаемое значение:
//  Массив - перечень табличных документов печатных форм.
//
Функция СформироватьПечатныеФормы(МассивОбъектов, СУчетомПриемника=Ложь, ДанныеСоответствия=Неопределено) Экспорт
	
	ПечатныеФормы = Новый Соответствие;
	
	Запрос = Новый Запрос(ТекстЗапросаДляФормированияОписания());
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Документ = Новый ТабличныйДокумент;
		ОписаниеОбъектов.НастроитьОписаниеОбъекта(Документ);
		
		Документ.НачатьАвтогруппировкуСтрок();
		СформироватьОписаниеОбъекта(Выборка, Документ);
		Документ.ЗакончитьАвтогруппировкуСтрок();
		
		СтруктураПечатныхФорм = Новый Структура;
		СтруктураПечатныхФорм.Вставить("Описание", Документ);
		
		ПечатныеФормы.Вставить(Выборка.Ссылка, СтруктураПечатныхФорм);
	КонецЦикла;
	
	Возврат ПечатныеФормы;
	
КонецФункции

// Выполняет печать описаний и схем переданных функций
//
// Параметры:
//  МассивОбъектов - массив функций, подлежащих печати
//  МассивСтруктурПечатныхФорм - Массив, перечень табличных документов печатных форм.
//
Процедура Печать(МассивОбъектов, ПечатныеФормы) Экспорт
	
	ПечатныеФормы = СформироватьПечатныеФормы(МассивОбъектов);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец.Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДляФормированияОписания()
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КлючевыеОперации.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(КлючевыеОперации.Владелец) КАК Владелец,
		|	КлючевыеОперации.Наименование КАК Наименование,
		|	КлючевыеОперации.Имя КАК Имя,
		|	КлючевыеОперации.Описание КАК Описание,
		|	ПРЕДСТАВЛЕНИЕ(КлючевыеОперации.ВидОперации) КАК ВидОперации,
		|	КлючевыеОперации.ЦелевоеВремя КАК ЦелевоеВремя,
		|	КлючевыеОперации.Утверждено КАК Утверждено,
		|	КлючевыеОперации.Важность КАК Важность
		|ИЗ
		|	Справочник.КлючевыеОперации КАК КлючевыеОперации
		|ГДЕ
		|	КлючевыеОперации.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	КлючевыеОперации.Имя";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьОписаниеОбъекта(Выборка, Документ)
	
	ОписаниеОбъектов.ВывестиЗаголовокОбъекта(НСтр("ru='Ключевая операция'"), Выборка.Ссылка, , Документ);
	ОписаниеОбъектов.ВывестиЗаголовокТекста("", , Документ);
	
	ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Реквизиты'"), Документ);
	
	Шаблон = НСтр("ru='Объект метаданных: %1'");
	Текст = СтрШаблон(Шаблон, Выборка.Владелец);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Текст, 1, Документ);
	
	Шаблон = НСтр("ru='Вид операции: %1'");
	Текст = СтрШаблон(Шаблон, Выборка.ВидОперации);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Текст, 1, Документ);
	
	Шаблон = НСтр("ru='Целевое время: %1'");
	Текст = СтрШаблон(Шаблон, Выборка.ЦелевоеВремя);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Текст, 1, Документ);
	
	Шаблон = НСтр("ru='Важность: %1'");
	Текст = СтрШаблон(Шаблон, Выборка.Важность);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Текст, 1, Документ);
	
	Шаблон = НСтр("ru='Утверждена: %1'");
	Текст = СтрШаблон(Шаблон, Выборка.Утверждено);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Текст, 1, Документ);
	
	Шаблон = НСтр("ru='Описание: %1'");
	Текст = СтрШаблон(Шаблон, Выборка.Описание);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Текст, 1, Документ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
