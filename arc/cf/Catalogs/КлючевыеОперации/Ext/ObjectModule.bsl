#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты = Неопределено)
	
	Перем Проверено;
	
	Если Не ДополнительныеСвойства.Свойство("ПроверкаЗаполненияВыполнена", Проверено) Или Не Проверено Тогда
		Если ПроверитьИзмененияСУчетомПризнакаУтверждено(Отказ) И Не ПометкаУдаления Тогда
			ПроверитьИмя(Отказ);
			ПроверитьНаличиеДублирующихсяКлючевыхОпераций(Отказ);
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ПроверкаЗаполненияВыполнена", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = Имя;
	ОбработкаПроверкиЗаполнения(Отказ);
	
	КоллекцияИзмененныхОбъектов = Новый Массив;
	ДополнительныеСвойства.Вставить("КоллекцияИзмененныхОбъектов", КоллекцияИзмененныхОбъектов);
	Версионирование.ЗарегистрироватьИзмененияОбъекта(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Утверждено = Ложь;
	Важность = КлючевыеОперацииКлиентСервер.ВажностьПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьИмя(Отказ)
	
	Если СтрНайти(Сред(Имя, 2, СтрДлина(Имя) - 2), "%") > 0 Тогда
		ШаблонОшибки = НСтр("ru = 'Символы шаблона ""%"" разрешено размещать только в начале и в конце имени ключевой операции. Имя ""%1"" недопустимо.'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Имя, Владелец);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Имя",, Отказ);
		ЗаписатьВЖурналИнформациюОбОшибке(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеДублирующихсяКлючевыхОпераций(Отказ)
	
	Если КлючевыеОперации.ЕстьДублирующиеся(Имя, Ссылка) Тогда
		ШаблонОшибки = НСтр("ru = 'Укажите уникальное имя ключевой операции. В информационной базе уже присутствует ключевая операция с именем ""%1"".'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Имя);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Имя",, Отказ);
		ЗаписатьВЖурналИнформациюОбОшибке(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьВЖурналИнформациюОбОшибке(ТекстОшибки)
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ИмяСобытия = НСтр("ru = 'Ключевые операции. Создание ключевой операции'", КодЯзыка);
	ЗаписьЖурналаРегистрации(ИмяСобытия,
		УровеньЖурналаРегистрации.Предупреждение,
		Метаданные.Справочники.КлючевыеОперации,
		Ссылка,
		ТекстОшибки);
	
КонецПроцедуры

Функция ПроверитьИзмененияСУчетомПризнакаУтверждено(Отказ)
	
	Перем ТекстОшибки;
	
	Если Не ЭтоНовый() И Модифицированность() Тогда
		БылоУтверждено = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Утверждено");
		
		Если Не КлючевыеОперации.УправлениеДоступно() Тогда
			Если БылоУтверждено Тогда
				ШаблонОшибки = НСтр("ru = 'Вам запрещено изменять ключевую операцию ""%1"", т.к. она утверждена.'");
				ТекстОшибки = СтрШаблон(ШаблонОшибки, Имя);
			ИначеЕсли БылоУтверждено <> Утверждено Тогда
				ТекстОшибки = НСтр("ru = 'Вам запрещено изменять признак ""Утверждено"".'");
			КонецЕсли;
			
			Если Не ПустаяСтрока(ТекстОшибки) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
				ЗаписатьВЖурналИнформациюОбОшибке(ТекстОшибки);
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли
