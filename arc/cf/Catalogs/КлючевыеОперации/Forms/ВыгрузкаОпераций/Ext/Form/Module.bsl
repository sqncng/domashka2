#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ВидВыгрузки = КлючевыеОперацииКлиентСервер.ВидВыгрузкиДляЦентраКонтроляКачества() Тогда
		Заголовок = НСтр("ru = 'Выгрузка ключевых операций для Центра контроля качества'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Владелец.Владелец",
		ПараметрыСеанса.ТекущийПроект,
		ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПроект));
	
	КлючевыеОперации.УстановитьЭлементуФормыОтборПоПроекту(Элементы.Подсистема);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаВыгрузка И ПустаяСтрока(Выгрузка) Тогда
		ОтборПоПодсистеме = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0];
		Подсистема = ?(ОтборПоПодсистеме.Использование, ОтборПоПодсистеме.ПравоеЗначение, Неопределено);
		Выгрузка = ТекстВыгрузки(Параметры.ВидВыгрузки, Подсистема);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсистемаПриИзменении(Элемент)
	
	УстановитьИспользованиеОтбора(Список, 0, Ложь, Выгрузка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	УстановитьИспользованиеОтбора(Список, 2, Ложь, Выгрузка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	УстановитьИспользованиеОтбора(Список, 1, Ложь, Выгрузка, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ПолучитьФайл(АдресВыгрузки(), "КлючевыеОперации.xml");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьИспользованиеОтбора(Список, Индекс, Использование, Выгрузка, Обязательно = Ложь)
	
	Выгрузка = "";
	Элемент = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[Индекс];
	
	Если Обязательно Или Не ЗначениеЗаполнено(Элемент.ПравоеЗначение) Тогда
		Элемент.Использование = Использование;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресВыгрузки()
	
	ОтборПоПодсистеме = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0];
	Подсистема = ?(ОтборПоПодсистеме.Использование, ОтборПоПодсистеме.ПравоеЗначение, Неопределено);
	
	Если Не ЗначениеЗаполнено(Выгрузка) Тогда
		Выгрузка = ТекстВыгрузки(Параметры.ВидВыгрузки, Подсистема);
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(Выгрузка, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция ТекстВыгрузки(ВидВыгрузки, Подсистема)
	
	Возврат КлючевыеОперации.КлючевыеОперацииВСтрокуXML(ВидВыгрузки,
		Подсистема,
		Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[2].ПравоеЗначение);
	
КонецФункции

#КонецОбласти
