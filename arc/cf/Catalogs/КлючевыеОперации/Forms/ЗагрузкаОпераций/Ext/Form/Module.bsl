#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьТекущийПроект();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		УстановитьТекущийПроект();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если ЗначениеЗаполнено(ТекущийПроект) Тогда
		Заголовок = НСтр("ru = 'Загрузка ключевых операций из основного хранилища проекта'");
		ШаблонВопроса = НСтр("ru = 
			|'ВНИМАНИЕ:
			|- Процесс загрузки ключевых операций из основного хранилища может быть длительным.
			|
			|Выполнить загрузку ключевых операций из основного хранилища проекта ""%1"" сейчас?'");
		ТекстВопроса = СтрШаблон(ШаблонВопроса, ТекущийПроект);
		ОбработчикОтветаНаВопрос = "ПриОтветеНаВопросЗагрузкиОперацийИзХранилища";
		ОписаниеОповещения = Новый ОписаниеОповещения(ОбработчикОтветаНаВопрос, ЭтаФорма, ТекущийПроект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, Заголовок);
	Иначе
		ТекстСообщения = НСтр("ru = 'Выберите вначале текущий проект, а затем повторите загрузку.'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ЗначениеЗаполнено(ТекущийПроект) Тогда
		ДлительнаяОперация = СохранитьКлючевыеОперации(ТекущийПроект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("СохранитьКлючевыеОперацииЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ТекстСообщения = НСтр("ru = 'Выберите вначале текущий проект, а затем повторите сохранение.'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьКлючевыеОперацииИзХранилищаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда 
		Если Результат.Статус = "Ошибка" Тогда
			Если Не ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
				ЖурналВыполнения = Результат.КраткоеПредставлениеОшибки;
			ИначеЕсли Не ПустаяСтрока(Результат.АдресРезультата) Тогда
				Значение = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
				Если ТипЗнч(Значение) = Тип("ТекстовыйДокумент") Тогда
					ЖурналВыполнения = Значение.ПолучитьТекст();
				КонецЕсли;
			КонецЕсли;
			
			Если Не ПустаяСтрока(ЖурналВыполнения) Тогда
				ШаблонСообщения = НСтр(
					"ru = '%1
					|Подробности в журнале регистрации.'");
				ПоказатьПредупреждение(, СтрШаблон(ШаблонСообщения, ЖурналВыполнения));
			КонецЕсли;
		Иначе
			УстановитьРезультатПоиска(Результат.АдресРезультата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтветеНаВопросЗагрузкиОперацийИзХранилища(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДлительнаяОперация = ЗагрузитьКлючевыеОперацииИзХранилища(ДополнительныеПараметры);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьКлючевыеОперацииИзХранилищаЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКлючевыеОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЖурналВыполнения = "";
	Оповестить("ИзмененыКлючевыеОперации");
	
	Если Результат <> Неопределено И Результат.Статус = "Ошибка" Тогда
		Если Не ПустаяСтрока(Результат.ПодробноеПредставлениеОшибки) Тогда
			ЖурналВыполнения = Результат.ПодробноеПредставлениеОшибки;
		КонецЕсли;
	ИначеЕсли Не ПустаяСтрока(Результат.АдресРезультата) Тогда
		Значение = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТипЗнч(Значение) = Тип("ТекстовыйДокумент") Тогда
			ЖурналВыполнения = Значение.ПолучитьТекст();
		Иначе
			ЖурналВыполнения = Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ЖурналВыполнения) Тогда
		ПоказатьЗначение(, ЖурналВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРезультатПоиска(АдресРезультата)
	
	Таблица = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Таблица) = Тип("ТаблицаЗначений") Тогда
		ЗначениеВРеквизитФормы(Таблица, "Результат");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущийПроект()
	
	ТекущийПроект = ПараметрыСеанса.ТекущийПроект;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьКлючевыеОперацииИзХранилища(Проект)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Проект", Проект);
	
	ШаблонНаименования = НСтр("ru = 'Загрузка ключевых операций из хранилища проекта ""%1""'");
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(ШаблонНаименования, Проект);
	ПараметрыВыполнения.КлючФоновогоЗадания = Строка(Проект.УникальныйИдентификатор());
	
	ИмяПроцедуры = "КлючевыеОперации.ЗагрузитьКлючевыеОперацииИзХранилищаПроекта";
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаСервере
Функция СохранитьКлючевыеОперации(Проект)
	
	ТаблицаОпераций = РеквизитФормыВЗначение("Результат");
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Сохранение ключевых операций'");
	ПараметрыВыполнения.КлючФоновогоЗадания = Строка(Проект.УникальныйИдентификатор());
	
	ИмяПроцедуры = "КлючевыеОперации.СоздатьКлючевыеОперации";
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ТаблицаОпераций, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти
