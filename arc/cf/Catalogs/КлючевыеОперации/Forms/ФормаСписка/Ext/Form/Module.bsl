#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеСписка();
	УстановитьОтборПоПроектуСервер();
	УстановитьОсновныеОтборы();
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
		"ТекущийПользователь",
		Пользователи.ТекущийПользователь());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		УстановитьОтборПоПроектуСервер();
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененыКлючевыеОперации" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОбъектМетаданныхПриИзменении(Элемент)
	
	ОтключитьИспользованиеОтбора(0);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРазделПроектаПриИзменении(Элемент)
	
	ОтключитьИспользованиеОтбора(1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьОперацииДляЦКК(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидВыгрузки", КлючевыеОперацииКлиентСервер.ВидВыгрузкиДляЦентраКонтроляКачества());
	ОткрытьФорму("Справочник.КлючевыеОперации.Форма.ВыгрузкаОпераций", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОперацииИзХранилища(Команда)
	
	ОткрытьФорму("Справочник.КлючевыеОперации.Форма.ЗагрузкаОпераций",, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УтверждениеСнять(Команда)
	
	УтверждениеУстановитьНаКлиенте(Элементы.Список.ВыделенныеСтроки, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УтверждениеУстановить(Команда)
	
	УтверждениеУстановитьНаКлиенте(Элементы.Список.ВыделенныеСтроки, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтключитьИспользованиеОтбора(Индекс)
	
	Элемент = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[Индекс];
	
	Если Не ЗначениеЗаполнено(Элемент.ПравоеЗначение) Тогда
		Элемент.Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УтвердитьКлючевыеОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат.Статус = "Ошибка" Тогда
		ШаблонСообщения = НСтр(
			"ru = 'Не удалось изменить значение признака ""Утверждено"".
			|
			|%1
			|
			|Подробнее в журнале регистрации.'");
		ПоказатьПредупреждение(, СтрШаблон(ШаблонСообщения, Результат.КраткоеПредставлениеОшибки));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПроектуСервер()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Владелец.Владелец",
		ПараметрыСеанса.ТекущийПроект,
		ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПроект));
	
	КлючевыеОперации.УстановитьЭлементуФормыОтборПоПроекту(Элементы.ОтборОбъектМетаданных);
	КлючевыеОперации.УстановитьЭлементуФормыОтборПоПроекту(Элементы.ОтборРазделПроекта);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОсновныеОтборы()
	
	Если ЗначениеЗаполнено(Параметры.Отбор) Тогда
		Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки.Видимость = Ложь;
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Владелец");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Владелец.РазделПроекта");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	УсловноеОформлениеСписка = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеСписка.Элементы.Очистить();
	
	ОбщегоНазначенияСППР.УстановитьУсловноеОформлениеСпискаОтветственный(УсловноеОформлениеСписка);
	КонтрольИзменений.УстановитьУсловноеОформлениеСпискаСостояниеКонтроля(УсловноеОформлениеСписка);
	
КонецПроцедуры

&НаКлиенте
Процедура УтверждениеУстановитьНаСервере(МассивОпераций, Значение)
	
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.ЗагрузитьЗначения(МассивОпераций);
	ДлительнаяОперация = УтвердитьКлючевыеОперации(СписокОпераций, Значение);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("УтвердитьКлючевыеОперацииЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура УтверждениеУстановитьНаКлиенте(СписокОпераций, Значение)
	
	Если СписокОпераций.Количество() > 0 Тогда
		УтверждениеУстановитьНаСервере(СписокОпераций, Значение);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УтвердитьКлючевыеОперации(СписокОпераций, Значение)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Операции", СписокОпераций);
	ПараметрыПроцедуры.Вставить("Значение", Значение);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Утверждение ключевых операций'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("КлючевыеОперации.Утвердить", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти
