#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ИзучатьПовторноПриИзменении(Элемент)
	
	Если Не Параметры.НесколькоПользователей Тогда
		ЗагрузитьСтандарты(ПереданныеСтандарты, Параметры.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	МассивПользователей = Новый Массив;
	
	Если Параметры.НесколькоПользователей Тогда
		ДобавитьПользователей(МассивПользователей, ПользователиГруппы.ПолучитьЭлементы());
	Иначе
		МассивПользователей.Добавить(Параметры.Пользователь);
	КонецЕсли;
	
	СохранитьТребования(МассивПользователей);
	Оповестить("ОбновлениеСтандартовПользователей", Параметры.Пользователь);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПользователей(Команда)
	
	Если Элементы.Пользователи.ТекущиеДанные = Неопределено Тогда
		ТекущаяСтрока = Неопределено;
	Иначе
		ТекущаяСтрока = Элементы.Пользователи.ТекущиеДанные.Пользователь;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ТекущаяСтрока);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("РасширенныйПодбор", Истина);
	ПараметрыФормы.Вставить("ПараметрыРасширеннойФормыПодбора", ПараметрыРасширеннойФормыПодбора());
	
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, Элементы.Пользователи);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимПодбора = Истина;
	Элементы.СтраницаПользователи.Видимость = Параметры.НесколькоПользователей;
	Элементы.СтандартыСостояние.Видимость = Не Параметры.НесколькоПользователей;
	
	Если Не Параметры.НесколькоПользователей И Не ЗначениеЗаполнено(Параметры.Пользователь) Тогда
		Параметры.Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ПереданныеСтандарты = Новый ФиксированныйМассив(Параметры.Стандарты);
	ЗагрузитьСтандарты(ПереданныеСтандарты, Параметры.Пользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура СтандартыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Стандарты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		СтандартыРазработкиКлиент.ОткрытьСтандарт(ТекущиеДанные.Стандарт);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область БСП

&НаСервере
Процедура ОбновитьПользователейГрупп(ИдентификаторСтроки = Неопределено,
                                     ИзмененныеСтроки = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	ИзмененныеСтроки = Новый Массив;
	
	Если ИдентификаторСтроки = Неопределено Тогда
		ЭлементыКоллекции = ПользователиГруппы.ПолучитьЭлементы();
		
	ИначеЕсли ТипЗнч(ИдентификаторСтроки) = Тип("Массив") Тогда
		ЭлементыКоллекции = Новый Массив;
		Для каждого Идентификатор Из ИдентификаторСтроки Цикл
			ЭлементыКоллекции.Добавить(ПользователиГруппы.НайтиПоИдентификатору(Идентификатор));
		КонецЦикла;
	Иначе
		ЭлементыКоллекции = Новый Массив;
		ЭлементыКоллекции.Добавить(ПользователиГруппы.НайтиПоИдентификатору(ИдентификаторСтроки));
	КонецЕсли;
	
	УчастникиГруппыПользователей = Новый Массив;
	Для каждого Элемент Из ЭлементыКоллекции Цикл
		
		Если ТипЗнч(Элемент.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей")
		 ИЛИ ТипЗнч(Элемент.Пользователь) = Тип("СправочникСсылка.ГруппыВнешнихПользователей") Тогда
		
			УчастникиГруппыПользователей.Добавить(Элемент.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчастникиГруппыПользователей", УчастникиГруппыПользователей);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоставыГруппПользователей.ГруппаПользователей,
	|	СоставыГруппПользователей.Пользователь
	|ИЗ
	|	РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
	|ГДЕ
	|	СоставыГруппПользователей.ГруппаПользователей В(&УчастникиГруппыПользователей)
	|	И СоставыГруппПользователей.Пользователь.Недействителен <> ИСТИНА";
	
	ПользователиГрупп = Запрос.Выполнить().Выгрузить();
	ПользователиГрупп.Индексы.Добавить("ГруппаПользователей");
	
	Для каждого Элемент Из ЭлементыКоллекции Цикл
		Элемент.Ссылка = Элемент.Пользователь;
		
		Если ТипЗнч(Элемент.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей")
		 ИЛИ ТипЗнч(Элемент.Пользователь) = Тип("СправочникСсылка.ГруппыВнешнихПользователей") Тогда
		
			// Заполнение пользователей группы.
			СтарыеПользователи = Элемент.ПолучитьЭлементы();
			Отбор = Новый Структура("ГруппаПользователей", Элемент.Пользователь);
			НовыеПользователи = ПользователиГрупп.НайтиСтроки(Отбор);
			
			ЕстьИзменения = Ложь;
			
			Если СтарыеПользователи.Количество() <> НовыеПользователи.Количество() Тогда
				СтарыеПользователи.Очистить();
				Для каждого Строка Из НовыеПользователи Цикл
					НовыйЭлемент = СтарыеПользователи.Добавить();
					НовыйЭлемент.Ссылка       = Строка.Пользователь;
					НовыйЭлемент.Пользователь = Строка.Пользователь;
				КонецЦикла;
				ЕстьИзменения = Истина;
			Иначе
				Индекс = 0;
				Для каждого Строка Из СтарыеПользователи Цикл
					
					Если Строка.Ссылка       <> НовыеПользователи[Индекс].Пользователь
					 ИЛИ Строка.Пользователь <> НовыеПользователи[Индекс].Пользователь Тогда
						
						Строка.Ссылка       = НовыеПользователи[Индекс].Пользователь;
						Строка.Пользователь = НовыеПользователи[Индекс].Пользователь;
						ЕстьИзменения = Истина;
					КонецЕсли;
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
			
			Если ЕстьИзменения Тогда
				ИзмененныеСтроки.Добавить(Элемент.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Пользователи.ЗаполнитьНомераКартинокПользователей(
		ПользователиГруппы, "Ссылка", "НомерКартинки", ИдентификаторСтроки, Истина);
	
	// Установка отображения дерева.
	ЕстьДерево = Ложь;
	Для каждого Элемент Из ПользователиГруппы.ПолучитьЭлементы() Цикл
		Если Элемент.ПолучитьЭлементы().Количество() > 0 Тогда
			ЕстьДерево = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Элементы.Пользователи.Отображение = ?(ЕстьДерево, ОтображениеТаблицы.Дерево, ОтображениеТаблицы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЕстьИзменения = Ложь;
	ПользователиГруппы.ПолучитьЭлементы().Очистить();
	ИзмененныеСтроки = Новый Массив;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для каждого Значение Из ВыбранноеЗначение Цикл
			ЗначениеНеНайдено = Истина;
			Для каждого Элемент Из ПользователиГруппы.ПолучитьЭлементы() Цикл
				Если Элемент.Пользователь = Значение Тогда
					ЗначениеНеНайдено = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ЗначениеНеНайдено Тогда
				НовыйЭлемент = ПользователиГруппы.ПолучитьЭлементы().Добавить();
				НовыйЭлемент.Пользователь = Значение;
				ИзмененныеСтроки.Добавить(НовыйЭлемент.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Элемент.ТекущиеДанные.Пользователь <> ВыбранноеЗначение Тогда
		Элемент.ТекущиеДанные.Пользователь = ВыбранноеЗначение;
		ИзмененныеСтроки.Добавить(Элемент.ТекущаяСтрока);
	КонецЕсли;
	
	Если ИзмененныеСтроки.Количество() > 0 Тогда
		ОбновленныеСтроки = Неопределено;
		ОбновитьПользователейГрупп(ИзмененныеСтроки, ОбновленныеСтроки);
		Для каждого ИдентификаторСтроки Из ОбновленныеСтроки Цикл
			Элементы.Пользователи.Развернуть(ИдентификаторСтроки);
		КонецЦикла;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРасширеннойФормыПодбора()
	
	ЭлементыКоллекции = ПользователиГруппы.ПолучитьЭлементы();
	
	ВыбранныеПользователи = Новый ТаблицаЗначений;
	ВыбранныеПользователи.Колонки.Добавить("Пользователь");
	ВыбранныеПользователи.Колонки.Добавить("НомерКартинки");
	
	Для каждого Элемент Из ЭлементыКоллекции Цикл
		
		СтрокаВыбранныеПользователи = ВыбранныеПользователи.Добавить();
		СтрокаВыбранныеПользователи.Пользователь = Элемент.Пользователь;
		СтрокаВыбранныеПользователи.НомерКартинки = Элемент.НомерКартинки;
		
	КонецЦикла;
	
	ЗаголовокФормыПодбора = НСтр("ru = 'Подбор участников группы доступа'");
	ПараметрыРасширеннойФормыПодбора = Новый Структура;
	ПараметрыРасширеннойФормыПодбора.Вставить("ЗаголовокФормыПодбора", ЗаголовокФормыПодбора);
	ПараметрыРасширеннойФормыПодбора.Вставить("ВыбранныеПользователи", ВыбранныеПользователи);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ПараметрыРасширеннойФормыПодбора);
	Возврат АдресХранилища;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ДобавитьПользователей(МассивПользователей, МассивЭлементов)
	
	Для каждого ЭлементМассива Из МассивЭлементов Цикл
		ВложенныеЭлементы = ЭлементМассива.ПолучитьЭлементы();
		
		Если ВложенныеЭлементы.Количество() > 0 Тогда
			ДобавитьПользователей(МассивПользователей, ВложенныеЭлементы);
		Иначе
			МассивПользователей.Добавить(ЭлементМассива.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСтандарты(МассивСтандартов, Пользователь)
	
	Если МассивСтандартов.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(Пользователь) Тогда
			Состояния = Новый Массив;
			Состояния.Добавить(Перечисления.СостоянияСтандартов.Новый);
			Состояния.Добавить(Перечисления.СостоянияСтандартов.ТребуетИзучения);
			
			Если ИзучатьПовторно Тогда
				Состояния.Добавить(Перечисления.СостоянияСтандартов.Изучен);
			КонецЕсли;
			
			Запрос = СтандартыРазработки.ЗапросСтандартовПоСостоянию(МассивСтандартов, Состояния, Пользователь);
		Иначе
			Запрос = СтандартыРазработки.ЗапросСтандартов(МассивСтандартов);
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ВыбранныеСтандарты.Очистить();
		
		Пока Выборка.Следующий() Цикл
			СтрокаСтандарта = ВыбранныеСтандарты.Добавить();
			СтрокаСтандарта.Стандарт = Выборка.Стандарт;
			СтрокаСтандарта.ДатаИзменения = Выборка.ДатаИзменения;
			СтрокаСтандарта.Картинка = Выборка.Картинка;
			
			Если Не Параметры.НесколькоПользователей Тогда
				СтрокаСтандарта.Состояние = Выборка.Состояние;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТребования(МассивПользователей)
	
	Состояния = Новый Массив;
	Состояния.Добавить(Перечисления.СостоянияСтандартов.Новый);
	Состояния.Добавить(Перечисления.СостоянияСтандартов.ТребуетИзучения);
	
	Для каждого ТекущийПользователь Из МассивПользователей Цикл
		Если Не ИзучатьПовторно И Параметры.НесколькоПользователей Тогда
			Запрос = СтандартыРазработки.ЗапросСтандартовПоСостоянию(ПереданныеСтандарты, Состояния, ТекущийПользователь);
			МассивСтандартов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Стандарт");
		Иначе
			МассивСтандартов = ВыбранныеСтандарты.Выгрузить(, "Стандарт").ВыгрузитьКолонку("Стандарт");
		КонецЕсли;
		
		Для каждого Стандарт Из МассивСтандартов Цикл
			СтандартыРазработки.УстановитьСостояниеСтандарта(ТекущийПользователь,
				Стандарт,
				Перечисления.СостоянияСтандартов.ТребуетИзучения,
				ПричинаИзучения);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
