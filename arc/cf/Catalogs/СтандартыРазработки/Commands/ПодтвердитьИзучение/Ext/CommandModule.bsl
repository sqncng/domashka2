#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Заголовок = НСтр("ru = 'Подтверждение изучения стандартов'");
	ТекстВопроса = НСтр("ru = 
		|'Подтверждая изучение выбранных стандартов, Вы обязуетесь соблюдать их при разработке.
		|Рекомендуется внимательно ознакомиться с выбранными стандартами, прежде чем подтверждать их изучение.
		|
		|СППР отслеживает, как нарушенные, так и не изученные вами стандарты разработки.
		|Старайтесь своевременно изучать новые и измененные стандарты разработки и не нарушать их.
		|
		|Подтверждаете изучение выбранных стандартов?'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьИзучениеОтвет",
		ЭтотОбъект,
		ПараметрКоманды);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да, подтверждаю'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет);
	
	ПоказатьВопрос(ОписаниеОповещения,
		ТекстВопроса,
		Кнопки,,
		КодВозвратаДиалога.Нет,
		Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьИзучениеОтвет(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Оповестить("ОбновлениеСтандартовПользователей", ПодтвердитьИзучениеНаСервере(ДополнительныеПараметры));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодтвердитьИзучениеНаСервере(Стандарты)
	
	СтандартыРазработки.ПодтвердитьИзучениеВыбранныхСтандартов(Стандарты);
	Возврат Пользователи.ТекущийПользователь();
	
КонецФункции

#КонецОбласти
