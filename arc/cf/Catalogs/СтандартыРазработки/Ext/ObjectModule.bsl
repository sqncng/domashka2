#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Если Не ЭтоГруппа Тогда
		Если Не Внешний Тогда
			ДатаИзменения = ТекущаяДатаСеанса;
		КонецЕсли;
		
		Если ЭтоНовый() Тогда
			Если Не Внешний Тогда
				ДатаСоздания = ТекущаяДатаСеанса;
			КонецЕсли;
		Иначе
			Если ДатаИзменения > Ссылка.ДатаИзменения Тогда
				УстановитьТребованиеИзучения();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Или Не Внешний Тогда
		Префикс = НСтр("ru = 'СТД'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьТребованиеИзучения()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИзучаемыеСтандартыСрезПоследних.Пользователь
		|ИЗ
		|	РегистрСведений.ИзучаемыеСтандарты.СрезПоследних(, Стандарт = &Стандарт) КАК ИзучаемыеСтандартыСрезПоследних
		|ГДЕ
		|	ИзучаемыеСтандартыСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСтандартов.Изучен)");
	Запрос.УстановитьПараметр("Стандарт", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтандартыРазработки.УстановитьСостояниеСтандарта(Выборка.Пользователь,
			Ссылка,
			Перечисления.СостоянияСтандартов.ТребуетИзучения,
			НСтр("ru = 'Стандарт изменился.'"));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
