#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьШагиИзФайлаJson(Команда)
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораКаталога.МножественныйВыбор = Ложь;
	ДиалогВыбораКаталога.Фильтр = "(*.json)|*.json";
	ДиалогВыбораКаталога.Показать(Новый ОписаниеОповещения("ЗагрузитьШагиИзJSONВыборФайлаЗавершение", ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСправочникИзвестныхШагов(Команда)
	ТекстВопроса = НСтр("ru='Вы уверены?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьОчисткуСправочника", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьШагиИзJSONВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	#Если НЕ ВебКлиент Тогда
	Если НЕ ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Возврат;
	КонецЕсли;	 
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ВыбранныеФайлы[0]);
	
	Шаги = ПрочитатьJSON(ЧтениеJSON);
	
	ЗагрузитьШагиСервер(Шаги);
	#КонецЕсли
КонецПроцедуры

&НаСервере
Функция ШагПоСнипету(Снипет)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШагиСценариевПользователя.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШагиСценариевПользователя КАК ШагиСценариевПользователя
		|ГДЕ
		|	ШагиСценариевПользователя.СнипетШага = &СнипетШага";
	
	Запрос.УстановитьПараметр("СнипетШага", Снипет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции	

&НаСервере
Функция ГруппаШагПоТипу(Наименование,ТекРодитель)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШагиСценариевПользователя.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШагиСценариевПользователя КАК ШагиСценариевПользователя
		|ГДЕ
		|	ШагиСценариевПользователя.ЭтоГруппа
		|	И ШагиСценариевПользователя.Наименование = &Наименование
		|	И ШагиСценариевПользователя.Родитель = &Родитель";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Родитель", ТекРодитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	ЭлементСправочника = Справочники.ШагиСценариевПользователя.СоздатьГруппу();
	ЭлементСправочника.Наименование = Наименование;
	Если ТекРодитель <> Неопределено Тогда
		ЭлементСправочника.Родитель = ТекРодитель;
	КонецЕсли;	 
	ЭлементСправочника.Записать();
	
	Возврат ЭлементСправочника.Ссылка;
КонецФункции	

&НаСервере
Функция РодительДляШага(ПолныйТипШага)
	МассивСтрок = СтрРазделить(ПолныйТипШага,".");
	
	ТекРодитель = Справочники.ШагиСценариевПользователя.ПустаяСсылка();
	Для Каждого Элем Из МассивСтрок Цикл
		ГруппаШаг   = ГруппаШагПоТипу(Элем,ТекРодитель);
		ТекРодитель = ГруппаШаг;
	КонецЦикла;	
	
	Возврат ГруппаШаг;
КонецФункции	

&НаСервере
Процедура ЗагрузитьШагиСервер(Шаги)
	Для Каждого Шаг Из Шаги Цикл
		МассивСтрок = СтрРазделить(Шаг.ИмяШага,Символы.ПС);
		СнипетШага  = НРег(ТестированиеГрафическиеСхемыСервер.СнипетИзСтроки(МассивСтрок[0]));
		
		
		СсылкаЭлементСправочника = ШагПоСнипету(СнипетШага);
		Если Не ЗначениеЗаполнено(СсылкаЭлементСправочника) Тогда
			ЭлементСправочника = Справочники.ШагиСценариевПользователя.СоздатьЭлемент();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Создаю элемент %1.'"),Шаг.ИмяШага));
		Иначе
			ЭлементСправочника = СсылкаЭлементСправочника.ПолучитьОбъект();
		КонецЕсли;
		
		ЭлементСправочника.Наименование = Шаг.ИмяШага;
		ЭлементСправочника.Описание     = Шаг.ОписаниеШага;
		ЭлементСправочника.СнипетШага   = СнипетШага;
		
		ЭлементСправочника.Родитель     = РодительДляШага(Шаг.ПолныйТипШага);
		ЭлементСправочника.Записать();
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьОчисткуСправочника(РезультатВопроса,ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;	 
	
	ОчиститьСправочникСервер();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьСправочникСервер()
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШагиСценариевПользователя.Ссылка КАК Ссылка,
		|	ШагиСценариевПользователя.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ШагиСценариевПользователя КАК ШагиСценариевПользователя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка ИЕРАРХИЯ УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		      НСтр("ru = 'Удаляю ""%1"".'"),ВыборкаДетальныеЗаписи.Наименование));
			  
		СпрОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Попытка
			СпрОбъект.Удалить();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти