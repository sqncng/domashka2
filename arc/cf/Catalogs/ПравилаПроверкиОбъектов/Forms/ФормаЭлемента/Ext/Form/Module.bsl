
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СвойствоОбъектаПредустановленное = Объект.Предустановленное;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	СвойствоОбъектаПредустановленное = ТекущийОбъект.Предустановленное;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если Объект.Предустановленное Тогда
			Если СвойствоОбъектаПредустановленное Тогда
				
				ТекстВопроса = НСтр("ru='Предустановленное правило проверки было изменено.
					|При обновлении правил из эталона изменения этого правила могут быть потеряны!
					|Снять флаг ""Предустановленное""?'");
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗаписьюЗавершение", ЭтотОбъект);
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        Предустановленное = Ложь;
    КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	СвойствоОбъектаПредустановленное = ТекущийОбъект.Предустановленное;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредустановленноеПриИзменении(Элемент)
	
	Если Объект.Предустановленное И НЕ СвойствоОбъектаПредустановленное Тогда
		
		Объект.Предустановленное = Ложь;
		
		ТекстВопроса = НСтр("ru='Если установить флаг ""Предустановленное"",
				    |при обновлении правил из эталона изменения этого правила могут быть потеряны!
					|Продолжить?'");
					
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеПредустановленногоЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеПредустановленногоЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Предустановленное = Истина;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КонструкторЗапроса(Команда)
	
	ОчиститьСообщения();
	
	#Если ТонкийКлиент ИЛИ ВебКлиент Тогда
		ТекстСообщения = НСтр("ru='Выполнение команды возможно только в толстом клиенте'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	#Иначе
		
		КонструкторЗапроса = Новый КонструкторЗапроса;
		Если Не ПустаяСтрока(Объект.ТекстЗапроса) Тогда
			КонструкторЗапроса.Текст = Объект.ТекстЗапроса;
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("КонструкторЗапросаЗавершение", ЭтотОбъект);
		КонструкторЗапроса.Показать(ОписаниеОповещения);
	
	#КонецЕсли
			
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапросаЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда
		Объект.ТекстЗапроса = Результат;
		ВыполнитьПроверкуПриПомощиПостроителя(Объект.ТекстЗапроса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатВыполненияЗапроса(Команда)
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(Объект.ТекстЗапроса) Тогда
		ТекстСообщения = НСтр("ru='Ошибка. Не заполнен текст запроса.'");
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьРезультатЗапроса();
	
	Если ТипЗнч(Результат) = Тип("ТабличныйДокумент") Тогда
		ЗаголовокДокумента = НСтр("ru='Результат выполнения запроса'");
		Результат.Показать(ЗаголовокДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекстЗапроса(Команда)
	
	Объект.ТекстЗапроса = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьРезультатЗапроса()
	
	ЕстьОшибки = Ложь;
	МассивСообщений = Новый Массив();
	
	// Заполнение построителя отчета с одновременной проверкой необходимых полей
	ПостроительОтчета = ПроверкаОбъектов.СоздатьПостроительОтчетаПоПравилу(Объект.ТекстЗапроса, ЕстьОшибки, МассивСообщений);
	
	Если МассивСообщений.Количество() > 0 Тогда
		Для Каждого Сообщение Из МассивСообщений Цикл
			Сообщить(Сообщение);
		КонецЦикла;
	КонецЕсли;
		
	Если ЕстьОшибки ИЛИ ТипЗнч(ПостроительОтчета) <> Тип("ПостроительОтчета") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПостроительОтчета.ВыводитьЗаголовокОтчета = Ложь;
	
	ПостроительОтчета.Выполнить();
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ПостроительОтчета.Вывести(ТабличныйДокумент);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

&НаСервере
Процедура ВыполнитьПроверкуПриПомощиПостроителя(ТекстЗапроса)
	
	ЕстьОшибки = Ложь;
	МассивСообщений = Новый Массив();
	
	ПроверкаОбъектов.СоздатьПостроительОтчетаПоПравилу(ТекстЗапроса, ЕстьОшибки, МассивСообщений);
	
	Если МассивСообщений.Количество() > 0 Тогда
		Для Каждого Сообщение Из МассивСообщений Цикл
			Сообщить(Сообщение);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
