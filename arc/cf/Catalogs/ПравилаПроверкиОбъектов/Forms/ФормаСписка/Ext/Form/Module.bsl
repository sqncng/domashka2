
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьОтличияОтПредустановленныхПравил(Команда)
	
	#Если ТонкийКлиент ИЛИ ВебКлиент Тогда
		ТекстСообщения = НСтр("ru='Выполнение команды возможно только в толстом клиенте'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	#Иначе
		ВременныеФайлы = КаталогВременныхФайлов();
		ИмяФайла1 = ВременныеФайлы + НСтр("ru='Правила из конфигурации.mxl'");
		ИмяФайла2 = ВременныеФайлы + НСтр("ru='Предустановленные правила.mxl'");
		
		ТекущиеПравила = ТабличныйДокументСТекущимиПравилами();
		ТекущиеПравила.Записать(ИмяФайла1, "mxl");
		
		Макет = Справочники.ПравилаПроверкиОбъектов.ПолучитьМакет("ЭталонныеПравилаПроверки");
		ТабДок = Новый ТабличныйДокумент;
		ТабДок.Вывести(Макет.ПолучитьОбласть(1, , Макет.ВысотаТаблицы));
		ТабДок.Записать(ИмяФайла2, "mxl");
		
		Сравнение = Новый СравнениеФайлов;
		Сравнение.СпособСравнения = СпособСравненияФайлов.ТабличныйДокумент;
		Сравнение.ПервыйФайл = ИмяФайла1;
		Сравнение.ВторойФайл = ИмяФайла2;
		Сравнение.ПоказатьРазличия();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПредустановленныеПравила(Команда)
	
	ЗагрузитьПредустановленныеПравилаСервер();
	
	ТекстЗаголовка = НСтр("ru='Загрузка правил'");
	ТекстПояснения = НСтр("ru='Выполнена загрузка предустановленных правил'");
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка,,ТекстПояснения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьТекущиеПравилаВТабличныйДокумент(Команда)
	
	ТабличныйДокумент = ТабличныйДокументСТекущимиПравилами();
	
	ТекстЗаголовка = НСтр("ru='Правила проверки объектов'");
	ТабличныйДокумент.Показать(ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьПредустановленныеПравилаСервер()
	
	ПроверкаОбъектов.ОбновитьПравилаПроверки();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Функция ТабличныйДокументСТекущимиПравилами()

	ТабДок = Новый ТабличныйДокумент;

	Макет = Справочники.ПравилаПроверкиОбъектов.ПолучитьМакет("Макет");

	Секция = Макет.ПолучитьОбласть(1, , 2);
	ТабДок.Вывести(Секция);

	Секция = Макет.ПолучитьОбласть(3, , 3);

	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Спр.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА Спр.ЭтоГруппа ТОГДА
	|		""1""
	|	ИНАЧЕ
	|		""0""
	|	КОНЕЦ КАК ЭтоГруппа,
	|	Спр.Родитель КАК Родитель,
	|	Спр.Код КАК Код,
	|	Спр.Наименование КАК Наименование,
	|	Спр.Описание КАК Описание,
	|	Спр.ТекстЗапроса КАК ТекстЗапроса,
	|	Спр.Важность КАК Важность,
	|	ВЫБОР КОГДА Спр.Активно ТОГДА
	|		""1""
	|	ИНАЧЕ
	|		""0""
	|	КОНЕЦ КАК Активно,
	|	ВЫБОР КОГДА Спр.Предустановленное ТОГДА
	|		""1""
	|	ИНАЧЕ
	|		""0""
	|	КОНЕЦ КАК Предустановленное
	|ИЗ
	|	Справочник.ПравилаПроверкиОбъектов КАК Спр
	|ГДЕ
	|	НЕ Спр.ПометкаУдаления
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка ИЕРАРХИЯ
	|");

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Секция.Параметры.Заполнить(Выборка);
		Секция.Параметры.Ссылка = ЗначениеВСтрокуВнутр(Выборка.Ссылка);
		Секция.Параметры.Родитель = ЗначениеВСтрокуВнутр(Выборка.Родитель);

		Если ЗначениеЗаполнено(Выборка.Важность) Тогда
			МетаданныеЗначения = Выборка.Важность.Метаданные();
			Секция.Параметры.Важность = МетаданныеЗначения.ЗначенияПеречисления.Получить(Перечисления[МетаданныеЗначения.Имя].Индекс(Выборка.Важность)).Имя;
		КонецЕсли;

		ТабДок.Вывести(Секция);
	КонецЦикла;

	Возврат ТабДок;

КонецФункции

#КонецОбласти
