#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список реквизитов, которые не нужно редактировать
// с помощью обработки группового изменения объектов
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	НеРедактируемыеРеквизиты = Новый Массив;
	
	НеРедактируемыеРеквизиты.Добавить("ДатаРегистрации");
	НеРедактируемыеРеквизиты.Добавить("ДатаЗакрытия");
	НеРедактируемыеРеквизиты.Добавить("ХранилищеОписания");
	
	Возврат НеРедактируемыеРеквизиты;
	
КонецФункции

Функция СформироватьПечатныеФормы(МассивОбъектов, СУчетомПриемника=Ложь, ДанныеСоответствия=Неопределено) Экспорт
	
	ПечатныеФормы = Новый Соответствие;
	
	ТекстЗапроса = ТекстЗапросаДляФормированияОписания();
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатовЗапроса[0].Выбрать();
	
	ТаблицаФайлов = МассивРезультатовЗапроса[1].Выгрузить();
	ТехническиеПроектыИспользующиеИдеи = МассивРезультатовЗапроса[2].Выгрузить();
	
	Пока Выборка.Следующий() Цикл
		
		Документ = Новый ТабличныйДокумент;
		
		ОписаниеОбъектов.НастроитьОписаниеОбъекта(Документ);
		Документ.НачатьАвтогруппировкуСтрок();
		
		СтруктураОтбора = Новый Структура("Идея", Выборка.Ссылка);
		
		ТаблицаФайловОбъекта = ОписаниеОбъектов.ТаблицаОбъектаПоОбщейТаблице(ТаблицаФайлов, СтруктураОтбора);
		ТехническиеПроектыИспользующиеИдея = ОписаниеОбъектов.ТаблицаОбъектаПоОбщейТаблице(ТехническиеПроектыИспользующиеИдеи, СтруктураОтбора);
		
		СформироватьОписаниеОбъекта(Выборка, ТаблицаФайловОбъекта, ТехническиеПроектыИспользующиеИдея, Документ);
		
	    Документ.ЗакончитьАвтогруппировкуСтрок();
		
		СтруктураПечатныхФорм = Новый Структура;
		СтруктураПечатныхФорм.Вставить("Описание", Документ);
		
		ПечатныеФормы.Вставить(Выборка.Ссылка, СтруктураПечатныхФорм);
		
	КонецЦикла;
	
	Возврат ПечатныеФормы;
	
КонецФункции

// Выполняет печать описаний и схем переданных объектов
//
// Параметры:
//  МассивОбъектов - массив объектов, подлежащих печати
//
Процедура Печать(МассивОбъектов, ПечатныеФормы) Экспорт
	
	ПечатныеФормы = СформироватьПечатныеФормы(МассивОбъектов);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДляФормированияОписания()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Идеи.Ссылка,
	|	Идеи.ПометкаУдаления,
	|	Идеи.Код,
	|	Идеи.Наименование,
	|	Идеи.Зарегистрировал,
	|	Идеи.Важность,
	|	Идеи.ДатаРегистрации,
	|	Идеи.ПодробноеОписание,
	|	Идеи.Ответственный,
	|	Идеи.Статус,
	|	Идеи.Тематика,
	|	Идеи.ДатаЗакрытия,
	|	Идеи.Источник,
	|	Идеи.КПросмотру,
	|	Идеи.Основание,
	|	Идеи.Владелец,
	|	Идеи.РазделПроекта,
	|	Идеи.РазделыПроекта.(
	|	Раздел КАК Раздел),
	|	Идеи.ЦелеваяЗадача
	|ИЗ
	|	Справочник.Идеи КАК Идеи
	|ГДЕ
	|	Идеи.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	""Файл"" КАК ВидПодраздела,
	|	"""" КАК НомерПодраздела,
	|	Файлы.Наименование КАК ЗаголовокПодраздела,
	|	Файлы.Ссылка КАК Ссылка,
	|	"""" КАК Текст1,
	|	"""" КАК Текст2,
	|	NULL КАК Гиперссылка,
	|	Файлы.ПометкаУдаления КАК ПометкаУдаления,
	|	"""" КАК СлужебнаяИнформация,
	|	Файлы.ВладелецФайла КАК Идея
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.ВладелецФайла В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Файлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	
	|	"""" КАК НомерПодраздела,
	|	"""" КАК ВидПодраздела,
	|	ТехническиеПроектыИдеи.Ссылка.Наименование КАК ЗаголовокПодраздела,
	|	ТехническиеПроектыИдеи.Ссылка КАК Ссылка,
	|	"""" КАК Текст1,
	|	"""" КАК Текст2,
	|	NULL КАК Гиперссылка,
	|	ТехническиеПроектыИдеи.Ссылка.ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА) КАК СлужебнаяИнформация,
	|	ТехническиеПроектыИдеи.Идея КАК Идея
	|ИЗ
	|	Справочник.ТехническиеПроекты.ИдеиИОшибки КАК ТехническиеПроектыИдеи
	|ГДЕ
	|	ТехническиеПроектыИдеи.Идея В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаголовокПодраздела"
	;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьОписаниеОбъекта(Выборка, ТаблицаФайлов, ТехническиеПроектыИспользующиеИдея, Документ)
	
	ОписаниеОбъектов.ВывестиЗаголовокОбъекта(НСтр("ru='Идея'"), Выборка.Ссылка, Выборка.Код, Документ);
	
	ОписаниеОбъектов.ВывестиЗаголовокТекста("", , Документ);
	
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Ответственный: '") + Выборка.Ответственный, 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Зарегистрировал: '") + Выборка.Зарегистрировал, 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Дата регистрации: '") + Формат(Выборка.ДатаРегистрации, "ДЛФ=D"), 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Источник: '") + Выборка.Источник, 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Основание: '") + Выборка.Основание, 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Тематика: '") + Выборка.Тематика, 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Статус: '") + Выборка.Статус, 1, Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Важность: '") + Выборка.Важность, 1, Документ);
	
	Если НЕ Выборка.КПросмотру Тогда
		ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Просмотрено ответственным'"), 1, Документ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.ДатаЗакрытия) Тогда
		ОписаниеОбъектов.ВывестиТекстПоАбзацам(НСтр("ru='Дата закрытия: '") + Формат(Выборка.ДатаЗакрытия, "ДЛФ=D"), 1, Документ);
	КонецЕсли;
	
	// Разделы проекта
	ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Раздел проекта'"), Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Выборка.РазделПроекта, 1, Документ, Выборка.РазделПроекта);
	
	РазделыПроекта = Выборка.РазделыПроекта.Выгрузить();
	
	Если РазделыПроекта.Количество()>0 Тогда
		
		ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Дополнительные разделы'"), Документ);
		
		Для Каждого СтрокаТЧ из РазделыПроекта Цикл
			ОписаниеОбъектов.ВывестиТекстПоАбзацам(СтрокаТЧ.Раздел, 1, Документ, СтрокаТЧ.Раздел);
		КонецЦикла;
		
	КонецЕсли;
	
	// Целевая задача
	ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Целевая задача'"), Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Выборка.ЦелеваяЗадача, 1, Документ, Выборка.ЦелеваяЗадача);
		
	ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Подробное описание'"), Документ);
	ОписаниеОбъектов.ВывестиТекстПоАбзацам(Выборка.ПодробноеОписание, , Документ);
	
	// Технические проекты, использующие идеи
	Если ТехническиеПроектыИспользующиеИдея.Количество()>0 Тогда
		ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Технические проекты, использующие идею'"), Документ);
		ОписаниеОбъектов.ВывестиПодразделы(ТехническиеПроектыИспользующиеИдея, , , Документ);
	КонецЕсли;
	
	// Хранимые файлы
	Если ТаблицаФайлов.Количество()>0 Тогда
		ОписаниеОбъектов.ВывестиЗаголовокРаздела(НСтр("ru='Файлы'"), Документ);
		ОписаниеОбъектов.ВывестиПодразделы(ТаблицаФайлов, , , Документ);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли