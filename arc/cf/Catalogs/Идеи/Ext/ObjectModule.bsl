#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ДатаРегистрации = ТекущаяДата();
	Зарегистрировал = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Ответственный") Тогда
			Ответственный = ДанныеЗаполнения.Ответственный;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Основание") Тогда
			Если ЗначениеЗаполнено (ДанныеЗаполнения.Основание) Тогда
				Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Основание, "Владелец");
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Тематика") Тогда
			Тематика = ДанныеЗаполнения.Тематика;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("РазделПроекта") Тогда
			РазделПроекта = ДанныеЗаполнения.РазделПроекта;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ТехническиеПроекты") 
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Ошибки") Тогда
		Основание = ДанныеЗаполнения;
		Владелец = ДанныеЗаполнения.Владелец;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Ошибки") Тогда
		
		Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Наименование");
		
		Если ЗначениеЗаполнено(Владелец) Тогда
			ЗаполнятьРаздел = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "УказыватьВИдеяхРазделПроекта");
		Иначе
			ЗаполнятьРаздел = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(РазделПроекта) И ЗаполнятьРаздел Тогда
			
			РазделОшибки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "РазделПроекта");
			Если ЗначениеЗаполнено(РазделОшибки) Тогда
				РазделПроекта = РазделОшибки;
				
				Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
					ОтветственныйЗаРаздел = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РазделОшибки, "Ответственный");
					Если ЗначениеЗаполнено(ОтветственныйЗаРаздел) Тогда
						Ответственный = ОтветственныйЗаРаздел;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНовый() И Не ЗначениеЗаполнено(Владелец) Тогда
		Владелец = Проекты.ПроектПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаРегистрации = ТекущаяДата();
	ДатаЗакрытия = '00010101';
	Статус = Перечисления.СтатусыИдей.Зарегистрирована;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УказыватьРаздел = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "УказыватьВИдеяхРазделПроекта");
	
	Если УказыватьРаздел = Неопределено Тогда
		УказыватьРаздел = Ложь;
	КонецЕсли;
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если НЕ УказыватьРаздел ИЛИ КПросмотру Тогда
		НепроверяемыеРеквизиты.Добавить("РазделПроекта");
	КонецЕсли;
	
	Если НепроверяемыеРеквизиты.Количество()>0 Тогда
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаРегистрации) Тогда
		ДатаРегистрации = ТекущаяДата();
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыИдей.Реализована ИЛИ Статус = Перечисления.СтатусыИдей.Отклонена Тогда
		Если НЕ ЗначениеЗаполнено(ДатаЗакрытия) Тогда
			ДатаЗакрытия = ТекущаяДата();
		КонецЕсли;
	Иначе
		ДатаЗакрытия = '00010101';
	КонецЕсли;
	
	СкорректироватьДанныеНеСоответствующиеПроекту();
	
	ДополнительныеСвойства.Вставить("КоллекцияИзмененныхОбъектов", Новый Массив);
	Версионирование.ЗарегистрироватьИзмененияОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СкорректироватьДанныеНеСоответствующиеПроекту()
	
	Если ЗначениеЗаполнено(РазделПроекта) Тогда
		
		ПроектРаздела = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РазделПроекта, "Владелец");
		
		Если ПроектРаздела <> Владелец Тогда
			РазделПроекта = Справочники.РазделыПроекта.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли