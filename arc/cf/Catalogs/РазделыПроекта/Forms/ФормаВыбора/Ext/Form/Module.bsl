#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отбор = Параметры.Отбор;
	Если Отбор.Свойство("Проект") Тогда
		Проект = Отбор.Проект;
	Иначе
		Проект = ПараметрыСеанса.ТекущийПроект;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Проект, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Проект));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"Владелец",
																			Проект,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			ЗначениеЗаполнено(Проект));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПереместитьВверх(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПереместитьРаздел(ТекущиеДанные.Ссылка, ТекущиеДанные.Владелец, ТекущиеДанные.Родитель, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВниз(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПереместитьРаздел(ТекущиеДанные.Ссылка, ТекущиеДанные.Владелец, ТекущиеДанные.Родитель, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПереместитьРаздел(Ссылка, Владелец, Родитель, Вверх = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РазделыПроекта.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РазделыПроекта КАК РазделыПроекта
	|ГДЕ
	|	РазделыПроекта.Владелец = &Владелец
	|	И РазделыПроекта.Родитель = &Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделыПроекта.Код"
	;
	
	Запрос.Параметры.Вставить("Владелец", Владелец);
	Запрос.Параметры.Вставить("Родитель", Родитель);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	КоличествоСтрок = Таблица.Количество();
	
	Если КоличествоСтрок < 2 Тогда
		// Нет смысла изменения порядка
		Возврат;
	КонецЕсли;
	
	// строка должна быть найдена всегда
	ТекущийИндекс = Таблица.Индекс(Таблица.Найти(Ссылка, "Ссылка"));
	
	Если Вверх Тогда
		НовыйИндекс = ТекущийИндекс - 1;
	Иначе
		// перемещение вниз
		НовыйИндекс = ТекущийИндекс + 1;
	КонецЕсли;
	
	Если НовыйИндекс < 0 ИЛИ НовыйИндекс > КоличествоСтрок - 1 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		ПервыйРаздел = Таблица[ТекущийИндекс].Ссылка.ПолучитьОбъект();
		ПервыйРаздел.Заблокировать();
		
		ВторойРаздел = Таблица[НовыйИндекс].Ссылка.ПолучитьОбъект();
		ВторойРаздел.Заблокировать();
	Исключение
		Сообщить(НСтр("ru = 'Перемещение не выполнено. Не удалось заблокировать раздел"));
		ОтменитьТранзакцию();
		Возврат;
	КонецПопытки;
	
	ПервыйКод = ПервыйРаздел.Код;
	ПервыйРаздел.Код = ВторойРаздел.Код;
	ВторойРаздел.Код = ПервыйКод;
	
	ПервыйРаздел.Записать();
	ПервыйРаздел.Разблокировать();
	ВторойРаздел.Записать();
	ВторойРаздел.Разблокировать();
	
	ЗафиксироватьТранзакцию();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти