#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует и возвращает таблицу значений, содержащую
// разделы проектов с информацией о проектах, к которым
// относятся разделы
//
// Параметры:
//  МассивРазделов - массив, содержащий интересующие разделы
//
// Возвращаемое значение:
//  Таблица значений, содержащую разделы, соотнесенные с проектами
// 
Функция ТаблицаРазделовСПроектами(МассивРазделов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РазделыПроекта.Ссылка КАК Раздел,
	|	РазделыПроекта.Владелец КАК Проект
	|ИЗ
	|	Справочник.РазделыПроекта КАК РазделыПроекта
	|ГДЕ
	|	РазделыПроекта.Ссылка В(&МассивРазделов)"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивРазделов", МассивРазделов);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ДеревоРазделовПроекта(Проект) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РазделыПроекта.Ссылка КАК РазделПроекта,
	|	РазделыПроекта.ЭтоГруппа КАК ЭтоГруппа,
	|	РазделыПроекта.Владелец КАК Проект,
	|	0 КАК Использование,
	|	ВЫБОР
	|		КОГДА НЕ РазделыПроекта.ЭтоГруппа
	|				И НЕ РазделыПроекта.ПометкаУдаления
	|			ТОГДА 0
	|		КОГДА НЕ РазделыПроекта.ЭтоГруппа
	|				И РазделыПроекта.ПометкаУдаления
	|			ТОГДА 1
	|		КОГДА РазделыПроекта.ЭтоГруппа
	|				И НЕ РазделыПроекта.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки
	|ИЗ
	|	Справочник.РазделыПроекта КАК РазделыПроекта
	|ГДЕ
	|	РазделыПроекта.Владелец = &Проект
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделПроекта ИЕРАРХИЯ"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
КонецФункции

// Возвращает список реквизитов, которые не нужно редактировать
// с помощью обработки группового изменения объектов
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	НеРедактируемыеРеквизиты = Новый Массив;
	
	НеРедактируемыеРеквизиты.Добавить("Владелец");
	НеРедактируемыеРеквизиты.Добавить("ПолныйКод");
	
	Возврат НеРедактируемыеРеквизиты;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли