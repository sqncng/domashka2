
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Проект") Тогда
		Проект = Параметры.Проект;
	КонецЕсли; 
	Если Параметры.Свойство("МассивОбъектов") Тогда
		СписокОбъектов.ЗагрузитьЗначения(Параметры.МассивОбъектов);
	КонецЕсли;
	
	СоздатьРеквизитыПроектовСправки();
	ОтметитьПроектыВключающиеСправку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Изменить(Команда)
	
	ИзменитьНаСервере();
	ОповеститьОбИзменении(Тип("СправочникСсылка.ЭлементыСправки"));
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроектыВключающиеСправкуПриИзменении(Элемент)
	
	ОтборСвязи  = Новый Структура("ИмяРеквизита", Элемент.Имя);
	СтрокаСвязи = СвязьПроектовИРеквизитов.НайтиСтроки(ОтборСвязи)[0];
	
	Если ЭтаФорма[Элемент.Имя] = Истина Тогда 
		
		СтрокаОтметки = ОтмеченныеПроекты.Добавить();
		СтрокаОтметки.Проект = СтрокаСвязи.Проект;
		
	Иначе 
		
		Если ОтмеченныеПроекты.Количество() = 1 Тогда
			ЭтаФорма[Элемент.Имя] = Истина;
			ПоказатьПредупреждение(, НСтр("ru = 'Элемент справки должен относиться хотя бы к одному проекту.'"));
			Возврат;
		КонецЕсли; 
		
		Отбор = Новый Структура("Проект", СтрокаСвязи.Проект);
		СтрокиОтметки = ОтмеченныеПроекты.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаОтметки Из СтрокиОтметки Цикл
			ОтмеченныеПроекты.Удалить(СтрокаОтметки);
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьРеквизитыПроектовСправки()
	
	ПроектыДляКоторыхВедетсяСправка = РаботаСоСправкойПовтИсп.ПроектыДляКоторыхВедетсяСправка(Проект);
	
	Если ПроектыДляКоторыхВедетсяСправка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Индекс = 0 По ПроектыДляКоторыхВедетсяСправка.Количество() - 1 Цикл
		
		СтрокаТаблицы = ПроектыДляКоторыхВедетсяСправка[Индекс];
		ИмяРеквизита  = "ПроектыСправки_" + Строка(Индекс);
		
		СтрокаСвязи = СвязьПроектовИРеквизитов.Добавить();
		СтрокаСвязи.Проект       = СтрокаТаблицы.Проект;
		СтрокаСвязи.ИмяРеквизита = ИмяРеквизита;
		
		РеквизитФормы = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Булево"),,СтрокаТаблицы.ИмяПроекта);
		ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		
	КонецЦикла; 
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для Индекс = 0 По ДобавляемыеРеквизиты.Количество() - 1 Цикл
	
		РеквизитФормы = ДобавляемыеРеквизиты[Индекс];
		
		НумераторГруппы = Индекс % 3;
		ГруппаФлажка = Элементы["ГруппаПроектыКолонка_" + Строка(НумераторГруппы)];
	
		ПолеФлажка = Элементы.Добавить(РеквизитФормы.Имя, Тип("ПолеФормы"), ГруппаФлажка);
		ПолеФлажка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		ПолеФлажка.ПутьКДанным = РеквизитФормы.Имя;
		ПолеФлажка.Вид = ВидПоляФормы.ПолеФлажка;
		
		ПолеФлажка.УстановитьДействие("ПриИзменении", "ПроектыВключающиеСправкуПриИзменении");
		
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьПроектыВключающиеСправку()
	
	ОтмеченныеПроекты.Очистить();
	
	Для Каждого Связь Из СвязьПроектовИРеквизитов Цикл
		
		СтрокаОтметки = ОтмеченныеПроекты.Добавить();
		СтрокаОтметки.Проект = Связь.Проект;
		ЭтаФорма[Связь.ИмяРеквизита] = Истина;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьНаСервере()
	
	Для Каждого ЭлементСписка Из СписокОбъектов Цикл
		
		СсылкаНаОбъект       = ЭлементСписка.Значение;
		ОбъектЭлементСправки = СсылкаНаОбъект.ПолучитьОбъект();
		ОбъектЭлементСправки.ПроектыВключающиеСправку.Очистить();
		
		Если СвязьПроектовИРеквизитов.Количество() > 0
		   И СвязьПроектовИРеквизитов.Количество() <> ОтмеченныеПроекты.Количество() Тогда
			
			Для Каждого СтрокаОтметки Из ОтмеченныеПроекты Цикл
				
				НоваяСтрока = ОбъектЭлементСправки.ПроектыВключающиеСправку.Добавить();
				НоваяСтрока.Проект = СтрокаОтметки.Проект;
				
			КонецЦикла; 
		КонецЕсли;
		
		ОбъектЭлементСправки.Записать();
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

