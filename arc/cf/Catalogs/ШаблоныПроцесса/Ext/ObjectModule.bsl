

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоГруппа  Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПредназначенДляОпределенногоТипаПредмета Тогда
		ЭтотОбъект.ТипПредмета = "";
		ЭтотОбъект.ТипПредметаПредставление = "";
	КонецЕсли;
	
	ЗаданВладелецШаблона = ЗначениеЗаполнено(ВладелецШаблона);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСогласованиеРесурсов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВидыСогласуемыхРесурсов.СогласуемыйРесурс
		|ПОМЕСТИТЬ ВидыСогласуемыхРесурсов
		|ИЗ
		|	&ВидыСогласуемыхРесурсов КАК ВидыСогласуемыхРесурсов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыСогласуемыхРесурсов.СогласуемыйРесурс КАК СогласуемыйРесурс,
		|	СУММА(1)                                  КАК КоличествоСогласуемогоРесурса
		|ИЗ
		|	ВидыСогласуемыхРесурсов КАК ВидыСогласуемыхРесурсов
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыСогласуемыхРесурсов.СогласуемыйРесурс
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) > 1";
		
		Запрос.УстановитьПараметр("ВидыСогласуемыхРесурсов", ВидыСогласуемыхРесурсов.Выгрузить());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НайденныеСтроки = ВидыСогласуемыхРесурсов.НайтиСтроки(Новый Структура("СогласуемыйРесурс", Выборка.СогласуемыйРесурс));
			КоличествоНайденныхСтрок = НайденныеСтроки.Количество(); 
			Если КоличествоНайденныхСтрок > 1 Тогда
				СтрокКУдалению = КоличествоНайденныхСтрок - 1;
				УдаленоСтрок   = 0;
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					Если УдаленоСтрок = СтрокКУдалению Тогда
						Прервать;
					КонецЕсли;
					
					ВидыСогласуемыхРесурсов.Удалить(НайденнаяСтрока);
					УдаленоСтрок = УдаленоСтрок + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЭтоГруппа Тогда
	
		МассивНепроверяемыхРеквизитов = Новый Массив;
		
		Если Не ПредназначенДляОпределенногоТипаПредмета Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ТипПредмета");
			МассивНепроверяемыхРеквизитов.Добавить("ТипПредметаПредставление");
		КонецЕсли;
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЭтоГруппа Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли