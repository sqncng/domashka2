#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Проект = Неопределено;
	ЕстьПравоПланирования = Ложь;
	
	Если НЕ ДанныеПринадлежатОдномуПроекту(ПараметрКоманды, Проект) Тогда
		ТекстСообщения = НСтр("ru='Выбранные данные относятся к разным проектам.
		|Добавление в технический проект невозможно.'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Владелец", Проект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежиВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	Структура = Новый Структура;
	Структура.Вставить("ПараметрКоманды", ПараметрКоманды);
	Структура.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	Структура.Вставить("Проект", Проект);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, Структура);
	ОткрытьФорму("Справочник.ТехническиеПроекты.ФормаВыбора", ПараметрыФормы,,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ПараметрКоманды = ДополнительныеПараметры.ПараметрКоманды;
    ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
    Проект = ДополнительныеПараметры.Проект;
    
    ТехническийПроект = Результат;
    
    Если ЗначениеЗаполнено(ТехническийПроект) Тогда
		
		ТекстСообщения = "";
        Если НЕ ДоступноДобавление(ТехническийПроект, Проект, ТекстСообщения) Тогда
            Сообщить(ТекстСообщения);
            Возврат;
        КонецЕсли;
		
        ЕстьДобавленные = ДобавитьИдеиВТехническийПроект(ПараметрКоманды, ТехническийПроект);
		
		Если ЕстьДобавленные Тогда
			ТекстСообщения = НСтр("ru='Данные добавлены в технический проект'");
			ТекстЗаголовка = НСтр("ru='Добавление идей и ошибок'");
			
			ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
			
			СтруктураОповещения = Новый Структура;
			СтруктураОповещения.Вставить("Ссылка", ТехническийПроект);
			СтруктураОповещения.Вставить("Идеи", ПараметрКоманды);
			
			Оповестить("ЗаписанТехническийПроектСИдеими", СтруктураОповещения, ПараметрыВыполненияКоманды.Источник);
		Иначе
			
			ТекстСообщения = НСтр("ru='Не выполнено добавление в технический проект. Допускается добавление только рассмотренных ошибок'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
        
    Иначе
        Возврат;
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеПринадлежатОдномуПроекту(Ссылки, Проект)
	
	Возврат Справочники.ТехническиеПроекты.ДанныеПринадлежатОдномуПроекту(Ссылки, Проект);
	
КонецФункции

&НаСервере
Функция ДоступноДобавление(ТехническийПроект, Проект, ТекстСообщения)
	
	Результат = Ложь;
	
	Реквизиты = "Владелец, Статус";
	РеквизитыТехническогоПроекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТехническийПроект, Реквизиты);
	
	ДоступноИзменениеТехническогоПроекта = 
		УправлениеДоступомСППР.РольДоступнаПоПроекту("ДобавлениеИзменениеТехническихПроектов", Проект);
		
	Если ДоступноИзменениеТехническогоПроекта Тогда
		Если Проект = РеквизитыТехническогоПроекта.Владелец Тогда
			Результат = Истина;
		Иначе
			ТекстСообщения = НСтр("ru='Невозможно добавление в указанный технический проект- не соответствует проект.'");
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru='Невозможно добавление в указанный технический проект. Недостаточно прав'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ДобавитьИдеиВТехническийПроект(Идеи, ТехническийПроект)
	
	ЕстьДобавленные = Ложь;
	
	ДобавляемыеИдеи = Новый Массив;
	
	МассивОшибок = Новый Массив;
	
	Для Каждого Идея из Идеи Цикл
		Если ТипЗнч(Идея) = Тип("СправочникСсылка.Ошибки") Тогда
			МассивОшибок.Добавить(Идея);
		КонецЕсли;
	КонецЦикла;
	
	МассивИгнорируемых = Новый Массив;
	
	Если МассивОшибок.Количество()>0 Тогда
		
		Соответствие = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОшибок, "ПометкаУдаления,Признана,НеПризнана");
		
		Для Каждого ЭлементСоответствия из Соответствие Цикл
			
			ЗначенияРеквизитов = ЭлементСоответствия.Значение;
			
			Если НЕ ЗначенияРеквизитов.ПометкаУдаления И НЕ ЗначенияРеквизитов.Признана И НЕ ЗначенияРеквизитов.НеПризнана Тогда
				МассивИгнорируемых.Добавить(ЭлементСоответствия.Ключ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого Идея из Идеи Цикл
		Если МассивИгнорируемых.Найти(Идея) = Неопределено Тогда
			ДобавляемыеИдеи.Добавить(Идея);
			ЕстьДобавленные = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ДобавляемыеИдеи.Количество()=0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ТехническийПроектОбъект = ТехническийПроект.ПолучитьОбъект();
		ТехническийПроектОбъект.Заблокировать();
	Исключение
		ТекстСообщения = НСтр("ru='Не удалось заблокировать технический проект'");
		ВызватьИсключение(ТекстСообщения);
	КонецПопытки;
	
	Для Каждого Идея из ДобавляемыеИдеи Цикл
		НоваяСтрока = ТехническийПроектОбъект.ИдеиИОшибки.Добавить();
		НоваяСтрока.Идея = Идея;
	КонецЦикла;
	
	ТехническийПроектОбъект.Записать();
	ТехническийПроектОбъект.Разблокировать();
	
	Возврат ЕстьДобавленные;
	
КонецФункции

#КонецОбласти