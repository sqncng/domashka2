#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Владелец = Проекты.ПроектПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Имя = "Пользователи" ИЛИ Имя = "ВнешниеПользователи" Тогда
		ПроверитьНаличиеПредустановленныхВидовДоступа(Отказ);
	КонецЕсли;
	
	СкорректироватьДанныеНеСоответствующиеПроекту();
	
	ДополнительныеСвойства.Вставить("КоллекцияИзмененныхОбъектов", Новый Массив);
	Версионирование.ЗарегистрироватьИзмененияОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНаличиеПредустановленныхВидовДоступа(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса=
	"ВЫБРАТЬ
	|	ВидыДоступа.Ссылка КАК ВидДоступа
	|ИЗ
	|	Справочник.ВидыДоступа КАК ВидыДоступа
	|ГДЕ
	|	ВидыДоступа.Имя = &Имя
	|	И ВидыДоступа.Владелец = &Проект
	|	И ВидыДоступа.Ссылка <> &Ссылка"
	;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Имя", Имя);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Проект", Владелец);
	
	Запрос.Текст = ТекстЗапроса;
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ТекстСообщения = НСтр("ru='В информационной базе уже существует вид доступа %Имя% для данного проекта'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Имя%", Имя);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура СкорректироватьДанныеНеСоответствующиеПроекту()
	
	Если ЗначениеЗаполнено(РазделПроекта) Тогда
		
		ПроектРаздела = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РазделПроекта, "Владелец");
		
		Если ПроектРаздела <> Владелец Тогда
			РазделПроекта = Справочники.РазделыПроекта.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли