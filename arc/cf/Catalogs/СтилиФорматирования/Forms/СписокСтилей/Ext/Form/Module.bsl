
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Родитель") Тогда
		Элементы.Список.РодительВерхнегоУровня = Параметры.Родитель;
	КонецЕсли;
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.ТекущаяСтрока = Параметры.ТекущаяСтрока;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияСписокСтилейФорматированияПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияСписокСтилейФорматированияПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И НЕ ПустаяСтрока(ТекущиеДанные.Шаблон) Тогда
		ОбразецОформления = ОбщегоНазначенияСППРКлиент.СформироватьОбразецФорматирования(ТекущиеДанные.Шаблон);
	Иначе
		ОбразецОформления = "<HTML> <HTML>";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьСтили(Команда)
	
	ТекстЗаголовка = НСтр("ru='Стили форматирования справки'");
	ВывестиСтилиВМакет().Показать(ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПредустановленныеСтили(Команда)
	
	ФормированиеСправкиСервер.ОбновитьСтили();
	
	ТекстСообщения = НСтр("ru='Загрузка завершена'");
	ПоказатьОповещениеПользователя(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВывестиСтилиВМакет()

	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Справочники.СтилиФорматирования.ПолучитьМакет("Макет");
	
	Секция = Макет.ПолучитьОбласть(1, , 2);
	ТабДок.Вывести(Секция);
	
	Секция = Макет.ПолучитьОбласть(3, , 3);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Спр.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Спр.ЭтоГруппа
	|			ТОГДА ""1""
	|		ИНАЧЕ ""0""
	|	КОНЕЦ КАК ЭтоГруппа,
	|	Спр.Родитель КАК Родитель,
	|	Спр.Наименование КАК Наименование,
	|	Спр.ИмяПредопределенныхДанных КАК ИмяПредопределенного,
	|	Спр.Шаблон КАК Шаблон,
	|	ВЫБОР
	|		КОГДА Спр.СтильКартинка
	|			ТОГДА ""1""
	|		ИНАЧЕ ""0""
	|	КОНЕЦ КАК СтильКартинка
	|ИЗ
	|	Справочник.СтилиФорматирования КАК Спр
	|ГДЕ
	|	НЕ Спр.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка ИЕРАРХИЯ
	|");

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Секция.Параметры.Заполнить(Выборка);
		Секция.Параметры.Ссылка = ЗначениеВСтрокуВнутр(Выборка.Ссылка);
		Секция.Параметры.Родитель = ЗначениеВСтрокуВнутр(Выборка.Родитель);
		
		ТабДок.Вывести(Секция);
	КонецЦикла;

	Возврат ТабДок;

КонецФункции

#КонецОбласти
