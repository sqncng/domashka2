#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Проект") И ЗначениеЗаполнено(Параметры.Проект) Тогда
		Проект = Параметры.Проект;
	Иначе
		Проект = ПараметрыСеанса.ТекущийПроект;
	КонецЕсли;
	
	АдресКартинки = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ХранилищеКартинки");
	
	ЗаполнитьОбъектыМетаданных();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКартинки);
		ТекущийОбъект.ХранилищеКартинки = Новый ХранилищеЗначения(ДвоичныеДанные);
		УдалитьИзВременногоХранилища(АдресКартинки);
		АдресКартинки = "";
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "ХранилищеКартинки");
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_КартинкаСправки", Объект.Ссылка);	

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПросмотрКартинки(Команда)
	
	ОткрытьПросмотр();

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выбор файла'");
	ДиалогВыбораФайла.Фильтр = "(*.png;*.jpg;*.jpeg)|*.png;*.jpg;*.jpeg|" + 
		"(*.bmp;*.dib;*.rle;*.tif;*.gif;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.tif;*.gif;*.ico;*.wmf;*.emf)";	
	ДиалогВыбораФайла.Каталог = КаталогДокументов();	
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);				
				
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Введите имя файла'");
	ДиалогВыбораФайла.Расширение = Объект.Расширение;
	Фильтр = НСтр("ru = '(*%Расширение%)|*%Расширение%'");
	Фильтр = СтрЗаменить(Фильтр, "%Расширение%", Объект.Расширение);
	ДиалогВыбораФайла.Фильтр = Фильтр;
	ДиалогВыбораФайла.Каталог = КаталогДокументов();			
	Оповещение = Новый ОписаниеОповещения("ОбработатьСохранениеФайла", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);				
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьПомещениеФайла", ЭтотОбъект);
		Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
			НачатьПомещениеФайла(Оповещение, АдресКартинки, ВыбранныеФайлы[0], Ложь, УникальныйИдентификатор);		
		Иначе
			НачатьПомещениеФайла(Оповещение,, ВыбранныеФайлы[0], Ложь, УникальныйИдентификатор);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПомещениеФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
                               
    Если Не Результат Тогда
        Возврат;
	КонецЕсли;
	
	АдресКартинки = Адрес;
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКартинки);
	КартинкаСправки = РеквизитФормыВЗначение("Объект");
	КартинкаСправки.Размер = ДвоичныеДанные.Размер();
	КартинкаСправки.Расширение = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла).Расширение;
	КартинкаСправки.ДатаИзменения = ТекущаяДата();
	Модифицированность = Истина;
	ЗначениеВРеквизитФормы(КартинкаСправки, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСохранениеФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Картинка = Новый Картинка(ДвоичныеДанныеКартинки());
		Картинка.Записать(ВыбранныеФайлы[0]);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПросмотр()
	
	Картинка = Новый Картинка(ДвоичныеДанныеКартинки());
	ПоказатьЗначение(,Картинка);
	
КонецПроцедуры

&НаСервере
Функция ДвоичныеДанныеКартинки()
	
	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		Возврат ПолучитьИзВременногоХранилища(АдресКартинки);
	Иначе
		Возврат Объект.Ссылка.ХранилищеКартинки.Получить();
	КонецЕсли;
		
КонецФункции

&НаСервере
Процедура ЗаполнитьОбъектыМетаданных()
	
	КоллекцияОбъектов = ОбъектыМетаданных.ПолучитьЭлементы();
	КоллекцияОбъектов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТипОбъектаИндексКартинки.ИндексКартинки КАК ИндексКартинки,
	|	ТаблицаТипОбъектаИндексКартинки.ТипОбъекта КАК ТипОбъекта
	|ПОМЕСТИТЬ ТаблицаТипОбъектаИндексКартинки
	|ИЗ
	|	&ТаблицаТипОбъектаИндексКартинки КАК ТаблицаТипОбъектаИндексКартинки ГДЕ НЕ ТаблицаТипОбъектаИндексКартинки.ЭтоГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаТипОбъектаИндексКартинки.ТипОбъекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Подсистемы.Ссылка КАК Подсистема,
	|	Подсистемы.Имя КАК ИмяПодсистемы,
	|	ОбъектыМетаданных.Ссылка КАК ОбъектМетаданных,
	|	ОбъектыМетаданных.Имя КАК ИмяОбъектаМетаданных,
	|	ОбъектыМетаданных.Родитель КАК ТипОбъекта,
	|	ОбъектыМетаданных.Родитель.Имя КАК ИмяТипаОбъекта,
	|	ФормыОбъектовМетаданных.Ссылка КАК ФормаОбъекта,
	|	ФормыОбъектовМетаданных.Имя КАК ИмяФормыОбъекта,
	|	ФормыОбъектовМетаданных.Владелец КАК ФормаОбъектаОбъект,
	|	ФормыОбъектовМетаданных.Владелец.Имя КАК ФормаОбъектаИмяОбъекта,
	|	ФормыОбъектовМетаданных.Владелец.Родитель КАК ФормаОбъектаТипОбъекта,
	|	ФормыОбъектовМетаданных.Владелец.Родитель.Имя КАК ФормаОбъектаИмяТипаОбъекта,
	|	ВЫБОР
	|		КОГДА Подсистемы.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА Подсистемы.Владелец
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОбъектыМетаданных.Ссылка ЕСТЬ НЕ NULL 
	|					ТОГДА ОбъектыМетаданных.Владелец
	|				ИНАЧЕ ФормыОбъектовМетаданных.Владелец.Владелец
	|			КОНЕЦ
	|	КОНЕЦ КАК Проект,
	|	ЕСТЬNULL(ТаблицаТипОбъектаИндексКартинки.ИндексКартинки, 91) КАК ИндексКартинки
	|ИЗ
	|	Справочник.ЭлементыСправки КАК ЭлементыСправки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭлементыСправки.КартинкиДляСправки КАК КартинкиДляСправкиТЧ
	|		ПО (КартинкиДляСправкиТЧ.Ссылка = ЭлементыСправки.Ссылка И КартинкиДляСправкиТЧ.Картинка = &КартинкаСправки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Подсистемы КАК Подсистемы
	|		ПО ЭлементыСправки.Владелец = Подсистемы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыМетаданных КАК ОбъектыМетаданных
	|		ПО ЭлементыСправки.Владелец = ОбъектыМетаданных.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТипОбъектаИндексКартинки КАК ТаблицаТипОбъектаИндексКартинки
	|		ПО (ОбъектыМетаданных.Родитель.Имя = ТаблицаТипОбъектаИндексКартинки.ТипОбъекта)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФормыОбъектовМетаданных КАК ФормыОбъектовМетаданных
	|		ПО ЭлементыСправки.Владелец = ФормыОбъектовМетаданных.Ссылка
	|ГДЕ
	|	(НЕ ОбъектыМетаданных.Ссылка ЕСТЬ NULL 
	|			ИЛИ НЕ ФормыОбъектовМетаданных.Ссылка ЕСТЬ NULL 
	|			ИЛИ НЕ Подсистемы.Ссылка ЕСТЬ NULL )
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектМетаданных,
	|	ФормаОбъекта,
	|	Подсистема";
	
	ТаблицаТипОбъектаИндексКартинки = РаботаСОбъектамиМетаданных.ТаблицаТипОбъектаИндексКартинки();	
	
	Запрос.УстановитьПараметр("ТаблицаТипОбъектаИндексКартинки", ТаблицаТипОбъектаИндексКартинки);
	Запрос.УстановитьПараметр("КартинкаСправки", Объект.Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ЭлементыДерева = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Подсистема) Тогда
			
			ТипОбъекта = ЭлементыДерева.Получить(Справочники.Подсистемы.ПустаяСсылка());
			Если ТипОбъекта = Неопределено Тогда
				ТипОбъекта = КоллекцияОбъектов.Добавить();
				ТипОбъекта.Ссылка = Справочники.Подсистемы.ПустаяСсылка();
				ТипОбъекта.Представление = НСтр("ru = 'Подсистемы'");
				ТипОбъекта.Проект = Выборка.Проект;
				ТипОбъекта.ИндексКартинки = 19;
				ЭлементыДерева.Вставить(ТипОбъекта.Ссылка, ТипОбъекта);
			КонецЕсли;
			
			Подсистема = ЭлементыДерева.Получить(Выборка.Подсистема);
			Если Подсистема = Неопределено Тогда
				Подсистема = ТипОбъекта.ПолучитьЭлементы().Добавить(); 
				Подсистема.Ссылка = Выборка.Подсистема;
				Подсистема.Представление = Выборка.ИмяПодсистемы;
				Подсистема.ИндексКартинки = 19;
				ЭлементыДерева.Вставить(Подсистема.Ссылка, Подсистема);
			КонецЕсли;
									
		ИначеЕсли ЗначениеЗаполнено(Выборка.ОбъектМетаданных) Тогда
			
			ТипОбъекта = ЭлементыДерева.Получить(Выборка.ТипОбъекта);
			Если ТипОбъекта = Неопределено Тогда
				ТипОбъекта = КоллекцияОбъектов.Добавить();
				ТипОбъекта.Ссылка = Выборка.ТипОбъекта;
				ТипОбъекта.Представление = Выборка.ИмяТипаОбъекта;
				ТипОбъекта.Проект = Выборка.Проект;
				ТипОбъекта.ИндексКартинки = Выборка.ИндексКартинки;
				ЭлементыДерева.Вставить(ТипОбъекта.Ссылка, ТипОбъекта);
			КонецЕсли;
			
			ОбъектМетаданных = ЭлементыДерева.Получить(Выборка.ОбъектМетаданных);
			Если ОбъектМетаданных = Неопределено Тогда
				ОбъектМетаданных = ТипОбъекта.ПолучитьЭлементы().Добавить(); 
				ОбъектМетаданных.Ссылка = Выборка.ОбъектМетаданных;
				ОбъектМетаданных.Представление = Выборка.ИмяОбъектаМетаданных;
				ОбъектМетаданных.ИндексКартинки = Выборка.ИндексКартинки;
				ЭлементыДерева.Вставить(ОбъектМетаданных.Ссылка, ОбъектМетаданных);
			КонецЕсли;

		ИначеЕсли ЗначениеЗаполнено(Выборка.ФормаОбъекта) Тогда
			
			ТипОбъекта = ЭлементыДерева.Получить(Выборка.ФормаОбъектаТипОбъекта);
			Если ТипОбъекта = Неопределено Тогда
				ТипОбъекта = КоллекцияОбъектов.Добавить();
				ТипОбъекта.Ссылка = Выборка.ФормаОбъектаТипОбъекта;
				ТипОбъекта.Представление = Выборка.ФормаОбъектаИмяТипаОбъекта;
				ТипОбъекта.Проект = Выборка.Проект;
				ТипОбъекта.ИндексКартинки = Выборка.ИндексКартинки;
				ЭлементыДерева.Вставить(ТипОбъекта.Ссылка, ТипОбъекта);
			КонецЕсли;

			ОбъектМетаданных = ЭлементыДерева.Получить(Выборка.ФормаОбъектаОбъект);
			Если ОбъектМетаданных = Неопределено Тогда
				ОбъектМетаданных = ТипОбъекта.ПолучитьЭлементы().Добавить();
				ОбъектМетаданных.Ссылка = Выборка.ФормаОбъектаОбъект;
				ОбъектМетаданных.Представление = Выборка.ФормаОбъектаИмяОбъекта;
				ОбъектМетаданных.ИндексКартинки = Выборка.ИндексКартинки;
				ЭлементыДерева.Вставить(ОбъектМетаданных.Ссылка, ОбъектМетаданных);
			КонецЕсли;
			
			ФормаОбъекта = ЭлементыДерева.Получить(Выборка.ФормаОбъекта);
			Если ФормаОбъекта = Неопределено Тогда
				ФормаОбъекта = ОбъектМетаданных.ПолучитьЭлементы().Добавить(); 
				ФормаОбъекта.Ссылка = Выборка.ФормаОбъекта;
				ФормаОбъекта.Представление = Выборка.ИмяФормыОбъекта;
				ФормаОбъекта.ИндексКартинки = 47;
				ЭлементыДерева.Вставить(ФормаОбъекта.Ссылка, ФормаОбъекта);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти