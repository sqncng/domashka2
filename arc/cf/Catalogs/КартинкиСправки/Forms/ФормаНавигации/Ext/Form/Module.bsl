
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("Проект") И ЗначениеЗаполнено(Параметры.Проект) Тогда
		Проект = Параметры.Проект;
	Иначе
		Проект = ПараметрыСеанса.ТекущийПроект;
	КонецЕсли;
	
	ЗаполнитьСпискиКартинок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	ЗаполнитьСпискиКартинок(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
		
	Если ТекущаяСтраница.Имя = "СтраницаНеИспользуемыеКартинки" Тогда
		ТекущаяТаблица = Элементы.НеИспользуемыеКартинки;
	ИначеЕсли ТекущаяСтраница.Имя = "СтраницаОбъектыМетаданных" Тогда
		ТекущаяТаблица = Элементы.ОбъектыМетаданных;
	КонецЕсли;
	
	Если ТекущаяТаблица.ТекущиеДанные <> Неопределено 
		И ТипЗнч(ТекущаяТаблица.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.КартинкиСправки") Тогда
		АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущаяТаблица.ТекущиеДанные.Ссылка, "ХранилищеКартинки");
	Иначе
		АдресКартинки = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыМетаданных

&НаКлиенте
Процедура ОбъектыМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(Элементы.ОбъектыМетаданных.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.КартинкиСправки") Тогда
		ПараметрыФормы = Новый Структура("Ключ", Элементы.ОбъектыМетаданных.ТекущиеДанные.Ссылка);
		ОткрытьФорму("Справочник.КартинкиСправки.ФормаОбъекта", ПараметрыФормы);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыМетаданныхПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено 
		И ТипЗнч(Элемент.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.КартинкиСправки") Тогда
		АдресКартинки = ПолучитьНавигационнуюСсылку(Элемент.ТекущиеДанные.Ссылка, "ХранилищеКартинки");
	Иначе
		АдресКартинки = "";
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНеИспользуемыеКартинки

&НаКлиенте
Процедура НеИспользуемыеКартинкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(Элементы.НеИспользуемыеКартинки.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.КартинкиСправки") Тогда
		ПараметрыФормы = Новый Структура("Ключ", Элементы.НеИспользуемыеКартинки.ТекущиеДанные.Ссылка);
		ОткрытьФорму("Справочник.КартинкиСправки.ФормаОбъекта", ПараметрыФормы);		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НеИспользуемыеКартинкиПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено 
		И ТипЗнч(Элемент.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.КартинкиСправки") Тогда
		АдресКартинки = ПолучитьНавигационнуюСсылку(Элемент.ТекущиеДанные.Ссылка, "ХранилищеКартинки");
	Иначе
		АдресКартинки = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСпискиКартинок(ЗаполнятьНеИспользуемые = Истина)
	
	Справочники.КартинкиСправки.ЗаполнитьОбъектыМетаданных(ОбъектыМетаданных, Проект);
	
	Если ЗаполнятьНеИспользуемые Тогда
		Справочники.КартинкиСправки.ЗаполнитьНеИспользуемыеКартинки(НеИспользуемыеКартинки);
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти