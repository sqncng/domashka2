#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		
		Владелец = Проекты.ПроектПоУмолчанию();
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
			
			КоллекцияИсходящихОМ = Неопределено;
			
			Если ДанныеЗаполнения.Свойство("ИсходящиеОбъектыМетаданных", КоллекцияИсходящихОМ) Тогда
				Если ТипЗнч(КоллекцияИсходящихОМ) = Тип("Массив") Тогда
					Для Каждого ЭлементМассива из КоллекцияИсходящихОМ Цикл
						НоваяСтрока = ИсходящиеОбъектыМетаданных.Добавить();
						НоваяСтрока.ОбъектМетаданных = ЭлементМассива;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	ЗаписьСправочников.УстановитьКодСправочника(ЭтотОбъект);
	ЗаписьСправочников.УстановитьПолныйКодСправочника(ЭтотОбъект,2);
	
	КоллекцияИзмененныхОбъектов = Новый Массив;
	ДополнительныеСвойства.Вставить("КоллекцияИзмененныхОбъектов", КоллекцияИзмененныхОбъектов);
	Версионирование.ЗарегистрироватьИзмененияОбъекта(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ЭтоГруппа И ТипФункции = Перечисления.ТипыФункций.Отчет Тогда
		НепроверяемыеРеквизиты.Добавить("Результат");
	КонецЕсли;
	
	Если НепроверяемыеРеквизиты.Количество()>0 Тогда
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли