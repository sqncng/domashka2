#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ВыборСвязейФункции = Параметры.ВыборСвязейФункции;
	
	Список.Параметры.УстановитьЗначениеПараметра("ФункцияПриемник", Параметры.ФункцияПриемник);
	Список.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
	Список.Параметры.УстановитьЗначениеПараметра("ВидОбъекта",
	      Версионирование.ВидВерсионируемогоОбъекта(Справочники.ФункцииСистемы.ПустаяСсылка()));
		  
	СписокКонечныхФункций.Параметры.УстановитьЗначениеПараметра("ФункцияПриемник", Параметры.ФункцияПриемник);
	
	УстановитьУсловноеОформлениеСписка();
	УстановитьУсловноеОформлениеСпискаКонечныхФункций();
	
	УстановитьСтраницуСпискаИОтображениеЭлементов();
	УстановитьВидимостьПереключателяРежима();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("РежимОтображенияСписков") <> Неопределено Тогда
		Если ВыборСвязейФункции Тогда
			ОтображениеСписка = РежимОтображенияСписков;
			УстановитьСтраницуСпискаИОтображениеЭлементов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображениеСпискаПриИзменении(Элемент)
	
	Если ОтображениеСписка = 1 Тогда
		РежимОтображенияСписков = 1;
	Иначе
		РежимОтображенияСписков = 0;
	КонецЕсли;
	
	УстановитьСтраницуСпискаИОтображениеЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписокКонечныхФункций

&НаКлиенте
Процедура СписокКонечныхФункцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.СписокКонечныхФункций.МножественныйВыбор Тогда
		ВыбранныеЗначения = Новый Массив;
		ВыбранныеЗначения.Добавить(ВыбраннаяСтрока);
	Иначе
		ВыбранныеЗначения = ВыбраннаяСтрока;
	КонецЕсли;
	
	ОповеститьОВыборе(ВыбранныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИзСпискаКонечныхФункций(Команда)
	
	ВыбранныеЗначения = Новый Массив;
	Для Каждого Значение из Элементы.СписокКонечныхФункций.ВыделенныеСтроки Цикл
		ВыбранныеЗначения.Добавить(Значение);
	КонецЦикла;
	
	ОповеститьОВыборе(ВыбранныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	УсловноеОформлениеСписка = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеСписка.Элементы.Очистить();
	
	ОбщегоНазначенияСППР.УстановитьУсловноеОформлениеСпискаОтветственный(УсловноеОформлениеСписка);
	КонтрольИзменений.УстановитьУсловноеОформлениеСпискаСостояниеКонтроля(УсловноеОформлениеСписка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСпискаКонечныхФункций()
	
	УсловноеОформлениеСписка = СписокКонечныхФункций.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеСписка.Элементы.Очистить();
	
	ОбщегоНазначенияСППР.УстановитьУсловноеОформлениеСпискаОтветственный(УсловноеОформлениеСписка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтраницуСпискаИОтображениеЭлементов()
	
	Если ВыборСвязейФункции Тогда
		Если РежимОтображенияСписков = 1 Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСписок;
		Иначе
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДерево;
		КонецЕсли;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДерево;
	КонецЕсли;
	
	Если (Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДерево) Тогда
		Элементы.СписокВыбрать.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.СписокКонечныхФункцийВыбрать.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПереключателяРежима()
	
	Элементы.ОтображениеСписка.Видимость = ВыборСвязейФункции;
	
КонецПроцедуры

#КонецОбласти