
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.НастройкиСогласованияРесурсов.РассчитатьНастройкиСогласования(Ссылка);
	
	СнятьПризнакОсновнойУДругихВидовЕслиНеобходимо();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СнятьПризнакОсновнойУДругихВидовЕслиНеобходимо()

	Если Основной Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВидыСогласуемыхРесурсов.Ссылка       КАК Ссылка,
		|	ВидыСогласуемыхРесурсов.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ВидыСогласуемыхРесурсов КАК ВидыСогласуемыхРесурсов
		|ГДЕ
		|	ВидыСогласуемыхРесурсов.Ссылка <> &Ссылка
		|	И ВидыСогласуемыхРесурсов.Основной";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли;
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
		
			Попытка
				ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			Исключение
				ТекстОшибки = СтрШаблон(НСтр("ru='Не удалось заблокировать вид ресурса ""%1"" для снятия признака ""Основной"". %2'"), 
				                        Выборка.Наименование,
				                        КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				                        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
				Отказ = Истина;
				Возврат;
			КонецПопытки;
			
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СправочникОбъект.Основной = Ложь;
			
			Попытка
				СправочникОбъект.Записать();
			Исключение
				ТекстОшибки = СтрШаблон(НСтр("ru='Не удалось записать вид ресурса ""%1"" для снятия признака ""Основной"". %2'"), 
				                        СправочникОбъект.Наименование, 
				                        КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, СправочникОбъект.Ссылка);
				Отказ = Истина;
				Возврат;
			КонецПопытки;
		
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли