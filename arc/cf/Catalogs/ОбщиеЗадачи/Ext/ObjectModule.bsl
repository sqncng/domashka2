#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ДатаСоздания = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаСоздания = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	Статус = Перечисления.СтатусыОбщихЗадач.ТребуетсяЗапланировать;
	ДополнительныеРеквизиты.Очистить();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ПроверитьЗаполнениеСтатуса(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеСтатуса(Отказ)
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	СтарыйСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
	
	Если СтарыйСтатус = Статус Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьКонфликтСтатусов = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВыполнениеОбщихЗадачСрезПоследних.Проект КАК Проект,
	|	ВыполнениеОбщихЗадачСрезПоследних.СтатусВыполнения КАК СтатусВыполнения
	|ИЗ
	|	РегистрСведений.ВыполнениеОбщихЗадач.СрезПоследних(, ОбщаяЗадача = &ОбщаяЗадача) КАК ВыполнениеОбщихЗадачСрезПоследних
	|ГДЕ
	|	ВыполнениеОбщихЗадачСрезПоследних.СтатусВыполнения <> ЗНАЧЕНИЕ(Перечисление.СтатусыОбщихЗадач.Отменена)"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбщаяЗадача", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Статус = Перечисления.СтатусыОбщихЗадач.Запланирована Тогда
			Если Выборка.СтатусВыполнения = Перечисления.СтатусыОбщихЗадач.ТребуетсяЗапланировать Тогда
				ЕстьКонфликтСтатусов = Истина;
				Прервать;
			КонецЕсли;
		ИначеЕсли Статус = Перечисления.СтатусыОбщихЗадач.Выполнена Тогда
			Если Выборка.СтатусВыполнения = Перечисления.СтатусыОбщихЗадач.ТребуетсяЗапланировать
				ИЛИ Выборка.СтатусВыполнения = Перечисления.СтатусыОбщихЗадач.Запланирована Тогда
				ЕстьКонфликтСтатусов = Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьКонфликтСтатусов Тогда
		ТекстСообщения = НСтр("ru='Задача не может быть записана в статусе %Статус%, т.к. имеются подчиненные записи в предыдущих статусах'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Статус%", Статус);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Статус",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
