#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;	
	
	Если Шаблон Тогда
		Отказ = НЕ ИмяШаблонаУникально(Наименование);
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Задано не уникальное имя шаблона <%1>'"),Наименование));
		КонецЕсли;	 
	КонецЕсли;
	
	СнипетСценария = НРег(ТестированиеГрафическиеСхемыСервер.СнипетИзСтроки(Наименование));
	
	СценарииДубли = СценарииПоСнипету(СнипетСценария);
	Если СценарииДубли.Количество() > 0 Тогда
		Отказ = Истина;
		
		СтрокаДублей = "";
		Для Каждого ДанныеСценария Из СценарииДубли Цикл
			СтрокаДублей = СтрокаДублей + Символы.ПС + ДанныеСценария.Наименование + ", " + ДанныеСценария.Код;
		КонецЦикла;	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Задано не уникальное имя сценария <%1>. Имя конфликтует с %2'"),Наименование,СтрокаДублей));
	КонецЕсли;	 
	
	ВерсияСценария = Справочники.СценарииРаботыПользователей.ПолучитьНовуюВерсиюСценария();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ДополнительныеСвойства.Вставить("ЗначениеКопирования", ОбъектКопирования.Ссылка);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		
		Владелец = Проекты.ПроектПоУмолчанию();
		
		Если НЕ ЭтоГруппа Тогда
			Ответственный = Пользователи.АвторизованныйПользователь();
			НеобратимоМеняетДанные = Истина;
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			Если ДанныеЗаполнения.Свойство("Владелец") Тогда
				ЭтотОбъект.Владелец = ДанныеЗаполнения.Владелец;
			КонецЕсли;	 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяШаблонаУникально(ИмяШаблона)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Сценарии.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СценарииРаботыПользователей КАК Сценарии
		|ГДЕ
		|	НЕ Сценарии.ПометкаУдаления
		|	И Сценарии.Шаблон
		|	И Сценарии.Наименование = &Наименование
		|	И Сценарии.Ссылка <> &Ссылка
		|	И НЕ Сценарии.ЭтоГруппа
		|	И Сценарии.Владелец = &Проект";
	
	Запрос.УстановитьПараметр("Наименование", ИмяШаблона);
	Запрос.УстановитьПараметр("Проект", Владелец);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Пустой();
КонецФункции	

Функция СценарииПоСнипету(СнипетСценария)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Сценарии.Ссылка КАК Ссылка,
		|	Сценарии.Наименование КАК Наименование,
		|	Сценарии.Код КАК Код
		|ИЗ
		|	Справочник.СценарииРаботыПользователей КАК Сценарии
		|ГДЕ
		|	НЕ Сценарии.ПометкаУдаления
		|	И Сценарии.Ссылка <> &Ссылка
		|	И НЕ Сценарии.ЭтоГруппа
		|	И Сценарии.Владелец = &Проект
		|	И Сценарии.СнипетСценария = &СнипетСценария";
	
	Запрос.УстановитьПараметр("СнипетСценария", СнипетСценария);
	Запрос.УстановитьПараметр("Проект", Владелец);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	

	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
КонецФункции	

#КонецОбласти

#КонецЕсли