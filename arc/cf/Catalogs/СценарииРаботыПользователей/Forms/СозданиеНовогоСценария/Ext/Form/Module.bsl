#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Наименование") Тогда
		Наименование = Параметры.Наименование;
	КонецЕсли;	 
	Если Параметры.Свойство("РазрешеноИспользоватьВПроцессах") Тогда
		РазрешеноИспользоватьВПроцессах = Параметры.РазрешеноИспользоватьВПроцессах;
	КонецЕсли;	 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ТекстИсключения = НСтр("ru = 'Не заполнено поле ""Наименование"".'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;	 
	Если НЕ ЗначениеЗаполнено(Группа) Тогда
		ТекстИсключения = НСтр("ru = 'Не заполнено поле ""Группа"".'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;	 
	Если НЕ ЗначениеЗаполнено(ФункцияСистемы) Тогда
		ТекстИсключения = НСтр("ru = 'Не заполнено поле ""Функция системы"".'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;	 
	
	Сценарий = СоздатьСценарийСервер(Наименование,Группа,ФункцияСистемы,
	     РазрешеноИспользоватьВПроцессах);
	
	Данные = Новый Структура;
	Данные.Вставить("СозданныйСценарий",Сценарий);
	ОповеститьОВыборе(Данные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СоздатьСценарийСервер(Наименование,Группа,ФункцияСистемы,РазрешеноИспользоватьВПроцессах)
	ОбъектСценарий = Справочники.СценарииРаботыПользователей.СоздатьЭлемент();
	ОбъектСценарий.Наименование = Наименование;
	ОбъектСценарий.Родитель = Группа;
	ОбъектСценарий.ФункцияСистемы = ФункцияСистемы;
	ОбъектСценарий.Владелец = ФункцияСистемы.Владелец;
	ОбъектСценарий.РазрешеноИспользоватьВПроцессах = РазрешеноИспользоватьВПроцессах;
	
	Попытка
		ОбъектСценарий.Записать();
	Исключение
		ТекстИсключения = НСтр("ru = 'Не получилось записать сценарий. Текст ошибки: %1'");
		ТекстИсключения = СтрШаблон(ТекстИсключения,Символы.ПС + ОписаниеОшибки());
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Возврат ОбъектСценарий.Ссылка; 
КонецФункции	 

#КонецОбласти

