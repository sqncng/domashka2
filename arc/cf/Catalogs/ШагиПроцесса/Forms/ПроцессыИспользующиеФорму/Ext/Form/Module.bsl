#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Форма", Параметры.Форма);
	
	ОбъектМетаданных = Неопределено;
	ТипыФормы = Новый Массив;
	
	Если ЗначениеЗаполнено(Параметры.Форма) Тогда
		ИнформацияОФорме = ИнформацияОформе(Параметры.Форма);
		ОбъектМетаданных = ИнформацияОформе.Владелец;
		ТипыФормы = ИнформацияОформе.ТипыФормы
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОбъектМетаданных", ОбъектМетаданных);
	Список.Параметры.УстановитьЗначениеПараметра("ТипыФормы", ТипыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "Процесс" Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", Элементы.Список.ТекущиеДанные.Процесс);
		ОткрытьФорму("Справочник.Процессы.ФормаОбъекта", ПараметрыФормы);
		
	ИначеЕсли Поле.Имя = "ШагПроцесса" Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", Элементы.Список.ТекущиеДанные.ШагПроцесса);
		ОткрытьФорму("Справочник.ШагиПроцесса.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

 &НаСервере
Функция ИнформацияОформе(Форма)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ФормыОбъектовМетаданных.Владелец КАК Владелец,
	|	ФормыОбъектовМетаданных.ТипыФормы.(
	|		ТипФормы КАК ТипФормы
	|	) КАК ТипыФормы
	|ИЗ
	|	Справочник.ФормыОбъектовМетаданных КАК ФормыОбъектовМетаданных
	|ГДЕ
	|	ФормыОбъектовМетаданных.Ссылка = &Форма"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Форма", Форма);
	
	Информация = Новый Структура;
	Информация.Вставить("Владелец", Неопределено);
	Информация.Вставить("ТипыФормы", Новый Массив);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Информация.Вставить("Владелец", Выборка.Владелец);
		
		ТипыФормы = Выборка.ТипыФормы.Выгрузить().ВыгрузитьКолонку("ТипФормы");
		Информация.Вставить("ТипыФормы", ТипыФормы);
	КонецЕсли;
	
	Возврат Информация;
	
КонецФункции

#КонецОбласти