
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Проект) Тогда
		ТекстИсключения = НСтр("ru = 'Не предусмотрено открытие формы без отбора по проекту.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																		"Владелец",
																		Параметры.Проект,
																		ВидСравненияКомпоновкиДанных.Равно,
																		,
																		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыборКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыборКлиент();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборКлиент()
	
	ВыбраннаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОтмечатьМакеты
		И Не ОтмечатьОбъекты
		И Не ОтмечатьФормы Тогда
		
		ТекстПредупрежения = НСтр("ru = 'Выберите, какого типа объекты нужно отмечать по подсистеме.'");
		
		ПоказатьПредупреждение(,ТекстПредупрежения);
		
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Подсистема", ВыбраннаяСтрока.Ссылка);
	Результат.Вставить("ОтмечатьМакеты", ОтмечатьМакеты);
	Результат.Вставить("ОтмечатьОбъекты", ОтмечатьОбъекты);
	Результат.Вставить("ОтмечатьФормы", ОтмечатьФормы);
	Результат.Вставить("ОтмечатьПоВыбраннойПодсистемеСУчетомПодчиненныхПодсистем",
					ОтмечатьПоВыбраннойПодсистемеСУчетомПодчиненныхПодсистем);
	
	Закрыть(Результат);
	
	
КонецПроцедуры

#КонецОбласти
