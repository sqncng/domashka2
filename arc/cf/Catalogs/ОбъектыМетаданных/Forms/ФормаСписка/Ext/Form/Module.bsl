
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Справочники.ОбъектыМетаданных.УстановитьТекстИндексаКартикиВТекстеЗапроса(Элементы.Список);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	РазделПроекта = Параметры.РазделПроекта;
	УстановитьПараметрРаздел(Список, РазделПроекта);
	
	Проект = ПараметрыСеанса.ТекущийПроект;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Проект, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Проект));
	
	УстановитьУсловноеОформлениеСписка();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Параметры.РазделПроекта) Тогда
		РазделПроекта = Параметры.РазделПроекта;
	Иначе
		Если Настройки.Получить("РазделПроекта")<>Неопределено Тогда
			УстановитьПараметрРаздел(Список, РазделПроекта);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		УстановитьОтборПоПроектуСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборРазделПроектаПриИзменении(Элемент)
	
	УстановитьПараметрРаздел(Список, РазделПроекта);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование и Группа Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТребуетсяУказатьВидОбъекта = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Родитель) Тогда
		ТребуетсяУказатьВидОбъекта = Истина;
	Иначе
		ИмяГруппы = ИмяГруппы(Родитель);
		
		Если ИмяГруппы = "ОбщиеОбъекты" Тогда
			ТребуетсяУказатьВидОбъекта = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяУказатьВидОбъекта Тогда
		ТекстПредупреждения = НСтр("ru = 'Объект метаданных может создаваться только в рамках конкретного вида объектов метаданных'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения); 
	
		Отказ = Истина;
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрРаздел(Список, РазделПроекта)

	Список.Параметры.УстановитьЗначениеПараметра("Раздел", РазделПроекта);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПроектуСервер()
	
	Проекты.УстановитьОтборВСпискеПоПроекту(Список);
	
КонецПроцедуры

&НаСервере
Функция ИмяГруппы(Группа)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Группа, "Имя");
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	УсловноеОформлениеСписка = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеСписка.Элементы.Очистить();
	
	ОбщегоНазначенияСППР.УстановитьУсловноеОформлениеСпискаОтветственный(УсловноеОформлениеСписка);
	
КонецПроцедуры

#КонецОбласти
