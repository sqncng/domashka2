#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список реквизитов, которые разрешается редактировать
// с помощью обработки группового изменения объектов
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	
	РедактируемыеРеквизиты.Добавить("ВеткаИсточник");
	РедактируемыеРеквизиты.Добавить("НеразделенныеДанные");
	РедактируемыеРеквизиты.Добавить("Ответственный");
	РедактируемыеРеквизиты.Добавить("ПереводПроверен");
	РедактируемыеРеквизиты.Добавить("Поставщик");
	РедактируемыеРеквизиты.Добавить("ПравилоПоддержки");
	РедактируемыеРеквизиты.Добавить("РазделПроекта");
	РедактируемыеРеквизиты.Добавить("СогласованиеИзменений");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

Функция СформироватьПечатныеФормы(Объекты, СУчетомПриемника=Ложь, ДанныеСоответствия=Неопределено) Экспорт
	
	Если ТипЗнч(Объекты) = Тип("Массив") Тогда
		МассивСсылок = Объекты;
	Иначе
		МассивСсылок = Новый Массив;
		МассивСсылок.Добавить(Объекты);
	КонецЕсли;
	
	ПечатныеФормы = Новый Соответствие;
	
	Для Каждого Ссылка из МассивСсылок Цикл
		
		СтруктураПечатныхФорм = Новый Структура();
		СтруктураПечатныхФорм.Вставить("ВариантыСправки", ФормированиеСправкиСервер.СформироватьВариантыСправки(Ссылка));
		
		ПечатныеФормы.Вставить(Ссылка, СтруктураПечатныхФорм);
		
	КонецЦикла;
	
	Возврат ПечатныеФормы;
	
КонецФункции

// Выполняет печать описаний и схем переданных объектов
//
// Параметры:
//  МассивОбъектов - массив объектов, подлежащих печати
//
Процедура Печать(МассивОбъектов, ПечатныеФормы) Экспорт
	
	ПечатныеФормы = СформироватьПечатныеФормы(МассивОбъектов);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		
		Если Параметры.Свойство("ИмяРодителя") Тогда
			
			Если Параметры.Отбор.Свойство("Владелец") Тогда
				ГруппаРоли = РаботаСОбъектамиМетаданных.ГруппаОбъектовМетаданных(Параметры.ИмяРодителя, Параметры.Отбор.Владелец);
				Параметры.Отбор.Вставить("Родитель", ГруппаРоли);
			КонецЕсли;
			
		ИначеЕсли Параметры.Свойство("МассивРодителей") И Параметры.Отбор.Свойство("Владелец") Тогда
			
			СтруктураГрупп = РаботаСОбъектамиМетаданных.СтруктураГруппОбъектовМетаданных(Параметры.Отбор.Владелец);
			
			СписокЗначений = Новый СписокЗначений;
			Для Каждого ИмяРодителя из Параметры.МассивРодителей Цикл
				СписокЗначений.Добавить(СтруктураГрупп[ИмяРодителя]);
			КонецЦикла;
			
			Параметры.Отбор.Вставить("Родитель", СписокЗначений);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	
	СтандартнаяОбработка = Ложь;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектыМетаданных.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыМетаданных КАК ОбъектыМетаданных
	|ГДЕ
	|	НЕ ОбъектыМетаданных.ПометкаУдаления
	|	И (НЕ &ОтбиратьПоРодителям
	|			ИЛИ ОбъектыМетаданных.Родитель.Имя В (&МассивРодителей))
	|	И (НЕ &ОтбиратьПоНаименованию
	|			ИЛИ ОбъектыМетаданных.Имя ПОДОБНО &СтрокаПоиска
	|			ИЛИ ОбъектыМетаданных.Наименование ПОДОБНО &СтрокаПоиска)
	|	И (НЕ &ОтбиратьПоПроектам
	|			ИЛИ ОбъектыМетаданных.Владелец = &Проект)
	|	И (НЕ &ОтбиратьПоРодителю
	|			ИЛИ ОбъектыМетаданных.Родитель.Имя = &ИмяРодителя)
	|	И (НЕ &ОтбиратьПоЭтоГруппа
	|			ИЛИ ОбъектыМетаданных.ЭтоГруппа = &ЭтоГруппа)
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектыМетаданных.Родитель.Код,
	|	ОбъектыМетаданных.Код"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Если Параметры.Свойство("ТипОбъектаДанных") Тогда
		
		ТипОбъектаДанных = Параметры.ТипОбъектаДанных;
		Параметры.Удалить("ТипОбъектаДанных");
		
		МассивРодителей = РаботаСОбъектамиМетаданных.ДоступныеКлассыОбъектовМетаданныхОбъектаДанных(ТипОбъектаДанных);
		
		Запрос.УстановитьПараметр("МассивРодителей", МассивРодителей);
		Запрос.УстановитьПараметр("ОтбиратьПоРодителям", Истина);
		
	ИначеЕсли Параметры.Свойство("МассивРодителей") Тогда
		Запрос.УстановитьПараметр("МассивРодителей", Параметры.МассивРодителей);
		Запрос.УстановитьПараметр("ОтбиратьПоРодителям", Истина);
	Иначе
		Запрос.УстановитьПараметр("МассивРодителей", Новый Массив);
		Запрос.УстановитьПараметр("ОтбиратьПоРодителям", Ложь);
	КонецЕсли;
		
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		Запрос.УстановитьПараметр("Проект", Параметры.Отбор.Владелец);
		Запрос.УстановитьПараметр("ОтбиратьПоПроектам", Истина);
	Иначе
		Запрос.УстановитьПараметр("Проект", Неопределено);
		Запрос.УстановитьПараметр("ОтбиратьПоПроектам", Ложь);
	КонецЕсли;
	
	Если Параметры.Свойство("ИмяРодителя") Тогда
		Запрос.УстановитьПараметр("ИмяРодителя", Параметры.ИмяРодителя);
		Запрос.УстановитьПараметр("ОтбиратьПоРодителю", Истина);
	Иначе
		Запрос.УстановитьПараметр("ИмяРодителя", Неопределено);
		Запрос.УстановитьПараметр("ОтбиратьПоРодителю", Ложь);
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Родитель") Тогда
		МассивРодителей = Новый Массив;
		МассивРодителей = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Отбор.Родитель,"Имя");
		Запрос.УстановитьПараметр("МассивРодителей",МассивРодителей);
		Запрос.УстановитьПараметр("ОтбиратьПоРодителям", Истина);
	КонецЕсли;
		
	СтрокаПоиска = Параметры.СтрокаПоиска;
		
	Если СтрокаПоиска <> Неопределено Тогда
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
		Запрос.УстановитьПараметр("ОтбиратьПоНаименованию", Истина);
	Иначе
		Запрос.УстановитьПараметр("СтрокаПоиска", "");
		Запрос.УстановитьПараметр("ОтбиратьПоНаименованию", Ложь);
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ЭтоГруппа") Тогда
		Запрос.УстановитьПараметр("ОтбиратьПоЭтоГруппа", Истина);
		Запрос.УстановитьПараметр("ЭтоГруппа", Параметры.Отбор.ЭтоГруппа);
	ИначеЕсли Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы Тогда
		Запрос.УстановитьПараметр("ОтбиратьПоЭтоГруппа", Истина);
		Запрос.УстановитьПараметр("ЭтоГруппа", Истина);
	ИначеЕсли Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы Тогда
		Запрос.УстановитьПараметр("ОтбиратьПоЭтоГруппа", Истина);
		Запрос.УстановитьПараметр("ЭтоГруппа", Ложь);
	Иначе
		Запрос.УстановитьПараметр("ОтбиратьПоЭтоГруппа", Ложь);
		Запрос.УстановитьПараметр("ЭтоГруппа", Ложь);
	КонецЕсли;
		
	Выборка = Запрос.Выполнить().Выбрать();
		
	ДанныеВыбора = Новый СписокЗначений();
		
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьТекстИндексаКартикиВТекстеЗапроса(Список) Экспорт
	
	ШаблонТекстаИндексаКартинки = 
	" КОГДА (СправочникОбъектыМетаданных.ЭтоГруппа И СправочникОбъектыМетаданных.Имя = ""%ИмяТипа%"")
	|	ТОГДА %ИндексКартинкиГруппы%
	| КОГДА (НЕ СправочникОбъектыМетаданных.ЭтоГруппа И СправочникОбъектыМетаданных.Родитель.Имя = ""%ИмяТипа%"")
	|	ТОГДА %ИндексКартинкиЭлемента% ";
	
	ТаблицаТипОбъектаИндексКартинки = РаботаСОбъектамиМетаданных.ТаблицаТипОбъектаИндексКартинки();

	МассивТекстов = Новый Массив;
	
	НачалоТекста = "	ВЫБОР
		|КОГДА СправочникОбъектыМетаданных.ПометкаУдаления
		|	ТОГДА 0 ";

	МассивТекстов.Добавить(НачалоТекста);
	
	Для Каждого СтрТабл из ТаблицаТипОбъектаИндексКартинки Цикл
		
		Если СтрТабл.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавляемыйТекст = СтрЗаменить(ШаблонТекстаИндексаКартинки, "%ИмяТипа%", СтрТабл.ТипОбъекта);
		ДобавляемыйТекст = СтрЗаменить(ДобавляемыйТекст, "%ИндексКартинкиЭлемента%", Формат(СтрТабл.ИндексКартинки,"ЧГ="));
		
		Если СтрТабл.ЕстьКартинкаГруппы Тогда
			ИндексКартинкиГруппы = РаботаСОбъектамиМетаданных.ИндексКартинкиПоТипуОбъекта(СтрТабл.ТипОбъекта, ТаблицаТипОбъектаИндексКартинки, Истина);	
		Иначе
			ИндексКартинкиГруппы = СтрТабл.ИндексКартинки;	
		КонецЕсли;
		
		ДобавляемыйТекст = СтрЗаменить(ДобавляемыйТекст, "%ИндексКартинкиГруппы%", Формат(ИндексКартинкиГруппы,"ЧГ="));
		
		МассивТекстов.Добавить(ДобавляемыйТекст);
		
	КонецЦикла;
	МассивТекстов.Добавить("КОНЕЦ");
	
	ТекстИндексаКартинки = СтрСоединить(МассивТекстов, Символы.ПС);
	
	Форма = Список.Родитель;
	ТипФормаКлиентскогоПриложения = Тип("ФормаКлиентскогоПриложения");
	Пока ТипЗнч(Форма) <> ТипФормаКлиентскогоПриложения Цикл
		Форма = Форма.Родитель;
	КонецЦикла;
	
	ДинамическийСписок = Форма[Список.ПутьКДанным];
	ТекстЗапроса = ДинамическийСписок.ТекстЗапроса;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИндексКартикиПереопределяемый", ТекстИндексаКартинки);
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = Неопределено;
	СвойстваСписка.ДинамическоеСчитываниеДанных = Неопределено;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Список, СвойстваСписка);

КонецПроцедуры

#КонецОбласти

#КонецЕсли