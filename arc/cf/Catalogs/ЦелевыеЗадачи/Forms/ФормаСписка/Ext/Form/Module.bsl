#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если НЕ ЗначениеЗаполнено(СтепеньУчастия) Тогда
		СтепеньУчастия = НСтр("ru='Ответственный/участник'");
	КонецЕсли;
	
	УстановитьПараметрРаздел(Список, РазделПроекта);
	УстановитьПараметрУчастник(Список, ОтветственныйУчастник);
	УстановитьПараметрСтепеньУчастия(Список, СтепеньУчастия);
	
	УстановитьОтборПоСтатусу(Список, СписокСтатусов);
	
	Проекты.УстановитьОтборВСпискеПоПроекту(Список);	
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки.Получить("РазделПроекта")<>Неопределено Тогда
		УстановитьПараметрРаздел(Список, РазделПроекта);
	КонецЕсли;
	
	Если Настройки.Получить("СписокСтатусов")<>Неопределено Тогда
		УстановитьОтборПоСтатусу(Список, СписокСтатусов);
	КонецЕсли;
	
	Если Настройки.Получить("ОтветственныйУчастник")<>Неопределено Тогда
		УстановитьПараметрУчастник(Список, ОтветственныйУчастник);
	КонецЕсли;
	
	Если Настройки.Получить("СтепеньУчастия")<>Неопределено Тогда
		УстановитьПараметрСтепеньУчастия(Список, СтепеньУчастия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		УстановитьОтборПоПроектуСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСтатусНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураПараметров = Новый Структура("СписокВыбора", СписокСтатусов);
	
	ОткрытьФорму("Перечисление.СтатусыЦелевыхЗадач.Форма.ВыборСпискаСтатусов", СтруктураПараметров, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРазделПроектаПриИзменении(Элемент)
	
	УстановитьПараметрРаздел(Список, РазделПроекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйУчастникПриИзменении(Элемент)
	
	УстановитьПараметрУчастник(Список, ОтветственныйУчастник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)
	
	УстановитьОтборПоСтатусу(Список, СписокСтатусов);
	
КонецПроцедуры

&НаКлиенте
Процедура СтепеньУчастияПриИзменении(Элемент)
	
	УстановитьПараметрСтепеньУчастия(Список, СтепеньУчастия);
	
КонецПроцедуры

&НаКлиенте
Процедура СтепеньУчастияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСтатусу(Список, СписокСтатусов)
	
	ИспользоватьОтбор = СписокСтатусов.Количество()>0;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Статус", СписокСтатусов, ВидСравненияКомпоновкиДанных.ВСписке,, ИспользоватьОтбор);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрРаздел(Список, РазделПроекта)

	Список.Параметры.УстановитьЗначениеПараметра("Раздел", РазделПроекта);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрУчастник(Список, Участник)

	Список.Параметры.УстановитьЗначениеПараметра("Участник", Участник);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрСтепеньУчастия(Список, СтепеньУчастия)

	Список.Параметры.УстановитьЗначениеПараметра("СтепеньУчастия", СтепеньУчастия);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПроектуСервер()
	
	Проекты.УстановитьОтборВСпискеПоПроекту(Список);
	
КонецПроцедуры

#КонецОбласти