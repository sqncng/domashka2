
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СтруктураДанных = ДанныеДляУведомления(ПараметрКоманды);
	
	Тема = СтруктураДанных.ТекстЗаголовка;
	ТекстУведомления = СтруктураДанных.ТекстУведомления;
	
	СписокАдресатов = СписокАдресатов();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Тема", Тема);
	ПараметрыФормы.Вставить("Текст", ТекстУведомления);
	ПараметрыФормы.Вставить("Получатель", СписокАдресатов);
	
	ОткрытьФорму("ОбщаяФорма.ОтправкаСообщения", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция ДанныеДляУведомления(Сборка)
	
	Возврат Справочники.СборкиВерсии.ДанныеУведомления(Сборка);	
	
КонецФункции

&НаСервере
Функция СписокАдресатов()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Список = Новый СписокЗначений;
	
	ТаблицаАдресатов = Константы.НастройкаАдресатовУведомленийОСборках.Получить().Получить();
	
	Если ТипЗнч(ТаблицаАдресатов) = Тип("ТаблицаЗначений") Тогда
		
		МассивПользователей = Новый Массив;
		
		Для Каждого СтрокаТаблицы из ТаблицаАдресатов Цикл
			
			Если ТипЗнч(СтрокаТаблицы.Адресат) = Тип("СправочникСсылка.Пользователи") Тогда
				МассивПользователей.Добавить(СтрокаТаблицы.Адресат);
			Иначе
				ДанныеАдресата = Новый Структура("Адресат,Представление");
				ЗаполнитьЗначенияСвойств(ДанныеАдресата, СтрокаТаблицы);
				Список.Добавить(ДанныеАдресата.Адресат, ДанныеАдресата.Представление);
			КонецЕсли;
			
		КонецЦикла;
		
		Если МассивПользователей.Количество()>0 Тогда
			
			ТипыКИ = Новый Массив;
			ТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			
			ВидыКИ = Новый Массив;
			ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.EmailПользователя);
			
			ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивПользователей, ТипыКИ, ВидыКИ);
			
			Для Каждого СтрокаТаблицы из ТаблицаКИ Цикл
				Если ЗначениеЗаполнено(СтрокаТаблицы.Представление) Тогда
					Список.Добавить(СтрокаТаблицы.Представление, СтрокаТаблицы.Объект);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Список;
	
КонецФункции
