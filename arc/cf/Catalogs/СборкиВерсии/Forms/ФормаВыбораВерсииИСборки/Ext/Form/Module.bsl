
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Проект = Параметры.Проект;
	Версия = Параметры.Версия;
	ТолькоПубликуемые = Параметры.ТолькоПубликуемые;
	ОтборПоВерсиямВоспроизведенияПроекта = Параметры.ОтборПоВерсиямВоспроизведенияПроекта;
	
	ОтобратьДанные();
	
	Элементы.Версия.Видимость = НЕ ОтборПоВерсиямВоспроизведенияПроекта;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Версия, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Версия));
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоПубликуемыеПриИзменении(Элемент)
	
	ОтобратьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоСборкамВоспроизведения()
	
	ПараметрыВыбораСборки = Новый Структура;
	
	ПараметрыВыбораСборки.Вставить("ОтборПоВерсиямВоспроизведенияПроекта", Истина);
	ПараметрыВыбораСборки.Вставить("Проект", Проект);
	ПараметрыВыбораСборки.Вставить("ТолькоПубликуемые", ТолькоПубликуемые);
	
	СборкиДляВыбора = Справочники.СборкиВерсии.ПолучитьДанныеВыбора(ПараметрыВыбораСборки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", СборкиДляВыбора, ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПорядокВСписке()
	
	ПолеПорядкаПоСборке = Новый ПолеКомпоновкиДанных("ДатаСборки");
	ПолеПорядкаПоВерсии = Новый ПолеКомпоновкиДанных("ПорядковыйНомерВерсии");
	
	ЭлементПорядкаПоВерсии = Список.Порядок.Элементы.Вставить(0, Тип("ЭлементПорядкаКомпоновкиДанных"));
	
	ЭлементПорядкаПоВерсии.Поле = ПолеПорядкаПоВерсии;
	ЭлементПорядкаПоВерсии.Использование = Истина;
	ЭлементПорядкаПоВерсии.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	
	Для Каждого ЭлементПорядка из Список.Порядок.Элементы Цикл
		
		Если ЭлементПорядка.Поле = ПолеПорядкаПоСборке Тогда
			ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтобратьДанные()
	
	Если ОтборПоВерсиямВоспроизведенияПроекта Тогда
		УстановитьОтборПоСборкамВоспроизведения();
		УстановитьПорядокВСписке();
	Иначе
	    ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Проект", Проект, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Проект));
	    ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Версия, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Версия));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Опубликована", ТолькоПубликуемые, ВидСравненияКомпоновкиДанных.Равно,,ТолькоПубликуемые);
	КонецЕсли;
	
КОнецПроцедуры

#КонецОбласти