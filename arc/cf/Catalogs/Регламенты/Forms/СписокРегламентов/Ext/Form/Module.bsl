
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Проекты.УстановитьОтборВСпискеПоПроекту(Список);
	УстановитьОтборПоИспользованиюСервер(Параметры);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборДействующие();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		УстановитьОтборПоПроектуСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображатьТолькоДействующиеПриИзменении(Элемент)
	
	УстановитьОтборДействующие();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборДействующие()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Действует", ОтображатьТолькоДействующие, ВидСравненияКомпоновкиДанных.Равно,,ОтображатьТолькоДействующие);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоИспользованиюСервер(Параметры)
	
	ГруппаИЛИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы, "Отбор по использованию", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаИЛИ.Элементы, "Отбор по использованию для объектов", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ДобавленЭлементОтбораПоИспользованию = Ложь;
	
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляЗадач", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляОшибок", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляОбъектовМетаданных", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляПрофилейПользователей", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляПроцессов", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляТехническихПроектов", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляИдей", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляФункций", ДобавленЭлементОтбораПоИспользованию);
	ДобавитьЭлементОтбораПоИспользованию(ГруппаИ, Параметры, "ИспользуетсяДляПроектов", ДобавленЭлементОтбораПоИспользованию);
	
	Если ДобавленЭлементОтбораПоИспользованию Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаИЛИ, "ИспользуетсяДляВсехОбъектов", Истина, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	КонецЕсли;
	
	ГруппаИЛИ.Использование = ДобавленЭлементОтбораПоИспользованию;
	ГруппаИ.Использование = ДобавленЭлементОтбораПоИспользованию;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементОтбораПоИспользованию(ГруппаЭлементов, Параметры, ИмяПараметра, ДобавленЭлементОтбора)
	
	Если Параметры.Свойство(ИмяПараметра) И Параметры[ИмяПараметра]Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаЭлементов, ИмяПараметра, Параметры[ИмяПараметра], ВидСравненияКомпоновкиДанных.Равно,,Истина);
		ДобавленЭлементОтбора = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПроектуСервер()
	
	Проекты.УстановитьОтборВСпискеПоПроекту(Список);
	
КонецПроцедуры

#КонецОбласти
