#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		
		Если НЕ ЗначениеЗаполнено(Владелец) Тогда
			Владелец = Проекты.ПроектПоУмолчанию();
		КонецЕсли;
		
		СогласованиеИзменений = Справочники.Проекты.ВариантСогласованиеИзмененийМетаданныхПоУмолчанию(Владелец);
		ПрисутствуетВОсновномХранилище = Ложь;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ПолноеНаименование = Имя;
	РодительПодсистемы = Родитель;
	СформироватьНаименование(РодительПодсистемы, ПолноеНаименование);
	Наименование = ПолноеНаименование;
	
	СкорректироватьДанныеНеСоответствующиеПроекту();
	
	ЗаписьСправочников.УстановитьКодСправочника(ЭтотОбъект);
	ЗаписьСправочников.УстановитьПолныйКодСправочника(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗначениеЗаполнено(Родитель) И ЗначениеЗаполнено(РольДляПросмотра) Тогда
		// Роль для просмотра указывается только для подсистем верхнего уровня
		РольДляПросмотра = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ЗначениеЗаполнено(Поставщик) Тогда
		НепроверяемыеРеквизиты.Добавить("ПравилоПоддержки");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьНаименование(РодительПодсистемы, ПолноеНаименование)
	
	ПолноеНаименование = ?(ЗначениеЗаполнено(РодительПодсистемы), СокрЛП(РодительПодсистемы.Имя) + ".", "") + ПолноеНаименование;
	Если ЗначениеЗаполнено(РодительПодсистемы) Тогда	
		СформироватьНаименование(РодительПодсистемы.Родитель, ПолноеНаименование);
	КонецЕсли;
	
КонецПроцедуры

Процедура СкорректироватьДанныеНеСоответствующиеПроекту()
	
	Если ЗначениеЗаполнено(РазделПроекта) Тогда
		
		ПроектРаздела = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РазделПроекта, "Владелец");
		
		Если ПроектРаздела <> Владелец Тогда
			РазделПроекта = Справочники.РазделыПроекта.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли