#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Владелец = Проекты.ПроектПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа И Система Тогда
		ПроверитьНаличиеЭлементовСистема(Отказ);
	КонецЕсли;
	
	СкорректироватьДанныеНеСоответствующиеПроекту();
	
	ЗаписьСправочников.УстановитьКодСправочника(ЭтотОбъект);
	ЗаписьСправочников.УстановитьПолныйКодСправочника(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("КоллекцияИзмененныхОбъектов", Новый Массив);
	Версионирование.ЗарегистрироватьИзмененияОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Управление дополнительным свойством объекта
//
Функция ДополнительноеСвойство(ИмяСвойства, ОчиститьСвойство = Ложь) Экспорт
	
	Перем ЗначениеСвойства;
	
	Если ОчиститьСвойство ИЛИ НЕ ДополнительныеСвойства.Свойство(ИмяСвойства, ЗначениеСвойства) Тогда
		
		Если ИмяСвойства = "ОтветственныйЗаРодителя" Тогда
			ЗначениеСвойства = Универсальные.ЗначенияРеквизитовСправочника(Родитель, "Ответственный").Ответственный;
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить(ИмяСвойства, ЗначениеСвойства);
	КонецЕсли;
	
	Возврат ЗначениеСвойства;
	
КонецФункции

Процедура ПроверитьНаличиеЭлементовСистема(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса=
	"ВЫБРАТЬ
	|	ПрофилиПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПрофилиПользователей КАК ПрофилиПользователей
	|ГДЕ
	|	ПрофилиПользователей.Система
	|	И ПрофилиПользователей.Владелец = &Проект
	|	И ПрофилиПользователей.Ссылка <> &Ссылка"
	;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Проект", Владелец);
	
	Запрос.Текст = ТекстЗапроса;
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ТекстСообщения = НСтр("ru='В информационной базе уже существует профиль-""система"" для данного проекта'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Имя%", Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура СкорректироватьДанныеНеСоответствующиеПроекту()
	
	Если ЗначениеЗаполнено(РазделПроекта) Тогда
		
		ПроектРаздела = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РазделПроекта, "Владелец");
		
		Если ПроектРаздела <> Владелец Тогда
			РазделПроекта = Справочники.РазделыПроекта.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли