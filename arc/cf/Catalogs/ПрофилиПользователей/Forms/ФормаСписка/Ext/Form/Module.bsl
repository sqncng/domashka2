
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Список.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
	Список.Параметры.УстановитьЗначениеПараметра("ВидОбъекта", Версионирование.ВидВерсионируемогоОбъекта(Справочники.ПрофилиПользователей.ПустаяСсылка()));
	РазделПроекта = Параметры.РазделПроекта;
	УстановитьПараметрРаздел(Список, РазделПроекта);
	
	Проект = ПараметрыСеанса.ТекущийПроект;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Проект, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(Проект));
	
	УстановитьУсловноеОформлениеСписка();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Параметры.РазделПроекта) Тогда
		РазделПроекта = Параметры.РазделПроекта;
	Иначе
		Если Настройки.Получить("РазделПроекта")<>Неопределено Тогда
			УстановитьПараметрРаздел(Список, РазделПроекта);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененТекущийПроект" Тогда
		УстановитьОтборПоПроектуСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборРазделПроектаПриИзменении(Элемент)
	
	УстановитьПараметрРаздел(Список, РазделПроекта);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьПрограммуНачальногоЗаполненияПрофилей(Команда)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ОписаниеНачальногоЗаполненияПрофилей());
	ТекстЗаголовка = НСтр("ru='Описание начального заполнения имеющихся профилей групп доступа'");
	ТекстовыйДокумент.Показать(ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрРаздел(Список, РазделПроекта)

	Список.Параметры.УстановитьЗначениеПараметра("Раздел", РазделПроекта);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПроектуСервер()
	
	Проекты.УстановитьОтборВСпискеПоПроекту(Список);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	УсловноеОформлениеСписка = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеСписка.Элементы.Очистить();
	
	ОбщегоНазначенияСППР.УстановитьУсловноеОформлениеСпискаОтветственный(УсловноеОформлениеСписка);
	КонтрольИзменений.УстановитьУсловноеОформлениеСпискаСостояниеКонтроля(УсловноеОформлениеСписка);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеНачальногоЗаполненияПрофилей()
	
	ТекущийПроект = ПараметрыСеанса.ТекущийПроект;
	Возврат Справочники.ПрофилиПользователей.ОписаниеНачальногоЗаполненияПрофилейНаВстроенномЯзыке(ТекущийПроект);
	
КонецФункции

#КонецОбласти
