
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если НЕ ЗначениеЗаполнено(ПараметрКоманды) Тогда
		Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения") Тогда
			Для Каждого ЭлементОтбора из ПараметрыВыполненияКоманды.Источник.Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы Цикл
				Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец") Тогда
					ПараметрКоманды = ЭлементОтбора.ПравоеЗначение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
		Если НЕ ЗначениеЗаполнено(ПараметрКоманды) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.ОбъектыМетаданных") Тогда
		
		Если ЭтоГруппаОбъектов(ПараметрКоманды) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		КомандаОбработана = Ложь;
		
		ФормаИсточник = ПараметрыВыполненияКоманды.Источник;
		
		Если ФормаИсточник.Параметры.Свойство("Ключ")
			И ФормаИсточник.Параметры.Ключ = ПараметрКоманды Тогда
			
			Если ФормаИсточник.Модифицированность Тогда
				
				ТекстВопроса = НСтр("ru='Для выполнения операции нужно сохранить изменения.
				|Сохранить и продолжить?'");
				
				Структура = Новый Структура("Форма, Ссылка", ФормаИсточник, ПараметрКоманды);
				ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьИзмененияЗавершение", ЭтотОбъект, Структура);
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				
				КомандаОбработана = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ КомандаОбработана Тогда
			ВыполнитьОткрытиеФормы(ПараметрКоманды);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзмененияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	Попытка
		ДополнительныеПараметры.Форма.Записать();
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
    ВыполнитьОткрытиеФормы(ДополнительныеПараметры.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОткрытиеФормы(Ссылка)
	
	ПараметрыФормы = Новый Структура("Ссылка", Ссылка);
	ОткрытьФорму("ОбщаяФорма.ФормаHTMLДокумента", ПараметрыФормы,, Ссылка);
	
КонецПроцедуры

&НаСервере
Функция ЭтоГруппаОбъектов(Ссылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ЭтоГруппа");
	
КонецФункции