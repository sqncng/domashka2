
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") Тогда
		
		Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения") Тогда
			
			ФормаИсточник = ПараметрыВыполненияКоманды.Источник;
			
			Если ФормаИсточник.Параметры.Свойство("Ключ")
				И ФормаИсточник.Параметры.Ключ = ПараметрКоманды[0] Тогда
				
				Структура = Новый Структура("Форма, Ссылка", ФормаИсточник, ПараметрКоманды[0]);
				
				Если ФормаИсточник.Модифицированность Тогда
					
					ТекстВопроса = НСтр("ru='Для выполнения операции нужно сохранить изменения.
					|Сохранить и продолжить?'");
					
					ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьИзмененияЗавершение", ЭтотОбъект, Структура);
					ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
					
				Иначе
					ВыполнитьУстановкуСтатусаКонтроля(Структура);
				КонецЕсли;
				
			Иначе
				Для Каждого Ссылка из ПараметрКоманды Цикл
					УстановитьСтатусКонтроля(Ссылка);
				КонецЦикла;
				ОповеститьОбИзменении(ТипЗнч(ПараметрКоманды[0]));
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзмененияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
    ВыполнитьУстановкуСтатусаКонтроля(ДополнительныеПараметры);

КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусКонтроля(Ссылка)
	
	КонтрольИзменений.УстановитьСтатусКонтроля(
			Ссылка, 
			Версионирование.ВидВерсионируемогоОбъекта(Ссылка), 
			Пользователи.ТекущийПользователь(), 
			Перечисления.СостоянияКонтроляОбъектов.НеПроверено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьУстановкуСтатусаКонтроля(ДополнительныеПараметры)
	
	Попытка
		УстановитьСтатусКонтроля(ДополнительныеПараметры.Ссылка);
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Форма = ДополнительныеПараметры.Форма;
	ОповеститьОбИзменении(ДополнительныеПараметры.Ссылка);
	Форма.СтатусКонтроля = "Не проверено";
	ОбщегоНазначенияСППРКлиентСервер.УстановитьОтображениеСостоянияКонтроля(Форма);
	
КонецПроцедуры