#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует гиперссылку в системе хранения кода по идентификатору запуска тестирования GitLab.
//
// Параметры:
//  Проект        - Тип - СправочникСсылка.Проекты.
//  Идентификатор - Тип - Строка.
//
// Возвращаемое значение:
//  Гиперссылка - Тип - Строка.
//
Функция АдресСсылки(Проект, Идентификатор) Экспорт
	
	Возврат СсылкаПоПроекту(Проект) + "pipelines/" + Идентификатор;
	
КонецФункции	 

// Формирует гиперссылку в системе хранения кода по идентификатору задания тестирования GitLab.
//
// Параметры:
//  Проект        - Тип - СправочникСсылка.Проекты.
//  Идентификатор - Тип - Строка.
//
// Возвращаемое значение:
//  Гиперссылка - Тип - Строка.
//
Функция АдресЗаданияТестирования(Проект, Идентификатор) Экспорт

	Возврат СсылкаПоПроекту(Проект) + "-/jobs/" + Идентификатор;

КонецФункции	 

#КонецОбласти
	
#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Проект)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СсылкаПоПроекту(Проект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.АдресGitСервера КАК АдресGitСервера
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстСсылки = "";
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстСсылки = ВыборкаДетальныеЗаписи.АдресGitСервера;
	КонецЦикла;
	
	ТекстСсылки = СтрЗаменить(ТекстСсылки,"git@","");
	ТекстСсылки = СтрЗаменить(ТекстСсылки,":","/");
	ТекстСсылки = СтрЗаменить(ТекстСсылки,".git","");
	
	Если Прав(ТекстСсылки,1) <> "/" Тогда
		ТекстСсылки = ТекстСсылки + "/";
	КонецЕсли;	 
	
	ТекстСсылки = "https://" + ТекстСсылки;
	Возврат ТекстСсылки;
	
КонецФункции

#КонецОбласти

#КонецЕсли