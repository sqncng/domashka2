#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПредставлениеГиперссылки(Объект.Проект, Объект.Pipeline_ID);
	УстановитьОтборПоРодительскимЗапускам();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И Объект.Проведен 
		И НЕ НеВыполнятьПроверкуПередЗаписью Тогда
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ПередЗаписьюЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Запуск тестирования уже был запущен. Отменить предыдущий запуск?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторЗапускаПриИзменении(Элемент)
	УстановитьПредставлениеГиперссылки(Объект.Проект, Объект.Pipeline_ID);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СтатусТестов(Команда)
	Ветки = Новый Массив;
	Ветки.Добавить(Объект.Ветка);
	
	ДанныеОтбор = Новый Структура;
	ДанныеОтбор.Вставить("ЗапускТестирования",Объект.Ссылка);
	
	ПараметрыФормы = Новый Структура("КлючВарианта, ВидимостьКомандВариантовОтчетов, СформироватьПриОткрытии, Ветка, Отбор", 
		"СтатусПрохожденияТестовВВетке",
		Истина, 
		Истина, 
		Ветки,
		ДанныеОтбор);
		
	ОткрытьФорму(
		"Отчет.СтатусПрохожденияТестовВВетке.Форма",
		ПараметрыФормы, ,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСсылкуЗапуска(Команда)
	ПерейтиПоНавигационнойСсылке(СсылкаНаЗапуск + Объект.Pipeline_ID);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыЗапускиДочернихПроектов

&НаКлиенте
Процедура ЗапускиДочернихПроектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЗапускиДочернихПроектовИдентификаторЗапуска" Тогда
		АдресСсылки = АдресСсылкиСервер(Элементы.ЗапускиДочернихПроектов.ТекущиеДанные.Проект,
			Элементы.ЗапускиДочернихПроектов.ТекущиеДанные.ИдентификаторЗапуска);
		Если ЗначениеЗаполнено(АдресСсылки) Тогда
			НачатьЗапускПриложения(Новый ОписаниеОповещения,АдресСсылки);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ЗапускиДочернихПроектовСсылка" Тогда		
		ПоказатьЗначение(Новый ОписаниеОповещения, Элементы.ЗапускиДочернихПроектов.ТекущиеДанные.Ссылка);
	ИначеЕсли Поле.Имя = "ЗапускиДочернихПроектовПроект" Тогда		
		ПоказатьЗначение(Новый ОписаниеОповещения, Элементы.ЗапускиДочернихПроектов.ТекущиеДанные.Проект);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПредставлениеГиперссылки(Проект, Pipeline_ID)

	АдресСсылки = Документы.ЗапускТестирования.АдресСсылки(Проект, Pipeline_ID);
	СсылкаНаЗапуск = СтрЗаменить(АдресСсылки, Pipeline_ID, ""); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = Неопределено
		Или РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	НеВыполнятьПроверкуПередЗаписью = Истина;
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Иначе
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	КонецЕсли;
	НеВыполнятьПроверкуПередЗаписью = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоРодительскимЗапускам()
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЗапускиДочернихПроектов,
																			"РодительскийЗапуск",
																			Объект.Ссылка,
																			Истина);
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресСсылкиСервер(Проект, ИдентификаторЗапуска)
	
	Возврат Документы.ЗапускТестирования.АдресСсылки(
		Проект, ИдентификаторЗапуска);
		
КонецФункции	

#КонецОбласти


