#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьУчетВремениЗаСегодня(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ДокументСсылка = ЕжедневныйОтчетЗаСегодня();
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда 
		ПараметрыФормы.Вставить("Ключ", ДокументСсылка);
	КонецЕсли;
	
	ОткрытьФорму("Документ.УчетВремени.ФормаОбъекта", ПараметрыФормы, Элементы.Список);

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЕжедневныйОтчетЗаСегодня()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	УчетВремени.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.УчетВремени КАК УчетВремени
	|ГДЕ
	|	НЕ УчетВремени.ПометкаУдаления
	|	И УчетВремени.Пользователь = &Пользователь
	|	И НАЧАЛОПЕРИОДА(УчетВремени.Дата, ДЕНЬ) = &ТекущаяДата"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДата()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти