#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Работы.РежимВыбора = Параметры.РежимВыбора;
	Элементы.ВедениеХронометража.Видимость = НЕ Параметры.РежимВыбора;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ПериодИзНастроек = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить("УчетВремениПодборРабот", "Период");
	
	Если ТипЗнч(ПериодИзНастроек) = Тип("СтандартныйПериод") Тогда
		Период = ПериодИзНастроек;
	Иначе
		Период.Вариант = ВариантСтандартногоПериода.Месяц;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Работы, "Пользователь", ТекущийПользователь, Истина);
	УстановитьПериод();
																			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПереключенХронометраж" Тогда
		Элементы.Работы.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПриИзмененииПериодаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыРаботы

&НаКлиенте
Процедура РаботыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Элемент.ТекущаяСтрока <> Неопределено Тогда
		ДанныеВыбраннойСтроки = ДанныеСтроки(Элемент.ТекущиеДанные);
		ОповеститьОВыборе(ДанныеВыбраннойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ВедениеХронометража" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Работа = Неопределено;
		
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Работа) Тогда
			Работа = Элемент.ТекущиеДанные.Работа;
		ИначеЕсли ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидДеятельности) Тогда
			Работа = Элемент.ТекущиеДанные.ВидДеятельности;
		КонецЕсли;
		
		ДанныеХронометража = ПереключитьХронометражНаСервере(Работа, Элемент.ТекущиеДанные.ОписаниеРаботы);
		
		Оповестить("ПереключенХронометраж", ДанныеХронометража, ЭтотОбъект);
		
		Если ДанныеХронометража.ХронометражЗавершен Тогда
			ДанныеОповещения = Новый Структура;
			ДанныеОповещения.Вставить("Начало", ДанныеХронометража.НачалоЗавершеннойРаботы);
			ДанныеОповещения.Вставить("Окончание", ДанныеХронометража.ОкончаниеЗавершеннойРаботы);
			
			Оповестить("ЗаписаныДанныеКалендаря", ДанныеОповещения, ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ДанныеСтроки(ДанныеСтроки)		
	
	Результат = Новый Структура;
	Результат.Вставить("Проект", ДанныеСтроки.Проект);
	Результат.Вставить("Работа", ДанныеСтроки.Работа);
	Результат.Вставить("ОписаниеРаботы", ДанныеСтроки.ОписаниеРаботы);
	Результат.Вставить("ВидДеятельности", ДанныеСтроки.ВидДеятельности);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииПериодаНаСервере()
	
	УстановитьПериод();
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить("УчетВремениПодборРабот", "Период", Период);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПериод()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Работы, "НачалоПериода", Период.ДатаНачала, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Работы, "КонецПериода", Период.ДатаОкончания, Истина);
	
КонецПроцедуры

&НаСервере
Функция ПереключитьХронометражНаСервере(Работа, ОписаниеРаботы)
	
	ДанныеХронометража = УчетВремени.ПереключитьХронометраж(Работа, ОписаниеРаботы);
	
	Элементы.Работы.Обновить();
	
	Возврат ДанныеХронометража;
	
КонецФункции

#КонецОбласти