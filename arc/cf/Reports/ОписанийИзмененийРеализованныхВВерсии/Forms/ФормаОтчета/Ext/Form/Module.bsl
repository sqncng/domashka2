#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДоступностьЭлементов(Элементы, ВидОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьДоступностьЭлементов(Элементы, ВидОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойВариантаНаСервере(Настройки)
	
	ОтчетыПереопределяемый.УстановитьМакетОформленияВРежимеТакси(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	ВариантыОтчетов.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОтчетаПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов(Элементы, ВидОтчета);
	ОтчетТабличныйДокумент = Новый ТабличныйДокумент;
	ТекстHTML="";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьОтчетСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Расширение = "htm";
	Диалог.Фильтр = "htm";
	
	Диалог.ПолноеИмяФайла = Диалог.Каталог + ИмяФайла;
	
	Если Диалог.Выбрать() Тогда
		
		ВыбранноеИмяФайла = Диалог.ПолноеИмяФайла;
		ОтчетТХТ.Записать(ВыбранноеИмяФайла);
		
		ТекстОповещения = НСтр("ru='Отчет сохранен в файл'");
		ПоказатьОповещениеПользователя(ТекстОповещения,,, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчетСервер()	
	
	Если ВидОтчета = 0 Тогда
		СкомпоноватьРезультат();
	Иначе
		ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
		
		СхемаКомпоновки = ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		
		ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений");
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
		
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновки, Отчет.КомпоновщикНастроек.ПолучитьНастройки(),,,ТипГенератора);
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
		
		ТаблицаРезультата = ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		СформироватьHTML(ТаблицаРезультата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьHTML(ТаблицаРезультата)
	
	Проект = Справочники.Проекты.ПустаяСсылка();
	Версия = Справочники.ВерсииПроекта.ПустаяСсылка();
	
	Настройки = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрПроект = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Проект"));
	Если ПараметрПроект <> Неопределено Тогда
		Проект = ПараметрПроект.Значение;
	КонецЕсли;
	
	ПараметрВерсия = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Версия"));
	Если ПараметрВерсия <> Неопределено Тогда
		Версия = ПараметрВерсия.Значение;
	КонецЕсли;
	
	РеквизитыПроекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Проект, "ИмяКонфигурации, НаименованиеКонфигурации");
	
	ПроектИмя = РеквизитыПроекта.ИмяКонфигурации;
	ПроектНаименование = РеквизитыПроекта.НаименованиеКонфигурации;
	ВерсияПроектаКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Версия, "Код");
	
	ЗаголовокОтчета = ПроектНаименование + ". Описание изменений версии " + ВерсияПроектаКод;
	ИмяФайла = ПроектИмя + ". Описание изменений версии " + ВерсияПроектаКод;
	
	ОтчетТХТ.Очистить();
	
	ТекстHTML = "<H1>" + ЗаголовокОтчета + "</H1>";
	ТекстHTML = ТекстHTML + "<BR>";
	
	Для Каждого СтрокаТаблицы из ТаблицаРезультата Цикл
		
		ФорматированноеОписание = СтрокаТаблицы.ХранилищеОписания.Получить();
		
		Если ТипЗнч(ФорматированноеОписание) = Тип("ФорматированныйДокумент") Тогда
			ТекстHTMLОписания = "";
			СтруктураВложений = Новый Структура;
			ФорматированноеОписание.ПолучитьHTML(ТекстHTMLОписания, СтруктураВложений);
			ТекстОписания = ТекстHTMLОписания;
		Иначе
			ТекстОписания = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстОписания) Тогда
			 ТекстОписания = ТекстОписания + "<BR>";
		КонецЕсли;
		
		ТекстHTML = ТекстHTML + ТекстОписания;
		
	КонецЦикла;
	
	ОтчетТХТ.УстановитьТекст(ТекстHTML);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементов(Элементы, ВидОтчета)
	
	Если ВидОтчета=0 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ТабличныйДокумент;
		Элементы.СохранитьВФайл.Доступность = Ложь;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.HTMLДокумент;
		Элементы.СохранитьВФайл.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
