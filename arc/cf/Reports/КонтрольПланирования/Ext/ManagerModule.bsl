
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует отчет контроль планирования
//
// Параметры:
//  ПараметрыФормирования  - Структура - см. ПланированиеКлиентСервер.НовыйПараметрыФормированияОтчетаКонтрольПланирования
//
// Возвращаемое значение:
//   Структура  - содержит:
//     * Успешно            - Булево - признак того, что отчет сформирован успешно.
//     * СообщенияОбОшибках - Массив - сообщения об ошибках, формирование отчета завершилось неудачей.
//     * ТабличныйДокумент  - ТабличныйДокумент - сформированный отчет.
//
Функция РезультатФормированияОтчета(ПараметрыФормирования) Экспорт
	
	РезультатФормированияОтчета = Новый Структура;
	РезультатФормированияОтчета.Вставить("Успешно",            Истина);
	РезультатФормированияОтчета.Вставить("СообщенияОбОшибках", Новый Массив);
	РезультатФормированияОтчета.Вставить("ТабличныйДокумент",  Неопределено);
	
	ПроверитьЗаполнениеПараметровФормирования(ПараметрыФормирования, РезультатФормированияОтчета); 
	
	Если Не РезультатФормированияОтчета.Успешно Тогда
		Возврат РезультатФормированияОтчета;
	КонецЕсли;
	
	ДанныеВидаПлана = Справочники.ВидыПланов.ДанныеВидаПлана(ПараметрыФормирования.ПланЗанятости);
	ДанныеПланов    = ТаблицаПланов(ПараметрыФормирования, ДанныеВидаПлана);
	ПараметрыФормирования.Вставить("НормативПланируемогоВремени", ДанныеВидаПлана.НормативПланируемогоВремени);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаОтчета(ПараметрыФормирования.ВариантГруппировки); 
	УстановитьПараметрыЗапроса(Запрос, ПараметрыФормирования, ДанныеПланов, ДанныеВидаПлана); 
	РезультатЗапроса = Запрос.Выполнить();
	
	ПодготовленныеДанные = ПодготовленныеДанныеДляВыводаВОтчет(РезультатЗапроса, ПараметрыФормирования, ДанныеПланов.ТаблицаПланов);
	
	ВывестиДанныеВТабличныйДокумент(РезультатФормированияОтчета, ПодготовленныеДанные, ПараметрыФормирования, ДанныеПланов.ТаблицаПланов);
	
	Возврат РезультатФормированияОтчета;
	
КонецФункции 

// Используется фоновым заданим по формированию отчета.
// Параметры:
//  ПараметрыФормирования  - Структура - см. ПланированиеКлиентСервер.НовыйПараметрыФормированияОтчетаКонтрольПланирования
//  АдресХранилища       - Строка - адрес временного хранилища, в которое будут помещены данный фактически выполненных работ
//
Процедура СформироватьОтчет(ПараметрыФормирования, АдресХранилища) Экспорт
	
	РезультатФормированияОтчета = РезультатФормированияОтчета(ПараметрыФормирования.ПараметрыФормирования);
	
	ПоместитьВоВременноеХранилище(РезультатФормированияОтчета, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Маршрутизаторы

Функция ТекстЗапросаОтчета(ВариантГруппировки)
	
	Если ВариантГруппировки = "ОтСотрудников" Тогда
		Возврат ТекстЗапросаГруппировкаОтСотрудников();
	ИначеЕсли ВариантГруппировки = "ОтТехПроектов" Тогда
		Возврат ТекстЗапросаГруппировкаОтТехПроектов();
	КонецЕсли;
	
КонецФункции 

Функция ПодготовленныеДанныеДляВыводаВОтчет(РезультатЗапроса, ПараметрыФормирования, ТаблицаПланов)
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников" Тогда
		Возврат ПодготовленныеДанныеГруппировкаОтСотрудников(РезультатЗапроса, ПараметрыФормирования, ТаблицаПланов);
	ИначеЕсли ПараметрыФормирования.ВариантГруппировки = "ОтТехПроектов" Тогда
		Возврат ПодготовленныеДанныеГруппировкаОтТехПроектов(РезультатЗапроса, ПараметрыФормирования, ТаблицаПланов);
	КонецЕсли;
	
КонецФункции 

Процедура УстановитьПараметрыЗапроса(Запрос, ПараметрыФормирования, ДанныеПланов, ДанныеВидаПлана)
	
	Запрос.УстановитьПараметр("ИдентификаторыОтоображаемыхПланов", ИдентификаторыОтображаемыхПланов(ДанныеПланов.ТаблицаПланов));
	Запрос.УстановитьПараметр("ПустаяДата",                        Дата(1, 1, 1));
	Запрос.УстановитьПараметр("ТаблицаПланов",                     ДанныеПланов.ТаблицаПланов);
	Запрос.УстановитьПараметр("НачалоПериода",                     ПараметрыФормирования.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",                      ПараметрыФормирования.ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("ПланЗанятости",                     ПараметрыФормирования.ПланЗанятости);
	Запрос.УстановитьПараметр("ВидРесурса",                        ДанныеПланов.ВидСогласуемогоРесурсаПлановыхРабот);
	Запрос.УстановитьПараметр("ВидПлана",                          ПараметрыФормирования.ПланЗанятости);
	
	СтрокаТаблицыПланов = СтрокаТаблицыПлановПоЗначениюКолонки(ДанныеПланов.ТаблицаПланов, "ТипПериода", СловарьТекущийПериодПлана());
	Если СтрокаТаблицыПланов <> Неопределено Тогда
		 Запрос.УстановитьПараметр("НачалоТекущегоПериода", СтрокаТаблицыПланов.НачалоПериода);
	Иначе
		Запрос.УстановитьПараметр("НачалоТекущегоПериода",  Дата(1, 1, 1));
	КонецЕсли;
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников" Тогда
		Запрос.УстановитьПараметр("ВыбранныеСотрудники", ПараметрыФормирования.ОтборПоУчастникам);
	ИначеЕсли ПараметрыФормирования.ВариантГруппировки = "ОтТехПроектов" Тогда
		Запрос.УстановитьПараметр("ВыводимыеТехническиеПроекты", ПараметрыФормирования.ОтборПоТехническимПроектам);
	КонецЕсли;
	
	ПараметрыОтображенияЭтапа  = ПланированиеКлиентСервер.ПараметрыОтображенияЭтапаПланаЗанятости(ДанныеВидаПлана.ПороговоеЗначениеЭтапаПопаданиеВПланЗанятости);
	Запрос.УстановитьПараметр("ПороговоеЗначениеДляЭтапаПоЗапланировано", ПараметрыОтображенияЭтапа.ПороговоеЗначениеДляЭтапаПоЗапланировано);
	Запрос.УстановитьПараметр("ПороговоеЗначениеДляЭтапаПоФакт",          ПараметрыОтображенияЭтапа.ПороговоеЗначениеДляЭтапаПоФакт);
	
КонецПроцедуры
 
Процедура ВывестиДанныеВТабличныйДокумент(РезультатФормированияОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов)
	
	РезультатФормированияОтчета.ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников" Тогда
		ВывестиДанныеВТабличныйДокументОтСотрудников(РезультатФормированияОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов);
	ИначеЕсли ПараметрыФормирования.ВариантГруппировки = "ОтТехПроектов" Тогда
		ВывестиДанныеВТабличныйДокументОтТехПроектов(РезультатФормированияОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПараметровФормирования(ПараметрыФормирования, РезультатФормированияОтчета)

	Если ПараметрыФормирования.ВариантГруппировки <> "ОтСотрудников" 
		И ПараметрыФормирования.ВариантГруппировки <> "ОтТехПроектов" Тогда
		
		СообщениеОбОшибке = НовыйСообщениеОбОшибке();
		
		Если Не ПустаяСтрока(ПараметрыФормирования.ВариантГруппировки) Тогда
			СообщениеОбОшибке.ТекстОшибки = СтрШаблон(НСтр("ru = 'В параметры формирования отчета передан некорректный вариант группировки - %1. Допустимый варианты - ""ОтСотрудников"", ""ОтТехПроектов"".'"), 
			                                          ПараметрыФормирования.ВариантГруппировки);
		Иначе
			СообщениеОбОшибке.ТекстОшибки = НСтр("ru = 'В параметры формирования отчета не передан вариант группировки.'")
		КонецЕсли;
		
		РезультатФормированияОтчета.СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		РезультатФормированияОтчета.Успешно = Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыФормирования.ПланЗанятости) Тогда
		
		СообщениеОбОшибке = НовыйСообщениеОбОшибке();
		
		СообщениеОбОшибке.ИмяРеквизита = "ПланЗанятости";
		СообщениеОбОшибке.ТекстОшибки = НСтр("ru = 'Не указан план занятости.'");
		РезультатФормированияОтчета.СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		РезультатФормированияОтчета.Успешно = Ложь;
		
	КонецЕсли;
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников" 
		И (ПараметрыФормирования.ОтборПоУчастникам = Неопределено
		Или ПараметрыФормирования.ОтборПоУчастникам.Количество() = 0) Тогда
		
		СообщениеОбОшибке = НовыйСообщениеОбОшибке();
		
		СообщениеОбОшибке.ТекстОшибки = НСтр("ru = 'Не выбран ни один участник плана для отображения.'");
		РезультатФормированияОтчета.СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		РезультатФормированияОтчета.Успешно = Ложь;
		
	КонецЕсли;
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтТехПроектов" 
		И (ПараметрыФормирования.ОтборПоТехническимПроектам = Неопределено
		Или ПараметрыФормирования.ОтборПоТехническимПроектам.Количество() = 0) Тогда
		
		СообщениеОбОшибке = НовыйСообщениеОбОшибке();
		
		СообщениеОбОшибке.ТекстОшибки = НСтр("ru = 'Не выбран ни один технический проект для отображения.'");
		РезультатФормированияОтчета.СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		РезультатФормированияОтчета.Успешно = Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыФормирования.ПериодОтчета.ДатаНачала)
		Или Не ЗначениеЗаполнено(ПараметрыФормирования.ПериодОтчета.ДатаОкончания) Тогда
		
		СообщениеОбОшибке = НовыйСообщениеОбОшибке();
		
		СообщениеОбОшибке.ИмяРеквизита = "ПериодОтчета.ДатаНачала";
		СообщениеОбОшибке.ТекстОшибки = НСтр("ru = 'Не задан период формирования отчета.'");
		РезультатФормированияОтчета.СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		РезультатФормированияОтчета.Успешно = Ложь;
		
	Иначе
		
		Если ПараметрыФормирования.ПериодОтчета.ДатаНачала > ПараметрыФормирования.ПериодОтчета.ДатаОкончания Тогда
			
			СообщениеОбОшибке = НовыйСообщениеОбОшибке();
			
			СообщениеОбОшибке.ИмяРеквизита = "ПериодОтчета.ДатаНачала";
			СообщениеОбОшибке.ТекстОшибки = НСтр("ru = 'Дата начала отчета должна быть меньше даты окончания.'");
			РезультатФормированияОтчета.СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
			РезультатФормированияОтчета.Успешно = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

Функция НовыйПодготовленныеДанныеОтчета(ТаблицаПланов)

	ИдентификаторыИменаКолонок     = Новый Соответствие; 
	ИменаКолонокПерерасчетРодители = Новый Массив;
	
	ОписаниеТипаЧисло152 = Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Любой));
	ОписаниеТипаЧисло5   = Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(5,0,ДопустимыйЗнак.Неотрицательный));
	
	ДеревоДанных = Новый ДеревоЗначений;
	ДеревоДанных.Колонки.Добавить("СотрудникРабота",                              ОписаниеТиповСотрудникРабота());
	ДеревоДанных.Колонки.Добавить("ТехническийПроект",                            Новый ОписаниеТипов("СправочникСсылка.ТехническиеПроекты"));
	ДеревоДанных.Колонки.Добавить("Проект",                                       Новый ОписаниеТипов("СправочникСсылка.Проекты"));
	ДеревоДанных.Колонки.Добавить("Версия",                                       Новый ОписаниеТипов("СправочникСсылка.ВерсииПроекта"));
	ДеревоДанных.Колонки.Добавить("ДатаНачала",                                   Новый ОписаниеТипов("Дата"));
	ДеревоДанных.Колонки.Добавить("ДатаОкончания",                                Новый ОписаниеТипов("Дата"));
	ДеревоДанных.Колонки.Добавить("ДатаНачалаПланирования",                       Новый ОписаниеТипов("Дата"));
	ДеревоДанных.Колонки.Добавить("ДатаОкончанияПланирования",                    Новый ОписаниеТипов("Дата"));
	ДеревоДанных.Колонки.Добавить("Статус",                                       ОписаниеТиповСтатус());
	ДеревоДанных.Колонки.Добавить("Факт",                                         ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("ФактМинусПланЗанятости",                       ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("ОстатокНаТекущийПериод",                       ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("Нераспределено",                               ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("Резерв",                                       ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("ЗапланированоНаНачалоТекущего",                ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("ПланируемыеТрудозатратыНачалоТекущегоПериода", ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("ФактТрудозатратыНачалоТекущегоПериода",        ОписаниеТипаЧисло152);
	ДеревоДанных.Колонки.Добавить("ВСтрокеЕстьДанныеСоответствующиеОтбору",       Новый ОписаниеТипов("Булево"));
	ДеревоДанных.Колонки.Добавить("ДатаТехПроектаМеньшеПлановыхДатЭтапа",         Новый ОписаниеТипов("Булево"));
	ДеревоДанных.Колонки.Добавить("ДатаВерсииМеньшеДатыТехПроекта",               Новый ОписаниеТипов("Булево"));
	
	ИменаКолонокПерерасчетРодители.Добавить("ФактМинусПланЗанятости");
	ИменаКолонокПерерасчетРодители.Добавить("ОстатокНаТекущийПериод");
	ИменаКолонокПерерасчетРодители.Добавить("ЗапланированоНаНачалоТекущего");
	ИменаКолонокПерерасчетРодители.Добавить("ПланируемыеТрудозатратыНачалоТекущегоПериода");
	ИменаКолонокПерерасчетРодители.Добавить("ФактТрудозатратыНачалоТекущегоПериода");
	ИменаКолонокПерерасчетРодители.Добавить("Нераспределено");
	ИменаКолонокПерерасчетРодители.Добавить("Резерв");
	
	Для Каждого СтрокаТаблицы Из ТаблицаПланов Цикл
		
		ИменаКолонок = НовыйИменаКолонокПериода();
		
		ИменаКолонокПерерасчетРодители.Добавить(ИмяДобавленнойКолонкиПериода(ДеревоДанных, СтрокаТаблицы.ИдентификаторПлана, "ПланЗанятости", ИменаКолонок, ОписаниеТипаЧисло152));
		ИмяДобавленнойКолонкиПериода(ДеревоДанных, СтрокаТаблицы.ИдентификаторПлана, "ПериодКорректен",        ИменаКолонок, Новый ОписаниеТипов("Булево"));
		ИмяДобавленнойКолонкиПериода(ДеревоДанных, СтрокаТаблицы.ИдентификаторПлана, "ЕстьОтклоненияПоОтбору", ИменаКолонок, Новый ОписаниеТипов("Булево")); 
		
		Если СтрокаТаблицы.ТипПериода = СловарьПрошлыйПериодПлана()
			Или СтрокаТаблицы.ТипПериода = СловарьТекущийПериодПлана() Тогда
			
			ИменаКолонокПерерасчетРодители.Добавить(ИмяДобавленнойКолонкиПериода(ДеревоДанных, СтрокаТаблицы.ИдентификаторПлана, "РабочиеПланы", ИменаКолонок, ОписаниеТипаЧисло152));
			ИменаКолонокПерерасчетРодители.Добавить(ИмяДобавленнойКолонкиПериода(ДеревоДанных, СтрокаТаблицы.ИдентификаторПлана, "Факт", ИменаКолонок, ОписаниеТипаЧисло152));
			
		КонецЕсли;
		
		ИменаКолонок.ТипПериода = СтрокаТаблицы.ТипПериода;
		
		ИдентификаторыИменаКолонок.Вставить(СтрокаТаблицы.ИдентификаторПлана, ИменаКолонок);
		
	КонецЦикла;
	
	ДеревоДанных.Колонки.Добавить("ПланЗанятостиНеотображаемыйПериод", ОписаниеТипаЧисло152);
	
	ДанныеОтчета = Новый Структура;
	ДанныеОтчета.Вставить("ДеревоДанных",                   ДеревоДанных);
	ДанныеОтчета.Вставить("ПредставлениеОтбора",            "");
	ДанныеОтчета.Вставить("ИдентификаторыИменаКолонок",     ИдентификаторыИменаКолонок);
	ДанныеОтчета.Вставить("ИменаКолонокПерерасчетРодители", ИменаКолонокПерерасчетРодители);
	
	
	Возврат ДанныеОтчета;
	
КонецФункции

Процедура УстановитьШапкуТаблицыОтчета(Макет, ТаблицаОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов)
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ШапкаВерхОсновное");
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаВерхПроект");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаВерхВерсия");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаВерхСроки");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ШапкаВерхТрудозатраты");
	ТаблицаОтчета.Присоединить(Область);
	
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ПодготовленныеДанные.ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() = 3 Тогда
				ИмяОбласти = "ПредставлениеПериодаТриКолонки"; 
				РазмерШрифта = 6;
			ИначеЕсли ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() = 2 Тогда
				ИмяОбласти = "ПредставлениеПериодаДвеКолонки";
				РазмерШрифта = 8;
			ИначеЕсли ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() = 1 Тогда
				ИмяОбласти = "ПредставлениеПериодаОднаКолонка";
				РазмерШрифта = 8;
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть(ИмяОбласти);
			
			Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана() Тогда
				Область.Области[ИмяОбласти].Шрифт = Новый Шрифт( , РазмерШрифта, Истина);
			КонецЕсли;
			
		Иначе
			
			Область = Макет.ПолучитьОбласть("ПредставлениеПериодаОднаКолонка");
			
		КонецЕсли;
		
		Область.Параметры.ПредставлениеПериода = СтрШаблон("%1 - %2", 
		                                                   Формат(СтрокаТаблицыПланов.НачалоПериода, "ДФ=dd.MM.yy"), 
		                                                   Формат(СтрокаТаблицыПланов.КонецПериода, "ДФ=dd.MM.yy"));
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ШапкаНизОсновное");
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаНизПроект");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаНизВерсия");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаНизСроки");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ШапкаНизТрудозатраты");
	Область.Параметры.ОстатокНаДату = ИмяКолонкиОстатокТекущийПериод(ПодготовленныеДанные, ТаблицаПланов);
	ТаблицаОтчета.Присоединить(Область);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ПодготовленныеДанные.ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если (ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана())
			И ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() > 1 Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("ШапкаКолонкиПЗ");
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("ШапкаКолонкиРП");
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("ШапкаКолонкиФакт");
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
		Иначе
			
			Область = Макет.ПолучитьОбласть("ПериодОднаКолонка");
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаОтчета.ФиксацияСверху = 20;
	ТаблицаОтчета.ФиксацияСлева  = ОпределенннаяФиксацияСлева(ПараметрыФормирования);
	
КонецПроцедуры

Функция ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования)
	
	ОтображаемыеКолонки = Новый Соответствие;
	ОтображаемыеКолонки.Вставить("ПЗ", Истина);
	
	Если Не ПараметрыФормирования.НеВыводитьРПВПериодах Тогда
		ОтображаемыеКолонки.Вставить("РП", Истина);
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьФактВПериодах Тогда
		ОтображаемыеКолонки.Вставить("Факт", Истина);
	КонецЕсли;
	
	Возврат ОтображаемыеКолонки;
	
КонецФункции

Функция ОпределенннаяФиксацияСлева(ПараметрыФормирования)
	
	ФиксацияСлева = 15;
	
	Если ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		ФиксацияСлева = ФиксацияСлева - 1;
		
	КонецЕсли;
	
	Если ПараметрыФормирования.НеВыводитьПроект Тогда
		
		ФиксацияСлева = ФиксацияСлева - 1;
		
	КонецЕсли;
	
	Если ПараметрыФормирования.НеВыводитьСроки Тогда
		
		ФиксацияСлева = ФиксацияСлева - 2;
		
	КонецЕсли;
		
	Возврат ФиксацияСлева
	
КонецФункции

Процедура УстановитьШапкуОтчета(Макет, ТаблицаОтчета, ПараметрыФормирования, ПодготовленныеДанные)
	
	Область = Макет.ПолучитьОбласть("ШапкаОтчета");
	ПараметрыОбластиДанных = ПараметрыОбластиДанныхШапкиОтчета(ПараметрыФормирования, ПодготовленныеДанные);
	ЗаполнитьЗначенияСвойств(Область.Параметры, ПараметрыОбластиДанных);
	ТаблицаОтчета.Вывести(Область);
	
КонецПроцедуры

Процедура УстановитьЛегенду(Макет, ТаблицаОтчета)
	
	Область = Макет.ПолучитьОбласть("ГруппировкаЛегенда");
	ТаблицаОтчета.Вывести(Область);
	ТаблицаОтчета.НачатьГруппуСтрок("Легенда", Ложь);
	
	Область = Макет.ПолучитьОбласть("Легенда");
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Область = Макет.ПолучитьОбласть("ПустаяСтрока");
	ТаблицаОтчета.Вывести(Область);
	
КонецПроцедуры

#КонецОбласти

#Область ОтСотрудников

Функция ТекстЗапросаГруппировкаОтСотрудников()
	
	Возврат "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Планы.ИдентификаторПлана КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ИдентификаторыРабочихПланов
	|ИЗ
	|	РегистрСведений.Планы КАК Планы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыПланов КАК ВидыПланов
	|		ПО Планы.ВидПлана = ВидыПланов.Ссылка
	|ГДЕ
	|	ВидыПланов.ТипПлана = ЗНАЧЕНИЕ(Перечисление.ТипыПланов.Рабочий)
	|	И НЕ ВидыПланов.ПометкаУдаления
	|	И ВидыПланов.РодительскийПлан.Ссылка = &ПланЗанятости
	|	И ВидыПланов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВидовПланов.Действует)
	|	И Планы.НачалоПериода >= &НачалоПериода
	|	И Планы.КонецПериода <= &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПланов.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ТаблицаПланов.НачалоПериода КАК ДатаНачала,
	|	ТаблицаПланов.КонецПериода КАК ДатаОкончания,
	|	ТаблицаПланов.Зафиксирован КАК Зафиксирован
	|ПОМЕСТИТЬ ТаблицаПланов
	|ИЗ
	|	&ТаблицаПланов КАК ТаблицаПланов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаНачала,
	|	ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаписиПлана.ИдентификаторПлана      КАК ИдентификаторПлана,
	|	ЗаписиПлана.Сотрудник               КАК Сотрудник,
	|	ЗаписиПлана.Работа                  КАК Работа,
	|	ЗаписиПлана.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ СуществующиеЗаписиПлана
	|ИЗ
	|	РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (ТаблицаПланов.ИдентификаторПлана = ЗаписиПлана.ИдентификаторПлана)
	|ГДЕ
	|	ЗаписиПлана.Сотрудник В(&ВыбранныеСотрудники)
	|	И ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа)
	|	И ЗаписиПлана.Работа НЕ В (ЗНАЧЕНИЕ(Справочник.ЗадачиПроцесса.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.ВидыДеятельности.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныЙЗапрос.Исполнитель               КАК Исполнитель,
	|	ВложенныЙЗапрос.Родитель                  КАК Родитель,
	|	СУММА(ВложенныЙЗапрос.Запланировано)      КАК Запланировано,
	|	СУММА(ВложенныЙЗапрос.УчетВремениСекунды) КАК УчетВремениСекунды
	|ПОМЕСТИТЬ ЭтапыСотрудникаЗапланированоУчетВремени
	|ИЗ
	|(ВЫБРАТЬ 
	|	ЗадачиЗапланированоПоСотрудникам.Пользователь            КАК Исполнитель,
	|	ВЫБОР 
	|		КОГДА ЗадачиПроцессаРодители.ЗадачаШаблона = ЗНАЧЕНИЕ(Справочник.ЗадачиШаблонаПроцесса.ПустаяСсылка)
	|			ТОГДА ЗадачиПроцессаРодители.Предмет
	|			ИНАЧЕ ИерархияЗадачПроцесса.Родитель
	|	КОНЕЦ                                                    КАК Родитель,
	|	ЕСТЬNULL(ЗадачиЗапланированоПоСотрудникам.Количество, 0) КАК Запланировано,
	|	ЕСТЬNULL(УчетВремени.Длительность, 0)                    КАК УчетВремениСекунды
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиЗапланированоПоСотрудникам КАК ЗадачиЗапланированоПоСотрудникам
	|		ПО (ЗадачиЗапланированоПоСотрудникам.Задача = ЗадачиПроцесса.Ссылка)
	|			И ЗадачиЗапланированоПоСотрудникам.Пользователь В(&ВыбранныеСотрудники)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ПО ЗадачиЗапланированоПоСотрудникам.Задача = ИерархияЗадачПроцесса.ЗадачаПроцесса 
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцессаРодители
	|		ПО ЗадачиПроцессаРодители.Ссылка = ИерархияЗадачПроцесса.Родитель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
	|		ПО ЗадачиПроцесса.Предмет = ТехническиеПроекты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО (УчетВремени.Задача = ЗадачиЗапланированоПоСотрудникам.Задача)
	|			И (УчетВремени.Пользователь = ЗадачиЗапланированоПоСотрудникам.Пользователь)
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|ГДЕ
	|	ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|	И НЕ ЗадачиПроцесса.ПометкаУдаления
	|	И ИерархияЗадачПроцесса.Уровень = 0
	|	И НЕ ТехническиеПроекты.ПометкаУдаления
	|	И ТехническиеПроекты.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Запланирован), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Активен))
	|
	|ОБЪЕДИНИТЬ ВСЕ 
	|
	|ВЫБРАТЬ
	|	ЗадачиПроцесса.Исполнитель                                       КАК Исполнитель,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыГруппировокСтрокПлана.РаботаСОшибками) КАК Родитель,
	|	ЕСТЬNULL(ИтогиСогласованияЗадачРесурсов.Запланировано, 0)        КАК Запланировано,
	|	ЕСТЬNULL(УчетВремени.Длительность, 0)                            КАК УчетВремениСекунды
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|			ПО ЗадачиПроцесса.Ссылка = ИерархияЗадачПроцесса.ЗадачаПроцесса 
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцессаРодители
	|			ПО ЗадачиПроцессаРодители.Ссылка = ИерархияЗадачПроцесса.Родитель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ошибки КАК Ошибки
	|			ПО ЗадачиПроцесса.Предмет = Ошибки.Ссылка
	|			И НЕ Ошибки.ПометкаУдаления
	|			И Ошибки.Статус НЕ В (ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.НеЗарегистрирована), ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.Отозвана))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИтогиСогласованияЗадачРесурсов КАК ИтогиСогласованияЗадачРесурсов
	|		ПО (ИтогиСогласованияЗадачРесурсов.ЗадачаПроцесса = ЗадачиПроцесса.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО (УчетВремени.Задача = ЗадачиПроцесса.Ссылка)
	|			И (УчетВремени.Пользователь = ЗадачиПроцесса.Исполнитель)
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|ГДЕ
	|	ЗадачиПроцесса.Исполнитель В(&ВыбранныеСотрудники)
	|	И ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|	И НЕ ЗадачиПроцесса.ПометкаУдаления
	|	И ИерархияЗадачПроцесса.Уровень = 0) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Родитель,
	|	ВложенныйЗапрос.Исполнитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Исполнитель,
	|	Родитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Исполнитель         КАК Исполнитель,
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Родитель            КАК Родитель,
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Запланировано       КАК Запланировано,
	|	ЭтапыСотрудникаЗапланированоУчетВремени.УчетВремениСекунды  КАК УчетВремениСекунды
	|ПОМЕСТИТЬ ЭтапыСотрудникаДляОтображенияВПланах
	|ИЗ
	|	ЭтапыСотрудникаЗапланированоУчетВремени КАК ЭтапыСотрудникаЗапланированоУчетВремени
	|ГДЕ
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Запланировано > &ПороговоеЗначениеДляЭтапаПоЗапланировано
	|	ИЛИ ЭтапыСотрудникаЗапланированоУчетВремени.УчетВремениСекунды > &ПороговоеЗначениеДляЭтапаПоФакт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	СуществующиеЗаписиПлана.Работа КАК Работа
	|ПОМЕСТИТЬ ОтображаемыеРаботы
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЭтапыСотрудникаДляОтображенияВПланах.Родитель
	|ИЗ
	|	ЭтапыСотрудникаДляОтображенияВПланах КАК ЭтапыСотрудникаДляОтображенияВПланах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуществующиеЗаписиПлана.Сотрудник КАК Сотрудник,
	|	СуществующиеЗаписиПлана.Работа    КАК Работа
	|ПОМЕСТИТЬ СотрудникиВидыДеятельности
	|ИЗ
	|	СуществующиеЗаписиПлана КАК СуществующиеЗаписиПлана
	|ГДЕ
	|	СуществующиеЗаписиПлана.Работа ССЫЛКА Справочник.ВидыДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты,
	|	ВложенныйЗапрос.Сотрудник               КАК Сотрудник,
	|	ВложенныйЗапрос.Работа                  КАК Работа,
	|	ВложенныйЗапрос.ИдентификаторПлана      КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ДанныеВыводимыхРабот
	|ИЗ
	|	(ВЫБРАТЬ
	|		СуществующиеЗаписиПлана.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты,
	|		СуществующиеЗаписиПлана.Сотрудник               КАК Сотрудник,
	|		СуществующиеЗаписиПлана.Работа                  КАК Работа,
	|		СуществующиеЗаписиПлана.ИдентификаторПлана      КАК ИдентификаторПлана
	|	ИЗ
	|		СуществующиеЗаписиПлана КАК СуществующиеЗаписиПлана
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ЭтапыСотрудникаДляОтображенияВПланах.Исполнитель,
	|		ЭтапыСотрудникаДляОтображенияВПланах.Родитель,
	|		0
	|	ИЗ
	|		ЭтапыСотрудникаДляОтображенияВПланах КАК ЭтапыСотрудникаДляОтображенияВПланах) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтображаемыеРаботы.Работа                  КАК Работа,
	|	ЗаписиПлана.Сотрудник                      КАК Сотрудник,
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ЗапланированоПланыЗанятостиПрошлыеПериоды
	|ИЗ
	|	ОтображаемыеРаботы КАК ОтображаемыеРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ПО ОтображаемыеРаботы.Работа = ЗаписиПлана.Работа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Планы КАК Планы
	|		ПО ЗаписиПлана.ИдентификаторПлана = Планы.ИдентификаторПлана
	|			И (Планы.ВидПлана = &ПланЗанятости)
	|			И (Планы.КонецПериода < &НачалоТекущегоПериода) И (Планы.НачалоПериода >= &НачалоПериода)
	|			И (ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа))
	|ГДЕ
	|	ЗаписиПлана.Сотрудник В(&ВыбранныеСотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтображаемыеРаботы.Работа,
	|	ЗаписиПлана.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиПроцесса.Ссылка  КАК ЭтапНеПоШаблону,
	|	ЗадачиПроцесса.Предмет КАК ТехПроект
	|ПОМЕСТИТЬ ЭтапыНеПоШаблону
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса 
	|ГДЕ
	|	ЗадачиПроцесса.ЗадачаШаблона = ЗНАЧЕНИЕ(Справочник.ЗадачиШаблонаПроцесса.ПустаяСсылка)
	|	И ЗадачиПроцесса.Родитель = ЗНАЧЕНИЕ(Справочник.ЗадачиПроцесса.ПустаяСсылка)
	|	И ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|	И ЗадачиПроцесса.Предмет В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ДанныеВыводимыхРабот.Работа КАК Работа
	|		ИЗ
	|			ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыГруппировокСтрокПлана.РаботаСОшибками) КАК ВидГруппировки,
	|		ИерархияЗадачПроцесса.Родитель                                   КАК ЭтапПоОшибке 
	|ПОМЕСТИТЬ ЭтапыОшибок
	|ИЗ
	|		Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|				ПО ЗадачиПроцесса.Ссылка = ИерархияЗадачПроцесса.ЗадачаПроцесса 
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцессаРодители
	|				ПО ЗадачиПроцессаРодители.Ссылка = ИерархияЗадачПроцесса.Родитель
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ошибки КАК Ошибки
	|				ПО ЗадачиПроцесса.Предмет = Ошибки.Ссылка
	|			И НЕ Ошибки.ПометкаУдаления
	|				И Ошибки.Статус НЕ В (ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.НеЗарегистрирована), ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.Отозвана))
	|ГДЕ
	|		ЗадачиПроцесса.Исполнитель В(&ВыбранныеСотрудники)
	|		И ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|		И НЕ ЗадачиПроцесса.ПометкаУдаления
	|		И ИерархияЗадачПроцесса.Уровень = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИерархияЗадачПроцесса.ЗадачаПроцесса КАК ЗадачаПроцесса,
	|	ИерархияЗадачПроцесса.Родитель       КАК Родитель
	|ПОМЕСТИТЬ ДочерниеЗадачиРабот
	|ИЗ
	|	РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтображаемыеРаботы КАК ОтображаемыеРаботы
	|		ПО (ОтображаемыеРаботы.Работа = ИерархияЗадачПроцесса.Родитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК СправочникЗадачиПроцесса
	|		ПО (ИерархияЗадачПроцесса.ЗадачаПроцесса = СправочникЗадачиПроцесса.Ссылка 
	|			И НЕ СправочникЗадачиПроцесса.ПометкаУдаления 
	|			И НЕ СправочникЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Отменена))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИерархияЗадачПроцесса.ЗадачаПроцесса КАК ЗадачаПроцесса,
	|	ЭтапыНеПоШаблону.ТехПроект           КАК Родитель
	|ИЗ
	|	РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЭтапыНеПоШаблону КАК ЭтапыНеПоШаблону
	|		ПО (ЭтапыНеПоШаблону.ЭтапНеПоШаблону = ИерархияЗадачПроцесса.Родитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК СправочникЗадачиПроцесса
	|		ПО (ИерархияЗадачПроцесса.ЗадачаПроцесса = СправочникЗадачиПроцесса.Ссылка 
	|			И НЕ СправочникЗадачиПроцесса.ПометкаУдаления 
	|			И НЕ СправочникЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Отменена))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИерархияЗадачПроцесса.ЗадачаПроцесса КАК ЗадачаПроцесса,
	|	ЭтапыОшибок.ВидГруппировки           КАК Родитель
	|ИЗ
	|	РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЭтапыОшибок КАК ЭтапыОшибок
	|		ПО (ЭтапыОшибок.ЭтапПоОшибке = ИерархияЗадачПроцесса.Родитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК СправочникЗадачиПроцесса
	|		ПО (ИерархияЗадачПроцесса.ЗадачаПроцесса = СправочникЗадачиПроцесса.Ссылка 
	|			И НЕ СправочникЗадачиПроцесса.ПометкаУдаления 
	|			И НЕ СправочникЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Отменена))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(УчетВремени.Длительность)  КАК Длительность,
	|	ДанныеВыводимыхРабот.Сотрудник   КАК Сотрудник,
	|	ДанныеВыводимыхРабот.Работа      КАК Работа,
	|	ТаблицаПланов.ИдентификаторПлана КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ЗатраченноеВремяВидыДеятельности
	|ИЗ
	|	ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО ДанныеВыводимыхРабот.Сотрудник = УчетВремени.Пользователь
	|			И ДанныеВыводимыхРабот.Работа = УчетВремени.ВидДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (УчетВремени.Период >= НАЧАЛОПЕРИОДА(ТаблицаПланов.ДатаНачала, ДЕНЬ))
	|			И (УчетВремени.Период <= КОНЕЦПЕРИОДА(ТаблицаПланов.ДатаОкончания, ДЕНЬ))
	|ГДЕ
	|	ТаблицаПланов.ИдентификаторПлана = ДанныеВыводимыхРабот.ИдентификаторПлана
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеВыводимыхРабот.Работа,
	|	ДанныеВыводимыхРабот.Сотрудник,
	|	ТаблицаПланов.ИдентификаторПлана
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(УчетВремени.Длительность)  КАК Длительность,
	|	УчетВремени.Пользователь         КАК Сотрудник,
	|	ТаблицаПланов.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ДочерниеЗадачиРабот.Родитель     КАК Работа
	|ПОМЕСТИТЬ ЗатраченноеВремяЗадачи
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО (УчетВремени.Задача = ДочерниеЗадачиРабот.ЗадачаПроцесса)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (УчетВремени.Период >= НАЧАЛОПЕРИОДА(ТаблицаПланов.ДатаНачала, ДЕНЬ))
	|			И (УчетВремени.Период <= КОНЕЦПЕРИОДА(ТаблицаПланов.ДатаОкончания, ДЕНЬ))
	|ГДЕ
	|	УчетВремени.Пользователь В(&ВыбранныеСотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	УчетВремени.Пользователь,
	|	ТаблицаПланов.ИдентификаторПлана
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель               КАК Работа,
	|	ЗаписиПлана.Сотрудник                      КАК Сотрудник,
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) КАК ПланируемыеТрудозатраты,
	|	ТаблицаПланов.ИдентификаторПлана           КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ДанныеРабочихПлановЭтапы
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗаписиПлана.Работа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Планы КАК Планы
	|		ПО (ЗаписиПлана.ИдентификаторПлана = Планы.ИдентификаторПлана)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (Планы.НачалоПериода = ТаблицаПланов.ДатаНачала)
	|ГДЕ
	|	ЗаписиПлана.ИдентификаторПлана В
	|			(ВЫБРАТЬ
	|				ИдентификаторыРабочихПланов.ИдентификаторПлана КАК ИдентификаторПлана
	|			ИЗ
	|				ИдентификаторыРабочихПланов КАК ИдентификаторыРабочихПланов)
	|	И ЗаписиПлана.Сотрудник В(&ВыбранныеСотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	ЗаписиПлана.Сотрудник,
	|	ТаблицаПланов.ИдентификаторПлана
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыДеятельности.Ссылка КАК Работа,
	|	ЗаписиПлана.Сотрудник КАК Сотрудник,
	|	ЗаписиПлана.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты,
	|	ТаблицаПланов.ИдентификаторПлана КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ДанныеРабочихПлановВидыДеятельности
	|ИЗ
	|	Справочник.ВидыДеятельности КАК ВидыДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ПО ВидыДеятельности.Ссылка = ЗаписиПлана.Работа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Планы КАК Планы
	|		ПО (ЗаписиПлана.ИдентификаторПлана = Планы.ИдентификаторПлана)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (Планы.НачалоПериода = ТаблицаПланов.ДатаНачала)
	|ГДЕ
	|	ЗаписиПлана.ИдентификаторПлана В
	|			(ВЫБРАТЬ
	|				ИдентификаторыРабочихПланов.ИдентификаторПлана КАК ИдентификаторПлана
	|			ИЗ
	|				ИдентификаторыРабочихПланов КАК ИдентификаторыРабочихПланов)
	|	И ЗаписиПлана.Сотрудник В(&ВыбранныеСотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаписиПлана.Сотрудник                      КАК Сотрудник,
	|	ЗаписиПлана.Работа                         КАК Работа,
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ЗаписиПлановЗанятостиНеотображаемыхПеридов
	|ИЗ
	|	РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|ГДЕ
	|	ЗаписиПлана.Сотрудник В(&ВыбранныеСотрудники)
	|	И ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа)
	|	И ЗаписиПлана.ИдентификаторПлана В
	|			(ВЫБРАТЬ
	|				Планы.ИдентификаторПлана КАК ИдентификаторПлана
	|			ИЗ
	|				РегистрСведений.Планы КАК Планы
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыПланов КАК ВидыПланов
	|					ПО
	|						Планы.ВидПлана = ВидыПланов.Ссылка
	|			ГДЕ
	|				ВидыПланов.Ссылка = &ПланЗанятости
	|				И Планы.НачалоПериода >= &КонецПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиПлана.Сотрудник,
	|	ЗаписиПлана.Работа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель                 КАК Этап,
	|	ДочерниеЗадачиРабот.ЗадачаПроцесса           КАК ЗадачаПроцесса,
	|	УчетВремени.Пользователь                     КАК Исполнитель,
	|	СУММА(ВЫБОР
	|			КОГДА ЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Выполнена)
	|				ТОГДА ВЫБОР
	|						КОГДА ЗадачиПроцесса.ФактическаяДатаОкончания < &НачалоТекущегоПериода
	|							ТОГДА 0
	|						ИНАЧЕ ЕСТЬNULL(УчетВремени.Длительность, 0)
	|					КОНЕЦ
	|			ИНАЧЕ ЕСТЬNULL(УчетВремени.Длительность, 0)
	|		КОНЕЦ) КАК ФактСекундыДоТекущегоПериода,
	|	СУММА(ВЫБОР 
	|		КОГДА (УчетВремени.Период < &НачалоТекущегоПериода И УчетВремени.Период >= &НачалоПериода)
	|			ТОГДА ЕСТЬNULL(УчетВремени.Длительность, 0) 
	|			ИНАЧЕ 0 
	|	КОНЕЦ)                                       КАК ФактСекундыНачалоОтчетаНачалоТекущегоПериода
	|ПОМЕСТИТЬ ФактЗадачиНачалоТекущегоПериода
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиПроцесса.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = УчетВремени.Задача
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|			И УчетВремени.Пользователь В(&ВыбранныеСотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	ДочерниеЗадачиРабот.ЗадачаПроцесса,
	|	УчетВремени.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(УчетВремени.Длительность)      КАК Длительность,
	|	СотрудникиВидыДеятельности.Сотрудник КАК Сотрудник,
	|	СотрудникиВидыДеятельности.Работа    КАК Работа
	|ПОМЕСТИТЬ ВидыДеятельностиПрошлыеПериоды
	|ИЗ
	|	СотрудникиВидыДеятельности КАК СотрудникиВидыДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО СотрудникиВидыДеятельности.Сотрудник = УчетВремени.Пользователь
	|			И СотрудникиВидыДеятельности.Работа = УчетВремени.ВидДеятельности
	|			И (УчетВремени.Период < &НачалоТекущегоПериода) И (УчетВремени.Период >= &НачалоПериода)
	|			И УчетВремени.Пользователь В(&ВыбранныеСотрудники)
	|
	|СГРУППИРОВАТЬ ПО 
	|	СотрудникиВидыДеятельности.Сотрудник,
	|	СотрудникиВидыДеятельности.Работа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	| 
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Этап                                                КАК Этап,
	|	ВложенныйЗапрос.Исполнитель                                         КАК Исполнитель,
	|	СУММА(ВложенныйЗапрос.ФактСекундыНачалоОтчетаНачалоТекущегоПериода) КАК ФактСекундыНачалоОтчетаНачалоТекущегоПериода
	|ПОМЕСТИТЬ ФактЭтапыНачалоТекущегоПериода
	|ИЗ
	|(ВЫБРАТЬ 
	|	ФактЗадачиНачалоТекущегоПериода.Этап                                                КАК Этап,
	|	ФактЗадачиНачалоТекущегоПериода.Исполнитель                                         КАК Исполнитель,
	|	ФактЗадачиНачалоТекущегоПериода.ФактСекундыНачалоОтчетаНачалоТекущегоПериода        КАК ФактСекундыНачалоОтчетаНачалоТекущегоПериода
	|ИЗ
	|	ФактЗадачиНачалоТекущегоПериода КАК ФактЗадачиНачалоТекущегоПериода
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВидыДеятельностиПрошлыеПериоды.Работа       КАК Этап,
	|	ВидыДеятельностиПрошлыеПериоды.Сотрудник    КАК Исполнитель,
	|	ВидыДеятельностиПрошлыеПериоды.Длительность КАК ФактСекундыНачалоОтчетаНачалоТекущегоПериода
	|ИЗ
	|	ВидыДеятельностиПрошлыеПериоды КАК ВидыДеятельностиПрошлыеПериоды) КАК ВложенныйЗапрос
 	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Этап,
	|	ВложенныйЗапрос.Исполнитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Исполнитель,
	|	Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель                              КАК Этап,
	|	ДочерниеЗадачиРабот.ЗадачаПроцесса                        КАК ЗадачаПроцесса,
	|	ЗадачиЗапланированоПоСотрудникам.Пользователь             КАК Исполнитель,
	|	ЕСТЬNULL(ЗадачиЗапланированоПоСотрудникам.Количество, 0)  КАК Запланировано
	|ПОМЕСТИТЬ ОстатокЗапланированоНачалоТекущегоПериода
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиПроцесса.Ссылка
	|			И (ВЫБОР
	|				КОГДА ЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Выполнена)
	|					ТОГДА ВЫБОР
	|							КОГДА ЗадачиПроцесса.ФактическаяДатаОкончания < &НачалоТекущегоПериода
	|								ТОГДА ЛОЖЬ
	|							ИНАЧЕ ИСТИНА
	|						КОНЕЦ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиЗапланированоПоСотрудникам КАК ЗадачиЗапланированоПоСотрудникам
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиЗапланированоПоСотрудникам.Задача
	|			И (ЗадачиЗапланированоПоСотрудникам.ВидРесурса = &ВидРесурса)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстатокЗапланированоНачалоТекущегоПериода.Этап        КАК Этап,
	|	ОстатокЗапланированоНачалоТекущегоПериода.Исполнитель КАК Исполнитель,
	|	СУММА(ВЫБОР
	|			КОГДА ОстатокЗапланированоНачалоТекущегоПериода.Запланировано * 3600 > ЕстьNULL(ФактЗадачиНачалоТекущегоПериода.ФактСекундыДоТекущегоПериода, 0)
	|				ТОГДА ОстатокЗапланированоНачалоТекущегоПериода.Запланировано * 3600 - ЕстьNULL(ФактЗадачиНачалоТекущегоПериода.ФактСекундыДоТекущегоПериода, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Остаток
	|
	|ПОМЕСТИТЬ ОстатокФактНачалоТекущегоПериода
	|ИЗ
	|	ОстатокЗапланированоНачалоТекущегоПериода КАК ОстатокЗапланированоНачалоТекущегоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактЗадачиНачалоТекущегоПериода КАК ФактЗадачиНачалоТекущегоПериода
	|		ПО ОстатокЗапланированоНачалоТекущегоПериода.Этап = ФактЗадачиНачалоТекущегоПериода.Этап
	|			И ОстатокЗапланированоНачалоТекущегоПериода.Исполнитель = ФактЗадачиНачалоТекущегоПериода.Исполнитель
	|			И ОстатокЗапланированоНачалоТекущегоПериода.ЗадачаПроцесса = ФактЗадачиНачалоТекущегоПериода.ЗадачаПроцесса 
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстатокЗапланированоНачалоТекущегоПериода.Исполнитель,
	|	ОстатокЗапланированоНачалоТекущегоПериода.Этап
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Исполнитель,
	|	Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель КАК Этап,
	|	ЗадачиПроцесса.Исполнитель КАК Исполнитель,
	|	СУММА(ЕСТЬNULL(УчетВремени.Длительность, 0)) КАК ФактСекундыДоТекущегоПериода
	|ПОМЕСТИТЬ ФактЗадачиПредыдущиеПериоды
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиПроцесса.Ссылка
	|			И (ЗадачиПроцесса.Исполнитель В (&ВыбранныеСотрудники))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = УчетВремени.Задача
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	ЗадачиПроцесса.Исполнитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеВыводимыхРабот.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ДанныеВыводимыхРабот.Сотрудник КАК Сотрудник,
	|	ДанныеВыводимыхРабот.Работа КАК Работа,
	|	ДанныеВыводимыхРабот.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ЕСТЬNULL(ЗатраченноеВремяЗадачи.Длительность, 0) КАК ФактическиеТрудозатратыСекунды
	|ПОМЕСТИТЬ ВыводимыеРаботыЗатраченноеВремяЭтапы
	|ИЗ
	|	ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗатраченноеВремяЗадачи КАК ЗатраченноеВремяЗадачи
	|		ПО ДанныеВыводимыхРабот.Сотрудник = ЗатраченноеВремяЗадачи.Сотрудник
	|			И ДанныеВыводимыхРабот.Работа = ЗатраченноеВремяЗадачи.Работа
	|			И ДанныеВыводимыхРабот.ИдентификаторПлана = ЗатраченноеВремяЗадачи.ИдентификаторПлана
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ЗатраченноеВремяЗадачи.Сотрудник,
	|	ЗатраченноеВремяЗадачи.Работа,
	|	ЗатраченноеВремяЗадачи.ИдентификаторПлана,
	|	ЗатраченноеВремяЗадачи.Длительность
	|ИЗ
	|	ЗатраченноеВремяЗадачи КАК ЗатраченноеВремяЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот
	|		ПО ЗатраченноеВремяЗадачи.Сотрудник = ДанныеВыводимыхРабот.Сотрудник
	|			И ЗатраченноеВремяЗадачи.Работа = ДанныеВыводимыхРабот.Работа
	|			И ЗатраченноеВремяЗадачи.ИдентификаторПлана = ДанныеВыводимыхРабот.ИдентификаторПлана
	|ГДЕ
	|	ДанныеВыводимыхРабот.ИдентификаторПлана ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыЗатраченноеВремяЭтапы.ПланируемыеТрудозатратыПланЗанятости                                                    КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыЗатраченноеВремяЭтапы.Сотрудник                                                                               КАК Сотрудник,
	|	ВыводимыеРаботыЗатраченноеВремяЭтапы.Работа                                                                                  КАК Работа,
	|	ВыводимыеРаботыЗатраченноеВремяЭтапы.ИдентификаторПлана                                                                      КАК ИдентификаторПлана,
	|	ЕСТЬNULL(ЗатраченноеВремяВидыДеятельности.Длительность, ВыводимыеРаботыЗатраченноеВремяЭтапы.ФактическиеТрудозатратыСекунды) КАК ФактическиеТрудозатратыСекунды
	|ПОМЕСТИТЬ ВыводимыеРаботыЗатраченноеВремяВсеРаботы
	|ИЗ
	|	ВыводимыеРаботыЗатраченноеВремяЭтапы КАК ВыводимыеРаботыЗатраченноеВремяЭтапы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗатраченноеВремяВидыДеятельности КАК ЗатраченноеВремяВидыДеятельности
	|		ПО ВыводимыеРаботыЗатраченноеВремяЭтапы.Сотрудник = ЗатраченноеВремяВидыДеятельности.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяЭтапы.Работа = ЗатраченноеВремяВидыДеятельности.Работа
	|			И ВыводимыеРаботыЗатраченноеВремяЭтапы.ИдентификаторПлана = ЗатраченноеВремяВидыДеятельности.ИдентификаторПлана
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ПланируемыеТрудозатратыПланЗанятости КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Сотрудник КАК Сотрудник,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Работа КАК Работа,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ФактическиеТрудозатратыСекунды КАК ФактическиеТрудозатратыСекунды,
	|	ЕСТЬNULL(ДанныеРабочихПлановЭтапы.ПланируемыеТрудозатраты, 0) КАК ПланируемыеТрудозатратыРабочиеПланы
	|ПОМЕСТИТЬ ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы
	|ИЗ
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы КАК ВыводимыеРаботыЗатраченноеВремяВсеРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРабочихПлановЭтапы КАК ДанныеРабочихПлановЭтапы
	|		ПО ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Сотрудник = ДанныеРабочихПлановЭтапы.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Работа = ДанныеРабочихПлановЭтапы.Работа
	|			И ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана = ДанныеРабочихПлановЭтапы.ИдентификаторПлана
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ДанныеРабочихПлановЭтапы.Сотрудник,
	|	ДанныеРабочихПлановЭтапы.Работа,
	|	ДанныеРабочихПлановЭтапы.ИдентификаторПлана,
	|	0,
	|	ДанныеРабочихПлановЭтапы.ПланируемыеТрудозатраты
	|ИЗ
	|	ДанныеРабочихПлановЭтапы КАК ДанныеРабочихПлановЭтапы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыводимыеРаботыЗатраченноеВремяВсеРаботы КАК ВыводимыеРаботыЗатраченноеВремяВсеРаботы
	|		ПО ДанныеРабочихПлановЭтапы.Сотрудник = ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Сотрудник
	|			И ДанныеРабочихПлановЭтапы.Работа = ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Работа
	|			И ДанныеРабочихПлановЭтапы.ИдентификаторПлана = ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана
	|ГДЕ
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.ПланируемыеТрудозатратыПланЗанятости    КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.Сотрудник                               КАК Сотрудник,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.Работа                                  КАК Работа,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.ИдентификаторПлана                      КАК ИдентификаторПлана,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.ФактическиеТрудозатратыСекунды          КАК ФактическиеТрудозатратыСекунды,
	|	ЕСТЬNULL(ДанныеРабочихПлановВидыДеятельности.ПланируемыеТрудозатраты, ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.ПланируемыеТрудозатратыРабочиеПланы) КАК ПланируемыеТрудозатратыРабочиеПланы
	|ПОМЕСТИТЬ ВыводимыеРаботыЗатраченноеВремяРабочиеПланы
	|ИЗ
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы КАК ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРабочихПлановВидыДеятельности КАК ДанныеРабочихПлановВидыДеятельности
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.Сотрудник = ДанныеРабочихПлановВидыДеятельности.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.Работа = ДанныеРабочихПлановВидыДеятельности.Работа
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланыЭтапы.ИдентификаторПлана = ДанныеРабочихПлановВидыДеятельности.ИдентификаторПлана
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник                                    КАК Сотрудник,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа                                       КАК Работа,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ИдентификаторПлана                           КАК ИдентификаторПлана,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ПланируемыеТрудозатратыПланЗанятости         КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ФактическиеТрудозатратыСекунды               КАК ФактическиеТрудозатратыСекунды,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ПланируемыеТрудозатратыРабочиеПланы          КАК ПланируемыеТрудозатратыРабочиеПланы,
	|	ЕСТЬNULL(ЗаписиПлановЗанятостиНеотображаемыхПеридов.ПланируемыеТрудозатраты, 0)          КАК ПланируемыеТрудозатратыНеотображаемыхПериодов,
	|	ЕСТЬNULL(ОстатокФактНачалоТекущегоПериода.Остаток, 0)                                    КАК ЗапланированоНаНачалоТекущего,
	|	ЕСТЬNULL(ФактЭтапыНачалоТекущегоПериода.ФактСекундыНачалоОтчетаНачалоТекущегоПериода, 0) - (ЕСТЬNULL(ЗапланированоПланыЗанятостиПрошлыеПериоды.ПланируемыеТрудозатраты, 0) * 3600) КАК ФактМинусПланЗанятостиСекунды,
	|	ЕСТЬNULL(ЗапланированоПланыЗанятостиПрошлыеПериоды.ПланируемыеТрудозатраты, 0)           КАК ПланируемыеТрудозатратыНачалоТекущегоПериода,
	|	ЕСТЬNULL(ФактЭтапыНачалоТекущегоПериода.ФактСекундыНачалоОтчетаНачалоТекущегоПериода, 0) КАК ФактТрудозатратыНачалоТекущегоПериода
	|ПОМЕСТИТЬ ВыводимыеРаботыПослеОпределенияТрудозатрат
	|ИЗ
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы КАК ВыводимыеРаботыЗатраченноеВремяРабочиеПланы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаписиПлановЗанятостиНеотображаемыхПеридов КАК ЗаписиПлановЗанятостиНеотображаемыхПеридов
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ЗаписиПлановЗанятостиНеотображаемыхПеридов.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ЗаписиПлановЗанятостиНеотображаемыхПеридов.Работа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстатокФактНачалоТекущегоПериода КАК ОстатокФактНачалоТекущегоПериода
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ОстатокФактНачалоТекущегоПериода.Исполнитель
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ОстатокФактНачалоТекущегоПериода.Этап
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапланированоПланыЗанятостиПрошлыеПериоды КАК ЗапланированоПланыЗанятостиПрошлыеПериоды
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ЗапланированоПланыЗанятостиПрошлыеПериоды.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ЗапланированоПланыЗанятостиПрошлыеПериоды.Работа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактЭтапыНачалоТекущегоПериода КАК ФактЭтапыНачалоТекущегоПериода
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ФактЭтапыНачалоТекущегоПериода.Исполнитель
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ФактЭтапыНачалоТекущегоПериода.Этап
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.Сотрудник                                                                                           КАК Сотрудник,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа                                                                                              КАК Работа,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ИдентификаторПлана                                                                                  КАК ИдентификаторПлана,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ПланируемыеТрудозатратыПланЗанятости                                                                КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ФактическиеТрудозатратыСекунды                                                                      КАК ФактическиеТрудозатратыСекунды,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ПланируемыеТрудозатратыРабочиеПланы                                                                 КАК ПланируемыеТрудозатратыРабочиеПланы,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ПланируемыеТрудозатратыНеотображаемыхПериодов                                                       КАК ПланируемыеТрудозатратыНеотображаемыхПериодов,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ЗапланированоНаНачалоТекущего                                                                       КАК ЗапланированоНаНачалоТекущего,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ФактМинусПланЗанятостиСекунды                                                                       КАК ФактМинусПланЗанятостиСекунды,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ПланируемыеТрудозатратыНачалоТекущегоПериода                                                        КАК ПланируемыеТрудозатратыНачалоТекущегоПериода,
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат.ФактТрудозатратыНачалоТекущегоПериода                                                               КАК ФактТрудозатратыНачалоТекущегоПериода,
	|	Пользователи.Подразделение                                                                                                                     КАК Подразделение,
	|	ЕСТЬNULL(ЗадачиПроцесса.Статус, ЗНАЧЕНИЕ(Справочник.ЗадачиПроцесса.ПустаяСсылка))                                                              КАК Статус,
	|	ЕСТЬNULL(ТехническиеПроекты.Версия, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Версия, ЗНАЧЕНИЕ(Справочник.ВерсииПроекта.ПустаяСсылка)))      КАК Версия,
	|	ЕСТЬNULL(ТехническиеПроекты.Владелец, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Владелец, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)))        КАК Проект,
	|	ЕСТЬNULL(ТехническиеПроекты.Ссылка, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Ссылка, ЗНАЧЕНИЕ(Справочник.ТехническиеПроекты.ПустаяСсылка))) КАК ТехПроект,
	|	ЕСТЬNULL(ЗадачиПроцесса.ПлановаяДатаНачала, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.ПлановаяДатаНачала, &ПустаяДата))                      КАК ПлановаяДатаНачала,
	|	ЕСТЬNULL(ЗадачиПроцесса.КрайняяДатаОкончания, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.ПлановаяДатаОкончания, &ПустаяДата))                 КАК КрайняяДатаОкончания,
	|	ВЫБОР
	|		КОГДА ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа ССЫЛКА Справочник.ЗадачиПроцесса
	|			ИЛИ ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа ССЫЛКА Справочник.ТехническиеПроекты
	|			ТОГДА 0 
	|		КОГДА ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа ССЫЛКА Перечисление.ТипыГруппировокСтрокПлана
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ЗначениеУпорядочиванияПоТипуРаботы,
	|	ЕСТЬNULL(СостоянияЗадачПроцессов.ЗначениеУпорядочивания, 0) КАК ЗначениеУпорядочиванияЭтапа
	|ИЗ
	|	ВыводимыеРаботыПослеОпределенияТрудозатрат КАК ВыводимыеРаботыПослеОпределенияТрудозатрат
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО ВыводимыеРаботыПослеОпределенияТрудозатрат.Сотрудник = Пользователи.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа = ЗадачиПроцесса.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
	|		ПО (ЗадачиПроцесса.Предмет = ТехническиеПроекты.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроектыЭтапыНеПоШаблону
	|		ПО (ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа = ТехническиеПроектыЭтапыНеПоШаблону.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗадачПроцессов КАК СостоянияЗадачПроцессов
	|		ПО ВыводимыеРаботыПослеОпределенияТрудозатрат.Работа = СостоянияЗадачПроцессов.ЗадачаПроцесса
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеУпорядочиванияПоТипуРаботы,
	|	Подразделение,
	|	Сотрудник,
	|	ТехПроект,
	|	ЗначениеУпорядочиванияЭтапа
	|ИТОГИ ПО
	|	Подразделение,
	|	Сотрудник,
	|	ЗначениеУпорядочиванияПоТипуРаботы,
	|	ТехПроект,
	|	Работа";
	
КонецФункции

Функция ПодготовленныеДанныеГруппировкаОтСотрудников(РезультатЗапроса, ПараметрыФормирования, ТаблицаПланов)
	
	ДанныеОтчета = НовыйПодготовленныеДанныеОтчета(ТаблицаПланов);
	
	ДеревоДанных                     = ДанныеОтчета.ДеревоДанных;
	ИдентификаторыИменаКолонок       = ДанныеОтчета.ИдентификаторыИменаКолонок;
	ДанныеОтчета.ПредставлениеОтбора = ПредставлениеОтбора(ПараметрыФормирования);
	
	ВыборкаПодразделение = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТребуетсяАнализОтклоненийРабочиеПланы = ПараметрыФормирования.ТолькоСОтклонениемРабочиеПланы;
	ТребуетсяАнализОтклоненийФакт         = ПараметрыФормирования.ТолькоСОтклонениемФакт;
	ТолькоСОтклонениемМеньшеНорматива     = ПараметрыФормирования.ТолькоСОтклонениемМеньшеНорматива;
	ТолькоСОтклонениемПревышениеНорматива = ПараметрыФормирования.ТолькоСОтклонениемПревышениеНорматива;
	
	ТребуетсяАнализОтклонений =  ТребуетсяАнализОтклоненийРабочиеПланы
	                             Или ТребуетсяАнализОтклоненийФакт
	                             Или ТолькоСОтклонениемМеньшеНорматива
	                             Или ТолькоСОтклонениемПревышениеНорматива; 
	
	Пока ВыборкаПодразделение.Следующий() Цикл
		
		СтрокаПодразделение = ДеревоДанных.Строки.Добавить();
		СтрокаПодразделение.СотрудникРабота = ВыборкаПодразделение.Подразделение;
		
		ВыборкаСотрудник = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСотрудник.Следующий() Цикл
			
			ЕстьОтклонениеРабочиеПланы = Ложь;
			ЕстьОтклонениеФакт         = Ложь;
			
			СтрокаСотрудник = СтрокаПодразделение.Строки.Добавить();
			СтрокаСотрудник.СотрудникРабота = ВыборкаСотрудник.Сотрудник;
			
			ВыборкаЗначениеУпорядочивания  = ВыборкаСотрудник.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
			Пока ВыборкаЗначениеУпорядочивания.Следующий() Цикл 
				
				ВыборкаТехПроект = ВыборкаЗначениеУпорядочивания.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаТехПроект.Следующий() Цикл
					
					ВыборкаРабота = ВыборкаТехПроект.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаРабота.Следующий() Цикл
					
						СтрокаРабота =  СтрокаСотрудник.Строки.Добавить();
						СтрокаРабота.СотрудникРабота = ВыборкаРабота.Работа;
						
						ВыборкаДетали = ВыборкаРабота.Выбрать();
						
						ДополнительныеДанныеУстановлены = Ложь;
						
						Пока ВыборкаДетали.Следующий() Цикл
							
							Если Не ДополнительныеДанныеУстановлены Тогда
								
								СтрокаРабота.ТехническийПроект                             = ВыборкаДетали.ТехПроект;
								СтрокаРабота.Версия                                        = ВыборкаДетали.Версия;
								СтрокаРабота.Статус                                        = ВыборкаДетали.Статус;
								СтрокаРабота.ДатаНачала                                    = ВыборкаДетали.ПлановаяДатаНачала;
								СтрокаРабота.ДатаОкончания                                 = ВыборкаДетали.КрайняяДатаОкончания;
								СтрокаРабота.Проект                                        = ВыборкаДетали.Проект;
								СтрокаРабота.ЗапланированоНаНачалоТекущего                 = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ЗапланированоНаНачалоТекущего);
								СтрокаРабота.ПланЗанятостиНеотображаемыйПериод             = ВыборкаДетали.ПланируемыеТрудозатратыНеотображаемыхПериодов;
								СтрокаРабота.ФактМинусПланЗанятости                        = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ФактМинусПланЗанятостиСекунды);
								СтрокаРабота.ПланируемыеТрудозатратыНачалоТекущегоПериода  = ВыборкаДетали.ПланируемыеТрудозатратыНачалоТекущегоПериода;
								СтрокаРабота.ФактТрудозатратыНачалоТекущегоПериода         = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ФактТрудозатратыНачалоТекущегоПериода);
								
								Если ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("СправочникСсылка.ЗадачиПроцесса")
									Или ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("СправочникСсылка.ТехническиеПроекты")
									Или ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("ПеречислениеСсылка.ТипыГруппировокСтрокПлана") Тогда
									СтрокаРабота.Нераспределено = СтрокаРабота.ЗапланированоНаНачалоТекущего - СтрокаРабота.ПланЗанятостиНеотображаемыйПериод;
								КонецЕсли;
								
								ДополнительныеДанныеУстановлены = Истина;
								
							КонецЕсли;
								
							Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
								
								ИменаКолонок = ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
								
								Если ИменаКолонок = Неопределено Тогда 
									Продолжить;
								КонецЕсли;
								
								Если СтрокаТаблицыПланов.НачалоПериода > ВыборкаДетали.КрайняяДатаОкончания
									Или СтрокаТаблицыПланов.КонецПериода < ВыборкаДетали.ПлановаяДатаНачала Тогда
									
									СтрокаРабота[ИменаКолонок.ПериодКорректен] = Ложь;
									
								Иначе
									
									СтрокаРабота[ИменаКолонок.ПериодКорректен] = Истина;
									
								КонецЕсли;
								
								Если (ВыборкаДетали.ПлановаяДатаНачала >= СтрокаТаблицыПланов.НачалоПериода
									И ВыборкаДетали.ПлановаяДатаНачала <= КонецДня(СтрокаТаблицыПланов.КонецПериода))
									Или (ВыборкаДетали.КрайняяДатаОкончания >= СтрокаТаблицыПланов.НачалоПериода
									И ВыборкаДетали.КрайняяДатаОкончания <= КонецДня(СтрокаТаблицыПланов.КонецПериода)) Тогда
									
									ИменаКолонок = ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
									
									Если ИменаКолонок <> Неопределено Тогда
										СтрокаРабота[ИменаКолонок.ПериодКорректен] = Истина;
									КонецЕсли;
									
								КонецЕсли;
								
							КонецЦикла;
							
							Если ВыборкаДетали.ИдентификаторПлана <> 0 Тогда
								
								ИменаКолонок = ИдентификаторыИменаКолонок.Получить(ВыборкаДетали.ИдентификаторПлана);
								
								Если ИменаКолонок = Неопределено Тогда
									Продолжить;
								КонецЕсли;
								
								СтрокаРабота[ИменаКолонок.ПланЗанятости] = ВыборкаДетали.ПланируемыеТрудозатратыПланЗанятости;
								Если ИменаКолонок.ТипПериода <> СловарьПрошлыйПериодПлана()
									И ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("СправочникСсылка.ЗадачиПроцесса")
									Или ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("СправочникСсылка.ТехническиеПроекты") Тогда
										СтрокаРабота.Нераспределено = СтрокаРабота.Нераспределено - СтрокаРабота[ИменаКолонок.ПланЗанятости];
								КонецЕсли;
								
								Если ИменаКолонок.ТипПериода = СловарьТекущийПериодПлана()
									Или ИменаКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
									
									СтрокаРабота[ИменаКолонок.РабочиеПланы] = ВыборкаДетали.ПланируемыеТрудозатратыРабочиеПланы;
									СтрокаРабота[ИменаКолонок.Факт]         = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ФактическиеТрудозатратыСекунды);
									
									Если Не ОтклонениеДопустимо(СтрокаРабота[ИменаКолонок.ПланЗанятости],
										                        СтрокаРабота[ИменаКолонок.РабочиеПланы],
										                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
										                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
										                        Истина) Тогда
										
										ЕстьОтклонениеРабочиеПланы                          = Истина;
										СтрокаРабота.ВСтрокеЕстьДанныеСоответствующиеОтбору = Истина;
										СтрокаРабота[ИменаКолонок.ЕстьОтклоненияПоОтбору]   = Истина;
										
									КонецЕсли;
									
									Если ИменаКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
										
										СтрокаРабота.Факт           = СтрокаРабота.Факт + СтрокаРабота[ИменаКолонок.Факт]; 
										
										Если Не ОтклонениеДопустимо(СтрокаРабота[ИменаКолонок.ПланЗанятости],
											                        СтрокаРабота[ИменаКолонок.Факт],
											                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
											                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
											                        Истина) Тогда
											
											ЕстьОтклонениеФакт                                  = Истина;
											СтрокаРабота.ВСтрокеЕстьДанныеСоответствующиеОтбору = Истина;
											СтрокаРабота[ИменаКолонок.ЕстьОтклоненияПоОтбору]   = Истина;
											
										КонецЕсли;
										
									КонецЕсли;
									
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЦикла;
						
						Если СтрокаРабота.Нераспределено > 0 Тогда
							СтрокаРабота.Нераспределено = СтрокаРабота.Нераспределено;
						Иначе 
							СтрокаРабота.Резерв         = - СтрокаРабота.Нераспределено;
							СтрокаРабота.Нераспределено = 0;
						КонецЕсли;
						
					КонецЦикла
					
				КонецЦикла;
				
			КонецЦикла;
			
			НуженПересчет = Истина;
			
			Если ТребуетсяАнализОтклонений Тогда
				
				ЕстьОтклонения = Ложь;
				
				Если ТребуетсяАнализОтклоненийРабочиеПланы
					И ЕстьОтклонениеРабочиеПланы Тогда
					
					ЕстьОтклонения = Истина;
					
				ИначеЕсли ТребуетсяАнализОтклоненийФакт
					И ЕстьОтклонениеФакт Тогда
					
					ЕстьОтклонения = Истина;
					
				КонецЕсли;
				
				Если ТолькоСОтклонениемМеньшеНорматива
					Или ТолькоСОтклонениемПревышениеНорматива Тогда
					
					ПересчитатьМассивКолонокРодительскойСтрокиПоДаннымПодчиненных(СтрокаСотрудник, ДанныеОтчета.ИменаКолонокПерерасчетРодители, Ложь);
					НуженПересчет = Ложь;
					
					Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
						
						ИменаКолонок = ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
						
						Если ИменаКолонок = Неопределено Тогда 
							Продолжить;
						КонецЕсли;
						
						Если ТолькоСОтклонениемПревышениеНорматива
						    И Не ОтклонениеДопустимо(СтрокаСотрудник[ИменаКолонок.ПланЗанятости],
						                             ПараметрыФормирования.НормативПланируемогоВремени,
						                             ПараметрыФормирования.ДопустимыйПроцентОтклонения,
						                             ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
						                             Истина) Тогда
							
							ЕстьОтклонения = Истина;
							СтрокаРабота[ИменаКолонок.ЕстьОтклоненияПоОтбору] = Истина;
							
							Прервать;
							
						КонецЕсли;
						
						Если ТолькоСОтклонениемМеньшеНорматива
						    И Не ОтклонениеДопустимо(ПараметрыФормирования.НормативПланируемогоВремени,
						                             СтрокаСотрудник[ИменаКолонок.ПланЗанятости],
						                             ПараметрыФормирования.ДопустимыйПроцентОтклонения,
						                             ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
						                             Истина) Тогда
							
							ЕстьОтклонения = Истина;
							СтрокаРабота[ИменаКолонок.ЕстьОтклоненияПоОтбору] = Истина;
							Прервать;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				Если Не ЕстьОтклонения Тогда
					
					СтрокаПодразделение.Строки.Удалить(СтрокаСотрудник);
					НуженПересчет = Ложь;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если НуженПересчет Тогда
				ПересчитатьМассивКолонокРодительскойСтрокиПоДаннымПодчиненных(СтрокаСотрудник, ДанныеОтчета.ИменаКолонокПерерасчетРодители, Ложь);
			КонецЕсли;
				
		КонецЦикла;
		
		Если СтрокаПодразделение.Строки.Количество() = 0 Тогда
			ДеревоДанных.Строки.Удалить(СтрокаПодразделение);
		КонецЕсли;
		
	КонецЦикла;
	
	СкрытьПериодыНеСоответствующиеОтбору(ПараметрыФормирования, ДеревоДанных, ТаблицаПланов, ИдентификаторыИменаКолонок);
	
	Возврат ДанныеОтчета;
	
КонецФункции

Процедура ВывестиДанныеВТабличныйДокументОтСотрудников(РезультатФормированияОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов)
	
	Макет = Отчеты.КонтрольПланирования.ПолучитьМакет("ОтСотрудников");
	
	ТаблицаОтчета = РезультатФормированияОтчета.ТабличныйДокумент;
	
	УстановитьШапкуОтчета(Макет, ТаблицаОтчета, ПараметрыФормирования, ПодготовленныеДанные);
	УстановитьЛегенду(Макет, ТаблицаОтчета);
	УстановитьШапкуТаблицыОтчета(Макет, ТаблицаОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов);
	ЭтоПоследняяСтрока = Ложь;
	
	Для Каждого СтрокаПодразделение Из ПодготовленныеДанные.ДеревоДанных.Строки Цикл
		
		ВывестиСтрокуПодразделениеОтСотрудников(СтрокаПодразделение,
		                                        Макет, 
		                                        ТаблицаОтчета,
		                                        ТаблицаПланов, 
		                                        ПодготовленныеДанные.ИдентификаторыИменаКолонок,
		                                        ПараметрыФормирования);
		ТаблицаОтчета.НачатьГруппуСтрок(СтрокаПодразделение.СотрудникРабота);
		
		Для Каждого СтрокаСотрудник Из СтрокаПодразделение.Строки Цикл
			
			ВывестиСтрокаСотрудникОтСотрудников(СтрокаСотрудник,
			                                    Макет,
			                                    ТаблицаОтчета, 
			                                    ТаблицаПланов, 
			                                    ПодготовленныеДанные.ИдентификаторыИменаКолонок, 
			                                    ПараметрыФормирования);
			
			ТаблицаОтчета.НачатьГруппуСтрок(СтрокаСотрудник.СотрудникРабота);
			
			Для Каждого СтрокаРабота Из СтрокаСотрудник.Строки Цикл
				
				Если ПодготовленныеДанные.ДеревоДанных.Строки.Индекс(СтрокаПодразделение) = ПодготовленныеДанные.ДеревоДанных.Строки.Количество() - 1
					И СтрокаПодразделение.Строки.Индекс(СтрокаСотрудник) = СтрокаПодразделение.Строки.Количество() - 1
					И СтрокаСотрудник.Строки.Индекс(СтрокаРабота) = СтрокаСотрудник.Строки.Количество() - 1 Тогда
					
					ЭтоПоследняяСтрока = Истина;
					
				КонецЕсли;
				
				ВывестиСтрокаРаботаОтСотрудников(СтрокаРабота,
				                                 Макет,
				                                 ТаблицаОтчета,
				                                 ТаблицаПланов,
				                                 ПодготовленныеДанные.ИдентификаторыИменаКолонок,
				                                 ЭтоПоследняяСтрока, 
				                                 ПараметрыФормирования);
				
			КонецЦикла;
			
			ТаблицаОтчета.ЗакончитьГруппуСтрок();
			
		КонецЦикла;
		
		ТаблицаОтчета.ЗакончитьГруппуСтрок();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокаРаботаОтСотрудников(СтрокаРабота, Макет, ТаблицаОтчета, ТаблицаПланов, ИдентификаторыИменаКолонок, ЭтоПоследняяСтрока, ПараметрыФормирования)
	
	Если ПараметрыФормирования.СкрыватьСтрокиСЭтапамиНеСоответствующимиОтбору
		И Не СтрокаРабота.ВСтрокеЕстьДанныеСоответствующиеОтбору Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("ПеречислениеСсылка.ТипыГруппировокСтрокПлана") Тогда
		Работа = НСтр("ru = 'Задачи по ошибкам'");
		ТехническийПроект = СтрокаРабота.СотрудникРабота;
	ИначеЕсли ТипЗнч(СтрокаРабота.СотрудникРабота) = Тип("СправочникСсылка.ТехническиеПроекты") Тогда
		Работа = НСтр("ru = 'Задачи не по шаблону'");
		ТехническийПроект = СтрокаРабота.ТехническийПроект;
	Иначе
		Работа            = СтрокаРабота.СотрудникРабота;
		ТехническийПроект = СтрокаРабота.ТехническийПроект;
	КонецЕсли; 
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("РаботаОсновное");
	Область.Параметры.Работа    = Работа;
	Область.Параметры.ТехПроект = ТехническийПроект;
	
	Если ЭтоПоследняяСтрока Тогда
		Область.Области.РаботаОсновное.ГраницаСнизу = ЛинияТолщинаДва();
	КонецЕсли;
	
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("РаботаПроект");
		Область.Параметры.Проект = СтрокаРабота.Проект;
		
		Если ЭтоПоследняяСтрока Тогда
			Область.Области.РаботаПроект.ГраницаСнизу = ЛинияТолщинаДва();
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("РаботаВерсия");
		Область.Параметры.Версия = СтрокаРабота.Версия;
		
		Если ЭтоПоследняяСтрока Тогда
			Область.Области.РаботаВерсия.ГраницаСнизу = ЛинияТолщинаДва();
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("РаботаСроки");
		Область.Параметры.ДатаНачала                  = Формат(СтрокаРабота.ДатаНачала,    "ДФ=dd.MM.yy");
		Область.Параметры.ДатаОкончания               = Формат(СтрокаРабота.ДатаОкончания, "ДФ=dd.MM.yy");
		
		Если Не ПараметрыФормирования.НеВыделятьСроки
			И СтрокаРабота.Статус <> Перечисления.СтатусыЗадачПроцессов.Выполнена
			И НачалоДня(СтрокаРабота.ДатаОкончания) < НачалоДня(ТекущаяДатаСеанса()) Тогда
			
			Область.Области.ДатаОкончанияЭтапа.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
			
		КонецЕсли; 
		
		Если ЭтоПоследняяСтрока Тогда
			Область.Области.РаботаСроки.ГраницаСнизу = ЛинияТолщинаДва();
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли; 
	
	Область = Макет.ПолучитьОбласть("РаботаТрудозатраты");
	Область.Параметры.ОстатокНаДату               = СтрокаРабота.ЗапланированоНаНачалоТекущего;
	Область.Параметры.Факт                        = СтрокаРабота.Факт;
	Область.Параметры.НераспределеноПланЗанятости = СтрокаРабота.Нераспределено;
	Область.Параметры.РезервПланЗанятости         = СтрокаРабота.Резерв;
	Область.Параметры.ФактМинусПланЗанятости      = СтрокаРабота.ФактМинусПланЗанятости;
	
	Если СтрокаРабота.Нераспределено <> 0 
		И Не ПараметрыФормирования.НеВыделятьНераспределено Тогда
		
		Область.Области.РаботаНераспределено.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если СтрокаРабота.Резерв <> 0 
		И Не ПараметрыФормирования.НеВыделятьРезерв Тогда
		
		Область.Области.РаботаРезерв.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиЗадачи;
		
	КонецЕсли;
	
	Если СтрокаРабота.ФактМинусПланЗанятости <> 0 
		И Не ПараметрыФормирования.НеВыделятьФактМинусПЗ Тогда
		
		Область.Области.РаботаФактМинусПЗ.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если ЭтоПоследняяСтрока Тогда
		Область.Области.РаботаТрудозатраты.ГраницаСнизу = ЛинияТолщинаДва();
	КонецЕсли;
	
	ТаблицаОтчета.Присоединить(Область);
	
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПланЗанятости = СтрокаРабота[ДанныеКолонок.ПланЗанятости];
		
		Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда
				
				ИмяОбласти = "РаботаПЗ";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.ПланЗанятости =  ПланЗанятости;
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаРабота, ДанныеКолонок, ЭтоПоследняяСтрока);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				
				ИмяОбласти = "РаботаРабочиеПланы";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.РабочиеПланы   = СтрокаРабота[ДанныеКолонок.РабочиеПланы];
				Если Не ОтклонениеДопустимо(СтрокаРабота[ДанныеКолонок.ПланЗанятости],
				                            СтрокаРабота[ДанныеКолонок.РабочиеПланы],
				                            ПараметрыФормирования.ДопустимыйПроцентОтклонения,
				                            ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
				                            Истина) Тогда
				
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаРабота, ДанныеКолонок, ЭтоПоследняяСтрока);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				
				ИмяОбласти = "РаботаФакт";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.Факт = СтрокаРабота[ДанныеКолонок.Факт];
				Если ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() 
					И Не ОтклонениеДопустимо(СтрокаРабота[ДанныеКолонок.ПланЗанятости],
					                         СтрокаРабота[ДанныеКолонок.Факт],
					                         ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                         ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                         Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаРабота, ДанныеКолонок, ЭтоПоследняяСтрока);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
		Иначе
			
			ИмяОбласти = "СтрокаРаботаОднаКолонка";
			Область = Макет.ПолучитьОбласть(ИмяОбласти);
			Область.Параметры.ПланЗанятости = ПланЗанятости;
			
			УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаРабота, ДанныеКолонок, ЭтоПоследняяСтрока);
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования, СтрокаДерева, ДанныеКолонок, ЭтоПоследняяСтрока)
	
	Если ЭтоПоследняяСтрока Тогда
		Область.Области[ИмяОбласти].ГраницаСнизу = ЛинияТолщинаДва();
	КонецЕсли;
	
	Если СтрокаДерева[ДанныеКолонок.ПериодКорректен]
		И Не ПараметрыФормирования.НеВыделятьДопустимыеДаты Тогда
		
		Область.Области[ИмяОбласти].ЦветФона = ЦветаСтиля.ЦветФонаВходитВПериодПланирования; 
		
	КонецЕсли;

	
КонецПроцедуры

Функция ОтклонениеДопустимо(Показатель1, Показатель2, ДопустимыйПроцент, АбсолютноеОтклонение, ДопустимоПревышениеПоказателя1)
	
	Если Показатель1 = Показатель2 Тогда 
		
		Возврат Истина;
		
	ИначеЕсли Показатель2 > Показатель1 
		И ДопустимоПревышениеПоказателя1 Тогда
		
		Возврат Истина;
		
	ИначеЕсли (Показатель2 = 0 
		Или Показатель1 = 0) Тогда
		
		Если (Показатель1 - Показатель2) > АбсолютноеОтклонение Тогда
			
			Возврат Ложь;
			
		Иначе
			
			Возврат Истина;
			
		КонецЕсли;
		
	Иначе
		
		Отклонение = Показатель1 - Показатель2;
		Отклонение = ?(Отклонение > 0, Отклонение, - Отклонение);
		ОтклонениеДопустимо = (Отклонение/Показатель1 * 100) <= ДопустимыйПроцент;
		
		Если ОтклонениеДопустимо Тогда
			Возврат Истина;
		Иначе
			Возврат Отклонение < АбсолютноеОтклонение;
		КонецЕсли;
		
	КонецЕсли; 
	
КонецФункции

Процедура ВывестиСтрокаСотрудникОтСотрудников(СтрокаСотрудник, Макет, ТаблицаОтчета, ТаблицаПланов, ИдентификаторыИменаКолонок, ПараметрыФормирования)
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("СотрудникОсновное");
	Область.Параметры.Сотрудник = СтрокаСотрудник.СотрудникРабота;
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("СотрудникПроект");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("СотрудникВерсия");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("СотрудникСроки");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("СотрудникТрудозатраты");
	
	Область.Параметры.ОстатокНаДату               = СтрокаСотрудник.ЗапланированоНаНачалоТекущего;
	Область.Параметры.Факт                        = СтрокаСотрудник.Факт;
	Область.Параметры.НераспределеноПланЗанятости = СтрокаСотрудник.Нераспределено;
	Область.Параметры.РезервПланЗанятости         = СтрокаСотрудник.Резерв;
	Область.Параметры.ФактМинусПланЗанятости      = СтрокаСотрудник.ФактМинусПланЗанятости;
	
	Если СтрокаСотрудник.Нераспределено <> 0 
		И Не ПараметрыФормирования.НеВыделятьНераспределено Тогда
		
		Область.Области.СотрудникНераспределено.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если СтрокаСотрудник.Резерв <> 0 
		И Не ПараметрыФормирования.НеВыделятьРезерв Тогда
		
		Область.Области.СотрудникРезерв.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиЗадачи;
		
	КонецЕсли;
	
	Если СтрокаСотрудник.ФактМинусПланЗанятости < 0 
		И Не ПараметрыФормирования.НеВыделятьФактМинусПЗ Тогда
		
		Область.Области.СотрудникФактМинусПЗ.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	ТаблицаОтчета.Присоединить(Область);
	
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ПланЗанятости =  СтрокаСотрудник[ДанныеКолонок.ПланЗанятости];
		
		Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда 
				
				Область = Макет.ПолучитьОбласть("СотрудникПЗ");
				Область.Параметры.ПланЗанятости = ПланЗанятости;
				Если Не ОтклонениеДопустимо(СтрокаСотрудник[ДанныеКолонок.ПланЗанятости],
				                            ПараметрыФормирования.НормативПланируемогоВремени,
				                            ПараметрыФормирования.ДопустимыйПроцентОтклонения,
				                            ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
				                            Ложь) Тогда
				
					Область.Области.СотрудникПЗ.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					
				КонецЕсли;
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				
				Область = Макет.ПолучитьОбласть("СотрудникРабочиеПланы");
				Область.Параметры.РабочиеПланы   = СтрокаСотрудник[ДанныеКолонок.РабочиеПланы];
				Если Не ОтклонениеДопустимо(СтрокаСотрудник[ДанныеКолонок.ПланЗанятости],
				                            СтрокаСотрудник[ДанныеКолонок.РабочиеПланы],
				                            ПараметрыФормирования.ДопустимыйПроцентОтклонения,
				                            ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
				                            Истина) Тогда
					
					Область.Области.СотрудникРабочиеПланы.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области.СотрудникРабочиеПланы.Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("СотрудникФакт");
				Область.Параметры.Факт           = СтрокаСотрудник[ДанныеКолонок.Факт];
				Если ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() 
				И Не ОтклонениеДопустимо(СтрокаСотрудник[ДанныеКолонок.ПланЗанятости],
				                         СтрокаСотрудник[ДанныеКолонок.Факт],
				                         ПараметрыФормирования.ДопустимыйПроцентОтклонения,
				                         ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
				                         Истина) Тогда
					
					Область.Области.СотрудникФакт.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области.СотрудникФакт.Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
		Иначе
		
			Область = Макет.ПолучитьОбласть("СтрокаСотрудникОднаКолонка");
			Область.Параметры.ПланЗанятости = ПланЗанятости;
			Если Не ОтклонениеДопустимо(СтрокаСотрудник[ДанныеКолонок.ПланЗанятости],
			                        ПараметрыФормирования.НормативПланируемогоВремени,
			                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
			                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
			                        Ложь) Тогда
				
				Область.Области.СтрокаСотрудникОднаКолонка.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
				
			КонецЕсли;
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуПодразделениеОтСотрудников(СтрокаПодразделение, Макет, ТаблицаОтчета, ТаблицаПланов, ИдентификаторыИменаКолонок, ПараметрыФормирования)
	
	Подразделение = ?(ЗначениеЗаполнено(СтрокаПодразделение.СотрудникРабота), СтрокаПодразделение.СотрудникРабота, НСтр("ru = '<без подразделения>'"));
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ПодразделениеОсновное");
	Область.Параметры.Подразделение = Подразделение;
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("ПодразделениеПроект");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("ПодразделениеВерсия");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("ПодразделениеСроки");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли; 
	
	Область = Макет.ПолучитьОбласть("ПодразделениеТрудозатраты");
	ТаблицаОтчета.Присоединить(Область);
	
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);

	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если (ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана())
			И ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() > 1 Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("ПодразделениеПЗ");
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("ПодразделениеРабочиеПланы");
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				Область = Макет.ПолучитьОбласть("ПодразделениеФакт");
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
		Иначе
			
			Область = Макет.ПолучитьОбласть("СтрокаПодразделениеОднаКолонка");
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОтТехПроектов

Функция ТекстЗапросаГруппировкаОтТехПроектов()
	
	Возврат "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Планы.ИдентификаторПлана КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ИдентификаторыРабочихПланов
	|ИЗ
	|	РегистрСведений.Планы КАК Планы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыПланов КАК ВидыПланов
	|		ПО Планы.ВидПлана = ВидыПланов.Ссылка
	|ГДЕ
	|	ВидыПланов.ТипПлана = ЗНАЧЕНИЕ(Перечисление.ТипыПланов.Рабочий)
	|	И НЕ ВидыПланов.ПометкаУдаления
	|	И ВидыПланов.РодительскийПлан.Ссылка = &ПланЗанятости
	|	И ВидыПланов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВидовПланов.Действует)
	|	И Планы.НачалоПериода >= &НачалоПериода
	|	И Планы.КонецПериода <= &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПланов.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ТаблицаПланов.НачалоПериода      КАК ДатаНачала,
	|	ТаблицаПланов.КонецПериода       КАК ДатаОкончания,
	|	ТаблицаПланов.Зафиксирован       КАК Зафиксирован
	|ПОМЕСТИТЬ ТаблицаПланов
	|ИЗ
	|	&ТаблицаПланов КАК ТаблицаПланов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаНачала,
	|	ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаписиПлана.ИдентификаторПлана      КАК ИдентификаторПлана,
	|	ЗаписиПлана.Сотрудник               КАК Сотрудник,
	|	ЗаписиПлана.Работа                  КАК Работа,
	|	ЗаписиПлана.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ СуществующиеЗаписиПлана
	|ИЗ
	|	РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (ТаблицаПланов.ИдентификаторПлана = ЗаписиПлана.ИдентификаторПлана)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ЗаписиПлана.Работа = ЗадачиПроцесса.Ссылка
	|ГДЕ
	|	ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа)
	|	И ЗаписиПлана.ПланируемыеТрудозатраты <> 0
	|	И (ЗадачиПроцесса.Предмет В (&ВыводимыеТехническиеПроекты) Или ЗаписиПлана.Работа В (&ВыводимыеТехническиеПроекты))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиЗапланированоПоСотрудникам.Пользователь                    КАК Исполнитель,
	|	ВЫБОР 
	|		КОГДА ЗадачиПроцессаРодители.ЗадачаШаблона = ЗНАЧЕНИЕ(Справочник.ЗадачиШаблонаПроцесса.ПустаяСсылка)
	|			ТОГДА ЗадачиПроцессаРодители.Предмет
	|			ИНАЧЕ ИерархияЗадачПроцесса.Родитель
	|	КОНЕЦ                                                            КАК Родитель,
	|	СУММА(ЕСТЬNULL(ЗадачиЗапланированоПоСотрудникам.Количество, 0))  КАК Запланировано,
	|	СУММА(ЕСТЬNULL(УчетВремени.Длительность, 0))                     КАК УчетВремениСекунды
	|ПОМЕСТИТЬ ЭтапыСотрудникаЗапланированоУчетВремени
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса 
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиЗапланированоПоСотрудникам КАК ЗадачиЗапланированоПоСотрудникам
	|		ПО (ЗадачиЗапланированоПоСотрудникам.Задача = ЗадачиПроцесса.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ПО ЗадачиЗапланированоПоСотрудникам.Задача = ИерархияЗадачПроцесса.ЗадачаПроцесса 
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцессаРодители
	|		ПО ЗадачиПроцессаРодители.Ссылка = ИерархияЗадачПроцесса.Родитель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
	|		ПО ЗадачиПроцесса.Предмет = ТехническиеПроекты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО (УчетВремени.Задача = ЗадачиЗапланированоПоСотрудникам.Задача)
	|			И (УчетВремени.Пользователь = ЗадачиЗапланированоПоСотрудникам.Пользователь)
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|ГДЕ
	|	ЗадачиПроцесса.Предмет В(&ВыводимыеТехническиеПроекты)
	|	И ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|	И НЕ ЗадачиПроцесса.ПометкаУдаления
	|	И ИерархияЗадачПроцесса.Уровень = 0
	|	И НЕ ТехническиеПроекты.ПометкаУдаления
	|	И ТехническиеПроекты.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Запланирован), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Активен))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА ЗадачиПроцессаРодители.ЗадачаШаблона = ЗНАЧЕНИЕ(Справочник.ЗадачиШаблонаПроцесса.ПустаяСсылка)
	|			ТОГДА ЗадачиПроцессаРодители.Предмет
	|			ИНАЧЕ ИерархияЗадачПроцесса.Родитель
	|	КОНЕЦ,
	|	ЗадачиЗапланированоПоСотрудникам.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Исполнитель         КАК Исполнитель,
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Родитель            КАК Родитель,
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Запланировано       КАК Запланировано,
	|	ЭтапыСотрудникаЗапланированоУчетВремени.УчетВремениСекунды  КАК УчетВремениСекунды
	|ПОМЕСТИТЬ ЭтапыСотрудникаДляОтображенияВПланахПоПланированиюУчетуВремени
	|ИЗ
	|	ЭтапыСотрудникаЗапланированоУчетВремени КАК ЭтапыСотрудникаЗапланированоУчетВремени
	|ГДЕ
	|	ЭтапыСотрудникаЗапланированоУчетВремени.Запланировано > &ПороговоеЗначениеДляЭтапаПоЗапланировано
	|	ИЛИ ЭтапыСотрудникаЗапланированоУчетВремени.УчетВремениСекунды > &ПороговоеЗначениеДляЭтапаПоФакт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиПроцесса.Исполнитель   КАК Исполнитель,
	|	ЗадачиПроцесса.Ссылка        КАК Ссылка
	|ПОМЕСТИТЬ ЭтапыСотрудникаПоИсполнителю
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|ГДЕ
	|	ЗадачиПроцесса.Предмет В (&ВыводимыеТехническиеПроекты)
	|	И ЗадачиПроцесса.Родитель = ЗНАЧЕНИЕ(Справочник.ЗадачиПроцесса.ПустаяСсылка)
	|	И ЗадачиПроцесса.ЗадачаШаблона <> ЗНАЧЕНИЕ(Справочник.ЗадачиШаблонаПроцесса.ПустаяСсылка)
	|	И ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|	И НЕ ЗадачиПроцесса.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Исполнитель,
	|	Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 
	|
	|ВЫБРАТЬ
	|	ЭтапыСотрудникаДляОтображенияВПланахПоПланированиюУчетуВремени.Исполнитель КАК Исполнитель,
	|	ЭтапыСотрудникаДляОтображенияВПланахПоПланированиюУчетуВремени.Родитель    КАК Родитель
	|ПОМЕСТИТЬ ЭтапыСотрудникаДляОтображенияВПланах
	|ИЗ 
	|	ЭтапыСотрудникаДляОтображенияВПланахПоПланированиюУчетуВремени
	|
	|ОБЪЕДИНИТЬ 
	|
	|ВЫБРАТЬ
	|	ЭтапыСотрудникаПоИсполнителю.Исполнитель,
	|	ЭтапыСотрудникаПоИсполнителю.Ссылка
	|ИЗ ЭтапыСотрудникаПоИсполнителю
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭтапыСотрудникаДляОтображенияВПланах.Исполнитель КАК Сотрудник,
	|	ЭтапыСотрудникаДляОтображенияВПланах.Родитель    КАК Работа,
	|	МИНИМУМ(Планы.НачалоПериода)                     КАК НачалоПериода,
	|	МАКСИМУМ(Планы.КонецПериода)                     КАК КонецПериода
	|ПОМЕСТИТЬ ДатыПланируемыхРабот
	|ИЗ
	|	ЭтапыСотрудникаДляОтображенияВПланах КАК ЭтапыСотрудникаДляОтображенияВПланах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ПО ЭтапыСотрудникаДляОтображенияВПланах.Исполнитель = ЗаписиПлана.Сотрудник
	|			И ЭтапыСотрудникаДляОтображенияВПланах.Родитель = ЗаписиПлана.Работа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Планы КАК Планы
	|			ПО (Планы.ИдентификаторПлана = ЗаписиПлана.ИдентификаторПлана)
	|ГДЕ
	|	Планы.ВидПлана = &ВидПлана
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыСотрудникаДляОтображенияВПланах.Исполнитель,
	|	ЭтапыСотрудникаДляОтображенияВПланах.Родитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СуществующиеЗаписиПлана.Работа КАК Работа
	|ПОМЕСТИТЬ ОтображаемыеРаботы
	|ИЗ
	|	СуществующиеЗаписиПлана КАК СуществующиеЗаписиПлана
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЭтапыСотрудникаДляОтображенияВПланах.Родитель
	|ИЗ
	|	ЭтапыСотрудникаДляОтображенияВПланах КАК ЭтапыСотрудникаДляОтображенияВПланах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты,
	|	ВложенныйЗапрос.Сотрудник               КАК Сотрудник,
	|	ВложенныйЗапрос.Работа                  КАК Работа,
	|	ВложенныйЗапрос.ИдентификаторПлана      КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ДанныеВыводимыхРабот
	|ИЗ
	|	(ВЫБРАТЬ
	|		СуществующиеЗаписиПлана.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатраты,
	|		СуществующиеЗаписиПлана.Сотрудник КАК Сотрудник,
	|		СуществующиеЗаписиПлана.Работа КАК Работа,
	|		СуществующиеЗаписиПлана.ИдентификаторПлана КАК ИдентификаторПлана
	|	ИЗ
	|		СуществующиеЗаписиПлана КАК СуществующиеЗаписиПлана
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ЭтапыСотрудникаДляОтображенияВПланах.Исполнитель,
	|		ЭтапыСотрудникаДляОтображенияВПланах.Родитель,
	|		0
	|	ИЗ
	|		ЭтапыСотрудникаДляОтображенияВПланах КАК ЭтапыСотрудникаДляОтображенияВПланах) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	ДанныеВыводимыхРабот.Работа КАК Работа,
	|	ДанныеВыводимыхРабот.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ОтображаемыеРаботыСотрудники
	|ИЗ
	|	ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Работа,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтображаемыеРаботыСотрудники.Работа КАК Работа,
	|	ОтображаемыеРаботыСотрудники.Сотрудник КАК Сотрудник,
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ЗапланированоПланыЗанятостиПрошлыеПериоды
	|ИЗ
	|	ОтображаемыеРаботыСотрудники КАК ОтображаемыеРаботыСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ПО ОтображаемыеРаботыСотрудники.Работа = ЗаписиПлана.Работа
	|			И ОтображаемыеРаботыСотрудники.Сотрудник = ЗаписиПлана.Сотрудник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Планы КАК Планы
	|		ПО (ЗаписиПлана.ИдентификаторПлана = Планы.ИдентификаторПлана)
	|			И (Планы.ВидПлана = &ПланЗанятости)
	|			И (Планы.КонецПериода < &НачалоТекущегоПериода) И (Планы.НачалоПериода >= &НачалоПериода)
	|			И (ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа))
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтображаемыеРаботыСотрудники.Работа,
	|	ОтображаемыеРаботыСотрудники.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиПроцесса.Ссылка  КАК ЭтапНеПоШаблону,
	|	ЗадачиПроцесса.Предмет КАК ТехПроект
	|ПОМЕСТИТЬ ЭтапыНеПоШаблону
	|ИЗ
	|	Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса 
	|ГДЕ
	|	ЗадачиПроцесса.ЗадачаШаблона = ЗНАЧЕНИЕ(Справочник.ЗадачиШаблонаПроцесса.ПустаяСсылка)
	|	И ЗадачиПроцесса.Родитель = ЗНАЧЕНИЕ(Справочник.ЗадачиПроцесса.ПустаяСсылка)
	|	И ЗадачиПроцесса.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Запланирована), ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.ПринятаКВыполнению))
	|	И ЗадачиПроцесса.Предмет В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ДанныеВыводимыхРабот.Работа КАК Работа
	|		ИЗ
	|			ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИерархияЗадачПроцесса.ЗадачаПроцесса КАК ЗадачаПроцесса,
	|	ИерархияЗадачПроцесса.Родитель       КАК Родитель
	|ПОМЕСТИТЬ ДочерниеЗадачиРабот
	|ИЗ
	|	РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтображаемыеРаботы КАК ОтображаемыеРаботы
	|		ПО (ОтображаемыеРаботы.Работа = ИерархияЗадачПроцесса.Родитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК СправочникЗадачиПроцесса
	|		ПО (ИерархияЗадачПроцесса.ЗадачаПроцесса = СправочникЗадачиПроцесса.Ссылка 
	|			И НЕ СправочникЗадачиПроцесса.ПометкаУдаления 
	|			И НЕ СправочникЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Отменена))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИерархияЗадачПроцесса.ЗадачаПроцесса КАК ЗадачаПроцесса,
	|	ЭтапыНеПоШаблону.ТехПроект           КАК Родитель
	|ИЗ
	|	РегистрСведений.ИерархияЗадачПроцесса КАК ИерархияЗадачПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЭтапыНеПоШаблону КАК ЭтапыНеПоШаблону
	|		ПО (ЭтапыНеПоШаблону.ЭтапНеПоШаблону = ИерархияЗадачПроцесса.Родитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК СправочникЗадачиПроцесса
	|		ПО (ИерархияЗадачПроцесса.ЗадачаПроцесса = СправочникЗадачиПроцесса.Ссылка 
	|			И НЕ СправочникЗадачиПроцесса.ПометкаУдаления 
	|			И НЕ СправочникЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Отменена))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(УчетВремени.Длительность)  КАК Длительность,
	|	УчетВремени.Пользователь         КАК Сотрудник,
	|	ТаблицаПланов.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ДочерниеЗадачиРабот.Родитель     КАК Работа
	|ПОМЕСТИТЬ ЗатраченноеВремяЗадачи
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО (УчетВремени.Задача = ДочерниеЗадачиРабот.ЗадачаПроцесса)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (УчетВремени.Период >= НАЧАЛОПЕРИОДА(ТаблицаПланов.ДатаНачала, ДЕНЬ))
	|			И (УчетВремени.Период <= КОНЕЦПЕРИОДА(ТаблицаПланов.ДатаОкончания, ДЕНЬ))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	УчетВремени.Пользователь,
	|	ТаблицаПланов.ИдентификаторПлана
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель               КАК Работа,
	|	ЗаписиПлана.Сотрудник                      КАК Сотрудник,
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) КАК ПланируемыеТрудозатраты,
	|	ТаблицаПланов.ИдентификаторПлана           КАК ИдентификаторПлана
	|ПОМЕСТИТЬ ДанныеРабочихПлановЭтапы
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗаписиПлана.Работа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Планы КАК Планы
	|		ПО (ЗаписиПлана.ИдентификаторПлана = Планы.ИдентификаторПлана)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПланов КАК ТаблицаПланов
	|		ПО (Планы.НачалоПериода = ТаблицаПланов.ДатаНачала)
	|ГДЕ
	|	ЗаписиПлана.ИдентификаторПлана В
	|			(ВЫБРАТЬ
	|				ИдентификаторыРабочихПланов.ИдентификаторПлана КАК ИдентификаторПлана
	|			ИЗ
	|				ИдентификаторыРабочихПланов КАК ИдентификаторыРабочихПланов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	ЗаписиПлана.Сотрудник,
	|	ТаблицаПланов.ИдентификаторПлана
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаписиПлана.Сотрудник КАК Сотрудник,
	|	ЗаписиПлана.Работа КАК Работа,
	|	СУММА(ЗаписиПлана.ПланируемыеТрудозатраты) КАК ПланируемыеТрудозатраты
	|ПОМЕСТИТЬ ЗаписиПлановЗанятостиНеотображаемыхПеридов
	|ИЗ
	|	РегистрСведений.ЗаписиПлана КАК ЗаписиПлана
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтображаемыеРаботыСотрудники КАК ОтображаемыеРаботыСотрудники
	|		ПО ЗаписиПлана.Работа = ОтображаемыеРаботыСотрудники.Работа
	|			И ЗаписиПлана.Сотрудник = ОтображаемыеРаботыСотрудники.Сотрудник
	|			И (ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа))
	|			И (ЗаписиПлана.ПланируемыеТрудозатраты <> 0)
	|ГДЕ
	|	ЗаписиПлана.ТипСтрокиПлана = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокПлана.Работа)
	|	И ЗаписиПлана.ИдентификаторПлана В
	|			(ВЫБРАТЬ
	|				Планы.ИдентификаторПлана КАК ИдентификаторПлана
	|			ИЗ
	|				РегистрСведений.Планы КАК Планы
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыПланов КАК ВидыПланов
	|					ПО
	|						Планы.ВидПлана = ВидыПланов.Ссылка
	|			ГДЕ
	|				ВидыПланов.Ссылка = &ПланЗанятости
	|				И Планы.НачалоПериода >= &КонецПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиПлана.Сотрудник,
	|	ЗаписиПлана.Работа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель                 КАК Этап,
	|	ДочерниеЗадачиРабот.ЗадачаПроцесса           КАК ЗадачаПроцесса,
	|	УчетВремени.Пользователь                     КАК Исполнитель,
	|	СУММА(ВЫБОР
	|			КОГДА ЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Выполнена)
	|				ТОГДА ВЫБОР
	|						КОГДА ЗадачиПроцесса.ФактическаяДатаОкончания < &НачалоТекущегоПериода
	|							ТОГДА 0
	|						ИНАЧЕ ЕСТЬNULL(УчетВремени.Длительность, 0)
	|					КОНЕЦ
	|			ИНАЧЕ ЕСТЬNULL(УчетВремени.Длительность, 0)
	|		КОНЕЦ) КАК ФактСекундыДоТекущегоПериода,
	|	СУММА(ВЫБОР 
	|		КОГДА (УчетВремени.Период < &НачалоТекущегоПериода И УчетВремени.Период >= &НачалоПериода)
	|			ТОГДА ЕСТЬNULL(УчетВремени.Длительность, 0) 
	|			ИНАЧЕ 0 
	|	КОНЕЦ)                                       КАК ФактСекундыНачалоОтчетаНачалоТекущегоПериода
	|ПОМЕСТИТЬ ФактЗадачиНачалоТекущегоПериода
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиПроцесса.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = УчетВремени.Задача
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	ДочерниеЗадачиРабот.ЗадачаПроцесса,
	|	УчетВремени.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФактЗадачиНачалоТекущегоПериода.Этап                                                КАК Этап,
	|	ФактЗадачиНачалоТекущегоПериода.Исполнитель                                         КАК Исполнитель,
	|	СУММА(ФактЗадачиНачалоТекущегоПериода.ФактСекундыДоТекущегоПериода)                 КАК ФактСекундыДоТекущегоПериода,
	|	СУММА(ФактЗадачиНачалоТекущегоПериода.ФактСекундыНачалоОтчетаНачалоТекущегоПериода) КАК ФактСекундыНачалоОтчетаНачалоТекущегоПериода
	|ПОМЕСТИТЬ ФактЭтапыНачалоТекущегоПериода
	|ИЗ
	|	ФактЗадачиНачалоТекущегоПериода КАК ФактЗадачиНачалоТекущегоПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактЗадачиНачалоТекущегоПериода.Этап,
	|	ФактЗадачиНачалоТекущегоПериода.Исполнитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель                             КАК Этап,
	|	ДочерниеЗадачиРабот.ЗадачаПроцесса                       КАК ЗадачаПроцесса,
	|	ЗадачиЗапланированоПоСотрудникам.Пользователь            КАК Исполнитель,
	|	ЕСТЬNULL(ЗадачиЗапланированоПоСотрудникам.Количество, 0) КАК Запланировано
	|ПОМЕСТИТЬ ОстатокЗапланированоНачалоТекущегоПериода
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиПроцесса.Ссылка
	|			И (ВЫБОР
	|				КОГДА ЗадачиПроцесса.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗадачПроцессов.Выполнена)
	|					ТОГДА ВЫБОР
	|							КОГДА ЗадачиПроцесса.ФактическаяДатаОкончания < &НачалоТекущегоПериода
	|								ТОГДА ЛОЖЬ
	|							ИНАЧЕ ИСТИНА
	|						КОНЕЦ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиЗапланированоПоСотрудникам КАК ЗадачиЗапланированоПоСотрудникам
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = ЗадачиЗапланированоПоСотрудникам.Задача
	|			И (ЗадачиЗапланированоПоСотрудникам.ВидРесурса = &ВидРесурса)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстатокЗапланированоНачалоТекущегоПериода.Этап        КАК Этап,
	|	ОстатокЗапланированоНачалоТекущегоПериода.Исполнитель КАК Исполнитель,
	|	СУММА(ВЫБОР
	|			КОГДА ОстатокЗапланированоНачалоТекущегоПериода.Запланировано * 3600 > ЕСТЬNULL(ФактЗадачиНачалоТекущегоПериода.ФактСекундыДоТекущегоПериода, 0)
	|				ТОГДА ОстатокЗапланированоНачалоТекущегоПериода.Запланировано * 3600 - ЕСТЬNULL(ФактЗадачиНачалоТекущегоПериода.ФактСекундыДоТекущегоПериода, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Остаток
	|
	|ПОМЕСТИТЬ ОстатокФактНачалоТекущегоПериода
	|ИЗ
	|	ОстатокЗапланированоНачалоТекущегоПериода КАК ОстатокЗапланированоНачалоТекущегоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактЗадачиНачалоТекущегоПериода КАК ФактЗадачиНачалоТекущегоПериода
	|		ПО ОстатокЗапланированоНачалоТекущегоПериода.Этап = ФактЗадачиНачалоТекущегоПериода.Этап
	|			И ОстатокЗапланированоНачалоТекущегоПериода.Исполнитель = ФактЗадачиНачалоТекущегоПериода.Исполнитель
	|			И ОстатокЗапланированоНачалоТекущегоПериода.ЗадачаПроцесса = ФактЗадачиНачалоТекущегоПериода.ЗадачаПроцесса 
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстатокЗапланированоНачалоТекущегоПериода.Исполнитель,
	|	ОстатокЗапланированоНачалоТекущегоПериода.Этап
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Исполнитель,
	|	Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДочерниеЗадачиРабот.Родитель КАК Этап,
	|	УчетВремени.Пользователь КАК Исполнитель,
	|	СУММА(ЕСТЬNULL(УчетВремени.Длительность, 0)) КАК ФактСекундыДоТекущегоПериода
	|ПОМЕСТИТЬ ФактЗадачиПредыдущиеПериоды
	|ИЗ
	|	ДочерниеЗадачиРабот КАК ДочерниеЗадачиРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.УчетВремени КАК УчетВремени
	|		ПО ДочерниеЗадачиРабот.ЗадачаПроцесса = УчетВремени.Задача
	|			И (УчетВремени.Период < &НачалоТекущегоПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДочерниеЗадачиРабот.Родитель,
	|	УчетВремени.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеВыводимыхРабот.ПланируемыеТрудозатраты КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ДанныеВыводимыхРабот.Сотрудник КАК Сотрудник,
	|	ДанныеВыводимыхРабот.Работа КАК Работа,
	|	ДанныеВыводимыхРабот.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ЕСТЬNULL(ЗатраченноеВремяЗадачи.Длительность, 0) КАК ФактическиеТрудозатратыСекунды
	|ПОМЕСТИТЬ ВыводимыеРаботыЗатраченноеВремяВсеРаботы
	|ИЗ
	|	ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗатраченноеВремяЗадачи КАК ЗатраченноеВремяЗадачи
	|		ПО ДанныеВыводимыхРабот.Сотрудник = ЗатраченноеВремяЗадачи.Сотрудник
	|			И ДанныеВыводимыхРабот.Работа = ЗатраченноеВремяЗадачи.Работа
	|			И ДанныеВыводимыхРабот.ИдентификаторПлана = ЗатраченноеВремяЗадачи.ИдентификаторПлана
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ЗатраченноеВремяЗадачи.Сотрудник,
	|	ЗатраченноеВремяЗадачи.Работа,
	|	ЗатраченноеВремяЗадачи.ИдентификаторПлана,
	|	ЗатраченноеВремяЗадачи.Длительность
	|ИЗ
	|	ЗатраченноеВремяЗадачи КАК ЗатраченноеВремяЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеВыводимыхРабот КАК ДанныеВыводимыхРабот
	|		ПО ЗатраченноеВремяЗадачи.Сотрудник = ДанныеВыводимыхРабот.Сотрудник
	|			И ЗатраченноеВремяЗадачи.Работа = ДанныеВыводимыхРабот.Работа
	|			И ЗатраченноеВремяЗадачи.ИдентификаторПлана = ДанныеВыводимыхРабот.ИдентификаторПлана
	|ГДЕ
	|	ДанныеВыводимыхРабот.ИдентификаторПлана ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ПланируемыеТрудозатратыПланЗанятости КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Сотрудник КАК Сотрудник,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Работа КАК Работа,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана КАК ИдентификаторПлана,
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ФактическиеТрудозатратыСекунды КАК ФактическиеТрудозатратыСекунды,
	|	ЕСТЬNULL(ДанныеРабочихПлановЭтапы.ПланируемыеТрудозатраты, 0) КАК ПланируемыеТрудозатратыРабочиеПланы
	|ПОМЕСТИТЬ ВыводимыеРаботыЗатраченноеВремяРабочиеПланы
	|ИЗ
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы КАК ВыводимыеРаботыЗатраченноеВремяВсеРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРабочихПлановЭтапы КАК ДанныеРабочихПлановЭтапы
	|		ПО ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Сотрудник = ДанныеРабочихПлановЭтапы.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Работа = ДанныеРабочихПлановЭтапы.Работа
	|			И ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана = ДанныеРабочихПлановЭтапы.ИдентификаторПлана
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ДанныеРабочихПлановЭтапы.Сотрудник,
	|	ДанныеРабочихПлановЭтапы.Работа,
	|	ДанныеРабочихПлановЭтапы.ИдентификаторПлана,
	|	0,
	|	ДанныеРабочихПлановЭтапы.ПланируемыеТрудозатраты
	|ИЗ
	|	ДанныеРабочихПлановЭтапы КАК ДанныеРабочихПлановЭтапы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыводимыеРаботыЗатраченноеВремяВсеРаботы КАК ВыводимыеРаботыЗатраченноеВремяВсеРаботы
	|		ПО ДанныеРабочихПлановЭтапы.Сотрудник = ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Сотрудник
	|			И ДанныеРабочихПлановЭтапы.Работа = ВыводимыеРаботыЗатраченноеВремяВсеРаботы.Работа
	|			И ДанныеРабочихПлановЭтапы.ИдентификаторПлана = ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана
	|ГДЕ
	|	ВыводимыеРаботыЗатраченноеВремяВсеРаботы.ИдентификаторПлана ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник                                    КАК Сотрудник,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа                                       КАК Работа,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ИдентификаторПлана                           КАК ИдентификаторПлана,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ПланируемыеТрудозатратыПланЗанятости         КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ФактическиеТрудозатратыСекунды               КАК ФактическиеТрудозатратыСекунды,
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.ПланируемыеТрудозатратыРабочиеПланы          КАК ПланируемыеТрудозатратыРабочиеПланы,
	|	ЕСТЬNULL(ЗаписиПлановЗанятостиНеотображаемыхПеридов.ПланируемыеТрудозатраты, 0)          КАК ПланируемыеТрудозатратыНеотображаемыхПериодов,
	|	ЕСТЬNULL(ОстатокФактНачалоТекущегоПериода.Остаток,0)                                     КАК ЗапланированоНаНачалоТекущего,
	|	ЕСТЬNULL(ФактЭтапыНачалоТекущегоПериода.ФактСекундыНачалоОтчетаНачалоТекущегоПериода, 0) - (ЕСТЬNULL(ЗапланированоПланыЗанятостиПрошлыеПериоды.ПланируемыеТрудозатраты, 0) * 3600) КАК ФактМинусПланЗанятостиСекунды,
	|	ЕСТЬNULL(ЗапланированоПланыЗанятостиПрошлыеПериоды.ПланируемыеТрудозатраты, 0)           КАК ПланируемыеТрудозатратыНачалоТекущегоПериода,
	|	ЕСТЬNULL(ФактЭтапыНачалоТекущегоПериода.ФактСекундыНачалоОтчетаНачалоТекущегоПериода, 0) КАК ФактТрудозатратыНачалоТекущегоПериода,
	|	ЕСТЬNULL(ДатыПланируемыхРабот.НачалоПериода, &ПустаяДата)                                КАК НачалоПериодаПланируемыхРабот,
	|	ЕСТЬNULL(ДатыПланируемыхРабот.КонецПериода, &ПустаяДата)                                 КАК КонецПериодаПланируемыхРабот
	|ПОМЕСТИТЬ ВыводимыеРаботыПослеОпределенияЗатраченноеВремя
	|ИЗ
	|	ВыводимыеРаботыЗатраченноеВремяРабочиеПланы КАК ВыводимыеРаботыЗатраченноеВремяРабочиеПланы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаписиПлановЗанятостиНеотображаемыхПеридов КАК ЗаписиПлановЗанятостиНеотображаемыхПеридов
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ЗаписиПлановЗанятостиНеотображаемыхПеридов.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ЗаписиПлановЗанятостиНеотображаемыхПеридов.Работа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстатокФактНачалоТекущегоПериода КАК ОстатокФактНачалоТекущегоПериода
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ОстатокФактНачалоТекущегоПериода.Исполнитель
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ОстатокФактНачалоТекущегоПериода.Этап
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапланированоПланыЗанятостиПрошлыеПериоды КАК ЗапланированоПланыЗанятостиПрошлыеПериоды
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ЗапланированоПланыЗанятостиПрошлыеПериоды.Сотрудник
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ЗапланированоПланыЗанятостиПрошлыеПериоды.Работа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактЭтапыНачалоТекущегоПериода КАК ФактЭтапыНачалоТекущегоПериода
	|		ПО ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ФактЭтапыНачалоТекущегоПериода.Исполнитель
	|			И ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ФактЭтапыНачалоТекущегоПериода.Этап 
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПланируемыхРабот КАК ДатыПланируемыхРабот
	|		ПО (ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Сотрудник = ДатыПланируемыхРабот.Сотрудник)
	|			И (ВыводимыеРаботыЗатраченноеВремяРабочиеПланы.Работа = ДатыПланируемыхРабот.Работа)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Работа,
	|	ИдентификаторПлана
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.Сотрудник                                                                                               КАК Сотрудник,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.Работа                                                                                                  КАК Работа,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ИдентификаторПлана                                                                                      КАК ИдентификаторПлана,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ПланируемыеТрудозатратыПланЗанятости                                                                    КАК ПланируемыеТрудозатратыПланЗанятости,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ФактическиеТрудозатратыСекунды                                                                          КАК ФактическиеТрудозатратыСекунды,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ПланируемыеТрудозатратыРабочиеПланы                                                                     КАК ПланируемыеТрудозатратыРабочиеПланы,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ПланируемыеТрудозатратыНеотображаемыхПериодов                                                           КАК ПланируемыеТрудозатратыНеотображаемыхПериодов,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ЗапланированоНаНачалоТекущего                                                                           КАК ЗапланированоНаНачалоТекущего,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ФактМинусПланЗанятостиСекунды                                                                           КАК ФактМинусПланЗанятостиСекунды,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ПланируемыеТрудозатратыНачалоТекущегоПериода                                                            КАК ПланируемыеТрудозатратыНачалоТекущегоПериода,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.ФактТрудозатратыНачалоТекущегоПериода                                                                   КАК ФактТрудозатратыНачалоТекущегоПериода,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.НачалоПериодаПланируемыхРабот                                                                           КАК НачалоПериодаПланируемыхРабот,
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.КонецПериодаПланируемыхРабот                                                                            КАК КонецПериодаПланируемыхРабот,
	|	Пользователи.Подразделение                                                                                                                              КАК Подразделение,
	|	ЕСТЬNULL(ЗадачиПроцесса.Статус,  ЗНАЧЕНИЕ(Справочник.ЗадачиПроцесса.ПустаяСсылка))                                                                      КАК Статус,
	|	ЕСТЬNULL(ТехническиеПроекты.Версия, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Версия, ЗНАЧЕНИЕ(Справочник.ВерсииПроекта.ПустаяСсылка)))               КАК Версия,
	|	ЕСТЬNULL(ВерсииПроектаТехническиеПроекты.ДатаОкончанияРазработки, ЕСТЬNULL(ВерсииПроектаЭтапыНеПоШаблону.ДатаОкончанияРазработки, &ПустаяДата))         КАК ДатаВерсии,
	|	ЕСТЬNULL(ТехническиеПроекты.Владелец, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Владелец, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)))                 КАК Проект,
	|	ЕСТЬNULL(ТехническиеПроекты.Ссылка, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Ссылка, ЗНАЧЕНИЕ(Справочник.ТехническиеПроекты.ПустаяСсылка)))          КАК ТехПроект,
	|	ЕСТЬNULL(ЗадачиПроцесса.ПлановаяДатаНачала, &ПустаяДата)                                                                                                КАК ПлановаяДатаНачала,
	|	ЕСТЬNULL(ЗадачиПроцесса.КрайняяДатаОкончания, &ПустаяДата)                                                                                              КАК КрайняяДатаОкончания,
	|	ЕСТЬNULL(ТехническиеПроекты.ПлановаяДатаНачала, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.ПлановаяДатаНачала, &ПустаяДата))                           КАК ПлановаяДатаНачалаТехПроект,
	|	ЕСТЬNULL(ТехническиеПроекты.Статус, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.Статус,ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.ПустаяСсылка))) КАК СтатусТехническийПроект,
	|	ЕСТЬNULL(ТехническиеПроекты.ПлановаяДатаОкончания, ЕСТЬNULL(ТехническиеПроектыЭтапыНеПоШаблону.ПлановаяДатаОкончания, &ПустаяДата))                     КАК ПлановаяДатаОкончанияТехПроект,
	|	ЕСТЬNULL(СостоянияЗадачПроцессов.ЗначениеУпорядочивания, 0)                                                                                             КАК ЗначениеУпорядочиванияЭтапа
	|ИЗ
	|	ВыводимыеРаботыПослеОпределенияЗатраченноеВремя КАК ВыводимыеРаботыПослеОпределенияЗатраченноеВремя
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|			ПО ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.Сотрудник = Пользователи.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиПроцесса КАК ЗадачиПроцесса
	|			ПО ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.Работа = ЗадачиПроцесса.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
	|			ПО (ЗадачиПроцесса.Предмет = ТехническиеПроекты.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииПроекта КАК ВерсииПроектаТехническиеПроекты
	|			ПО (ВерсииПроектаТехническиеПроекты.Ссылка = ТехническиеПроекты.Версия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроектыЭтапыНеПоШаблону
	|			ПО (ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.Работа = ТехническиеПроектыЭтапыНеПоШаблону.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииПроекта КАК ВерсииПроектаЭтапыНеПоШаблону
	|			ПО (ВерсииПроектаЭтапыНеПоШаблону.Ссылка = ТехническиеПроектыЭтапыНеПоШаблону.Версия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗадачПроцессов КАК СостоянияЗадачПроцессов
	|			ПО (ВыводимыеРаботыПослеОпределенияЗатраченноеВремя.Работа =  СостоянияЗадачПроцессов.ЗадачаПроцесса)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТехПроект,
	|	ПлановаяДатаНачалаТехПроект,
	|	ЗначениеУпорядочиванияЭтапа,
	|	КрайняяДатаОкончания,
	|	Сотрудник
	|ИТОГИ ПО
	|	ТехПроект,
	|	Работа,
	|	Сотрудник";
	
КонецФункции

Функция ПодготовленныеДанныеГруппировкаОтТехПроектов(РезультатЗапроса, ПараметрыФормирования, ТаблицаПланов)
	
	ДанныеОтчета = НовыйПодготовленныеДанныеОтчета(ТаблицаПланов);
	
	ДеревоДанных                     = ДанныеОтчета.ДеревоДанных;
	ИдентификаторыИменаКолонок       = ДанныеОтчета.ИдентификаторыИменаКолонок;
	ДанныеОтчета.ПредставлениеОтбора = ПредставлениеОтбора(ПараметрыФормирования);
	
	ВыборкаТехПроект = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТребуетсяАнализОтклоненийРабочиеПланы = ПараметрыФормирования.ТолькоСОтклонениемРабочиеПланы;
	ТребуетсяАнализОтклоненийФакт         = ПараметрыФормирования.ТолькоСОтклонениемФакт; 
	
	ТребуетсяАнализОтклонений =  ТребуетсяАнализОтклоненийРабочиеПланы
	                             Или ТребуетсяАнализОтклоненийФакт;
	
	Пока ВыборкаТехПроект.Следующий() Цикл
		
		СтрокаТехПроект = ДеревоДанных.Строки.Добавить();
		СтрокаТехПроект.ТехническийПроект = ВыборкаТехПроект.ТехПроект;
		
		ВыборкаЭтап = ВыборкаТехПроект.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		ПлановаяДатаОкончанияЭтапов = Дата(1,1,1);
		
		Пока ВыборкаЭтап.Следующий() Цикл 
			
			ЕстьОтклонениеРабочиеПланы = Ложь;
			ЕстьОтклонениеФакт         = Ложь;
			
			СтрокаЭтап = СтрокаТехПроект.Строки.Добавить();
			СтрокаЭтап.СотрудникРабота = ВыборкаЭтап.Работа;
			
			ВыборкаСотрудник = ВыборкаЭтап.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаСотрудник.Следующий() Цикл
				
				СтрокаСотрудник = СтрокаЭтап.Строки.Добавить();
				СтрокаСотрудник.СотрудникРабота = ВыборкаСотрудник.Сотрудник; 
				
				ВыборкаДетали = ВыборкаСотрудник.Выбрать();
				
				ДополнительныеДанныеУстановлены = Ложь;
				
				Пока ВыборкаДетали.Следующий() Цикл
					
					Если Не ДополнительныеДанныеУстановлены Тогда
						
						СтрокаТехПроект.Версия                            = ВыборкаДетали.Версия;
						СтрокаТехПроект.Проект                            = ВыборкаДетали.Проект;
						СтрокаТехПроект.ДатаНачала                        = ВыборкаДетали.ПлановаяДатаНачалаТехПроект;
						СтрокаТехПроект.ДатаОкончания                     = ВыборкаДетали.ПлановаяДатаОкончанияТехПроект;
						СтрокаТехПроект.Статус                            = ВыборкаДетали.СтатусТехническийПроект;
						Если ЗначениеЗаполнено(ВыборкаДетали.ДатаВерсии)
							И ЗначениеЗаполнено(ВыборкаДетали.ПлановаяДатаОкончанияТехПроект) 
							И ВыборкаДетали.ДатаВерсии < ВыборкаДетали.ПлановаяДатаОкончанияТехПроект Тогда
							
							СтрокаТехПроект.ДатаВерсииМеньшеДатыТехПроекта = Истина;
							
						КонецЕсли;
						
						Если ТипЗнч(СтрокаЭтап.СотрудникРабота) = Тип("СправочникСсылка.ТехническиеПроекты") Тогда
							
							СтрокаЭтап.ДатаНачала                         = ВыборкаДетали.ПлановаяДатаНачалаТехПроект;
							СтрокаЭтап.ДатаОкончания                      = ВыборкаДетали.ПлановаяДатаОкончанияТехПроект;
							СтрокаЭтап.Статус                             = ВыборкаДетали.СтатусТехническийПроект;
							
						Иначе
							
							СтрокаЭтап.ДатаНачала                         = ВыборкаДетали.ПлановаяДатаНачала;
							СтрокаЭтап.ДатаОкончания                      = ВыборкаДетали.КрайняяДатаОкончания;
							СтрокаЭтап.Статус                             = ВыборкаДетали.Статус;
							
						КонецЕсли;
						
						СтрокаСотрудник.ЗапланированоНаНачалоТекущего                 = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ЗапланированоНаНачалоТекущего);
						СтрокаСотрудник.ПланЗанятостиНеотображаемыйПериод             = ВыборкаДетали.ПланируемыеТрудозатратыНеотображаемыхПериодов;
						СтрокаСотрудник.Нераспределено                                = СтрокаСотрудник.ЗапланированоНаНачалоТекущего - СтрокаСотрудник.ПланЗанятостиНеотображаемыйПериод;
						СтрокаСотрудник.ФактМинусПланЗанятости                        = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ФактМинусПланЗанятостиСекунды);
						СтрокаСотрудник.ПланируемыеТрудозатратыНачалоТекущегоПериода  = ВыборкаДетали.ПланируемыеТрудозатратыНачалоТекущегоПериода;
						СтрокаСотрудник.ФактТрудозатратыНачалоТекущегоПериода         = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ФактТрудозатратыНачалоТекущегоПериода);
						СтрокаСотрудник.ДатаНачалаПланирования                        = ВыборкаДетали.НачалоПериодаПланируемыхРабот;
						СтрокаСотрудник.ДатаОкончанияПланирования                     = ВыборкаДетали.КонецПериодаПланируемыхРабот;
						СтрокаСотрудник.ДатаНачала                                    = СтрокаЭтап.ДатаНачала;
						СтрокаСотрудник.ДатаОкончания                                 = СтрокаЭтап.ДатаОкончания;
						
						Если ЗначениеЗаполнено(СтрокаЭтап.ДатаОкончания) Тогда
							
							ПлановаяДатаОкончанияЭтапов = Макс(ПлановаяДатаОкончанияЭтапов, СтрокаЭтап.ДатаОкончания);
							
						КонецЕсли;
						
						
						ДополнительныеДанныеУстановлены = Истина;
						
					КонецЕсли;
					
					Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
						
						ИменаКолонок = ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
						
						Если ИменаКолонок = Неопределено Тогда 
							Продолжить;
						КонецЕсли;
						
						Если СтрокаТаблицыПланов.НачалоПериода > ВыборкаДетали.КрайняяДатаОкончания
							Или СтрокаТаблицыПланов.КонецПериода < ВыборкаДетали.ПлановаяДатаНачала Тогда
							
							СтрокаЭтап[ИменаКолонок.ПериодКорректен]      = Ложь;
							СтрокаСотрудник[ИменаКолонок.ПериодКорректен] = Ложь;
							
						Иначе
							
							СтрокаЭтап[ИменаКолонок.ПериодКорректен]      = Истина;
							СтрокаСотрудник[ИменаКолонок.ПериодКорректен] = Истина
							
						КонецЕсли;
						
						Если СтрокаТаблицыПланов.НачалоПериода > ВыборкаДетали.ПлановаяДатаОкончанияТехПроект
							Или СтрокаТаблицыПланов.КонецПериода < ВыборкаДетали.ПлановаяДатаНачалаТехПроект Тогда
							
							СтрокаТехПроект[ИменаКолонок.ПериодКорректен] = Ложь;
							
						Иначе
							
							СтрокаТехПроект[ИменаКолонок.ПериодКорректен] = Истина;
							
						КонецЕсли;
						
						Если (ВыборкаДетали.ПлановаяДатаНачала >= СтрокаТаблицыПланов.НачалоПериода
							И ВыборкаДетали.ПлановаяДатаНачала <= КонецДня(СтрокаТаблицыПланов.КонецПериода))
							Или (ВыборкаДетали.КрайняяДатаОкончания >= СтрокаТаблицыПланов.НачалоПериода
							И ВыборкаДетали.КрайняяДатаОкончания <= КонецДня(СтрокаТаблицыПланов.КонецПериода)) Тогда
							
							ИменаКолонок = ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
							
							Если ИменаКолонок <> Неопределено Тогда
								СтрокаЭтап[ИменаКолонок.ПериодКорректен]    = Истина;
								СтрокаСотрудник[ИменаКолонок.ПериодКорректен] = Истина
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если ВыборкаДетали.ИдентификаторПлана <> 0 Тогда
						
						ИменаКолонок = ИдентификаторыИменаКолонок.Получить(ВыборкаДетали.ИдентификаторПлана);
						
						Если ИменаКолонок = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаСотрудник[ИменаКолонок.ПланЗанятости] = ВыборкаДетали.ПланируемыеТрудозатратыПланЗанятости;
						Если ИменаКолонок.ТипПериода <> СловарьПрошлыйПериодПлана() Тогда
							СтрокаСотрудник.Нераспределено = СтрокаСотрудник.Нераспределено - СтрокаСотрудник[ИменаКолонок.ПланЗанятости];
						КонецЕсли;
						
						Если ИменаКолонок.ТипПериода = СловарьТекущийПериодПлана()
							Или ИменаКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
							
							СтрокаСотрудник[ИменаКолонок.РабочиеПланы]       = ВыборкаДетали.ПланируемыеТрудозатратыРабочиеПланы;
							СтрокаСотрудник[ИменаКолонок.Факт]               = ПланированиеКлиентСервер.ЧасыПоДлительностиВСекундах(ВыборкаДетали.ФактическиеТрудозатратыСекунды);
							
							Если Не ОтклонениеДопустимо(СтрокаСотрудник[ИменаКолонок.ПланЗанятости],
									                    СтрокаСотрудник[ИменаКолонок.РабочиеПланы],
									                    ПараметрыФормирования.ДопустимыйПроцентОтклонения,
									                    ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
									                    Истина) Тогда
									
									ЕстьОтклонениеРабочиеПланы                             = Истина;
									СтрокаСотрудник.ВСтрокеЕстьДанныеСоответствующиеОтбору = Истина;
									СтрокаСотрудник[ИменаКолонок.ЕстьОтклоненияПоОтбору]   = Истина;
									
							КонецЕсли;
							
							Если ИменаКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
								СтрокаСотрудник.Факт = СтрокаСотрудник.Факт + СтрокаСотрудник[ИменаКолонок.Факт];
								
								Если Не ОтклонениеДопустимо(СтрокаСотрудник[ИменаКолонок.ПланЗанятости],
									                        СтрокаСотрудник[ИменаКолонок.Факт],
									                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
									                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
									                        Истина) Тогда
									
									ЕстьОтклонениеФакт                                     = Истина;
									СтрокаСотрудник.ВСтрокеЕстьДанныеСоответствующиеОтбору = Истина;
									СтрокаСотрудник[ИменаКолонок.ЕстьОтклоненияПоОтбору]   = Истина;
									
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если СтрокаСотрудник.Нераспределено > 0 Тогда
					СтрокаСотрудник.Нераспределено = СтрокаСотрудник.Нераспределено;
				Иначе
					СтрокаСотрудник.Резерв         = - СтрокаСотрудник.Нераспределено;
					СтрокаСотрудник.Нераспределено = 0;
				КонецЕсли;
				
			КонецЦикла;
			
			НуженПересчет = Истина;
			
			Если ТребуетсяАнализОтклонений Тогда
				
				ЕстьОтклонения = Ложь;
				
				Если ТребуетсяАнализОтклоненийРабочиеПланы
					И ЕстьОтклонениеРабочиеПланы Тогда
					
					ЕстьОтклонения = Истина;
					
				ИначеЕсли ТребуетсяАнализОтклоненийФакт
					И ЕстьОтклонениеФакт Тогда
					
					ЕстьОтклонения = Истина;
					
				КонецЕсли;
				
				Если Не ЕстьОтклонения Тогда
					
					СтрокаТехПроект.Строки.Удалить(СтрокаЭтап);
					НуженПересчет = Ложь;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если НуженПересчет Тогда
				ПересчитатьМассивКолонокРодительскойСтрокиПоДаннымПодчиненных(СтрокаЭтап, ДанныеОтчета.ИменаКолонокПерерасчетРодители, Ложь);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СтрокаТехПроект.Строки.Количество() = 0 Тогда
			
			ДеревоДанных.Строки.Удалить(СтрокаТехПроект);
			
		Иначе
			
			ПересчитатьМассивКолонокРодительскойСтрокиПоДаннымПодчиненных(СтрокаТехПроект, ДанныеОтчета.ИменаКолонокПерерасчетРодители, Ложь);
			Если ЗначениеЗаполнено(СтрокаТехПроект.ДатаОкончания)
				И ЗначениеЗаполнено(ПлановаяДатаОкончанияЭтапов)
				И СтрокаТехПроект.ДатаОкончания < ПлановаяДатаОкончанияЭтапов Тогда
				
				СтрокаТехПроект.ДатаТехПроектаМеньшеПлановыхДатЭтапа = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СкрытьПериодыНеСоответствующиеОтбору(ПараметрыФормирования, ДеревоДанных, ТаблицаПланов, ИдентификаторыИменаКолонок);
	
	Возврат ДанныеОтчета;
	
КонецФункции

Процедура ВывестиДанныеВТабличныйДокументОтТехПроектов(РезультатФормированияОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов)
	
	Макет = Отчеты.КонтрольПланирования.ПолучитьМакет("ОтТехническихПроектов");
	
	ТаблицаОтчета = РезультатФормированияОтчета.ТабличныйДокумент;
	
	УстановитьШапкуОтчета(Макет, ТаблицаОтчета, ПараметрыФормирования, ПодготовленныеДанные);
	УстановитьЛегенду(Макет, ТаблицаОтчета);
	УстановитьШапкуТаблицыОтчета(Макет, ТаблицаОтчета, ПодготовленныеДанные, ПараметрыФормирования, ТаблицаПланов);
	
	ЭтоПоследняяСтрока = Ложь;
	
	Для Каждого СтрокаТехПроект Из ПодготовленныеДанные.ДеревоДанных.Строки Цикл
		
		ВывестиСтрокуТехПроектОтТехПроектов(СтрокаТехПроект,
		                                    Макет, 
		                                    ТаблицаОтчета, 
		                                    ТаблицаПланов, 
		                                    ПодготовленныеДанные.ИдентификаторыИменаКолонок,
		                                    ПараметрыФормирования);
		
		ТаблицаОтчета.НачатьГруппуСтрок(СтрокаТехПроект.ТехническийПроект);
		
		Для Каждого СтрокаЭтап Из СтрокаТехПроект.Строки Цикл
			
			ВывестиСтрокаЭтапОтТехПроектов(СтрокаЭтап,
			                               Макет,
			                               ТаблицаОтчета,
			                               ТаблицаПланов,
			                               ПодготовленныеДанные.ИдентификаторыИменаКолонок,
			                               ПараметрыФормирования);
			
			ТаблицаОтчета.НачатьГруппуСтрок(СтрокаЭтап.СотрудникРабота);
			
			Для Каждого СтрокаСотрудник Из СтрокаЭтап.Строки Цикл
				
				Если ПодготовленныеДанные.ДеревоДанных.Строки.Индекс(СтрокаТехПроект) = ПодготовленныеДанные.ДеревоДанных.Строки.Количество() - 1
					И СтрокаТехПроект.Строки.Индекс(СтрокаЭтап) = СтрокаТехПроект.Строки.Количество() - 1
					И СтрокаЭтап.Строки.Индекс(СтрокаСотрудник) = СтрокаЭтап.Строки.Количество() - 1 Тогда
					
					ЭтоПоследняяСтрока = Истина;
					
				КонецЕсли;
				
				ВывестиСтрокаCотрудникОтТехПроектов(СтрокаСотрудник,
				                                    Макет, 
				                                    ТаблицаОтчета, 
				                                    ТаблицаПланов, 
				                                    ПодготовленныеДанные.ИдентификаторыИменаКолонок,
				                                    ЭтоПоследняяСтрока,
				                                    ПараметрыФормирования);
				
			КонецЦикла;
			
			ТаблицаОтчета.ЗакончитьГруппуСтрок();
			
		КонецЦикла;
		
		ТаблицаОтчета.ЗакончитьГруппуСтрок();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокаCотрудникОтТехПроектов(СтрокаСотрудник, Макет, ТаблицаОтчета, ТаблицаПланов, ИдентификаторыИменаКолонок, ЭтоПоследняяСтрока, ПараметрыФормирования)
	
	Если ПараметрыФормирования.СкрыватьСтрокиССотрудникамиНеСоответствующимиОтбору
		И Не СтрокаСотрудник.ВСтрокеЕстьДанныеСоответствующиеОтбору Тогда
		Возврат;
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("СотрудникОсновное");
	Область.Параметры.Сотрудник = СтрокаСотрудник.СотрудникРабота;
	Если ЭтоПоследняяСтрока Тогда
		Область.Области.СотрудникОсновное.ГраницаСнизу = ЛинияТолщинаДва();
	КонецЕсли;
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("СотрудникПроект");
		Если ЭтоПоследняяСтрока Тогда
			Область.Области.СотрудникПроект.ГраницаСнизу = ЛинияТолщинаДва();
		КонецЕсли;
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("СотрудникВерсия");
		Если ЭтоПоследняяСтрока Тогда
			Область.Области.СотрудникВерсия.ГраницаСнизу = ЛинияТолщинаДва();
		КонецЕсли;
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("СотрудникСроки");
		Область.Параметры.ДатаНачалаПланирования    = Формат(СтрокаСотрудник.ДатаНачалаПланирования,    "ДФ=dd.MM.yy");
		Область.Параметры.ДатаОкончанияПланирования = Формат(СтрокаСотрудник.ДатаОкончанияПланирования, "ДФ=dd.MM.yy");
		Если ЭтоПоследняяСтрока Тогда
			Область.Области.СотрудникСроки.ГраницаСнизу = ЛинияТолщинаДва();
		КонецЕсли;
		
		Если Не ПараметрыФормирования.НеВыделятьСроки 
			И СтрокаСотрудник.ДатаОкончанияПланирования > СтрокаСотрудник.ДатаОкончания Тогда
			
			Область.Области.СотрудникДатаОкончанияПланирования.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
			
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("СотрудникТрудозатраты");
	
	Область.Параметры.ОстатокНаДату               = СтрокаСотрудник.ЗапланированоНаНачалоТекущего;
	Область.Параметры.Факт                        = СтрокаСотрудник.Факт;
	Область.Параметры.НераспределеноПланЗанятости = СтрокаСотрудник.Нераспределено;
	Область.Параметры.РезервПланЗанятости         = СтрокаСотрудник.Резерв;
	Область.Параметры.ФактМинусПланЗанятости      = СтрокаСотрудник.ФактМинусПланЗанятости; 
	
	Если СтрокаСотрудник.Нераспределено <> 0 
		И Не ПараметрыФормирования.НеВыделятьНераспределено Тогда
		
		Область.Области.СотрудникНераспределено.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если СтрокаСотрудник.Резерв <> 0 
		И Не ПараметрыФормирования.НеВыделятьРезерв Тогда
		
		Область.Области.СотрудникРезерв.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиЗадачи;
		
	КонецЕсли;
	
	Если СтрокаСотрудник.ФактМинусПланЗанятости < 0 
		И Не ПараметрыФормирования.НеВыделятьФактМинусПЗ Тогда
		
		Область.Области.СотрудникФактМинусПЗ.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если ЭтоПоследняяСтрока Тогда
		Область.Области.СотрудникТрудозатраты.ГраницаСнизу = ЛинияТолщинаДва();
	КонецЕсли;

	ТаблицаОтчета.Присоединить(Область);
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПланЗанятости = СтрокаСотрудник[ДанныеКолонок.ПланЗанятости];
		
		Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда
				
				ИмяОбласти = "СотрудникПЗ";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.ПланЗанятости = ПланЗанятости;
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования, СтрокаСотрудник, ДанныеКолонок, ЭтоПоследняяСтрока);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				
				ИмяОбласти = "СотрудникРабочиеПланы";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.РабочиеПланы = СтрокаСотрудник[ДанныеКолонок.РабочиеПланы];
				
				Если Не ОтклонениеДопустимо(СтрокаСотрудник[ДанныеКолонок.ПланЗанятости],
					                        СтрокаСотрудник[ДанныеКолонок.РабочиеПланы],
					                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                        Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования, СтрокаСотрудник, ДанныеКолонок, ЭтоПоследняяСтрока);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				
				ИмяОбласти = "СотрудникФакт";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.Факт = СтрокаСотрудник[ДанныеКолонок.Факт];
				Если ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() 
					И Не ОтклонениеДопустимо(СтрокаСотрудник[ДанныеКолонок.ПланЗанятости],
					                         СтрокаСотрудник[ДанныеКолонок.Факт],
					                         ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                         ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                         Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли; 
				
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования, СтрокаСотрудник, ДанныеКолонок, ЭтоПоследняяСтрока);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
		Иначе
			
			ИмяОбласти = "СтрокаСотрудникОднаКолонка";
			Область = Макет.ПолучитьОбласть(ИмяОбласти);
			Область.Параметры.ПланЗанятости = ПланЗанятости;
			УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования, СтрокаСотрудник, ДанныеКолонок, ЭтоПоследняяСтрока);
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокаЭтапОтТехПроектов(СтрокаЭтап, Макет, ТаблицаОтчета, ТаблицаПланов, ИдентификаторыИменаКолонок, ПараметрыФормирования)
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Если ТипЗнч(СтрокаЭтап.СотрудникРабота) = Тип("СправочникСсылка.ЗадачиПроцесса") Тогда
		
		Этап = СтрокаЭтап.СотрудникРабота;
		
	Иначе
		
		Этап = НСтр("ru = 'Задачи не по шаблону'");
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ЭтапОсновное");
	Область.Параметры.Этап = Этап;
	ТаблицаОтчета.Присоединить(Область); 
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("ЭтапПроект");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("ЭтапВерсия");
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("ЭтапСроки");
		Область.Параметры.ДатаНачала    = Формат(СтрокаЭтап.ДатаНачала,    "ДФ=dd.MM.yy");
		Область.Параметры.ДатаОкончания = Формат(СтрокаЭтап.ДатаОкончания, "ДФ=dd.MM.yy");
		
		Если Не ПараметрыФормирования.НеВыделятьСроки
			И СтрокаЭтап.Статус <> Перечисления.СтатусыЗадачПроцессов.Выполнена
			И НачалоДня(СтрокаЭтап.ДатаОкончания) < НачалоДня(ТекущаяДатаСеанса()) Тогда
			
			Область.Области.ДатаОкончанияЭтапа.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
			
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ЭтапТрудозатраты");
	Область.Параметры.ОстатокНаДату               = СтрокаЭтап.ЗапланированоНаНачалоТекущего;
	Область.Параметры.Факт                        = СтрокаЭтап.Факт;
	Область.Параметры.НераспределеноПланЗанятости = СтрокаЭтап.Нераспределено;
	Область.Параметры.РезервПланЗанятости         = СтрокаЭтап.Резерв;
	Область.Параметры.ФактМинусПланЗанятости      = СтрокаЭтап.ФактМинусПланЗанятости;
	
	Если СтрокаЭтап.Нераспределено <> 0 
		И Не ПараметрыФормирования.НеВыделятьНераспределено Тогда
		
		Область.Области.ЭтапНераспределено.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если СтрокаЭтап.Резерв <> 0 
		И Не ПараметрыФормирования.НеВыделятьРезерв Тогда
		
		Область.Области.ЭтапРезерв.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиЗадачи;
		
	КонецЕсли;
	
	Если СтрокаЭтап.ФактМинусПланЗанятости < 0 
		И Не ПараметрыФормирования.НеВыделятьФактМинусПЗ Тогда
		
		Область.Области.ЭтапФактМинусПЗ.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	ТаблицаОтчета.Присоединить(Область);
	
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПланЗанятости = СтрокаЭтап[ДанныеКолонок.ПланЗанятости];
		
		Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана()
			И ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() > 1 Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда
				ИмяОбласти = "ЭтапПЗ";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.ПланЗанятости = ПланЗанятости;
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаЭтап, ДанныеКолонок, Ложь);
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				
				ИмяОбласти = "ЭтапРабочиеПланы";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.РабочиеПланы   = СтрокаЭтап[ДанныеКолонок.РабочиеПланы];
				
				Если Не ОтклонениеДопустимо(СтрокаЭтап[ДанныеКолонок.ПланЗанятости],
					                        СтрокаЭтап[ДанныеКолонок.РабочиеПланы],
					                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                        Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаЭтап, ДанныеКолонок, Ложь);
				ТаблицаОтчета.Присоединить(Область); 
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				
				ИмяОбласти = "ЭтапФакт";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.Факт = СтрокаЭтап[ДанныеКолонок.Факт];
				Если ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() 
					И Не ОтклонениеДопустимо(СтрокаЭтап[ДанныеКолонок.ПланЗанятости],
					                         СтрокаЭтап[ДанныеКолонок.Факт],
					                         ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                         ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                         Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли; 
				
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования, СтрокаЭтап, ДанныеКолонок, Ложь);
				ТаблицаОтчета.Присоединить(Область);
				
			КонецЕсли;
			
		Иначе
			
			ИмяОбласти = "СтрокаЭтапОднаКолонка";
			Область = Макет.ПолучитьОбласть(ИмяОбласти);
			Область.Параметры.ПланЗанятости = ПланЗанятости;
			УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаЭтап, ДанныеКолонок, Ложь);
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуТехПроектОтТехПроектов(СтрокаТехПроект, Макет, ТаблицаОтчета, ТаблицаПланов, ИдентификаторыИменаКолонок, ПараметрыФормирования)
	
	Область = Макет.ПолучитьОбласть("ПустаяОтступ");
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ТехПроектОсновное");
	Область.Параметры.ТехПроект                   = СтрокаТехПроект.ТехническийПроект;
	ТаблицаОтчета.Присоединить(Область);
	
	Если Не ПараметрыФормирования.НеВыводитьПроект Тогда
		
		Область = Макет.ПолучитьОбласть("ТехПроектПроект");
		Область.Параметры.Проект = СтрокаТехПроект.Проект;
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьВерсию Тогда
		
		Область = Макет.ПолучитьОбласть("ТехПроектВерсия");
		Область.Параметры.Версия = СтрокаТехПроект.Версия;
		
		Если Не ПараметрыФормирования.НеВыделятьСроки
			И СтрокаТехПроект.ДатаВерсииМеньшеДатыТехПроекта Тогда
			
			Область.Области.ТехПроектВерсия.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
			
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Если Не ПараметрыФормирования.НеВыводитьСроки Тогда
		
		Область = Макет.ПолучитьОбласть("ТехПроектСроки");
		Область.Параметры.ДатаНачала    = Формат(СтрокаТехПроект.ДатаНачала,    "ДФ=dd.MM.yy");
		Область.Параметры.ДатаОкончания = Формат(СтрокаТехПроект.ДатаОкончания, "ДФ=dd.MM.yy");
		
		Если Не ПараметрыФормирования.НеВыделятьСроки 
			И СтрокаТехПроект.Статус <> Перечисления.СтатусыТехническихПроектов.Выполнен
			И СтрокаТехПроект.ДатаТехПроектаМеньшеПлановыхДатЭтапа Тогда
		
			Область.Области.ДатаОкончанияТехПроекта.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
		КонецЕсли;
		
		ТаблицаОтчета.Присоединить(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ТехПроектТрудозатраты");
	Область.Параметры.ОстатокНаДату               = СтрокаТехПроект.ЗапланированоНаНачалоТекущего;
	Область.Параметры.Факт                        = СтрокаТехПроект.Факт;
	Область.Параметры.НераспределеноПланЗанятости = СтрокаТехПроект.Нераспределено;
	Область.Параметры.РезервПланЗанятости         = СтрокаТехПроект.Резерв;
	Область.Параметры.ФактМинусПланЗанятости      = СтрокаТехПроект.ФактМинусПланЗанятости;
	
	Если СтрокаТехПроект.Нераспределено <> 0 
		И Не ПараметрыФормирования.НеВыделятьНераспределено Тогда
		
		Область.Области.ТехПроектНераспределено.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	Если СтрокаТехПроект.Резерв <> 0 
		И Не ПараметрыФормирования.НеВыделятьРезерв Тогда
		
		Область.Области.ТехПроектРезерв.ЦветТекста = ЦветаСтиля.ЦветГиперссылкиЗадачи;
		
	КонецЕсли;
	
	Если СтрокаТехПроект.ФактМинусПланЗанятости < 0 
		И Не ПараметрыФормирования.НеВыделятьФактМинусПЗ Тогда
		
		Область.Области.ТехПроектФактМинусПЗ.ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
		
	КонецЕсли;
	
	ТаблицаОтчета.Присоединить(Область);
	ОтображаемыеКолонкиПрошлыйТекущийПериод = ОтображаемыеКолонкиПрошлыйТекущийПериод(ПараметрыФормирования);
	
	Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
		
		ДанныеКолонок =  ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана);
		
		Если ДанныеКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ПланЗанятости = СтрокаТехПроект[ДанныеКолонок.ПланЗанятости];
		
		Если ДанныеКолонок.ТипПериода = СловарьТекущийПериодПлана()
			Или ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана()
			И ОтображаемыеКолонкиПрошлыйТекущийПериод.Количество() > 1 Тогда
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("ПЗ") <> Неопределено Тогда
				ИмяОбласти = "ТехПроектПЗ";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.ПланЗанятости = ПланЗанятости;
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаТехПроект, ДанныеКолонок, Ложь);
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("РП") <> Неопределено Тогда
				
				ИмяОбласти = "ТехПроектРабочиеПланы";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.РабочиеПланы   = СтрокаТехПроект[ДанныеКолонок.РабочиеПланы];
				
				Если Не ОтклонениеДопустимо(СтрокаТехПроект[ДанныеКолонок.ПланЗанятости],
					                        СтрокаТехПроект[ДанныеКолонок.РабочиеПланы],
					                        ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                        ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                        Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаТехПроект, ДанныеКолонок, Ложь);
				ТаблицаОтчета.Присоединить(Область); 
				
			КонецЕсли;
			
			Если ОтображаемыеКолонкиПрошлыйТекущийПериод.Получить("Факт") <> Неопределено Тогда
				
				ИмяОбласти = "ТехПроектФакт";
				Область = Макет.ПолучитьОбласть(ИмяОбласти);
				Область.Параметры.Факт = СтрокаТехПроект[ДанныеКолонок.Факт];
				Если ДанныеКолонок.ТипПериода = СловарьПрошлыйПериодПлана() 
					И Не ОтклонениеДопустимо(СтрокаТехПроект[ДанныеКолонок.ПланЗанятости],
					                         СтрокаТехПроект[ДанныеКолонок.Факт],
					                         ПараметрыФормирования.ДопустимыйПроцентОтклонения,
					                         ПараметрыФормирования.АбсолютноеДопустимоеОтлонение,
					                         Истина) Тогда
					
					Область.Области[ИмяОбласти].ЦветТекста = ЦветаСтиля.ЗонаЗадачиКрасный;
					Если ПланЗанятости <> 0 Тогда
						Область.Области[ИмяОбласти].Формат = "ЧН=0";
					КонецЕсли;
					
				КонецЕсли;
				УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаТехПроект, ДанныеКолонок, Ложь);
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
		Иначе
			
			ИмяОбласти = "СтрокаТехПроектОднаКолонка";
			Область = Макет.ПолучитьОбласть(ИмяОбласти);
			Область.Параметры.ПланЗанятости = ПланЗанятости;
			УстановитьГраницуСнизуИПериодКорректенЕслиНеобходимо(Область, ИмяОбласти, ПараметрыФормирования,СтрокаТехПроект, ДанныеКолонок, Ложь);
			ТаблицаОтчета.Присоединить(Область);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОбластиДанныхШапкиОтчета(ПараметрыФормирования, ПодготовленныеДанные)
	
	ПараметрыОбласти = Новый Структура;
	ПараметрыОбласти.Вставить("ТипГруппировки",      "");
	ПараметрыОбласти.Вставить("ПериодФормирования",  "");
	ПараметрыОбласти.Вставить("ПредставлениеОтбора", "" );
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников" Тогда
		ПараметрыОбласти.ТипГруппировки = НСтр("ru = 'От сотрудников'");
			ИначеЕсли ПараметрыФормирования.ВариантГруппировки = "ОтТехПроектов" Тогда
		ПараметрыОбласти.ТипГруппировки = НСтр("ru = 'От технических проектов'");
	КонецЕсли;

	ПараметрыОбласти.ПериодФормирования  = Строка(ПараметрыФормирования.ПериодОтчета);
	ПараметрыОбласти.ПредставлениеОтбора = ПодготовленныеДанные.ПредставлениеОтбора;
	
	Возврат ПараметрыОбласти;
	
КонецФункции

Функция ЛинияТолщинаДва()
	
	Возврат Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2, Ложь);
	
КонецФункции

#КонецОбласти

#Область ПредставлениеВыводимыхДанных

Функция ПредставлениеОтбора(ПараметрыФормирования)
	
	ТекстПредставления = ПараметрыФормирования.ПредставлениеОтбора;
	
	ТекстПредставленияПрочиеОтборы = "";
	Если ПараметрыФормирования.ДопустимыйПроцентОтклонения > 0 Тогда
		
		ТекстПредставленияПрочиеОтборы = ТекстПредставленияПрочиеОтборы 
		                                 + СтрШаблон(НСтр("ru = 'Допустимый процент отклонения - %1.'"), ПараметрыФормирования.ДопустимыйПроцентОтклонения);
		
	КонецЕсли;
	
	КоличествоПараметров = 0;
	ТекстПредставленияОтборыПоОтклонениям = "";
	
	Если ПараметрыФормирования.ТолькоСОтклонениемРабочиеПланы Тогда
		
		ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + ?(КоличествоПараметров > 0, ", ", "");
		ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + НСтр("ru = 'рабочие планы'");
		КоличествоПараметров           = КоличествоПараметров + 1;
		
	КонецЕсли;
	
	Если ПараметрыФормирования.ТолькоСОтклонениемФакт Тогда
		
		ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + ?(КоличествоПараметров > 0, ", ", "");
		ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + НСтр("ru = 'факт'");
		КоличествоПараметров           = КоличествоПараметров + 1;
		
	КонецЕсли;
	
	Если ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников" Тогда
		
		Если ПараметрыФормирования.ТолькоСОтклонениемПревышениеНорматива Тогда
		
			ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + ?(КоличествоПараметров > 0, ", ", "");
			ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + НСтр("ru = 'превышение норматива рабочего времени'");
			КоличествоПараметров           = КоличествоПараметров + 1;
		
		КонецЕсли;
		
		Если ПараметрыФормирования.ТолькоСОтклонениемПревышениеНорматива Тогда
		
			ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + ?(КоличествоПараметров > 0, ", ", "");
			ТекстПредставленияОтборыПоОтклонениям = ТекстПредставленияОтборыПоОтклонениям + НСтр("ru = ' меньше рабочего времени'");
			КоличествоПараметров           = КоличествоПараметров + 1;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Если КоличествоПараметров > 0 Тогда
		
		ТекстЗаголовокОтборыПоОтклонениям = ?(ПустаяСтрока(ТекстПредставленияПрочиеОтборы), "", " "); 
		ТекстЗаголовокОтборыПоОтклонениям = ТекстЗаголовокОтборыПоОтклонениям 
		                                    + СтрШаблон(НСтр("ru = 'Только по %1, у которых отклонения превышают допустимые по %2:'"), 
		                                           ?(ПараметрыФормирования.ВариантГруппировки = "ОтСотрудников", НСтр("ru = 'сотрудникам'"), НСтр("ru = 'сотрудникам'")),
		                                           ?(КоличествоПараметров > 1, НСтр("ru = 'показателям'"), НСтр("ru = 'показателю'"))); 
		ТекстПредставленияОтборыПоОтклонениям = ТекстЗаголовокОтборыПоОтклонениям + " " + ТекстПредставленияОтборыПоОтклонениям;
		
	КонецЕсли; 
	
	ТекстПредставленияПрочиеОтборы = ТекстПредставленияПрочиеОтборы + ТекстПредставленияОтборыПоОтклонениям;
	
	Если КоличествоПараметров > 0 Тогда
		
		ТекстПредставления = ТекстПредставления + Символы.ПС + ТекстПредставленияПрочиеОтборы;
		
	КонецЕсли;
	
	Возврат ТекстПредставления ;
	
КонецФункции

#КонецОбласти

#Область Пересчеты

Процедура РассчитатьЗначениеРодительскойСтрокиПоДаннымПодчиненных(СтрокаДерева, ИмяКолонки) Экспорт
	
	СтрокаДерева[ИмяКолонки] = 0;
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		СтрокаДерева[ИмяКолонки] = СтрокаДерева[ИмяКолонки] + ПодчиненнаяСтрока[ИмяКолонки];
	КонецЦикла;
	
КонецПроцедуры

Процедура ПересчитатьМассивКолонокРодительскихСтрокДереваПоДаннымПодчиненных(Форма, ИмяДерева, МассивИменКолонок) Экспорт
	
	Для Каждого СтрокаВерхнегоУровня Из Форма[ИмяДерева].Строки Цикл
		
		ПересчитатьМассивКолонокРодительскойСтрокиПоДаннымПодчиненных(СтрокаВерхнегоУровня, МассивИменКолонок, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПересчитатьМассивКолонокРодительскойСтрокиПоДаннымПодчиненных(СтрокаВерхнегоУровня, МассивИменКолонок, ВключаяПодчиненные) Экспорт

	Если ВключаяПодчиненные Тогда
	
		Для Каждого ПодчиненнаяСтрока Из СтрокаВерхнегоУровня.Строки Цикл
			
			Для Каждого ИмяКолонки Из МассивИменКолонок Цикл
				
				РассчитатьЗначениеРодительскойСтрокиПоДаннымПодчиненных(ПодчиненнаяСтрока, ИмяКолонки);
				
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Для Каждого ИмяКолонки Из МассивИменКолонок Цикл
		
		РассчитатьЗначениеРодительскойСтрокиПоДаннымПодчиненных(СтрокаВерхнегоУровня, ИмяКолонки);
		
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти 

#Область Словарь

Функция СловарьТекущийПериодПлана()

	Возврат "Текущий";
	
КонецФункции 

Функция СловарьБудующийПериодПлана()

	Возврат "Будующий";
	
КонецФункции

Функция СловарьПрошлыйПериодПлана()

	Возврат "Прошлый";
	
КонецФункции

Функция НовыйСообщениеОбОшибке()
	
	ДанныеСообщения = Новый Структура;
	ДанныеСообщения.Вставить("ТекстОшибки", "");
	ДанныеСообщения.Вставить("ИмяРеквизита","");
	
	Возврат ДанныеСообщения;
	
КонецФункции

Функция ОписаниеТиповСотрудникРабота()
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Подразделения"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ЗадачиПроцесса"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ВидыДеятельности"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ТехническиеПроекты"));
	МассивТипов.Добавить(Тип("ПеречислениеСсылка.ТипыГруппировокСтрокПлана"));
	
	Возврат Новый ОписаниеТипов(МассивТипов);
	
КонецФункции

Функция ОписаниеТиповСтатус()
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ПеречислениеСсылка.СтатусыЗадачПроцессов"));
	МассивТипов.Добавить(Тип("ПеречислениеСсылка.СтатусыТехническихПроектов"));
	
	Возврат Новый ОписаниеТипов(МассивТипов);
	
КонецФункции

#КонецОбласти

#Область ИменаКолонок

Функция ИмяДобавленнойКолонкиПериода(ДеревоДанных, ИдентификаторПлана, ПрефиксКолонки, ИменаКолонок, ОписаниеТипа)
	
	ИмяКолонки = ПрефиксКолонки + "_" + ПланированиеКлиентСервер.ИдентификаторПланаСтрокой(ИдентификаторПлана);
	ДеревоДанных.Колонки.Добавить(ИмяКолонки,ОписаниеТипа);
	ИменаКолонок[ПрефиксКолонки] = ИмяКолонки; 
	
	Возврат ИмяКолонки;
	
КонецФункции

Функция НовыйИменаКолонокПериода()
	
	ИменаКолонок = Новый Структура;
	ИменаКолонок.Вставить("ПланЗанятости",          "");
	ИменаКолонок.Вставить("РабочиеПланы",           "");
	ИменаКолонок.Вставить("Факт",                   "");
	ИменаКолонок.Вставить("ЗапланированоЗадач",     "");
	ИменаКолонок.Вставить("ПериодКорректен",        Ложь);
	ИменаКолонок.Вставить("ЕстьОтклоненияПоОтбору", Ложь);
	ИменаКолонок.Вставить("ТипПериода",             ""); 

	Возврат ИменаКолонок;
	
КонецФункции

Функция ИмяКолонкиОстатокТекущийПериод(ПодготовленныеДанные, ТаблицаПланов) 
	
	ИмяКолонки = "";
	
	Для Каждого ИменаКолонокПоИдентификатору Из ПодготовленныеДанные.ИдентификаторыИменаКолонок Цикл
		
		Если ИменаКолонокПоИдентификатору.Значение.ТипПериода = СловарьТекущийПериодПлана() Тогда
			
			СтрокаТаблицыПланов = СтрокаТаблицыПлановПоЗначениюКолонки(ТаблицаПланов, "ИдентификаторПлана", ИменаКолонокПоИдентификатору.Ключ);
			
			Если СтрокаТаблицыПланов <> Неопределено Тогда
				
				ИмяКолонки = СтрШаблон(НСтр("ru = 'Остаток на %1'"), Формат(СтрокаТаблицыПланов.НачалоПериода, "ДФ=dd.MM.yy"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИмяКолонки;
	
КонецФункции

#КонецОбласти

#Область ТаблицаПланов

Функция ТаблицаПланов(ПараметрыФормирования, ДанныеВидаПлана)
	
	ДанныеПланов = Новый Структура;
	ДанныеПланов.Вставить("ТаблицаПланов",                     Неопределено);
	ДанныеПланов.Вставить("ВидСогласуемогоРесурсаПлановыхРабот", Неопределено);
	
	ПараметрыПолученияПлановПоПериоду = Справочники.ВидыПланов.НовыйПараметрыПолученияДанныхПлановПоПериоду();
	ПараметрыПолученияПлановПоПериоду.ВидПлана              = ПараметрыФормирования.ПланЗанятости;
	ПараметрыПолученияПлановПоПериоду.ДатаНачала            = ПараметрыФормирования.ПериодОтчета.ДатаНачала;
	ПараметрыПолученияПлановПоПериоду.НастройкиПланирования = ДанныеВидаПлана.ВсеНастройкиПланирования[0];
	ПараметрыПолученияПлановПоПериоду.ДатаОкончания         = ПараметрыФормирования.ПериодОтчета.ДатаОкончания;
	
	ТаблицаПланов =  Справочники.ВидыПланов.ДанныеПлановПоЗаданномуПериоду(ПараметрыПолученияПлановПоПериоду);
	
	ДополнитьТаблицуПлановТипомПериода(ТаблицаПланов, ТекущаяДатаСеанса());
	
	ДанныеПланов.ВидСогласуемогоРесурсаПлановыхРабот = ДанныеВидаПлана.ВидСогласуемогоРесурсаПлановыхРабот;
	ДанныеПланов.ТаблицаПланов                       = ТаблицаПланов;
	
	Возврат ДанныеПланов;
	
КонецФункции 

Процедура ДополнитьТаблицуПлановТипомПериода(ТаблицаПланов, ТекущаяДата) 
	
	ТаблицаПланов.Колонки.Добавить("ТипПериода", Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаТаблицы Из ТаблицаПланов Цикл
		
		Если СтрокаТаблицы.НачалоПериода >  ТекущаяДата Тогда
			СтрокаТаблицы.ТипПериода = СловарьБудующийПериодПлана();
		ИначеЕсли СтрокаТаблицы.КонецПериода < ТекущаяДата Тогда
			СтрокаТаблицы.ТипПериода = СловарьПрошлыйПериодПлана();
		Иначе
			СтрокаТаблицы.ТипПериода = СловарьТекущийПериодПлана();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИдентификаторыОтображаемыхПланов(ТаблицаПланов)
	
	Возврат ТаблицаПланов.ВыгрузитьКолонку("ИдентификаторПлана");
	
КонецФункции

Функция СтрокаТаблицыПлановПоЗначениюКолонки(ТаблицаПланов, ИмяКолонки, Значение) 
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить(ИмяКолонки, Значение);
	
	НайденныеСтроки = ТаблицаПланов.НайтиСтроки(ПараметрыПоиска);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат НайденныеСтроки[0];
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Отборы

Процедура СкрытьПериодыНеСоответствующиеОтбору(ПараметрыФормирования, ДеревоДанных, ТаблицаПланов, ИдентификаторыИменаКолонок)

	Если ПараметрыФормирования.СкрыватьПериодыНеСоотствующиеОтбору Тогда
		
		Для Каждого СтрокаТаблицыПланов Из ТаблицаПланов Цикл
			
			ИменаКолонок = ИдентификаторыИменаКолонок.Получить(СтрокаТаблицыПланов.ИдентификаторПлана); 
			
			ВПериодеЕстьОтклонения = ВПериодеЕстьОтклонения(ДеревоДанных, ИменаКолонок);
			
			Если Не ВПериодеЕстьОтклонения Тогда
				
				УдалитьКолонкиПериодаИзТаблицыПодготовленныхДанных(ДеревоДанных, ИменаКолонок);
				ИдентификаторыИменаКолонок.Удалить(СтрокаТаблицыПланов.ИдентификаторПлана);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьКолонкиПериодаИзТаблицыПодготовленныхДанных(ДеревоДанных, ИменаКолонок)
	
	Для Каждого ИмяКолонки Из ИменаКолонок Цикл
		
		КолонкаДерева = ДеревоДанных.Колонки.Найти(ИмяКолонки.Значение);
		
		Если КолонкаДерева <> Неопределено  Тогда
			
			ДеревоДанных.Колонки.Удалить(КолонкаДерева);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВПериодеЕстьОтклонения(ДеревоДанных, ИменаКолонок)
	
	ИмяКолонкиЕстьОтклонения = ИменаКолонок.ЕстьОтклоненияПоОтбору;
	
	Для Каждого СтрокаПервогоУровня Из ДеревоДанных.Строки Цикл
		
		Если СтрокаПервогоУровня[ИмяКолонкиЕстьОтклонения] Тогда
			Возврат Истина;
		КонецЕсли;
		
		Для Каждого СтрокаВторогоУровня Из СтрокаПервогоУровня.Строки Цикл
			
			Если СтрокаВторогоУровня[ИмяКолонкиЕстьОтклонения] Тогда
				Возврат Истина;
			КонецЕсли;
			
			Для Каждого СтрокаТретьегоУровня Из СтрокаВторогоУровня.Строки Цикл
				
				Если СтрокаТретьегоУровня[ИмяКолонкиЕстьОтклонения] Тогда
					Возврат Истина;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
