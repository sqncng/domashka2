#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПереданныеПараметры();
	
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиОбъектПоКоду(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СписокСсылок = ОбъектПоКодуСервер();
	
	Если СписокСсылок = Неопределено Тогда
		Если ВариантПоиска = 0 Тогда
			ТекстСообщения = НСтр("ru='Ошибка с указанным кодом не найдена'");
		ИначеЕсли ВариантПоиска = 1 Тогда
			ТекстСообщения = НСтр("ru='Идея с указанным кодом не найдена'");
		ИначеЕсли ВариантПоиска = 2 Тогда
			ТекстСообщения = НСтр("ru='Проект с указанным кодом не найден'");
		ИначеЕсли ВариантПоиска = 3 Тогда
			ТекстСообщения = НСтр("ru='Задача с указанным кодом не найдена'");
		ИначеЕсли ВариантПоиска = 4 Тогда
			ТекстСообщения = НСтр("ru='Сценарий с указанным кодом не найден'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если СписокСсылок.Количество() > 1 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("НайтиОбъектПоКодуЗавершение", ЭтотОбъект);
		СписокСсылок.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Результат поиска'"),);
	Иначе
		НайтиОбъектПоКодуЗавершение(СписокСсылок[0], Неопределено);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НайтиОбъектПоКодуЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		Если ВозвращатьНайденноеЗначение Тогда
			
			Закрыть(ВыбранныйЭлемент.Значение);
			
		Иначе
		
			ПоказатьЗначение(, ВыбранныйЭлемент.Значение);
			Если ЗакрыватьФормуПослеУдачногоПоиска Тогда
				Закрыть();
			КонецЕсли;
		
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ОбъектПоКодуСервер()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 10
	|	ИмяТаблицы.Ссылка
	|ИЗ
	|	&ИмяТаблицы КАК ИмяТаблицы
	|ГДЕ
	|	ИмяТаблицы.Код ПОДОБНО &Код";
	
	Если ВариантПоиска = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", "Справочник.Ошибки");
	ИначеЕсли ВариантПоиска = 1 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", "Справочник.Идеи");
	ИначеЕсли ВариантПоиска = 2 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", "Справочник.ТехническиеПроекты");
	ИначеЕсли ВариантПоиска = 3 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", "Справочник.ЗадачиПроцесса");
	ИначеЕсли ВариантПоиска = 4 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", "Справочник.СценарииРаботыПользователей");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	КодДляПоиска = СОКРЛП(Код);
	
	Если СтрДлина(КодДляПоиска)<12 Тогда
		КодДляПоиска = "%" + КодДляПоиска;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Код", КодДляПоиска);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СписокСсылок = Новый СписокЗначений;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокСсылок.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат СписокСсылок;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)
	
	Если Форма.ВозвращатьНайденноеЗначение Тогда
		Форма.Элементы.ЗакрыватьФормуПослеУдачногоПоиска.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПереданныеПараметры()

	Если ЗначениеЗаполнено(Параметры.ОбластьПоиска) Тогда
		
		СписокВыбора = Элементы.ВариантПоиска.СписокВыбора;
		СписокВыбора.Очистить();
		
		Для Каждого ЭлементСписка Из Параметры.ОбластьПоиска Цикл
			 СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
	ВозвращатьНайденноеЗначение = Параметры.ВозвращатьНайденноеЗначение;

КонецПроцедуры

#КонецОбласти
