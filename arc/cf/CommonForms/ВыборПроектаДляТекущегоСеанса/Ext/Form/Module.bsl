
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Проект = ПараметрыСеанса.ТекущийПроект;
	ТекущийПроект = Проект;
	
	//УстановитьЗначениеФООтображенияКомандИнтерфейсаСценариевРаботыПользователя(Проект)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	Если Проект <> ТекущийПроект  Тогда
		
		УстановитьПараметрСеансаИНастройкуПоУмолчанию();
		Оповестить("ИзмененТекущийПроект", Проект);
		УстановитьЗначениеФООтображенияКомандИнтерфейсаСценариевРаботыПользователя(Проект);
		
		ОбновитьПовторноИспользуемыеЗначения();
		СтандартныеПодсистемыКлиент.УстановитьРасширенныйЗаголовокПриложения();
		
		ТекущийПроект = Проект;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьЗначениеФООтображенияКомандИнтерфейсаСценариевРаботыПользователя(Проект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрСеансаИНастройкуПоУмолчанию()
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ТекущийПроект = Проект;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ПроектПоУмолчанию", Проект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеФООтображенияКомандИнтерфейсаСценариевРаботыПользователя(Проект)
	Если ЗначениеЗаполнено(Проект) Тогда
		УстанавливаемыеПараметры = Новый Структура;
		УстанавливаемыеПараметры.Вставить("Проект", Проект);
		УстановитьПараметрыФункциональныхОпцийИнтерфейса(УстанавливаемыеПараметры);
	Иначе	
		СбрасываемыеПараметры = Новый Массив;
		СбрасываемыеПараметры.Добавить("Проект");
		УстановитьПараметрыФункциональныхОпцийИнтерфейса(Новый Структура,СбрасываемыеПараметры);
	КонецЕсли;	 
КонецПроцедуры 

#КонецОбласти
