
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	АдресСогласующиеПоПредмету = Параметры.АдресСогласующиеПоПредмету;
	
	ЕстьОшибка = Ложь;
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресСогласующиеПоПредмету) Тогда
		ДанныеСогласующих = ПолучитьИзВременногоХранилища(Параметры.АдресСогласующиеПоПредмету);
		Если ТипЗнч(ДанныеСогласующих) = Тип("ТаблицаЗначений") Тогда
			СогласующиеПоПредмету.Загрузить(ДанныеСогласующих);
		Иначе
			ЕстьОшибка = Истина;
		КонецЕсли;
	Иначе
		ЕстьОшибка = Истина;
	КонецЕсли;
		
	Если ЕстьОшибка Тогда
		ВызватьИсключение НСтр("ru = 'В форму согласующих по предмету переданы некорректные параметры'");
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность
		И Не ВыполняетсяЗакрытие
		И Не ЗавершениеРаботы Тогда
		
		Отказ = Истина;
		
		ОповещениеПослеОтветаНаВопрос = Новый ОписаниеОповещения("ОтветНаВопросЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		ПоказатьВопрос(ОповещениеПослеОтветаНаВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);		
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ПеренестиИзмененныеДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СогласующиеПоПредметуСогласующий.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СогласующиеПоПредмету.Согласующий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указан'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступнаяДляВыбораЗадача);
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеСогласующихВоВременноеХранилище()

	ПоместитьВоВременноеХранилище(СогласующиеПоПредмету.Выгрузить(), АдресСогласующиеПоПредмету);

КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()

	ПравоИзменения = ЗадачиПроцессов.ПравоИзмененияЗадачБезКонтроля();
	Если Не ПравоИзменения
		Или ТолькоПросмотр Тогда
		
		ТолькоПросмотр = Истина;
		Элементы.ФормаГотово.Видимость         = Ложь;
		Элементы.ФормаОтмена.Заголовок         = НСтр("ru = 'Закрыть'");
		Элементы.ФормаОтмена.КнопкаПоУмолчанию = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт

	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		
		ПеренестиИзмененныеДанные();
		
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		
		ВыполняетсяЗакрытие = Истина;
		Закрыть(Неопределено);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиИзмененныеДанные()

	ВыполняетсяЗакрытие = Истина;
	
	Если Модифицированность Тогда
		ПоместитьДанныеСогласующихВоВременноеХранилище();
		Закрыть(АдресСогласующиеПоПредмету)
	Иначе
		Закрыть(Неопределено);
	КонецЕсли;

КонецПроцедуры


#КонецОбласти
