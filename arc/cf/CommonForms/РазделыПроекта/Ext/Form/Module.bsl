#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Проект = Параметры.Проект;
	МассивРазделов = Параметры.МассивРазделов;
	
	ИзмененияДоступны = Параметры.ИзмененияДоступны;
	
	Если ЗначениеЗаполнено(Проект) Тогда
		ДеревоРазделов = Справочники.РазделыПроекта.ДеревоРазделовПроекта(Проект);
		УстановитьОтметкиИспользованияРазделов(МассивРазделов, ДеревоРазделов);
		ЗначениеВРеквизитФормы(ДеревоРазделов, "РазделыПроекта");
	КонецЕсли;
	
	УстановитьДоступностьЭлементов(ИзмененияДоступны);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовУправленияТаблицыРазделыПроекта

&НаКлиенте
Процедура РазделыПроектаИспользованиеПриИзменении(Элемент)
	
	СтрокаДерева = РазделыПроекта.НайтиПоИдентификатору(Элементы.РазделыПроекта.ТекущаяСтрока);
	
	Если СтрокаДерева <> Неопределено Тогда
		
		Если СтрокаДерева.Использование = 2 Тогда
			СтрокаДерева.Использование = 0;
		КонецЕсли;
		
		УстановитьОтметкиРекурсивно(СтрокаДерева.ПолучитьЭлементы(), СтрокаДерева.Использование);
		УстановитьОтметкиРодителейРекурсивно(СтрокаДерева);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьОтметки(Команда)
	
	Строки = РазделыПроекта.ПолучитьЭлементы();
	УстановитьОтметкиРекурсивно(Строки, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Строки = РазделыПроекта.ПолучитьЭлементы();
	УстановитьОтметкиРекурсивно(Строки, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	МассивРазделов = Неопределено;
	
	Если Модифицированность Тогда
		МассивРазделов = Новый Массив;
		ЗаполнитьМассивИспользуемыхРазделов(МассивРазделов, РазделыПроекта.ПолучитьЭлементы());
		Оповестить("ИзменениеСоставаРазделовПроекта", МассивРазделов, ЭтаФорма);
	КонецЕсли;
	
	Закрыть(МассивРазделов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтметкиРекурсивно(Строки, Отметка)
	
	Для Каждого Строка из Строки Цикл
		Строка.Использование = Отметка;
		УстановитьОтметкиРекурсивно(Строка.ПолучитьЭлементы(), Отметка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтметкиРодителейРекурсивно(СтрокаДерева)
	
	Родитель = СтрокаДерева.ПолучитьРодителя();
	
	Если Родитель <> Неопределено Тогда
		
		Подчиненные = Родитель.ПолучитьЭлементы();
		
		ЕстьИспользуемые = Ложь;
		ЕстьНеиспользуемые = Ложь;
		
		Для Каждого Элемент из Подчиненные Цикл
			Если Элемент.Использование = 1 Тогда
				ЕстьИспользуемые = Истина;
			ИначеЕсли Элемент.Использование = 0 Тогда
				ЕстьНеиспользуемые = Истина;
			Иначе
				ЕстьИспользуемые = Истина;
				ЕстьНеиспользуемые = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьИспользуемые И ЕстьНеиспользуемые Тогда
			Родитель.Использование = 2;
		ИначеЕсли Не ЕстьИспользуемые Тогда
			Родитель.Использование = 0;
		Иначе
			Родитель.Использование = 1;
		КонецЕсли;
		
		УстановитьОтметкиРодителейРекурсивно(Родитель);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкиГруппРекурсивно(Строки)
	
	Для Каждого Строка из Строки Цикл
		
		Если Не Строка.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураОтбораИспользуемых = Новый Структура;
		СтруктураОтбораНеиспользуемых = Новый Структура;
		
		СтруктураОтбораИспользуемых.Вставить("Использование", 1);
		СтруктураОтбораИспользуемых.Вставить("ЭтоГруппа", Ложь);
		
		СтруктураОтбораНеиспользуемых.Вставить("Использование", 0);
		СтруктураОтбораНеиспользуемых.Вставить("ЭтоГруппа", Ложь);
		
		МассивИспользуемыхСтрок = Строка.Строки.НайтиСтроки(СтруктураОтбораИспользуемых, Истина);
		МассивНеиспользуемыхСтрок = Строка.Строки.НайтиСтроки(СтруктураОтбораНеиспользуемых, Истина);
		
		Если МассивИспользуемыхСтрок.Количество()=0 Тогда
			Строка.Использование = 0;
		ИначеЕсли МассивИспользуемыхСтрок.Количество()>0 И МассивНеиспользуемыхСтрок.Количество()=0 Тогда
			Строка.Использование = 1;
		Иначе
			Строка.Использование = 2;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМассивИспользуемыхРазделов(Массив, СтрокиДерева)
	
	Для Каждого Строка из СтрокиДерева Цикл
		Если Строка.Использование = 1 И НЕ Строка.ЭтоГруппа Тогда
			Массив.Добавить(Строка.РазделПроекта);
		КонецЕсли;
		
		Если Строка.Использование>0 Тогда
			ЗаполнитьМассивИспользуемыхРазделов(Массив, Строка.ПолучитьЭлементы());
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкиИспользованияРазделов(МассивРазделов, ДеревоРазделов)
	
	Если ТипЗнч(МассивРазделов) = Тип("Массив") Тогда
		
		Для Каждого Раздел из МассивРазделов Цикл
			СтруктураОтбора = Новый Структура("РазделПроекта", Раздел);
			МассивСтрок = ДеревоРазделов.Строки.НайтиСтроки(СтруктураОтбора, Истина);
			
			Если МассивСтрок.Количество()>0 Тогда
				СтрокаДерева = МассивСтрок[0];
				СтрокаДерева.Использование = 1;
			КонецЕсли;
			
		КонецЦикла;
		
		УстановитьОтметкиГруппРекурсивно(ДеревоРазделов.Строки);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов(ИзмененияДоступны)
	
	ТолькоПросмотр = НЕ ИзмененияДоступны;
	
	Если Элементы.Найти("РазделыПроектаОК") <> Неопределено Тогда
		Элементы.РазделыПроектаОК.Доступность = ИзмененияДоступны; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти