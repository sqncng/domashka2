
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Справочники.ОбъектыМетаданных.УстановитьТекстИндексаКартикиВТекстеЗапроса(Элементы.ОбъектыМетаданных);
	
	ВыборФункций 						= Параметры.ВыборФункций;
	ВыборЭлементовСправки 				= Параметры.ВыборЭлементовСправки;
	ВыборОбъектовМетаданных 			= Параметры.ВыборОбъектовМетаданных;
	ВыборФормОбъектовМетаданных 		= Параметры.ВыборФормОбъектовМетаданных;
	ВыборОсновныхФормОбъектовМетаданных = Параметры.ВыборОсновныхФормОбъектовМетаданных;
	ВыборКомандОбъектовМетаданных 		= Параметры.ВыборКомандОбъектовМетаданных;
	ВыборПодсистем 						= Параметры.ВыборПодсистем;
	ФункцияДляОтбора 					= Параметры.Функция;
	
	Если ВыборЭлементовСправки Тогда
		Заголовок = НСтр("ru='Выбор элементов справки'");
	ИначеЕсли ВыборФункций Тогда
		Заголовок = НСтр("ru='Выбор гиперссылки'");
	ИначеЕсли ВыборФормОбъектовМетаданных Тогда
		Заголовок = НСтр("ru='Выбор форм'");
	ИначеЕсли ВыборКомандОбъектовМетаданных Тогда
		Заголовок = НСтр("ru='Выбор команд'");
	Иначе
		Заголовок = НСтр("ru='Выбор гиперссылки'");
	КонецЕсли;
		
	Если Параметры.Свойство("МножественныйВыбор") Тогда
		МножественныйВыбор = Параметры.МножественныйВыбор;
	КонецЕсли;
	
	Если ВыборОбъектовМетаданных Тогда
		
		Элементы.ОбъектыМетаданных.МножественныйВыбор 		  = МножественныйВыбор;
		Элементы.ФормыОбъектовМетаданных.МножественныйВыбор   = МножественныйВыбор;
		Элементы.КомандыОбъектовМетаданных.МножественныйВыбор = МножественныйВыбор;
		Элементы.СписокТиповФорм.МножественныйВыбор			  = МножественныйВыбор;
		
		Если НЕ МножественныйВыбор Тогда
			Элементы.ОбъектыМетаданных.РежимВыделения 		  = РежимВыделенияТаблицы.Одиночный;
			Элементы.ФормыОбъектовМетаданных.РежимВыделения   = РежимВыделенияТаблицы.Одиночный;
			Элементы.КомандыОбъектовМетаданных.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
			Элементы.СписокТиповФорм.РежимВыделения			  = РежимВыделенияТаблицы.Одиночный;
		КонецЕсли;
		
	КонецЕсли;
	
	ТипФормы = Параметры.ТипФормы;
	
	Если Параметры.Свойство("Проект") Тогда
		Проект = Параметры.Проект;
	Иначе
		Проект = Неопределено;
	КонецЕсли;
	
	ОтбиратьМетаданныеПоФункции = ВыборОбъектовМетаданных И ЗначениеЗаполнено(ФункцияДляОтбора);
	
	ОбъектыМетаданных.Параметры.УстановитьЗначениеПараметра("Функция", ФункцияДляОтбора);
	ОбъектыМетаданных.Параметры.УстановитьЗначениеПараметра("ОтбиратьПоФункции", ОтбиратьМетаданныеПоФункции);
	ОбъектыМетаданных.Параметры.УстановитьЗначениеПараметра("Проект", Проект);
	
	УстановитьВидимостьДоступностьЭлементов();
	
	НачальноеЗначениеВыбора = Неопределено;
	Параметры.Свойство("НачальноеЗначениеВыбора", НачальноеЗначениеВыбора);
	ЕстьНачальноеЗначениеВыбора = ЗначениеЗаполнено(НачальноеЗначениеВыбора);
	
	Если НЕ ЕстьНачальноеЗначениеВыбора Тогда
		НачальноеЗначениеВыбора = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ФормаВыбораГиперссылки", "НачальноеЗначениеВыбора");
	КонецЕсли;
		
	Если ТипЗнч(НачальноеЗначениеВыбора) = Тип("СправочникСсылка.ФункцииСистемы") И ВыборФункций Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаФункции;
		Элементы.Функции.ТекущаяСтрока = НачальноеЗначениеВыбора;
		
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("СправочникСсылка.ОбъектыМетаданных") И ВыборОбъектовМетаданных Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбъектыМетаданныхФормы;
		Элементы.ОбъектыМетаданных.ТекущаяСтрока = НачальноеЗначениеВыбора;
		
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("СправочникСсылка.ФормыОбъектовМетаданных") И ВыборФормОбъектовМетаданных Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбъектыМетаданныхФормы;
		Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока = НачальноеЗначениеВыбора;
		
		ВладелецФормыОбъекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НачальноеЗначениеВыбора, "Владелец");
		Элементы.ОбъектыМетаданных.ТекущаяСтрока = ВладелецФормыОбъекта;
		
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("СправочникСсылка.КомандыОбъектовМетаданных") И ВыборКомандОбъектовМетаданных Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбъектыМетаданныхФормы;
		Элементы.КомандыОбъектовМетаданных.ТекущаяСтрока = НачальноеЗначениеВыбора;
		
		ВладелецКоманды = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НачальноеЗначениеВыбора, "Владелец");
		Элементы.ОбъектыМетаданных.ТекущаяСтрока = ВладелецКоманды;
		
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("СправочникСсылка.Подсистемы") И ВыборПодсистем Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПодсистемы;
		Элементы.Подсистемы.ТекущаяСтрока = НачальноеЗначениеВыбора;
		
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("СправочникСсылка.ЭлементыСправки") И ВыборЭлементовСправки Тогда
		
		ВладелецЭлементаСправки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НачальноеЗначениеВыбора, "Владелец");
		
		Если ТипЗнч(ВладелецЭлементаСправки) = Тип("СправочникСсылка.ОбъектыМетаданных") Тогда
			
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбъектыМетаданныхФормы;
			ТекущийЭлемент = Элементы.ОбъектыМетаданных;
			Элементы.ОбъектыМетаданных.ТекущаяСтрока = ВладелецЭлементаСправки;
			
		ИначеЕсли ТипЗнч(ВладелецЭлементаСправки) = Тип("СправочникСсылка.ФормыОбъектовМетаданных") Тогда
			
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбъектыМетаданныхФормы;
			ТекущийЭлемент = Элементы.ФормыОбъектовМетаданных;
			Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока = ВладелецЭлементаСправки;
			
		Иначе
			
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПодсистемы;
			Элементы.Подсистемы.ТекущаяСтрока = ВладелецЭлементаСправки;
			
		КонецЕсли;
		
		Элементы.ЭлементыСправки.ТекущаяСтрока = НачальноеЗначениеВыбора;
		
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("Структура") Тогда
		
		Если ТипЗнч(НачальноеЗначениеВыбора.Владелец) = Тип("СправочникСсылка.ФункцииСистемы") Тогда
			
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаФункции;
			Элементы.Функции.ТекущаяСтрока = НачальноеЗначениеВыбора.Владелец;
			
		ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора.Владелец) = Тип("СправочникСсылка.ОбъектыМетаданных") Тогда
			
		    Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбъектыМетаданныхФормы;
			Элементы.ОбъектыМетаданных.ТекущаяСтрока = НачальноеЗначениеВыбора.Владелец;
			Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока = НачальноеЗначениеВыбора.ТекущаяСтрока;
			
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ВыборОбъектовМетаданных И ЕстьНачальноеЗначениеВыбора Тогда
		НачальноеЗначениеОбъектаМетаданных = Элементы.ОбъектыМетаданных.ТекущаяСтрока;
		НачальноеЗначениеФормыОбъекта 	   = Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока;
		НачальноеЗначениеКомандыОбъекта    = Элементы.КомандыОбъектовМетаданных.ТекущаяСтрока;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Функции, "Владелец", Проект, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОбъектыМетаданных, "Владелец", Проект, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Подсистемы, "Владелец", Проект, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	
	Если НЕ ПустаяСтрока(Параметры.ИмяКласса) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОбъектыМетаданных, "ИмяКласса", Параметры.ИмяКласса, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(НачальноеЗначениеОбъектаМетаданных)
	 И НачальноеЗначениеОбъектаМетаданных <> Элементы.ОбъектыМетаданных.ТекущаяСтрока
	 И ОтбиратьМетаданныеПоФункции Тогда
		
		// Текущая строка в таблице объектов метаданных не соответствует переданному в форму начальному значению,
		// значит начальное значение не попадает в отбор по функции - снимем этот отбор.
		ОтбиратьМетаданныеПоФункции = Ложь;
		ОтбиратьМетаданныеПоФункцииПриИзменении(Неопределено);
		
		Элементы.ОбъектыМетаданных.ТекущаяСтрока 		 = НачальноеЗначениеОбъектаМетаданных;
		Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока 	 = НачальноеЗначениеФормыОбъекта;
		Элементы.КомандыОбъектовМетаданных.ТекущаяСтрока = НачальноеЗначениеКомандыОбъекта
		
 	КонецЕсли;
 	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтбиратьМетаданныеПоФункцииПриИзменении(Элемент)
	
	ОбъектыМетаданных.Параметры.УстановитьЗначениеПараметра("ОтбиратьПоФункции",ОтбиратьМетаданныеПоФункции);
	
КонецПроцедуры

&НаКлиенте
Процедура ФункцииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТиповФормВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено 
		И Элемент.ТекущиеДанные.Значение = ПредопределенноеЗначение("Перечисление.ТипыФорм.ПроизвольнаяФорма") Тогда
		ТекущийЭлемент = Элементы.ФормыОбъектовМетаданных;
	Иначе
		ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормыОбъектовМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандыОбъектовМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсистемыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлементыСправкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыМетаданныхПриАктивизацииСтроки(Элемент)
	
	Если ВыборФормОбъектовМетаданных Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФормыОбъектовМетаданных,
																			"Владелец",
																			Элементы.ОбъектыМетаданных.ТекущаяСтрока,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			Истина);
	КонецЕсли;
																		
	Если ВыборЭлементовСправки Тогда
		
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭлементыСправки,
																			"Владелец",
																			Элементы.ОбъектыМетаданных.ТекущаяСтрока,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			Истина);
	КонецЕсли;
	
	Если ВыборКомандОбъектовМетаданных Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(КомандыОбъектовМетаданных,
																			"Владелец",
																			Элементы.ОбъектыМетаданных.ТекущаяСтрока,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			Истина);
	КонецЕсли;
																		
	Если Элементы.ЭлементыСправки.ТекущаяСтрока <> Неопределено Тогда
		Элементы.ЭлементыСправки.Развернуть(Элементы.ЭлементыСправки.ТекущаяСтрока);
	КонецЕсли;
	
	ЗаполнитьСписокТиповФорм();
	
	ТекущиеДанные = Элементы.ОбъектыМетаданных.ТекущиеДанные;
	Владелец = ?(ТекущиеДанные <> Неопределено, ТекущиеДанные.Ссылка, Неопределено);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФормыОбъектовМетаданных, 
																			"Владелец",
																			Владелец,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТиповФормПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокТиповФорм.ТекущиеДанные;
	ДоступностьФорм = (ТекущиеДанные <> Неопределено И ТекущиеДанные.Значение = ПредопределенноеЗначение("Перечисление.ТипыФорм.ПроизвольнаяФорма"));
	Элементы.ФормыОбъектовМетаданных.Доступность = ДоступностьФорм;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормыОбъектовМетаданныхПриАктивизацииСтроки(Элемент)
	
	Если ТекущийЭлемент = Элементы.ФормыОбъектовМетаданных Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭлементыСправки,
		                                                                        "Владелец",
																				Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока,
																				ВидСравненияКомпоновкиДанных.Равно,
																				,
																				Истина);
		Если Элементы.ЭлементыСправки.ТекущаяСтрока <> Неопределено Тогда
			Элементы.ЭлементыСправки.Развернуть(Элементы.ЭлементыСправки.ТекущаяСтрока);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
                
&НаКлиенте
Процедура ПодсистемыПриАктивизацииСтроки(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭлементыСправки, "Владелец", Элементы.Подсистемы.ТекущаяСтрока, 
															ВидСравненияКомпоновкиДанных.Равно,,Истина);              															
	Если Элементы.ЭлементыСправки.ТекущаяСтрока <> Неопределено Тогда
		Элементы.ЭлементыСправки.Развернуть(Элементы.ЭлементыСправки.ТекущаяСтрока);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборГиперссылки(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборГиперссылки(СтандартнаяОбработка)
	
	ВыбранноеЗначение = Неопределено;
	
	Если ТекущийЭлемент = Элементы.Функции Тогда
		
		ВыбранноеЗначение = Элементы.Функции.ТекущаяСтрока;
		
	ИначеЕсли ТекущийЭлемент = Элементы.ОбъектыМетаданных Тогда
		
		Если МножественныйВыбор Тогда
			ВыбранноеЗначение = Элементы.ОбъектыМетаданных.ВыделенныеСтроки;
		Иначе
			Если ВыборФормОбъектовМетаданных И ВыборОсновныхФормОбъектовМетаданных Тогда
				СтруктураВыбора = Новый Структура;
				СтруктураВыбора.Вставить("ОбъектМетаданных", Элементы.ОбъектыМетаданных.ТекущаяСтрока);
				СтруктураВыбора.Вставить("ТипФормы", ПредопределенноеЗначение("Перечисление.ТипыФорм.ПроизвольнаяФорма"));
				СтруктураВыбора.Вставить("ФормаОбъектаМетаданных", ПредопределенноеЗначение("Справочник.ФормыОбъектовМетаданных.ПустаяСсылка"));
				
				ВыбранноеЗначение = СтруктураВыбора;
				
			Иначе
				ВыбранноеЗначение = Элементы.ОбъектыМетаданных.ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли;
	
	ИначеЕсли ТекущийЭлемент = Элементы.СписокТиповФорм Тогда
		
		СтруктураВыбора = Новый Структура;
		СтруктураВыбора.Вставить("ОбъектМетаданных", Элементы.ОбъектыМетаданных.ТекущаяСтрока);
		СтруктураВыбора.Вставить("ТипФормы", Элементы.СписокТиповФорм.ТекущиеДанные.Значение);
		СтруктураВыбора.Вставить("ФормаОбъектаМетаданных", ПредопределенноеЗначение("Справочник.ФормыОбъектовМетаданных.ПустаяСсылка"));
		
		ВыбранноеЗначение = СтруктураВыбора;
	
	ИначеЕсли ТекущийЭлемент = Элементы.ФормыОбъектовМетаданных Тогда
		
		Если МножественныйВыбор Тогда
			ВыбранноеЗначение = Элементы.ФормыОбъектовМетаданных.ВыделенныеСтроки;
		Иначе
			Если ВыборФормОбъектовМетаданных И ВыборОсновныхФормОбъектовМетаданных Тогда
				СтруктураВыбора = Новый Структура;
				СтруктураВыбора.Вставить("ОбъектМетаданных", Элементы.ОбъектыМетаданных.ТекущаяСтрока);
				СтруктураВыбора.Вставить("ТипФормы", ПредопределенноеЗначение("Перечисление.ТипыФорм.ПроизвольнаяФорма"));
				СтруктураВыбора.Вставить("ФормаОбъектаМетаданных", Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока);
				
				ВыбранноеЗначение = СтруктураВыбора;
			Иначе
				ВыбранноеЗначение = Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТекущийЭлемент = Элементы.КомандыОбъектовМетаданных Тогда
		
		Если МножественныйВыбор Тогда
			ВыбранноеЗначение = Элементы.КомандыОбъектовМетаданных.ВыделенныеСтроки;
		Иначе
			СтруктураВыбора = Новый Структура;
			СтруктураВыбора.Вставить("ОбъектМетаданных", Элементы.ОбъектыМетаданных.ТекущаяСтрока);
			СтруктураВыбора.Вставить("КомандаОбъектаМетаданных", Элементы.КомандыОбъектовМетаданных.ТекущаяСтрока);
				
			ВыбранноеЗначение = СтруктураВыбора;
		КонецЕсли;
		
	ИначеЕсли ТекущийЭлемент = Элементы.Подсистемы Тогда
		
		ВыбранноеЗначение = Элементы.Подсистемы.ТекущаяСтрока;
		
	ИначеЕсли ТекущийЭлемент = Элементы.ЭлементыСправки Тогда
		
		ВыбранноеЗначение = Элементы.ЭлементыСправки.ТекущаяСтрока;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыборЭлементовСправки И ТипЗнч(ВыбранноеЗначение) <> Тип("СправочникСсылка.ЭлементыСправки") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Следует выбирать только элементы справки'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройку()
	
	Значение = Неопределено;
	
	Если ТекущийЭлемент = Элементы.Функции Тогда
		
		Значение = Элементы.Функции.ТекущаяСтрока;
		
	ИначеЕсли ТекущийЭлемент = Элементы.ОбъектыМетаданных Тогда
		
		Значение = Элементы.ОбъектыМетаданных.ТекущаяСтрока;
		
	ИначеЕсли ТекущийЭлемент = Элементы.ФормыОбъектовМетаданных Тогда
		
		Структура = Новый Структура;
		Структура.Вставить("Владелец", Элементы.ОбъектыМетаданных.ТекущаяСтрока);
		Структура.Вставить("ТекущаяСтрока", Элементы.ФормыОбъектовМетаданных.ТекущаяСтрока);
		Значение = Структура;
		
	ИначеЕсли ТекущийЭлемент = Элементы.Подсистемы Тогда
		
		Значение = Элементы.Подсистемы.ТекущаяСтрока;
		
	ИначеЕсли ТекущийЭлемент = Элементы.ЭлементыСправки Тогда
		
		Значение = Элементы.ЭлементыСправки.ТекущаяСтрока;
		
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ФормаВыбораГиперссылки", "НачальноеЗначениеВыбора", Значение);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	Элементы.СтраницаФункции.Видимость = ВыборФункций;
	Элементы.СтраницаОбъектыМетаданныхФормы.Видимость = (ВыборОбъектовМетаданных ИЛИ ВыборЭлементовСправки);
	Элементы.СтраницаПодсистемы.Видимость = (ВыборПодсистем ИЛИ ВыборЭлементовСправки);
	
	Элементы.ЭлементыСправки.Видимость = ВыборЭлементовСправки;
	Элементы.ФормыОбъектовМетаданных.Видимость = (ВыборФормОбъектовМетаданных ИЛИ ВыборЭлементовСправки);
	Элементы.ГруппаТипыФорм.Видимость = ВыборОсновныхФормОбъектовМетаданных;
	Элементы.КомандыОбъектовМетаданных.Видимость = ВыборКомандОбъектовМетаданных;
	
	Элементы.ОтбиратьМетаданныеПоФункции.Видимость = (ВыборОбъектовМетаданных И ЗначениеЗаполнено(ФункцияДляОтбора));
	
	КоличествоВидимыхСтраниц = 0;
	Для Каждого Страница из Элементы.ГруппаСтраницы.ПодчиненныеЭлементы Цикл
		Если Страница.Видимость Тогда
			КоличествоВидимыхСтраниц = КоличествоВидимыхСтраниц + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоВидимыхСтраниц = 1 Тогда
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокТиповФорм()
	
	ТекущиеДанные = Элементы.ОбъектыМетаданных.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		СписокТиповФорм = РаботаСОбъектамиМетаданныхКлиентСервер.СписокДоступныхТиповФорм(ТекущиеДанные.ИмяКласса);
	Иначе
		СписокТиповФорм.Очистить();
	КонецЕсли;
	
	ТекущийТипФормы = СписокТиповФорм.НайтиПоЗначению(ТипФормы);
	
	Если ТекущийТипФормы <> Неопределено Тогда
		Элементы.СписокТиповФорм.ТекущаяСтрока = ТекущийТипФормы.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
