
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ссылка = Параметры.Ссылка;
	
	НачальноеИмяЗакладки = "";
	НачальныйТекстСправки = ВывестиСправку(Ссылка, НачальноеИмяЗакладки, Параметры.ВариантПроектСправки, Параметры.ДатаВерсииСправки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(НачальныйТекстСправки) Тогда
		ЗаписатьФайлПерейти(НачальныйТекстСправки, НачальноеИмяЗакладки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
		
	Попытка
		Идентификатор = ДанныеСобытия.Anchor.outerHTML;
	Исключение
		Идентификатор = "";
	КонецПопытки;
	
	ПозицияТочек = Найти(Идентификатор, "../");
	Если ПозицияТочек > 0 Тогда
		
		Идентификатор = Сред(Идентификатор, ПозицияТочек);
		КоличествоСимовлов = Найти(Идентификатор, """>");
		Если КоличествоСимовлов > 0 Тогда
			Идентификатор = Сред(Идентификатор, 1, КоличествоСимовлов - 1);
		КонецЕсли;
		
		ИмяЗакладки = "";
		ТекстСправки = ВывестиСправку(Идентификатор, ИмяЗакладки);
		ЗаписатьФайлПерейти(ТекстСправки, ИмяЗакладки);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.ПолеHTMLДокумента.Назад();
	
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	
	Элементы.ПолеHTMLДокумента.Вперед();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВывестиСправку(Идентификатор, ИмяЗакладки, ВариантПроектСправки = Неопределено, ДатаВерсииСправки = '00010101000000')
	
	// Имитация html - перехода по ссылке
	ИмяЗакладки = "";
	Если ТипЗнч(Идентификатор) = Тип("Строка") Тогда
		
		// Имитировать переход на закладку другой страницы не удается
		// обеспечим переход только на новую страницу
		КоличествоСимволов = Найти(Идентификатор, "#");
		
		Если КоличествоСимволов > 0 Тогда
			ИмяЗакладки = Сред(Идентификатор,КоличествоСимволов + 1);
			Идентификатор = Сред(Идентификатор, 1, КоличествоСимволов - 1);
		КонецЕсли;
		
		Идентификатор = СтрЗаменить(Идентификатор, "../", "");
		
		Идентификатор = "{""#""" + Идентификатор + "}";
		Ссылка = ЗначениеИзСтрокиВнутр(Идентификатор);
	Иначе
	    Ссылка = Идентификатор;
	КонецЕсли;
	
	ПараметрыФормированияСправки = ФормированиеСправкиСервер.ПараметрыФормированияСправки();
	ПараметрыФормированияСправки.Вставить("ИспользоватьСсылкиСППР", Истина);
	ПараметрыФормированияСправки.Вставить("ВариантПроектСправки", ВариантПроектСправки);
	ПараметрыФормированияСправки.Вставить("ДатаВерсии",           ДатаВерсииСправки);
	ТекстСправки = ФормированиеСправкиСервер.СформироватьСправку(Ссылка, ПараметрыФормированияСправки);
	
	ТекстСправки ="<HTML>" + ТекстСправки + "<HTML>";
	
	Если ЗначениеЗаполнено(ДатаВерсииСправки) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Справка объекта %1 от %2'"),
							Строка(Ссылка),
							Формат(Параметры.ДатаВерсииСправки, "ДФ='dd.MM.yyyy ЧЧ:мм'"));
	Иначе	
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Справка объекта %1'"),
							Строка(Ссылка));
	КонецЕсли;
	
	Возврат ТекстСправки;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьФайлПерейти(ТекстСправки, ИмяЗакладки)
	
	#Если Не ВебКлиент Тогда
	Если ПустаяСтрока(Каталог) Тогда
		Каталог = КаталогВременныхФайлов();
	КонецЕсли;
	
	// Обработка ситуации, когда каталог заканчивается или нет на слэш
	Если Сред(Каталог, СтрДлина(Каталог)) = "\" Тогда
		ИмяФайла = Каталог;
	Иначе
		ИмяФайла = Каталог + "\";
	КонецЕсли;
	
	// Удаление из имени запрещенных символов
	Наименование = НаименованиеОбъекта(Ссылка);
	
	ПрообразИмени = СтрЗаменить(СтрЗаменить(СтрЗаменить(Наименование, "/", ""),"%",""),"\","");
	ПрообразИмени = СтрЗаменить(СтрЗаменить(СтрЗаменить(ПрообразИмени, "<", ""),">",""),"|","");
	ПрообразИмени = СтрЗаменить(СтрЗаменить(ПрообразИмени, "*", ""),"?","");
	ПрообразИмени = СтрЗаменить(ПрообразИмени, """","");
	ПрообразИмени = СтрЗаменить(ПрообразИмени, Символы.ПС,"");
	ПрообразИмени = СтрЗаменить(ПрообразИмени, ":","");
	
	ИмяФайла = ИмяФайла + ПрообразИмени + ".htm";
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстСправки);
	ТекстовыйДокумент.Записать(ИмяФайла);
	
	Если ПустаяСтрока(ИмяЗакладки) Тогда
		Текст = ИмяФайла;
	Иначе
		Текст = ИмяФайла+"#"+ИмяЗакладки;
	КонецЕсли;
	
	#КонецЕсли

КонецПроцедуры

&НаСервере
Функция НаименованиеОбъекта(Ссылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование");
	
КонецФункции

#КонецОбласти
