#Область Переменные

&НаКлиенте
Перем ЗакладкиРедактора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("ЕстьКешФайловРедактора") Тогда
		ЕстьКешФайловРедактора = Параметры.ЕстьКешФайловРедактора;
	КонецЕсли;
	
	Если НЕ ЕстьКешФайловРедактора Тогда
		Попытка
			ДвоичныеДанные = ПолучитьОбщийМакет("РедакторТекстов");
			МакетРедактора = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		Исключение
			МакетРедактора = "";
		КонецПопытки;
	КонецЕсли;	
	
	Если Параметры.Свойство("КаталогРедактора") Тогда
		КаталогРедактора = Параметры.КаталогРедактора;
	КонецЕсли;	
	Если Параметры.Свойство("ТекстДляСравнения1") Тогда
		ТекстДляСравнения1 = Параметры.ТекстДляСравнения1;
	КонецЕсли;	
	Если Параметры.Свойство("ТекстДляСравнения2") Тогда
		ТекстДляСравнения2 = Параметры.ТекстДляСравнения2;
	КонецЕсли;	
	Если Параметры.Свойство("ОписаниеТекста1") Тогда
		ОписаниеТекста1 = Параметры.ОписаниеТекста1;
	КонецЕсли;	
	Если Параметры.Свойство("ОписаниеТекста2") Тогда
		ОписаниеТекста2 = Параметры.ОписаниеТекста2;
	КонецЕсли;	
	Если Параметры.Свойство("ВыполнитьСравнениеДвухТекстов") Тогда
		ВыполнитьСравнениеДвухТекстов = Параметры.ВыполнитьСравнениеДвухТекстов;
	КонецЕсли;	
	Если Параметры.Свойство("ИмяФайлаРепозитория") Тогда
		ИмяФайлаРепозитория = Параметры.ИмяФайлаРепозитория;
		Заголовок = ИмяФайлаРепозитория;
	КонецЕсли;	
	
	ПоказыватьРазличияДвумяСтолбцами = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"Тестирование", 
		"ПоказыватьРазличияДвумяСтолбцами");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если НЕ ВебКлиент Тогда
	
	Если НЕ ЕстьКешФайловРедактора Тогда
		Если НЕ ЗначениеЗаполнено(КаталогРедактора) Тогда
			КаталогРедактора = ПолучитьИмяВременногоФайла();
			СоздатьКаталог(КаталогРедактора);
			НадоУдалитьКаталогРедактора = Истина;
		КонецЕсли;	

		Файл = Новый Файл(КаталогРедактора);
		Если НЕ Файл.Существует() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Каталог <%1> не существует.'"),КаталогРедактора);
		КонецЕсли;	
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(МакетРедактора);
		УдалитьИзВременногоХранилища(МакетРедактора);
		МакетРедактора = ""; 
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ДвоичныеДанные.ОткрытьПотокДляЧтения());
		ЧтениеZipФайла.ИзвлечьВсе(КаталогРедактора, РежимВосстановленияПутейФайловZIP.Восстанавливать);
		ЧтениеZipФайла.Закрыть();
	КонецЕсли;                     
	
	Файл = Новый Файл(КаталогРедактора);
	Если НЕ Файл.Существует() Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Каталог <%1> не существует.'"),КаталогРедактора);
	КонецЕсли;	
	
	СсылкаРедактора = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогРедактора) + "index.html?localeCode=" + Лев(ТекущийЯзыкСистемы(), 2);
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НадоУдалитьКаталогРедактора Тогда
		Попытка
			УдалитьФайлы(КаталогРедактора);
		Исключение
			ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации("РедакторСценариев","Ошибка",ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаРедактораДокументСформирован(Элемент)
	ИнициализироватьРедактор();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьРазличияДвумяСтолбцамиПриИзменении(Элемент)
	
	ЗакладкиРедактора.current.editor.setSideBySide(ПоказыватьРазличияДвумяСтолбцами);
	СохранитьНастройкиСервер(ПоказыватьРазличияДвумяСтолбцами);
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НужноЗакрытьФорму = Ложь;
	
	Element = ДанныеСобытия.Element;
	Если Element.id = "VanessaEditorEventForwarder" Тогда
		Пока Истина Цикл
			msg = Элементы.Редактор.Document.defaultView.popVanessaMessage();
			Если (msg = Неопределено) Тогда
				Прервать;
			КонецЕсли;
			
			Если msg.type = "PRESS_ESCAPE" Тогда
				НужноЗакрытьФорму = Истина;
			КонецЕсли;	
			
		КонецЦикла;
	КонецЕсли;
	
	Если НужноЗакрытьФорму Тогда
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКПредыдущемуИзменению(Команда)
	ЗакладкиРедактора.previousDiff();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСледующемуИзменению(Команда)
	ЗакладкиРедактора.nextDiff();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнициализироватьРедактор()
	Представление = Элементы.Редактор.Document.defaultView;
	ЗакладкиРедактора = Представление.createVanessaTabs();
	
	ЗакладкиРедактора.diff(
		ТекстДляСравнения1,
		ОписаниеТекста1,
		"Файл1",
		ТекстДляСравнения2,
		ОписаниеТекста2,
		"Файл2",
		СтрШаблон(НСтр("ru = 'Сравнивается <%1> с <%2>'"), ОписаниеТекста1, ОписаниеТекста2),
		0,
		Истина, Истина); 
		
	ЗакладкиРедактора.current.editor.setSideBySide(ПоказыватьРазличияДвумяСтолбцами);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиСервер(ПоказыватьРазличияДвумяСтолбцами)
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Тестирование",
		                                                 "ПоказыватьРазличияДвумяСтолбцами",
														 ПоказыватьРазличияДвумяСтолбцами
														 ,
														 ,
														 ,
														 Истина);
КонецПроцедуры

#КонецОбласти