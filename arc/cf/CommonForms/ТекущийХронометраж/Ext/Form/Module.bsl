#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ПрочитатьДанныеХронометража();
	УстановитьОтображаемуюСтраницу(Элементы, ТекущаяРабота, ОписаниеРаботы);
	УстановитьПризнакГиперссылки(Элементы, ТекущаяРабота);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПереключенХронометраж" Тогда
		ПрочитатьДанныеХронометража();
		УстановитьОтображаемуюСтраницу(Элементы, ТекущаяРабота, ОписаниеРаботы);
		УстановитьПризнакГиперссылки(Элементы, ТекущаяРабота);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Завершить(Команда)
	
	ДанныеХронометража = ЗавершитьНаСервере();
	
	ТекущаяРабота = Неопределено;
	ОписаниеРаботы = "";
	
	УстановитьОтображаемуюСтраницу(Элементы, ТекущаяРабота, ОписаниеРаботы);
	УстановитьПризнакГиперссылки(Элементы, ТекущаяРабота);
	
	Оповестить("ПереключенХронометраж", ДанныеХронометража, ЭтотОбъект);
	
	Если ДанныеХронометража.ХронометражЗавершен Тогда
		ДанныеОповещения = Новый Структура;
		ДанныеОповещения.Вставить("Начало", ДанныеХронометража.НачалоЗавершеннойРаботы);
		ДанныеОповещения.Вставить("Окончание", ДанныеХронометража.ОкончаниеЗавершеннойРаботы);
		
		Оповестить("ЗаписаныДанныеКалендаря", ДанныеОповещения, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОписаниеИЗавершить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьОписаниеЗавершение", ЭтотОбъект);
	
	КвалификаторыСтроки = Новый КвалификаторыСтроки(250);
	ОписаниеТипов = Новый ОписаниеТипов("Строка",,,, КвалификаторыСтроки);
	
	ЗаголовокВвода = НСтр("ru='Укажите описание работы'");
	
	ПоказатьВводЗначения(ОписаниеОповещения, ОписаниеРаботы, ЗаголовокВвода, ОписаниеТипов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОписаниеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовоеОписаниеРаботы = Неопределено;
	
	Если ОписаниеРаботы <> Результат Тогда
		НовоеОписаниеРаботы = Результат;
	КонецЕсли;
	
	ДанныеХронометража = ЗавершитьНаСервере(НовоеОписаниеРаботы);
	
	ТекущаяРабота = Неопределено;
	ОписаниеРаботы = "";
	
	УстановитьОтображаемуюСтраницу(Элементы, ТекущаяРабота, ОписаниеРаботы);
	УстановитьПризнакГиперссылки(Элементы, ТекущаяРабота);
	
	Оповестить("ПереключенХронометраж", ДанныеХронометража, ЭтотОбъект);
	
	Если ДанныеХронометража.ХронометражЗавершен Тогда
		ДанныеОповещения = Новый Структура;
		ДанныеОповещения.Вставить("Начало", ДанныеХронометража.НачалоЗавершеннойРаботы);
		ДанныеОповещения.Вставить("Окончание", ДанныеХронометража.ОкончаниеЗавершеннойРаботы);
		
		Оповестить("ЗаписаныДанныеКалендаря", ДанныеОповещения, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьДанныеХронометража()
	
	ДанныеХронометража = УчетВремени.ДанныеХронометража(ТекущийПользователь);
	
	ТекущаяРабота = ДанныеХронометража.Работа;
	ОписаниеРаботы = ДанныеХронометража.ОписаниеРаботы;
	НачалоРаботы = ДанныеХронометража.Начало;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображаемуюСтраницу(Элементы, ТекущаяРабота, ОписаниеРаботы)
	
	Если ЗначениеЗаполнено(ТекущаяРабота) ИЛИ ЗначениеЗаполнено(ОписаниеРаботы) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТекущаяРабота;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаХронометражОтключен;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПризнакГиперссылки(Элементы, ТекущаяРабота);
	
	Если ТипЗнч(ТекущаяРабота) = Тип("СправочникСсылка.ЗадачиПроцесса")
		ИЛИ ТипЗнч(ТекущаяРабота) = Тип("СправочникСсылка.Ошибки")
		ИЛИ ТипЗнч(ТекущаяРабота) = Тип("СправочникСсылка.ТехническиеПроекты") Тогда
		Элементы.ТекущаяРабота.Гиперссылка = Истина;
	Иначе
		Элементы.ТекущаяРабота.Гиперссылка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗавершитьНаСервере(НовоеОписаниеРаботы = Неопределено)
	
	Возврат УчетВремени.ПереключитьХронометраж(ТекущаяРабота, ОписаниеРаботы, НовоеОписаниеРаботы);
	
КонецФункции

#КонецОбласти