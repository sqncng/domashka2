#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеИзХранилища = Константы.НастройкаАдресатовУведомленийОСборках.Получить().Получить();
	
	Если ТипЗнч(ДанныеИзХранилища) = Тип("ТаблицаЗначений") Тогда
		
		Для Каждого СтрокаТаблицы из ДанныеИзХранилища Цикл
			
			Если ТипЗнч(СтрокаТаблицы.Адресат) = Тип("СправочникСсылка.Пользователи") Тогда
				НоваяСтрока = ТаблицаПользователей.Добавить();
				НоваяСтрока.Пользователь = СтрокаТаблицы.Адресат;
			Иначе
				НоваяСтрока = ТаблицаВнешнихАдресатов.Добавить();
				НоваяСтрока.Адрес = СтрокаТаблицы.Адресат;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаПользователейОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Для Каждого Значение из ВыбранноеЗначение Цикл
		
		СтруктураОтбора = Новый Структура("Пользователь", Значение);
		МассивСтрок = ТаблицаПользователей.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСтрок.Количество()=0 Тогда
			НоваяСтрока = ТаблицаПользователей.Добавить();
			НоваяСтрока.Пользователь = Значение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборПользователей(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, Элементы.ТаблицаПользователей,,,, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНастройку();
	ПоказатьОповещениеПользователя(НСтр("ru='Настройка алресатов уведомлений сохранена'"),,, БиблиотекаКартинок.Информация32);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНастройку();
	Модифицированность = Ложь;
	ПоказатьОповещениеПользователя(НСтр("ru='Настройка алресатов уведомлений сохранена'"),,, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНастройку()
	
	ТаблицаДанных = Новый ТаблицаЗначений();
	ТаблицаДанных.Колонки.Добавить("Адресат");
	ТаблицаДанных.Колонки.Добавить("Представление");
	
	Для Каждого СтрокаТаблицыПользователей из ТаблицаПользователей Цикл
		НоваяСтрока = ТаблицаДанных.Добавить();
		НоваяСтрока.Адресат = СтрокаТаблицыПользователей.Пользователь;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицыАдресов из ТаблицаВнешнихАдресатов Цикл
		НоваяСтрока = ТаблицаДанных.Добавить();
		НоваяСтрока.Адресат = СтрокаТаблицыАдресов.Адрес;
		НоваяСтрока.Представление = СтрокаТаблицыАдресов.Представление;
	КонецЦикла;
	
	Константы.НастройкаАдресатовУведомленийОСборках.Установить(Новый ХранилищеЗначения(ТаблицаДанных));
		
КонецПроцедуры

#КонецОбласти

