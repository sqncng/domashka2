
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект = Параметры.Объект;
	ДатаВерсии1 = Параметры.ДатаВерсии1;
	ДатаВерсии2 = Параметры.ДатаВерсии2;
	
	СтруктураНастроек = Версионирование.СтруктураНастроекПользователяДляСравненияОбъектов();
	
	СравнениеСредствами1СПредприятия = Число(СтруктураНастроек.СравнениеСредствами1СПредприятия);
	Если СравнениеСредствами1СПредприятия = 1 Тогда
		ТекстовыйФормат = ?(СтруктураНастроек.ТекстовыйФормат, 1, 2);
	КонецЕсли;
														
	УстановитьДоступностьЭлементов(Элементы, СравнениеСредствами1СПредприятия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СравнениеСредствами1СПредприятия1ПриИзменении(Элемент)
	
	Если СравнениеСредствами1СПредприятия = 1 Тогда
		ТекстовыйФормат = 1;
	Иначе
		ТекстовыйФормат = 0;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов(Элементы, СравнениеСредствами1СПредприятия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сравнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект) Тогда
		ТекстСообщения = НСтр("ru='Не выбран версионируемый объект'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект");
		Возврат;
	КонецЕсли;
	
	ДанныеДляСравнения = СохранитьНастройкиИПолучитьДанныеДляСравнения();
	ВерсионированиеКлиент.СравнитьОбъекты(ДанныеДляСравнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОписаниеНастройки(Объект, Настройка, Значение)
	
	Элемент = Новый Структура;
	Элемент.Вставить("Объект", Объект);
	Элемент.Вставить("Настройка", Настройка);
	Элемент.Вставить("Значение", Значение);
	
	Возврат Элемент;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементов(Элементы, СравнениеСредствами1СПредприятия)
	
	Если СравнениеСредствами1СПредприятия = 1 Тогда
		Элементы.ТекстовыйФормат.Доступность = Истина;
	Иначе
		Элементы.ТекстовыйФормат.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()

	МассивСтруктур = Новый Массив;
	
	НастройкаСравнениеСредствами1СПредприятия = Ложь;
	НастройкаТекстовыйФормат = Ложь;
	
	Если СравнениеСредствами1СПредприятия = 1 Тогда
		НастройкаСравнениеСредствами1СПредприятия = Истина;
	КонецЕсли;
	
	Если ТекстовыйФормат = 1 Тогда
		НастройкаТекстовыйФормат = Истина;
	КонецЕсли;
	
	МассивСтруктур.Добавить(ОписаниеНастройки(
	    "ОбщиеНастройкиПользователя",
	    "НастройкаСравненияСравнениеСредствами1СПредприятия",
	    НастройкаСравнениеСредствами1СПредприятия));
	
	МассивСтруктур.Добавить(ОписаниеНастройки(
	    "ОбщиеНастройкиПользователя",
	    "НастройкаСравненияТекстовыйФормат",
	    НастройкаТекстовыйФормат));
		
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранитьМассив(МассивСтруктур);

КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиИПолучитьДанныеДляСравнения()

	СохранитьНастройки();
	ДанныеДляСравнения = Версионирование.ПодготовитьДанныеДляСравнения(Объект, ДатаВерсии1, ДатаВерсии2);
	
	Возврат ДанныеДляСравнения;
	
КонецФункции

#КонецОбласти
